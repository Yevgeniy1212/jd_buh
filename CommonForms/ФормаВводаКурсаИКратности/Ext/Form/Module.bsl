
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Валюта") Тогда
		Валюта = Параметры.Валюта;
	КонецЕсли; 
	
	Если Параметры.Свойство("ДатаУстановкиКурсаИКратности") Тогда
		ДатаУстановкиКурсаИКратности = Параметры.ДатаУстановкиКурсаИКратности;
	КонецЕсли;
	
	Если Параметры.Свойство("КратностьВалюты") Тогда
		КратностьВалюты = Параметры.КратностьВалюты;
	КонецЕсли;
	
	Если Параметры.Свойство("КурсВалюты") Тогда
		КурсВалюты = Параметры.КурсВалюты;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ЗаполнитьНаДату(Команда)
	
	Оповещение = Новый ОписаниеОповещения("ПослеВводаДаты", ЭтаФорма, Параметры);
	ПоказатьВводДаты(Оповещение, ДатаУстановкиКурсаИКратности, Нстр("ru = 'Выберите дату установки курса и кратности.'"), ЧастиДаты.ДатаВремя);
		
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуИПередатьПараметры(Команда)

	Если КратностьВалюты = 0 Или КурсВалюты = 0 Тогда 
										
		Если КурсВалюты = 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru=' Установлен курс равный 0'"), , "КурсВалюты");
		КонецЕсли; 
		
		Если КратностьВалюты = 0 Тогда
			ОбщегоНазначенияКлиент.СообщитьПользователю(НСтр("ru=' Установлена кратность равная 0'"), , "КратностьВалюты");
		КонецЕсли;
		
		Возврат;
		
	КонецЕсли;
	
	// Заполним структуру возвращаемых параметров
	СтруктураВозвращаемыхЗначений = Новый Структура();
	СтруктураВозвращаемыхЗначений.Вставить("КратностьВалюты", КратностьВалюты);
	СтруктураВозвращаемыхЗначений.Вставить("КурсВалюты",      КурсВалюты);

	Закрыть(СтруктураВозвращаемыхЗначений); 
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПослеВводаДаты(Дата, Параметры) Экспорт
	ПолучитьКурс(Дата, Валюта, КурсВалюты, КратностьВалюты);	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьКурс(Дата, Валюта, КурсВалюты, КратностьВалюты)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	КурсыВалютСрезПоследних.Курс,
	|	КурсыВалютСрезПоследних.Кратность
	|ИЗ
	|	РегистрСведений.КурсыВалют.СрезПоследних(&Период, Валюта = &Валюта) КАК КурсыВалютСрезПоследних";
	Запрос.УстановитьПараметр("Период", Дата);
	Запрос.УстановитьПараметр("Валюта", Валюта);
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	Если РезультатЗапроса.Следующий() Тогда
		КурсВалюты      = РезультатЗапроса.Курс;
		КратностьВалюты = РезультатЗапроса.Кратность;
	КонецЕсли; 
	              	
КонецПроцедуры  



