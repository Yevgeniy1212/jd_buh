
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	МассивЭлементов = Новый Массив();
	ЗаполнитьЗначенияСвойств(ЭтаФорма, Параметры,,"ЗакрыватьПриВыборе,ЗакрыватьПриЗакрытииВладельца");
	
	Если Параметры.ТипОбъекта = "ПриходныйКассовыйОрдер" Тогда
		ОбщегоНазначенияБККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Выдать", 		"Видимость", Ложь);		
		ОбщегоНазначенияБККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПоДокументу", "Видимость", Ложь);
	КонецЕсли;
	
	Если Параметры.ТипОбъекта = "РасходныйКассовыйОрдер" Тогда
		ОбщегоНазначенияБККлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "ПринятоОт", 	"Видимость", Ложь);		
	КонецЕсли;
	
	СтруктураПараметров = ЗначенияПриОткрытии(Параметры);
	
	МассивЭлементов = Новый Массив();
	
	Для Каждого ЭлементСтруктуры Из СтруктураПараметров Цикл
		МассивЭлементов.Добавить(ЭлементСтруктуры.Ключ);
	КонецЦикла;

	Если ТолькоПросмотр Тогда 	
				
		ОбщегоНазначенияБККлиентСервер.УстановитьСвойствоЭлементовФормы(Элементы, МассивЭлементов, "ТолькоПросмотр", Истина);
		
	КонецЕсли;  
	
	Если Параметры.Свойство("СписокВыбораОснований") Тогда
		Элементы.Основание.СписокВыбора.Очистить();
		СписокОснованийМассив = Параметры.СписокВыбораОснований.СписокОснований;
		Для Каждого ЭлементОснование Из СписокОснованийМассив Цикл
			Элементы.Основание.СписокВыбора.Добавить(ЭлементОснование);
		КонецЦикла;
		Если НЕ ЗначениеЗаполнено(Основание) Тогда
			Основание = Элементы.Основание.СписокВыбора[0].Значение;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ЗначенияПриОткрытии(Источник)
	
	СтруктураПараметров = Новый Структура();
	
	СтруктураПараметров.Вставить("Основание", 	  	Источник.Основание);
	СтруктураПараметров.Вставить("Приложение",	  	Источник.Приложение);
	
	Если Источник.ТипОбъекта = "ПриходныйКассовыйОрдер" Тогда
		СтруктураПараметров.Вставить("ПринятоОт", 	Источник.ПринятоОт);
	КонецЕсли;
	
	Если Источник.ТипОбъекта = "РасходныйКассовыйОрдер" Тогда
		СтруктураПараметров.Вставить("Выдать",		Источник.Выдать);
		СтруктураПараметров.Вставить("ПоДокументу", Источник.ПоДокументу);
	КонецЕсли;

	Возврат СтруктураПараметров;
	
КонецФункции

&НаКлиенте
Процедура ОК(Команда)
	
	Если Не ТолькоПросмотр Тогда
		СохранитьПараметры = Истина;
	КонецЕсли;
	
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не СохранитьПараметры  Тогда
		
		СписокКнопок = Новый СписокЗначений();
		СписокКнопок.Добавить("Закрыть", НСтр("ru = 'Закрыть'"));
		СписокКнопок.Добавить("НеЗакрывать", НСтр("ru = 'Не закрывать'"));
		
		ОтветНаВопрос = Вопрос(НСтр("ru = 'Реквизиты печати были изменены. Закрыть форму без сохранения реквизитов?'"), СписокКнопок);
		
		Если ОтветНаВопрос = "НеЗакрывать" Тогда
			Отказ = Истина;
			СохранитьПараметры = Ложь;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если СохранитьПараметры Тогда
		
		СтруктураПараметров = ЗначенияПриОткрытии(ЭтаФорма);
		ОповеститьОВыборе(СтруктураПараметров);
		
	КонецЕсли;

КонецПроцедуры
