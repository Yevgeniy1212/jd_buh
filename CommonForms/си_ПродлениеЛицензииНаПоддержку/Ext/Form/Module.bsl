
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;

	ПрефиксПродукта = "си_";
	
	НомерОсновногоКлюча = Константы[ПрефиксПродукта + "НомерОсновногоКлюча"].Получить();
	НомерРегистрационнойАнкеты = Константы[ПрефиксПродукта + "НомерРегистрационнойАнкеты"].Получить();
	                                           
	ИнформацияОПодписке = Вычислить(ПрефиксПродукта + "ЗащитаКлиентСервер.ПолучитьСведенияОПоддержке()");
	Если ЗначениеЗаполнено(ИнформацияОПодписке) И ИнформацияОПодписке.Свойство("СрокОкончанияПоддержки") Тогда
		СрокОкончанияПоддержки 	= НачалоДня(ИнформацияОПодписке.СрокОкончанияПоддержки);
		ОсталосьДней 			= Макс((СрокОкончанияПоддержки - НачалоДня(ТекущаяДатаСеанса())) / 86400, 0);
		ОсталосьДнейСтрокой 	= Строка(ОсталосьДней);
		ТекстЗаголовка 			= "Срок действия Лицензии истекает через %1!";
		Если ОсталосьДней = 0 Тогда
			ТекстЗаголовка = "Срок действия Лицензии истек!";
			Элементы.Декорация2.Заголовок = Символы.ПС + "Для полнофункциональной работы необходимо продлить Лицензию!";
		ИначеЕсли ОсталосьДней = 1 Тогда
			ТекстЗаголовка = "Срок действия Лицензии истекает завтра!";
			Элементы.НеНапоминатьОПродленииЛицензииДо.Видимость = Ложь;
		ИначеЕсли ОсталосьДней = 21 Тогда
			ОсталосьДнейСтрокой = ОсталосьДнейСтрокой + " день"; 
		ИначеЕсли ОсталосьДней < 5 ИЛИ (ОсталосьДней > 21 И ОсталосьДней < 25) Тогда
			ОсталосьДнейСтрокой = ОсталосьДнейСтрокой + " дня";
		Иначе
			ОсталосьДнейСтрокой = ОсталосьДнейСтрокой + " дней";
		КонецЕсли;
		ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", ОсталосьДнейСтрокой);
		Элементы.Декорация1.Заголовок = ТекстЗаголовка;
	Иначе
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	ОднаНеделя = 60 * 60 * 24 * 7;
	СледующаяДатаНапоминания = НачалоДня(ТекущаяДатаСеанса() + ОднаНеделя);

	Если СрокОкончанияПоддержки > ТекущаяДатаСеанса() И СрокОкончанияПоддержки <= СледующаяДатаНапоминания Тогда
		СледующаяДатаНапоминания = СрокОкончанияПоддержки - 86400;
		ТекстЗаголовка = "Не напоминать о продлении Лицензии до %1";
	Иначе	
		ТекстЗаголовка = "Не напоминать о продлении Лицензии семь дней (до %1)";
	КонецЕсли;
	ТекстЗаголовка = СтрЗаменить(ТекстЗаголовка, "%1", Формат(СледующаяДатаНапоминания, "ДФ=dd.MM.yyyy"));
	Элементы.НеНапоминатьОПродленииЛицензииДо.Заголовок = ТекстЗаголовка;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура НеНапоминатьОПродленииЛицензииДоПриИзменении(Элемент)
	
	СохранитьСостояниеФлажка(НеНапоминатьОПродленииЛицензииДо, СледующаяДатаНапоминания);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура СведенияОЛицензииНаПоддержку(Команда)
	
	ОткрытьФорму("Обработка." + ПрефиксПродукта + "СведенияОЛицензииНаПоддержку.Форма");
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ПродлитьЛицензию(Команда)
	
	Попытка
		ИмяОбработкиАктивации = Вычислить(ПрефиксПродукта + "ЗащитаКлиентСервер.ПолучитьИмяФормыДляОткрытия()");
		Если ЗначениеЗаполнено(ИмяОбработкиАктивации) Тогда
			ОткрытьФорму(ИмяОбработкиАктивации + ".Форма.Форма", Новый Структура("ПрефиксПродукта", ПрефиксПродукта), ЭтаФорма);
		КонецЕсли;
		Закрыть();
	Исключение
		Сообщить(ОписаниеОшибки());
	КонецПопытки;

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьСостояниеФлажка(НеНапоминатьОПродленииЛицензииДо, СледующаяДатаНапоминания)
	
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ДатаНапоминания = ?(НеНапоминатьОПродленииЛицензииДо, СледующаяДатаНапоминания, Дата(1, 1, 1));
		ХранилищеОбщихНастроек.Сохранить("ПлатнаяПоддержкаТОР", "НеНапоминатьОПродленииЛицензииДо", ДатаНапоминания);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти