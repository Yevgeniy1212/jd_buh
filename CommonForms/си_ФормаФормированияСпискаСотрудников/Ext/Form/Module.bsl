
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнициализироватьПараметры(Параметры);
	СформироватьСписокНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура ФлагОрганизацияПриИзменении(Элемент)
	УстановитьДоступность();
КонецПроцедуры

&НаКлиенте
Процедура СотрудникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	УстановитьОтборДолжностиДляСпискаСотрудников()	
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьФормирование(Команда)
	СформироватьСписокНаСервере();
	
	Если НЕ Адрес = Неопределено Тогда
		ПараметрыВыбора = Новый Структура;
		ПараметрыВыбора.Вставить("Идентификатор",ИдентификаторИсточника);
		ПараметрыВыбора.Вставить("Адрес",Адрес);
		Оповестить("ПолучитьСписокСотрудников",ПараметрыВыбора);
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьДоступность()
	
	Элементы.Организация.Доступность 				= ФлагОрганизация;
	Элементы.СтруктурноеПодразделение.Доступность 	= ФлагСтруктурноеПодразделение;
	Элементы.Должность.Доступность 					= ФлагДолжность;
	Элементы.Сотрудник.Доступность 					= ФлагСотрудник;
	
КонецПроцедуры

&НаСервере
Процедура СформироватьСписокНаСервере()
	УчетПоФизЛицам = си_УчетСпецодеждыСервер.ПолучитьВедениеУчетаПоФизЛицам();
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	РаботникиОрганизацийСрезПоследних.Сотрудник" + ?(УчетПоФизЛицам,".ФизЛицо","") + " КАК Сотрудник,
	               |	РаботникиОрганизацийСрезПоследних.Организация,
	               |	РаботникиОрганизацийСрезПоследних.Должность,
	               |	РаботникиОрганизацийСрезПоследних.ПодразделениеОрганизации
	               |ПОМЕСТИТЬ ВТ
	               |ИЗ
	               |	РегистрСведений.РаботникиОрганизаций.СрезПоследних(&Момент, 
				   |	"+?(ФлагОрганизация,"Организация = &Организация","") + "
				   |	) КАК РаботникиОрганизацийСрезПоследних
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ВТ.Сотрудник,
	               |	ВТ.Организация,
	               |	ВТ.Должность,
	               |	ВТ.ПодразделениеОрганизации
	               |ИЗ
	               |	ВТ КАК ВТ
	               |ГДЕ
				   |    ИСТИНА
	               |	"+?(ФлагСтруктурноеПодразделение,"И ВТ.ПодразделениеОрганизации = &ПодразделениеОрганизации","") + "
	               |	"+?(ФлагДолжность,"И ВТ.Должность = &Должность","") + "
	               |	"+?(ФлагСотрудник,"И ВТ.Сотрудник = &Сотрудник","")+"";
	Запрос.УстановитьПараметр("Момент",Дата);
	Запрос.УстановитьПараметр("Организация",Организация);
	Запрос.УстановитьПараметр("Должность",Должность);
	Запрос.УстановитьПараметр("Сотрудник",Сотрудник);
	Запрос.УстановитьПараметр("ПодразделениеОрганизации",СтруктурноеПодразделение);
	Результат = Запрос.Выполнить();
	ТЧ = Результат.Выгрузить();

	Адрес = ПоместитьВоВременноеХранилище(ТЧ,ИдентификаторИсточника);
	
	
КонецПроцедуры

&НаСервере
Процедура ИнициализироватьПараметры(Параметры)

	Если Параметры.Свойство("Организация") Тогда
		ФлагОрганизация = Истина;
		Организация = Параметры.Организация;
	КонецЕсли;
	Если Параметры.Свойство("СтруктурноеПодразделение") Тогда
		ФлагСтруктурноеПодразделение = Истина;
		СтруктурноеПодразделение = Параметры.СтруктурноеПодразделение;
	КонецЕсли;
	Если Параметры.Свойство("Сотрудник") Тогда
		Если ЗначениеЗаполнено(Параметры.Сотрудник) Тогда
			ФлагСотрудник = Истина;
			Сотрудник = Параметры.Сотрудник;
			// принудительная установка вида формы если указан этот параметр
			СостояниеМатериалов = Перечисления.си_СостоянияМатериалов.НаходящиесяВЭксплуатации;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("Должность") Тогда
		Если ЗначениеЗаполнено(Параметры.Должность) Тогда
			ФлагДолжность = Истина;
			Должность = Параметры.Должность;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("Дата") Тогда
		Если ЗначениеЗаполнено(Параметры.Дата) Тогда
			Дата = Параметры.Дата;
		КонецЕсли;
	КонецЕсли;
	Если Параметры.Свойство("ИдентификаторИсточника") Тогда
		ИдентификаторИсточника = Параметры.ИдентификаторИсточника;
	КонецЕсли;
	Если Не ЗначениеЗаполнено(Дата) Тогда
		Дата = ТекущаяДата();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьОтборДолжностиДляСпискаСотрудников()
	Если ФлагДолжность Тогда                               
		НоваяСвязь = Новый СвязьПараметраВыбора("Отбор.ТекущаяДолжностьОрганизации", "Должность");
		НовыйМассив = Новый Массив();
		НовыйМассив.Добавить(НоваяСвязь);
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.Сотрудник.СвязиПараметровВыбора = НовыеСвязи
	Иначе
		НовыйМассив = Новый Массив();
		НовыеСвязи = Новый ФиксированныйМассив(НовыйМассив);
		Элементы.Сотрудник.СвязиПараметровВыбора = НовыеСвязи;
	КонецЕсли;
КонецПроцедуры


