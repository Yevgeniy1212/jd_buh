/////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ОБРАБОТКИ ШТРИХКОДОВ В СПРАВОЧНИКАХ И ДОКУМЕНТАХ

// Сформировать новый штрихкод.
//
Функция ПолучитьМаксимальныйШтрихкод() Экспорт
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МАКСИМУМ(ПОДСТРОКА(ОсновныеСредства.Штрихкод, 2, 11)) КАК Код
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Возврат ?(Выборка.Код = "", 0, Мин(Число(Выборка.Код), 99999999999));
	                                        
КонецФункции	

// Сформировать новый штрихкод для номенклатуры.
//
Функция ПолучитьМаксимальныйШтрихкодНоменклатуры() Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	МАКСИМУМ(ЕСТЬNULL(ПОДСТРОКА(ШтрихкодыНоменклатуры.Штрихкод, 2, 11), "" "")) КАК Код
	               |ИЗ
	               |	РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры";

	Выборка = Запрос.Выполнить().Выбрать();
	Выборка.Следующий();
	Код = Выборка.Код;
	Если Код = Null Тогда
		Код = "";	
	КонецЕсли; 
	Возврат ?(Код = "", 0, Мин(Число(Код), 99999999999));
	                                        
КонецФункции

// Сформировать новый штрихкод.
//
Функция СформироватьШтрихкод(ТекущийКод = Неопределено) Экспорт
	
	Если ТекущийКод = Неопределено Тогда
		ТекущийКод = ПолучитьМаксимальныйШтрихкод() + 1;
	КонецЕсли;	
	                                        
	Штрихкод = "2" + Формат(ТекущийКод, "ЧЦ=11; ЧВН=; ЧГ=");
	Штрихкод = Штрихкод + КонтрольныйСимволEAN13(ШтрихКод);

	Возврат Штрихкод;
	
КонецФункции	

// Сформировать новый штрихкод.
//
Функция СформироватьШтрихкодНоменклатуры(ТекущийКод = Неопределено) Экспорт
	
	Если ТекущийКод = Неопределено Тогда
		ТекущийКод = ПолучитьМаксимальныйШтрихкод() + 1;
	КонецЕсли;	
	                                        
	Штрихкод = "1" + Формат(ТекущийКод, "ЧЦ=11; ЧВН=; ЧГ=");
	Штрихкод = Штрихкод + КонтрольныйСимволEAN13(ШтрихКод);

	Возврат Штрихкод;
	
КонецФункции

// Функция возвращает контрольный символ штрихкода EAN13.
//
// Параметры:
//  Штрихкод - Строка
//           - Неполный штрихкод (12 символов), для которого вычисляется
//             контрольный 13-й символ.
//
// Возвращаемое значение:
//  Строка - Контрольный символ EAN13.
//
Функция КонтрольныйСимволEAN13(Штрихкод) Экспорт
	
	Результат   = "";
	Сумма       = 0;
	Коэффициент = 1;
	
	Для Индекс = 1 По 12 Цикл
		ВремКодСимвола = КодСимвола(Штрихкод, Индекс);
		Сумма       = Сумма + Коэффициент * (ВремКодСимвола - 48);
		Коэффициент = 4 - Коэффициент;
	КонецЦикла;
	Сумма     = (10 - Сумма % 10) % 10;
	Результат = Символ(Сумма + 48);
	
	Возврат Результат;
	
КонецФункции 

Процедура СформироватьШтрихкодаМассиваОС(МассивОС, ФормироватьТолькоНовыеШтрихкода = Истина) Экспорт 
	
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК ОС
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.Ссылка В(&МассивОС)
	|	" + ?(ФормироватьТолькоНовыеШтрихкода, " И ОсновныеСредства.Штрихкод = &ПустаяСтрока", "");
	
	Запрос.УстановитьПараметр("МассивОС"	, МассивОС);
	Запрос.УстановитьПараметр("ПустаяСтрока", "");
	РезультатЗапроса 	= Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'В табличной части нет ни одного основного средства с пустым штрихкодом.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;	
	ВыборкаЗапроса 		= РезультатЗапроса.Выбрать();
	ТекущийКод 			= ПолучитьМаксимальныйШтрихкод() + 1;
	Пока ВыборкаЗапроса.Следующий() Цикл
		ОС 							= ВыборкаЗапроса.ОС.ПолучитьОбъект();
		Штрихкод 					= СформироватьШтрихкод(ТекущийКод);
		ОС.Штрихкод 				= Штрихкод;
		ОС.ОбменДанными.Загрузка 	= Истина;
		Попытка 
			ОС.Записать();
		Исключение	
			ТекстСообщения = НСтр("ru='При записи основного средства ""%1""  произошла ошибка: %2'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОС, ИнформацияОбОшибке());
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Продолжить;
		КонецПопытки;
		ТекущийКод = ТекущийКод + 1;
	КонецЦикла;
	
	ТекстСообщения = НСтр("ru='Штрихкоды сформированы'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры	

Процедура СформироватьШтрихкодаМассиваНоменклатур(МассивНом, ФормироватьТолькоНовыеШтрихкода = Истина) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Номенклатура.Ссылка КАК Ссылка
		|ПОМЕСТИТЬ вт_Ном
		|ИЗ
		|	Справочник.Номенклатура КАК Номенклатура
		|ГДЕ
		|	Номенклатура.Ссылка В(&МассивНом)
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	Штрих.Штрихкод КАК Штрихкод,
		|	вт_Ном.Ссылка КАК Номенклатура
		|ИЗ
		|	вт_Ном КАК вт_Ном
		|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
		|			РегШтрих.Номенклатура КАК Номенклатура,
		|			РегШтрих.Штрихкод КАК Штрихкод
		|		ИЗ
		|			РегистрСведений.ШтрихкодыНоменклатуры КАК РегШтрих) КАК Штрих
		|		ПО вт_Ном.Ссылка = Штрих.Номенклатура
		| " + ?(ФормироватьТолькоНовыеШтрихкода, "ГДЕ (Штрих.Штрихкод = """" ИЛИ Штрих.Штрихкод ЕСТЬ NULL)", ""); 
		
	Запрос.УстановитьПараметр("МассивНом"	, МассивНом);
	
	РезультатЗапроса 	= Запрос.Выполнить();
	Если РезультатЗапроса.Пустой() Тогда
		ТекстСообщения = НСтр("ru = 'В табличной части нет ни одной номенклатуры с пустым штрихкодом.'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;
	
	ВыборкаЗапроса 		= РезультатЗапроса.Выбрать();
	ТекущийКод 			= ПолучитьМаксимальныйШтрихкодНоменклатуры() + 1;
	Пока ВыборкаЗапроса.Следующий() Цикл
		
		Штрихкод = СформироватьШтрихкодНоменклатуры(ТекущийКод);
		
		Если ФормироватьТолькоНовыеШтрихкода Тогда
			//записать в регистр
			СоздатьЗаписьВРегистре(ВыборкаЗапроса.Номенклатура, Штрихкод);
		Иначе
			Если ЗначениеЗаполнено(ВыборкаЗапроса.Штрихкод) Тогда
				//найти в регистре и перезаписать 
				НаборЗаписей = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьНаборЗаписей();
				НаборЗаписей.Отбор.Штрихкод.Установить(ВыборкаЗапроса.Штрихкод);
				НаборЗаписей.Прочитать();
				НаборЗаписей.Очистить();
				НаборЗаписей.Записать();
				СоздатьЗаписьВРегистре(ВыборкаЗапроса.Номенклатура, Штрихкод);
			Иначе
				//записать в регистр
				СоздатьЗаписьВРегистре(ВыборкаЗапроса.Номенклатура, Штрихкод);
			КонецЕсли; 
		КонецЕсли; 
		
		ТекущийКод = ТекущийКод + 1;
		
	КонецЦикла;
	
	ТекстСообщения = НСтр("ru='Штрихкоды сформированы'");
	ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
	
КонецПроцедуры	

Процедура СоздатьЗаписьВРегистре(Номенклатура, Штрихкод)
	
	МенеджерЗаписи = РегистрыСведений.ШтрихкодыНоменклатуры.СоздатьМенеджерЗаписи();
	МенеджерЗаписи.Номенклатура = Номенклатура;
	МенеджерЗаписи.Штрихкод = Штрихкод;
	МенеджерЗаписи.Записать();
	
КонецПроцедуры
 
Процедура СоздатьШтрихкодДляНовойНоменклатуры(Номенклатура) Экспорт
	
	ТекущийКод = ПолучитьМаксимальныйШтрихкодНоменклатуры() + 1;
	Штрихкод = СформироватьШтрихкодНоменклатуры(ТекущийКод);
	СоздатьЗаписьВРегистре(Номенклатура, Штрихкод);
	
КонецПроцедуры

Функция ПолучитьДанныеДляПечатиОС(ИмяДокумента, МассивДокументов) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос 			= Новый Запрос;
	ТекстЗапроса 	= "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОС.ОсновноеСредство КАК ОсновноеСредство
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.ОС КАК ОС
	|ГДЕ
	|	ОС.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументОС.Организация КАК Организация
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументОС
	|ГДЕ
	|	ДокументОС.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПоступлениеТоваровУслуг", ИмяДокумента);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ДанныеДляЗаполнения = МассивРезультатов[1].Выгрузить();
	
	// Подготовка данных для заполенения табличной части обработки печати
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("ОС"			, МассивРезультатов[0].Выгрузить());
	СтруктураРезультат.Вставить("Организация"	, ?(ДанныеДляЗаполнения.Количество() = 1, ДанныеДляЗаполнения[0].Организация , Неопределено));
	СтруктураРезультат.Вставить("ДатаСведений"	, ?(МассивДокументов.Количество() = 1	, МассивДокументов[0].Дата, Дата(1,1,1)));
	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СтруктураРезультат;
	
КонецФункции	

Функция ПолучитьДанныеДляПечатиНоменклатуры(ИмяДокумента, МассивДокументов) Экспорт 
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос 			= Новый Запрос;
	ТекстЗапроса 	= "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	Товары.Номенклатура КАК Номенклатура
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг.Товары КАК Товары
	|ГДЕ
	|	Товары.Ссылка В(&МассивДокументов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ДокументТМЗ.Организация КАК Организация
	|ИЗ
	|	Документ.ПоступлениеТоваровУслуг КАК ДокументТМЗ
	|ГДЕ
	|	ДокументТМЗ.Ссылка В(&МассивДокументов)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Организация";
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса,"ПоступлениеТоваровУслуг", ИмяДокумента);
	
	Запрос.Текст = ТекстЗапроса;
	Запрос.УстановитьПараметр("МассивДокументов", МассивДокументов);
	
	МассивРезультатов = Запрос.ВыполнитьПакет();
	
	ДанныеДляЗаполнения = МассивРезультатов[1].Выгрузить();
	
	// Подготовка данных для заполенения табличной части обработки печати
	СтруктураРезультат = Новый Структура;
	СтруктураРезультат.Вставить("Номенклатура"  , МассивРезультатов[0].Выгрузить());
	СтруктураРезультат.Вставить("Организация"	, ?(ДанныеДляЗаполнения.Количество() = 1, ДанныеДляЗаполнения[0].Организация , Неопределено));
	СтруктураРезультат.Вставить("ДатаСведений"	, ?(МассивДокументов.Количество() = 1	, МассивДокументов[0].Дата, Дата(1,1,1)));
	
	
	УстановитьПривилегированныйРежим(Ложь);
	
	Возврат СтруктураРезультат;
	
КонецФункции	
// 
/////////////////////////////////////////////////////////////////////
