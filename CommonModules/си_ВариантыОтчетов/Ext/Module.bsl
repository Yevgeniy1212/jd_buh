
#Область ПрограммныйИнтерфейс

// Настраивает варианты отчетов
//
// Параметры:
//  Настройки	 - ДеревоЗначений	
//
Процедура НастроитьВариантыОтчетов(Настройки) Экспорт
	
	Для Каждого Отчет Из Метаданные.Отчеты Цикл
		Если Найти(Отчет.Имя,"си_")=0 Тогда
			Продолжить;
		КонецЕсли;
		Если Отчет.ОсновнаяСхемаКомпоновкиДанных<>Неопределено И Отчет.ОсновнаяФорма = Метаданные.ОбщиеФормы.ФормаОтчета Тогда
			ФункциональныеОпции = "";
			Для Каждого ФО Из Метаданные.ФункциональныеОпции Цикл
				Если ФО.Состав.Содержит(Отчет) Тогда
					ФункциональныеОпции = ФункциональныеОпции + ?(ФункциональныеОпции="","",",")+ФО.Имя;
				КонецЕсли;
			КонецЦикла;
			МассивИмен = Новый Массив;
			ЗаполнитьИменаПодсистем(Метаданные.Подсистемы,МассивИмен,Отчет);
			ОтчетИБ = Отчеты[Отчет.Имя].Создать();
			Для Каждого НастройкаОтчета Из ОтчетИБ.СхемаКомпоновкиДанных.ВариантыНастроек Цикл
				Для Каждого ИмяПодсистемы Из МассивИмен Цикл
					НастройкиВарианта = ВариантыОтчетов.ОписаниеВарианта(Настройки, Отчет, НастройкаОтчета.Имя);
					НастройкиВарианта.Размещение.Вставить(ИмяПодсистемы);
					Если ЗначениеЗаполнено(ФункциональныеОпции) Тогда
						НастройкиВарианта.ФункциональныеОпции = ФункциональныеОпции;
					КонецЕсли;
				КонецЦикла;
			КонецЦикла;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

// Заполняет имена подсистем
//
// Параметры:
//  СписокПодсистем	 - список подсистем 
//  МассивИмен		 - Массив	 
//  Отчет			 - ссылка на отчет 
//
Процедура ЗаполнитьИменаПодсистем(СписокПодсистем,МассивИмен,Отчет)  Экспорт
	Для Каждого Подсистема Из СписокПодсистем Цикл
		Если Подсистема.Состав.Содержит(Отчет) Тогда
			МассивИмен.Добавить(Подсистема.ПолноеИмя());
		КонецЕсли;
		ЗаполнитьИменаПодсистем(Подсистема.Подсистемы,МассивИмен,Отчет);
	КонецЦикла;
КонецПроцедуры

// Определяеть разделы с вариантами отчетов
//
// Параметры:
//  Разделы	 - СписокЗначений - разделы, в которые выведены команды открытия панели отчетов. 
//
Процедура ОпределитьРазделыСВариантамиОтчетов(Разделы) Экспорт
	Разделы.Добавить(Метаданные.Подсистемы.си_СпецодеждаИИнвентарь, НСтр("ru = 'Отчеты по учету спецодежды и инвентаря'"));
КонецПроцедуры

#КонецОбласти

