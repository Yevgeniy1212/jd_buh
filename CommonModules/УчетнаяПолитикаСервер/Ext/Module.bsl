////////////////////////////////////////////////////////////////////////////////
// УчетнаяПолитика: модуль содержит методы получения учетных политик.
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция Существует(Организация, Период, РазделУчета = "БУ", ВыводитьСообщениеОбОтсутствииУчетнойПолитики = Ложь, ДокументСсылка = Неопределено) Экспорт
	
	ИмяФункциональнойОпции = "СпособРасчетаСебестоимостиПроизводства";
	Если РазделУчета = "НУ" Тогда 
		ИмяФункциональнойОпции = "НДСНалоговыйПериод";
	КонецЕсли;
	
	ЗначениеФункциональнойОпции = ПолучитьФункциональнуюОпцию(ИмяФункциональнойОпции, 
		Новый Структура("Организация, Период", Организация, НачалоМесяца(Период)));

	УчетнаяПолитикаСуществует = НЕ (ЗначениеФункциональнойОпции = Ложь);

	Если НЕ УчетнаяПолитикаСуществует
		И ВыводитьСообщениеОбОтсутствииУчетнойПолитики = Истина Тогда
		
		Если РазделУчета = "НУ" Тогда 
			ШаблонСообщения = НСтр("ru='Для организации ""%1"" на %2 не указаны параметры учетной политики налогового учета.'");
		Иначе
			ШаблонСообщения = НСтр("ru='Для организации ""%1"" на %2 не указаны параметры учетной политики бухгалтерского учета.'");
		КонецЕсли;
		
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ШаблонСообщения,
			Организация,
			Формат(Период, "ДЛФ=DD"));
		
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ДокументСсылка, , "Объект");
	КонецЕсли;

	Возврат УчетнаяПолитикаСуществует;
	
КонецФункции

// Параметры учетной политики по бухгалтерскому учету

Функция УчетВременныхРазницПоНалогуНаПрибыль(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиБухгалтерскогоУчета("УчетВременныхРазницПоНалогуНаПрибыль", Организация, Период, Ложь);
	
КонецФункции

Функция ВедетсяУчетВременныхРазницБалансовымМетодом(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитики("ВедениеУчетаВременныхРазницБалансовымМетодом", Организация, Период);
	
КонецФункции

Функция ПолучитьПодразделениеДляРеглОпераций(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитики("ПодразделениеДляРеглОпераций", Организация, Период);
	
КонецФункции

Функция ОпределятьСтоимостьПриобретенияПоКурсуАванса(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиБухгалтерскогоУчета("ОпределятьСтоимостьПриобретенияПоКурсуАванса", Организация, Период);
	
КонецФункции

Функция ОпределятьДоходОтРеализацииПоКурсуАванса(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиБухгалтерскогоУчета("ОпределятьДоходОтРеализацииПоКурсуАванса", Организация, Период);
	
КонецФункции

Функция СпособРасчетаСебестоимостиПроизводства(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиБухгалтерскогоУчета("СпособРасчетаСебестоимостиПроизводства", Организация, Период);
	
КонецФункции

Функция ПрименятьПараметрыНачисленияАмортизацииВМесяцеИзменения(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиБухгалтерскогоУчета("ПрименятьПараметрыНачисленияАмортизацииВМесяцеИзменения", Организация, Период);
	
КонецФункции

Функция ПолучитьТипЦенПлановойСебестоимости() Экспорт
	
	Возврат Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить();
	
КонецФункции 

// Параметры учетной политики по налоговому учету

Функция ПлательщикНалогаНаПрибыль(Организация, Знач Период) Экспорт
	
	Результат = ПолучитьФункциональнуюОпцию("ПлательщикКПН", 
		Новый Структура("Организация, Период", Организация, НачалоМесяца(Период)));
	
	Возврат Результат;
	
КонецФункции

Функция ВкладчикОППВ(Организация, Период) Экспорт
    
    Возврат ЗначениеУчетнойПолитики("ВкладчикОППВ", Организация, Период);
    
КонецФункции

Функция ОрганизацияЯвляетсяПлательщикомСН(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("ОрганизацияЯвляетсяПлательщикомСН", Организация, Период, Истина);
	
КонецФункции

Функция ОтражениеПоПериодуРегистрации(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("ОтражениеПоПериодуРегистрации", Организация, Период);
	
КонецФункции

Функция ПрименятьОграничениеНаМинимальныйОбъектОСМС(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("ПрименятьОграничениеНаМинимальныйОбъектОСМС", Организация, Период);
	
КонецФункции

Функция НеПрименятьКорректировкуПрочихНалоговВзносовОтчислений(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("НеПрименятьКорректировкуПрочихНалоговВзносовОтчислений", Организация, Период);
	
КонецФункции

Функция ПрименятьВычетВОСМСДляФизическихЛицИГПХ(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("ПрименятьВычетВОСМСДляФизическихЛицИГПХ", Организация, Период);
	
КонецФункции

Функция ПрименятьВычетОПВиВОСМСДляГПХ(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("ПрименятьВычетОПВиВОСМСДляГПХ", Организация, Период);
	
КонецФункции

Функция ПредоставлятьВычетВМесяцеОтсутствияДохода(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("ПредоставлятьВычетВМесяцеОтсутствияДохода", Организация, Период);
	
КонецФункции

Функция ПолучитьМетодОтнесенияНДСВЗачет(Организация, Период) Экспорт
	
	МетодОтнесенияНДСВЗачет = ЗначениеУчетнойПолитикиНалоговогоУчета("НДСМетодОтнесенияВЗачет", Организация, Период);

	Возврат ?(ЗначениеЗаполнено(МетодОтнесенияНДСВЗачет), МетодОтнесенияНДСВЗачет, Перечисления.МетодыОтнесенияНДСВЗачет.Пропорциональный);
	
КонецФункции

Функция ПолучитьПериодОтнесенияНДСВЗачет(Организация, Период) экспорт
	
	ПериодОтнесенияНДСВЗачет = ЗначениеУчетнойПолитикиНалоговогоУчета("НДСНалоговыйПериод", Организация, Период);
	
	Возврат ?(ЗначениеЗаполнено(ПериодОтнесенияНДСВЗачет), ПериодОтнесенияНДСВЗачет, Перечисления.Периодичность.Месяц);
	
КонецФункции

Функция УпрощенныйУчетИПНиОПВ(Организация, Период) экспорт
	
	УпрощенныйУчетИПНиОПВ = ЗначениеУчетнойПолитикиНалоговогоУчета("УпрощенныйУчетИПНиОПВ", Организация, Период);
	
	Возврат УпрощенныйУчетИПНиОПВ;
	
КонецФункции

Функция ПорядокОбложенияДоходовРаботников(Организация, Период) Экспорт
	
	ПорядокОбложенияДоходовРаботников = ЗначениеУчетнойПолитикиНалоговогоУчета("ПорядокОбложенияДоходовРаботников", Организация, Период);
	
	Возврат ПорядокОбложенияДоходовРаботников;
	
КонецФункции

// Функция возвращает признак распределения налогов по структурным единицам.
//
Функция ПолучитьПризнакиРаспределенияНалогов(Организация, Период) Экспорт

	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("ДатаАктуальности", Период);
	Запрос.УстановитьПараметр("парамОрганизация", Организация);
	
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	УчетнаяПолитикаНУ.РаспределятьНалогиПоСтруктурнымЕдиницам,
		|	УчетнаяПолитикаНУ.РаспределятьНалогиПоПодразделениямОрганизаций
		|ИЗ
		|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ДатаАктуальности, Организация = &парамОрганизация) КАК УчетнаяПолитикаНУ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	СтруктураПризнаковРаспределения = Новый Структура;
	
	Если Выборка.Следующий() Тогда
		СтруктураПризнаковРаспределения.Вставить("РаспределятьНалогиПоСтруктурнымЕдиницам", Выборка.РаспределятьНалогиПоСтруктурнымЕдиницам);
		СтруктураПризнаковРаспределения.Вставить("РаспределятьНалогиПоПодразделениямОрганизаций", Выборка.РаспределятьНалогиПоПодразделениямОрганизаций);
	Иначе
		СтруктураПризнаковРаспределения.Вставить("РаспределятьНалогиПоСтруктурнымЕдиницам", Ложь);
		СтруктураПризнаковРаспределения.Вставить("РаспределятьНалогиПоПодразделениямОрганизаций", Ложь);
	КонецЕсли;
	
	Возврат СтруктураПризнаковРаспределения;
	
КонецФункции 

Функция ПолучитьПорядокСписанияЗадолженностиПоЗарплате(Организация, Период) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиНалоговогоУчета("ПорядокСписанияЗадолженностиПоЗарплате", Организация, Период, Перечисления.ПорядокСписанияЗадолженностиПоЗарплате.ПоСпособуЗаполненияДокумента);
	
КонецФункции

// Параметры учетной политики по персоналу организаций

Функция ПолучитьПризнакВеденияУчетаПоГоловнойОрганизации(Организация) Экспорт
	
	Возврат ЗначениеУчетнойПолитикиПоПерсоналуОрганизаций("ВестиУчетПоГоловнойОрганизации", Организация, Истина);
	
КонецФункции

// Функция возвращает текст запроса для получения учетной политики 
// по персоналу организации
//
Функция ПолучитьТекстЗапросаУчетнойПолитикиПоПерсоналу() Экспорт
	
	Возврат "ВЫБРАТЬ РАЗРЕШЕННЫЕ
				|	Организации.Ссылка КАК Организация,
				|	ЕСТЬNULL(УчетнаяПолитика.ВестиУчетПоГоловнойОрганизации, ИСТИНА) КАК ВедениеУчетаПоГоловнойОрганизации,
				|	ЕСТЬNULL(УчетнаяПолитика.УчитыватьКадровыеПерестановкиПриРасчетеСреднегоЗаработка, ЛОЖЬ) КАК УчитыватьКадровыеПерестановкиПриРасчетеСреднегоЗаработка,
				|	ЕСТЬNULL(УчетнаяПолитика.ВариантУчетаКадровыхПерестановок, ЗНАЧЕНИЕ(Перечисление.ВариантыУчетаКадровыхПерестановок.ПустаяСсылка)) КАК ВариантУчетаКадровыхПерестановок,
 				|	ЕСТЬNULL(УчетнаяПолитика.РасчетКоэффициентаНарастающимИтогом, ЛОЖЬ) КАК РасчетКоэффициентаНарастающимИтогом,
				|	ЕСТЬNULL(УчетнаяПолитика.РассчитыватьКоэффициентИндексацииВПределахКадровыхПерестановок, ЛОЖЬ) КАК РассчитыватьКоэффициентИндексацииВПределахКадровыхПерестановок
                |ИЗ
				|	Справочник.Организации КАК Организации
				|		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ
				|			УчетнаяПолитикаПоПерсоналуОрганизаций.Организация КАК Организация,
				|			УчетнаяПолитикаПоПерсоналуОрганизаций.ВестиУчетПоГоловнойОрганизации КАК ВестиУчетПоГоловнойОрганизации,
				|			УчетнаяПолитикаПоГолове.УчитыватьКадровыеПерестановкиПриРасчетеСреднегоЗаработка КАК УчитыватьКадровыеПерестановкиПриРасчетеСреднегоЗаработка,
				|			УчетнаяПолитикаПоГолове.ВариантУчетаКадровыхПерестановок КАК ВариантУчетаКадровыхПерестановок,
				|			УчетнаяПолитикаПоГолове.РасчетКоэффициентаНарастающимИтогом КАК РасчетКоэффициентаНарастающимИтогом,
				|			УчетнаяПолитикаПоГолове.РассчитыватьКоэффициентИндексацииВПределахКадровыхПерестановок КАК РассчитыватьКоэффициентИндексацииВПределахКадровыхПерестановок
				|		ИЗ
				|			РегистрСведений.УчетнаяПолитикаПоПерсоналуОрганизаций КАК УчетнаяПолитикаПоПерсоналуОрганизаций
				|				ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаПоПерсоналуОрганизаций КАК УчетнаяПолитикаПоГолове
				|				ПО (ВЫБОР
				|						КОГДА УчетнаяПолитикаПоПерсоналуОрганизаций.ВестиУчетПоГоловнойОрганизации
				|							ТОГДА ВЫБОР
				|									КОГДА УчетнаяПолитикаПоПерсоналуОрганизаций.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
				|										ТОГДА УчетнаяПолитикаПоПерсоналуОрганизаций.Организация.Ссылка
				|									ИНАЧЕ УчетнаяПолитикаПоПерсоналуОрганизаций.Организация.ГоловнаяОрганизация
				|								КОНЕЦ
				|						ИНАЧЕ УчетнаяПолитикаПоПерсоналуОрганизаций.Организация
				|					КОНЕЦ = УчетнаяПолитикаПоГолове.Организация)) КАК УчетнаяПолитика
				|		ПО Организации.Ссылка = УчетнаяПолитика.Организация.Ссылка";
	
КонецФункции

Функция ИспользоватьПереводыВПутиПриПеремещенияДенежныхСредств(Организация, Дата) Экспорт
	
	//!!! Возможно нужна ФО и настройка учетной политики
	Возврат Ложь;
	
КонецФункции

Функция ОсновнойСчетУчетаЗатрат(Организация, Период) Экспорт
	
	//!!! Нужна настройка учетной политики
	Возврат ПланыСчетов.Типовой.АдминистративныеРасходы;
	
КонецФункции

// Функция возвращает Истина, если в конфигурации ведется учет по валютам.
//
Функция ИспользоватьВалютныйУчет() Экспорт

	Возврат ПолучитьФункциональнуюОпцию("ИспользоватьВалютныйУчет");

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ЗначениеУчетнойПолитики(ИмяРесурса, Организация, Период)
	
	Параметры = Новый Структура;
	Параметры.Вставить("Организация", Организация);
	Параметры.Вставить("Период",      НачалоМесяца(Период));
	
	Возврат ПолучитьФункциональнуюОпцию(ИмяРесурса, Параметры);
	
КонецФункции

Функция ЗначениеУчетнойПолитикиБухгалтерскогоУчета(ИмяРесурса, Организация, Период, ЗначениеПоУмолчанию = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаБУ." + ИмяРесурса + " КАК Значение
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаБухгалтерскийУчет.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитикаБУ";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);

	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Возврат ВыборкаДетальныеЗаписи.Значение;
	КонецЕсли;

	Возврат ЗначениеПоУмолчанию;

КонецФункции

Функция ЗначениеУчетнойПолитикиНалоговогоУчета(ИмяРесурса, Организация, Период, ЗначениеПоУмолчанию = Ложь)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаНУ." + ИмяРесурса + " КАК Значение
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&Период, Организация = &Организация) КАК УчетнаяПолитикаНУ";
	
	Запрос.УстановитьПараметр("Организация", Организация);
	Запрос.УстановитьПараметр("Период", Период);

	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Возврат ВыборкаДетальныеЗаписи.Значение;
	КонецЕсли;

	Возврат ЗначениеПоУмолчанию;

КонецФункции

Функция ЗначениеУчетнойПолитикиПоПерсоналуОрганизаций(ИмяРесурса, Организация, ЗначениеПоУмолчанию = Неопределено)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	УчетнаяПолитикаПоПерсоналу." + ИмяРесурса + " КАК Значение
	|ИЗ
	|	РегистрСведений.УчетнаяПолитикаПоПерсоналуОрганизаций КАК УчетнаяПолитикаПоПерсоналу
	|ГДЕ
	|	УчетнаяПолитикаПоПерсоналу.Организация = &Организация";
	
	Запрос.УстановитьПараметр("Организация", Организация);

	ВыборкаДетальныеЗаписи = Запрос.Выполнить().Выбрать();

	Если ВыборкаДетальныеЗаписи.Следующий() Тогда 
		Возврат ВыборкаДетальныеЗаписи.Значение;
	КонецЕсли;

	Возврат ЗначениеПоУмолчанию;

КонецФункции
