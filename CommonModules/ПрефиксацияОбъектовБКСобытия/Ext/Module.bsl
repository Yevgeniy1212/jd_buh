////////////////////////////////////////////////////////////////////////////////
// Подсистема "Префиксация объектов"
//
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Устанавливает префикс источника подписки в соответствии с префиксом организации. 
// Источник подписки должен содержать
// обязательный реквизит шапки "Организация", тип: "СправочникСсылка.Организации"
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача]
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
// Префикс - Строка - префикс объекта, который нужно изменить
//
Процедура УстановитьПрефиксОрганизации(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	УстановитьПрефикс(Источник, Префикс, Ложь, Истина);
	
КонецПроцедуры

// Устанавливает префикс источника подписки в соответствии с префиксом информационной базы.
// Ограничения на реквизиты источника не накладываются
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача]
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
// Префикс - Строка - префикс объекта, который нужно изменить
//
Процедура УстановитьПрефиксИнформационнойБазы(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	УстановитьПрефикс(Источник, Префикс, Истина, Ложь);
	
КонецПроцедуры

// Устанавливает префикс источника подписки в соответствии с префиксом информационной базы и префиксом организации.
// Источник подписки должен содержать
// обязательный реквизит шапки "Организация", тип: "СправочникСсылка.Организации"
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача]
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
// Префикс - Строка - префикс объекта, который нужно изменить
//
Процедура УстановитьПрефиксИнформационнойБазыИОрганизации(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	УстановитьПрефикс(Источник, Префикс, Истина, Истина);
	
КонецПроцедуры

// Устанавливает префикс источника подписки в соответствии с префиксом информационной базы и префиксом организации.
// Источник подписки должен содержать
// обязательный реквизит шапки "Организация", тип: "СправочникСсылка.Организации"
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача]
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
// Префикс - Строка - префикс объекта, который нужно изменить
//
Процедура УстановитьПрефиксИнформационнойБазыОрганизацииИСклада(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ДобавитьПрефиксСклада(Источник, Префикс);
	УстановитьПрефикс(Источник, Префикс, Истина, Истина);
	
КонецПроцедуры

// Устанавливает префикс источника подписки в соответствии с префиксом информационной базы и префиксом организации.
// Источник подписки должен содержать
// обязательный реквизит шапки "Организация", тип: "СправочникСсылка.Организации"
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача]
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
// Префикс - Строка - префикс объекта, который нужно изменить
//
Процедура УстановитьПрефиксИнформационнойБазыОрганизацииИКассы(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ДобавитьПрефиксКассы(Источник, Префикс);
	УстановитьПрефикс(Источник, Префикс, Истина, Истина);
	
КонецПроцедуры

// Устанавливает префикс источника подписки в соответствии с префиксом информационной базы и префиксом организации.
// Источник подписки должен содержать
// обязательный реквизит шапки "Организация", тип: "СправочникСсылка.Организации"
//
// Параметры:
//  Источник - Источник события подписки.
//             Любой объект из множества [Справочник, Документ, План видов характеристик, Бизнес процесс, Задача]
// СтандартнаяОбработка - Булево - флаг стандартной обработки подписки
// Префикс - Строка - префикс объекта, который нужно изменить
//
Процедура УстановитьПрефиксИнформационнойБазыОрганизацииИБанковскогоСчета(Источник, СтандартнаяОбработка, Префикс) Экспорт
	
	ДобавитьПрефиксБанковскогоСчета(Источник, Префикс);
	УстановитьПрефикс(Источник, Префикс, Истина, Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Для справочников

// Выполняет проверку модифицированности реквизита Организация элемента справочника
// Если реквизит Организация изменен, то Код элемента обнуляется.
// Это необходимо для назначения нового кода элементу
//
// Параметры:
//  Источник - СправочникОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьКодСправочникаПоОрганизации(Источник, Отказ) Экспорт
	
	ПроверитьКодОбъектаПоОрганизации(Источник);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Для бизнес-процессов

// Выполняет проверку модифицированности Даты бизнес процесса
// Если дата не входит в предыдущий период, то номер бизнес процесса обнуляется.
// Это необходимо для назначения нового номера бизнес процессу
//
// Параметры:
//  Источник - БизнесПроцессОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерБизнесПроцессаПоДате(Источник, Отказ) Экспорт
	
	ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Источник);
	
КонецПроцедуры

// Выполняет проверку модифицированности Даты и Организации бизнес процесса
// Если дата не входит в предыдущий период или изменен реквизит Организация, то номер бизнес процесса обнуляется.
// Это необходимо для назначения нового номера бизнес процессу
//
// Параметры:
//  Источник - БизнесПроцессОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерБизнесПроцессаПоДатеИОрганизации(Источник, Отказ) Экспорт
	
	ТаблицаИзмененныхРеквизитов = ПустаяТаблицаИзмененныхРеквизитов();
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаОрганизация(Источник), "Организация", ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаОрганизация(Источник));
	
	ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Источник, ТаблицаИзмененныхРеквизитов);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Для документов

// Выполняет проверку модифицированности Даты документа
// Если дата не входит в предыдущий период, то номер документа обнуляется.
// Это необходимо для назначения нового номера документу
//
// Параметры:
//  Источник - ДокументОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерДокументаПоДате(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Источник);
	
КонецПроцедуры

// Выполняет проверку модифицированности Даты и Организации документа
// Если дата не входит в предыдущий период или изменен реквизит Организация, то номер документа обнуляется.
// Это необходимо для назначения нового номера документу
//
// Параметры:
//  Источник - ДокументОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерДокументаПоДатеИОрганизации(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ТаблицаИзмененныхРеквизитов = ПустаяТаблицаИзмененныхРеквизитов();
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаОрганизация(Источник), "Организация", ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаОрганизация(Источник));
	
	ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Источник, ТаблицаИзмененныхРеквизитов);
	
КонецПроцедуры

// Выполняет проверку модифицированности Даты и Организации документа
// Если дата не входит в предыдущий период или изменен реквизит Организация, то номер документа обнуляется.
// Это необходимо для назначения нового номера документу
//
// Параметры:
//  Источник - ДокументОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерДокументаПоДатеОрганизацииИСкладу(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ТаблицаИзмененныхРеквизитов = ПустаяТаблицаИзмененныхРеквизитов();
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаОрганизация(Источник), "Организация", ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаОрганизация(Источник));
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаСклад(Источник),       "Склад",       ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаСклад(Источник));
	
	ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Источник, ТаблицаИзмененныхРеквизитов);
	
КонецПроцедуры

// Выполняет проверку модифицированности Даты и Организации документа
// Если дата не входит в предыдущий период или изменен реквизит Организация, то номер документа обнуляется.
// Это необходимо для назначения нового номера документу
//
// Параметры:
//  Источник - ДокументОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерДокументаПоДатеОрганизацииИКассе(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ТаблицаИзмененныхРеквизитов = ПустаяТаблицаИзмененныхРеквизитов();
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаОрганизация(Источник), "Организация", ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаОрганизация(Источник));
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаКасса(Источник),       "Касса",       ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаКасса(Источник));
	
	ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Источник, ТаблицаИзмененныхРеквизитов);
	
КонецПроцедуры

// Выполняет проверку модифицированности Даты и Организации документа
// Если дата не входит в предыдущий период или изменен реквизит Организация, то номер документа обнуляется.
// Это необходимо для назначения нового номера документу
//
// Параметры:
//  Источник - ДокументОбъект - источник события подписки
//  Отказ    - Булево - флаг отказа
// 
Процедура ПроверитьНомерДокументаПоДатеОрганизацииИБанковскомуСчету(Источник, Отказ, РежимЗаписи, РежимПроведения) Экспорт
	
	ТаблицаИзмененныхРеквизитов = ПустаяТаблицаИзмененныхРеквизитов();
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаОрганизация(Источник),    "Организация",    ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаОрганизация(Источник));
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаБанковскийСчет(Источник), "БанковскийСчет", ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаБанковскийСчет(Источник));
	
	ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Источник, ТаблицаИзмененныхРеквизитов);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура УстановитьПрефикс(Источник, 
							Префикс, 
							УстановитьПрефиксИнформационнойБазы, 
							УстановитьПрефиксОрганизации)
	
	ПрефиксИнформационнойБазы = "";
	ПрефиксОрганизации        = "";
	
	Реквизиты = Новый Структура;
	
	Если УстановитьПрефиксИнформационнойБазы Тогда
		
		ПрефиксИнформационнойБазы = Константы.ПрефиксУзлаРаспределеннойИнформационнойБазы.Получить();
		
	КонецЕсли;
	
	Если УстановитьПрефиксОрганизации Тогда
		
		Организация = ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаОрганизация(Источник);
		
		Если ЗначениеЗаполнено(Организация) Тогда
			
			ПрефиксОрганизации = СокрЛП(Организация.Префикс);
			
		КонецЕсли;
		
		Если НЕ ЗначениеЗаполнено(ПрефиксОрганизации) И НЕ ЗначениеЗаполнено(Префикс) Тогда 
			
			Префикс = "0";
			
		КонецЕсли;
		
	КонецЕсли;
	
	ШаблонПрефикса = "[ИБ][ОР][Префикс]";
	ШаблонПрефикса = СтрЗаменить(ШаблонПрефикса, "[ОР]", ПрефиксОрганизации);
	ШаблонПрефикса = СтрЗаменить(ШаблонПрефикса, "[ИБ]", ПрефиксИнформационнойБазы);
	ШаблонПрефикса = СтрЗаменить(ШаблонПрефикса, "[Префикс]", Префикс);
	
	Префикс = ШаблонПрефикса;
	
КонецПроцедуры

Процедура ДобавитьПрефиксСклада(Источник, Префикс)
	
	ПрефиксСклада = "";
	
	Склад = ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаСклад(Источник);
	Если ЗначениеЗаполнено(Склад) Тогда 
		
		ПрефиксСклада = СокрЛП(Склад.Префикс);
		
	КонецЕсли;
	
	Префикс = ПрефиксСклада + Префикс;
	Если НЕ ЗначениеЗаполнено(Префикс) Тогда 
		Префикс = "0";
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПрефиксКассы(Источник, Префикс)
	
	ПрефиксКассы = "";
	
	Касса = ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаКасса(Источник);
	Если ЗначениеЗаполнено(Касса) Тогда 
		
		ПрефиксКассы = СокрЛП(Касса.Префикс);
		
	КонецЕсли;
	
	Префикс = ПрефиксКассы + Префикс;
	Если НЕ ЗначениеЗаполнено(Префикс) Тогда 
		Префикс = "0";
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьПрефиксБанковскогоСчета(Источник, Префикс)
	
	ПрефиксБанковскогоСчета = "";
	
	БанковскийСчет = ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаБанковскийСчет(Источник);
	Если ЗначениеЗаполнено(БанковскийСчет) Тогда 
		
		ПрефиксБанковскогоСчета = СокрЛП(БанковскийСчет.Префикс);
		
	КонецЕсли;
	
	Префикс = ПрефиксБанковскогоСчета + Префикс;
	Если НЕ ЗначениеЗаполнено(Префикс) Тогда 
		Префикс = "0";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПроверитьНомерОбъектаПоДатеИПрефиксообразующимРеквизитам(Объект, ТаблицаИзмененныхРеквизитов = Неопределено)
	
	Если Объект.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли Объект.ЭтоНовый() Тогда
		Возврат;
	КонецЕсли;
	
	Если ПрефиксацияОбъектовБКСлужебный.ДатаИлиПрефиксообразующиеРеквизитыИзменены(
			Объект.Ссылка,
			Объект.Дата,
			ТаблицаИзмененныхРеквизитов,
			Объект.Метаданные().ПолноеИмя())
		Тогда
		
		Объект.Номер = "";
		
	КонецЕсли;

КонецПроцедуры

Процедура ПроверитьКодОбъектаПоОрганизации(Объект)
	
	Если Объект.ОбменДанными.Загрузка Тогда
		Возврат;
	ИначеЕсли Объект.ЭтоНовый() Тогда
		Возврат;
	ИначеЕсли Не (ПрефиксацияОбъектовБКСлужебный.РеквизитДоступен(Объект, "Организация") ИЛИ ПрефиксацияОбъектовБКСлужебный.РеквизитДоступен(Объект, "ОрганизацияОтправитель")) Тогда
		Возврат;
	КонецЕсли;
	
	ТаблицаИзмененныхРеквизитов = ПустаяТаблицаИзмененныхРеквизитов();
	ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(ТаблицаИзмененныхРеквизитов, ПрефиксацияОбъектовБКСлужебный.ИмяРеквизитаОрганизация(Объект), "Организация", ПрефиксацияОбъектовБКСлужебный.ЗначениеРеквизитаОрганизация(Объект));

	Если ПрефиксацияОбъектовБКСлужебный.ПрефиксообразующиеРеквизитыИзменены(Объект.Ссылка,	
		ТаблицаИзмененныхРеквизитов, Объект.Метаданные().ПолноеИмя()) Тогда
		
		Объект.Код = "";
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПустаяТаблицаИзмененныхРеквизитов()
	
	Результат = Новый ТаблицаЗначений;
	Результат.Колонки.Добавить("ПутьКРеквизиту");
	Результат.Колонки.Добавить("ПсевдонимРеквизита");
	Результат.Колонки.Добавить("ЗначениеРеквизита");
	
	Возврат Результат;
	
КонецФункции

Процедура ДобавитьСтрокуВТаблицуИзмененныхРеквизитов(Таблица, ПутьКРеквизиту, ПсевдонимРеквизита, ЗначениеРеквизита)
	
	Если НЕ ЗначениеЗаполнено(СокрЛП(ПутьКРеквизиту)) ИЛИ НЕ ЗначениеЗаполнено(ЗначениеРеквизита) Тогда 
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = Таблица.Добавить();
	
	НоваяСтрока.ПутьКРеквизиту     = ПутьКРеквизиту;
	НоваяСтрока.ПсевдонимРеквизита = ПсевдонимРеквизита;
	НоваяСтрока.ЗначениеРеквизита  = ЗначениеРеквизита;
	
КонецПроцедуры
