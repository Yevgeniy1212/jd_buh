
////////////////////////////////////////////////////////////////////////////////
// ОбменСБанкамиПереопределяемый: механизм обмена электронными документами с банками.
//
////////////////////////////////////////////////////////////////////////////////

#Область ПрограммныйИнтерфейс

// Заполняет массив актуальными видами электронных документов для прикладного решения.
//
// Параметры:
//  Массив - Массив из ПеречислениеСсылка.ВидыЭДОбменСБанками  - виды актуальных ЭД
//   Добавлять можно только следующие значения:
//    - Перечисления.ВидыЭДОбменСБанками.ЗапросВыписки
//    - Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение
//    - Перечисления.ВидыЭДОбменСБанками.ПлатежноеТребование
//    - Перечисления.ВидыЭДОбменСБанками.ПоручениеНаПереводВалюты
//    - Перечисления.ВидыЭДОбменСБанками.ПоручениеНаПокупкуВалюты
//    - Перечисления.ВидыЭДОбменСБанками.ПоручениеНаПродажуВалюты
//    - Перечисления.ВидыЭДОбменСБанками.РаспоряжениеНаОбязательнуюПродажуВалюты
//    - Перечисления.ВидыЭДОбменСБанками.СписокНаЗачислениеДенежныхСредствНаСчетаСотрудников
//    - Перечисления.ВидыЭДОбменСБанками.СписокНаОткрытиеСчетовПоЗарплатномуПроекту
//    - Перечисления.ВидыЭДОбменСБанками.СписокУволенныхСотрудников
//    - Перечисления.ВидыЭДОбменСБанками.Письмо
//
Процедура ПолучитьАктуальныеВидыЭД(Массив) Экспорт
	
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ЗапросВыписки);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ВыпискаБанка);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ЗапросОСостоянииЭД);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ИзвещениеОСостоянииЭД);
	Массив.Добавить(Перечисления.ВидыЭДОбменСБанками.ЗапросНаОтзывЭД);
	
КонецПроцедуры

// Используется для получения номеров счетов в виде массив строк
//
// Параметры:
//  Организация - СправочникСсылка.Организации - отбор по организации.
//  Банк - СправочникСсылка.КлассификаторБанков - отбор по банку.
//  МассивНомеровБанковскихСчетов - Массив - Массив возврата, в элементах строки с номерами счетов.
//
Процедура ПолучитьНомераБанковскихСчетов(Организация, Банк, МассивНомеровБанковскихСчетов) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ВЫБОР
	|		КОГДА БанковскиеСчета.Ссылка = Организации.ОсновнойБанковскийСчет
	|			ТОГДА 1
	|		ИНАЧЕ 2
	|	КОНЕЦ КАК Приоритет,
	|	БанковскиеСчета.НомерСчета КАК НомерСчета
	|ИЗ
	|	Справочник.БанковскиеСчета КАК БанковскиеСчета
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО БанковскиеСчета.Владелец = Организации.Ссылка
	|ГДЕ
	|	БанковскиеСчета.Банк = &Банк
	|	И Организации.Ссылка = &Организация
	|	И НЕ БанковскиеСчета.ПометкаУдаления
	|
	|УПОРЯДОЧИТЬ ПО
	|	Приоритет";
	Запрос.УстановитьПараметр("Банк", Банк);
	Запрос.УстановитьПараметр("Организация", Организация);
	ТабРез = Запрос.Выполнить().Выгрузить();
	МассивНомеровБанковскихСчетов = ТабРез.ВыгрузитьКолонку("НомерСчета");
	
КонецПроцедуры

// Определяет параметры электронного документа по типу владельца.
//
// Параметры:
//  Источник - ДокументСсылка, ДокументОбъект - Источник объекта, либо ссылка документа/справочника-источника.
//  ПараметрыЭД - Структура - структура параметров источника, необходимых для определения
//                настроек обмена ЭД. Обязательные параметры: ВидЭД, Банк, Организация.
//
Процедура ЗаполнитьПараметрыЭДПоИсточнику(Источник, ПараметрыЭД) Экспорт
	
	ТипИсточника = ТипЗнч(Источник);
	
	Если ТипИсточника = Тип("ДокументСсылка.ПлатежноеПоручениеИсходящее")
		ИЛИ ТипИсточника = Тип("ДокументОбъект.ПлатежноеПоручениеИсходящее") Тогда
	
		ПараметрыЭД.ВидЭД = Перечисления.ВидыЭДОбменСБанками.ПлатежноеПоручение;
		ПараметрыЭД.Организация = Источник.Организация;
		ПараметрыЭД.Банк = Источник.СчетОрганизации.Банк;
		
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьОтветственныеЛицаОрганизации(Организация, Знач СтруктурноеПодразделениеОтправитель = Неопределено, Дата) Экспорт
	
	ОтветсвенныеЛица = Новый Структура;
	
	СтруктурнаяЕдиницаОрганизация = ОбщегоНазначенияБК.ПолучитьСтруктурнуюЕдиницу(Организация, СтруктурноеПодразделениеОтправитель); //БК3
	ОтветственныеЛица = ОбщегоНазначенияБКВызовСервера.ОтветственныеЛицаОрганизаций(СтруктурнаяЕдиницаОрганизация, Дата,, Истина, Истина);//БК3
	
	//ОтветственныеЛица = ОтветственныеЛицаСервер.ПолучитьОтветственныеЛицаОрганизации(Организация, КонецДня(Дата));//ЕРП
	
	ОтветсвенныеЛица.Вставить("Руководитель", ОтветственныеЛица.Руководитель);
	ОтветсвенныеЛица.Вставить("ГлавныйБухгалтер",ОтветственныеЛица.ГлавныйБухгалтер);
	
	Возврат ОтветсвенныеЛица;

КонецФункции

// Подготавливает данные для электронного документа типа Платежное поручение.
//
// Параметры:
//  МассивСсылок - Массив - содержит ссылки на документы информационной базы, на основании которых будут созданы электронные документы.
//  ДанныеДляЗаполнения - Массив - содержит пустые деревья значений, которые необходимо заполнить данными.
//           Дерево значений повторяет структуру макета ПлатежноеПоручение из обработки ОбменСБанками.
//           Если по какому-либо документу не удалось получить данные, то текст ошибки необходимо поместить вместо дерева значений.
//           ВНИМАНИЕ! Порядок элементов массива ДанныеДляЗаполнения соответствует порядку элементов массива МассивСсылок.
//
Процедура ЗаполнитьДанныеПлатежныхПоручений(МассивСсылок, ДанныеДляЗаполнения) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// ПоддержкаРаботыСоСтруктурнымиПодразделениями
	ПоддержкаРаботыСоСтруктурнымиПодразделениями = ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями");
	Если ПоддержкаРаботыСоСтруктурнымиПодразделениями Тогда
		Если ЗначениеЗаполнено(МассивСсылок[0].Ссылка.СтруктурноеПодразделениеОтправитель) Тогда
			Если МассивСсылок[0].Ссылка.СтруктурноеПодразделениеОтправитель.УказыватьРеквизитыГоловнойОрганизации Тогда
				СтруктурнаяЕдиница = "Организация";
			Иначе
				СтруктурнаяЕдиница = "СтруктурноеПодразделениеОтправитель";
			КонецЕсли;
		Иначе
			СтруктурнаяЕдиница = "Организация";
		КонецЕсли;
	Иначе
		СтруктурнаяЕдиница = "Организация";
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	ФИОФизЛиц.Период КАК Период,
	|	ФИОФизЛиц.ФизЛицо КАК ФизЛицо,
	|	ФИОФизЛиц.Фамилия КАК Фамилия,
	|	ФИОФизЛиц.Имя КАК Имя,
	|	ФИОФизЛиц.Отчество КАК Отчество
	|ПОМЕСТИТЬ ВТ_ФИОФизЛиц
	|ИЗ
	|	РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|ГДЕ
	|	ФИОФизЛиц.ФизЛицо В
	|			(ВЫБРАТЬ РАЗЛИЧНЫЕ
	|				КартСчета.ФизЛицоВладелецСчета
	|			ИЗ
	|				Справочник.КартСчета КАК КартСчета
	|			ГДЕ
	|				КартСчета.НеЯвляетсяВладельцемСчета
	|				И КартСчета.ФизЛицоВладелецСчета <> ЗНАЧЕНИЕ(Справочник.ФизическиеЛица.ПустаяСсылка))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Период
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ФИОФизЛиц1.Период КАК Период,
	|	МИНИМУМ(ЕСТЬNULL(ФИОФизЛиц2.Период, ДАТАВРЕМЯ(2099, 1, 1))) КАК ПериодПо,
	|	ФИОФизЛиц1.ФизЛицо КАК ФизЛицо,
	|	ФИОФизЛиц1.Фамилия КАК Фамилия,
	|	ФИОФизЛиц1.Имя КАК Имя,
	|	ФИОФизЛиц1.Отчество КАК Отчество
	|ПОМЕСТИТЬ ВТ_ФИОФизЛицСПериодомДействия
	|ИЗ
	|	ВТ_ФИОФизЛиц КАК ФИОФизЛиц1
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФИОФизЛиц КАК ФИОФизЛиц2
	|		ПО ФИОФизЛиц1.ФизЛицо = ФИОФизЛиц2.ФизЛицо
	|			И ФИОФизЛиц1.Период < ФИОФизЛиц2.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ФИОФизЛиц1.Период,
	|	ФИОФизЛиц1.ФизЛицо,
	|	ФИОФизЛиц1.Фамилия,
	|	ФИОФизЛиц1.Имя,
	|	ФИОФизЛиц1.Отчество
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	ФизЛицо,
	|	Период,
	|	ПериодПо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ФИОФизЛиц";
	
	Запрос.Выполнить();
	
	Запрос.Текст = 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПлатежноеПоручениеИсходящее.Ссылка КАК Ссылка,
	|	НАЧАЛОПЕРИОДА(ПлатежноеПоручениеИсходящее.ДатаВыписки, ДЕНЬ) КАК Дата,
	|	ГражданствоФизЛиц.ФизЛицо КАК ФизЛицо,
	|	ЕСТЬNULL(МАКСИМУМ(ГражданствоФизЛиц.Период), ДАТАВРЕМЯ(1, 1, 1)) КАК ПериодПоРегиструСведений
	|ПОМЕСТИТЬ ВТ_СрезПоследнихНаКаждуюДату
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее КАК ПлатежноеПоручениеИсходящее
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО (ГражданствоФизЛиц.Период <= НАЧАЛОПЕРИОДА(ПлатежноеПоручениеИсходящее.ДатаВыписки, ДЕНЬ))
	|			И (ГражданствоФизЛиц.ФизЛицо = ПлатежноеПоручениеИсходящее.Организация.ИндивидуальныйПредприниматель)
	|ГДЕ
	|	НЕ ПлатежноеПоручениеИсходящее.ПометкаУдаления
	|	И ПлатежноеПоручениеИсходящее.Ссылка В(&Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ПлатежноеПоручениеИсходящее.Ссылка,
	|	ГражданствоФизЛиц.ФизЛицо,
	|	НАЧАЛОПЕРИОДА(ПлатежноеПоручениеИсходящее.ДатаВыписки, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_СрезПоследнихНаКаждуюДату.Ссылка КАК Ссылка,
	|	ВТ_СрезПоследнихНаКаждуюДату.Дата КАК Дата,
	|	ВТ_СрезПоследнихНаКаждуюДату.ФизЛицо КАК ФизЛицо,
	|	ЕСТЬNULL(ГражданствоФизЛиц.Страна.КодАльфа2, """") КАК КодАльфа2,
	|	ВТ_СрезПоследнихНаКаждуюДату.ПериодПоРегиструСведений КАК ПериодПоРегиструСведений
	|ПОМЕСТИТЬ ВТ_СрезПоследнихГражданствоФизЛиц
	|ИЗ
	|	ВТ_СрезПоследнихНаКаждуюДату КАК ВТ_СрезПоследнихНаКаждуюДату
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО ВТ_СрезПоследнихНаКаждуюДату.ПериодПоРегиструСведений = ГражданствоФизЛиц.Период
	|			И ВТ_СрезПоследнихНаКаждуюДату.ФизЛицо = ГражданствоФизЛиц.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПлатежноеПоручениеИсходящее.ДатаВыписки КАК Дата,
	|	ПлатежноеПоручениеИсходящее.СуммаДокумента КАК Сумма,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеЗаработнойПлаты)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭДОбменСБанками.ПлатежноеПоручениеНаПеречислениеЗаработнойПлаты)
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеПенсионныхВзносов)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭДОбменСБанками.ПлатежноеПоручениеНаПеречислениеПенсионныхВзносов)
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеСоциальныхОтчислений)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭДОбменСБанками.ПлатежноеПоручениеНаПеречислениеСоциальныхОтчислений)
	|		ИНАЧЕ ЗНАЧЕНИЕ(Перечисление.ВидыОперацийЭДОбменСБанками.ПлатежноеПоручение)
	|	КОНЕЦ КАК ВидЭД,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеЗаработнойПлаты)
	|			ТОГДА ""41""
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеПенсионныхВзносов)
	|			ТОГДА ""42""
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеСоциальныхОтчислений)
	|			ТОГДА ""43""
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеПоИсполнительнымЛистам)
	|			ТОГДА ""18""
	|		ИНАЧЕ ""10""
	|	КОНЕЦ КАК ВидОплаты,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручениеИсходящее.ВидОперации В (&ОперацииПеречисленияВБюджет)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК ПеречислениеВБюджет,
	|	ПлатежноеПоручениеИсходящее.Ссылка КАК Ссылка,
	|	ПлатежноеПоручениеИсходящее.Номер КАК Номер,
	|	ПлатежноеПоручениеИсходящее.НазначениеПлатежа КАК НазначениеПлатежа,
	|	ПлатежноеПоручениеИсходящее.СуммаДокумента КАК СуммаДокумента,
	|	ПлатежноеПоручениеИсходящее.ФактическийПлательщик КАК ФактическийПлательщик,
	|	ВЫБОР
	|		КОГДА ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.ТекстПлательщика КАК СТРОКА(1000)) = """" ТОГДА
	|			ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее." + СтруктурнаяЕдиница + ".НаименованиеПолное КАК СТРОКА(1000))
	|		ИНАЧЕ
	|			ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.ТекстПлательщика КАК СТРОКА(1000))
	|	КОНЕЦ КАК ТекстПлательщика,
	|	ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.ТекстПолучателя КАК СТРОКА(1000)) КАК ТекстПолучателя,
	|	ПлатежноеПоручениеИсходящее." + СтруктурнаяЕдиница + ".ИдентификационныйНомер КАК ПлательщикБИН_ИНН,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручениеИсходящее.Организация.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ВЫБОР
	|					КОГДА ПлатежноеПоручениеИсходящее.Организация.ИностраннаяОрганизация = ЛОЖЬ
	|						ТОГДА ""KZ""
	|					ИНАЧЕ ЕСТЬNULL(ПлатежноеПоручениеИсходящее.Организация.СтранаРегистрации.КодАльфа2, """")
	|				КОНЕЦ
	|		ИНАЧЕ ЕСТЬNULL(ГражданствоФизЛиц.КодАльфа2, """")
	|	КОНЕЦ КАК ПлательщикКодСтраныРегистрации,
	|	ПлатежноеПоручениеИсходящее.Организация.КБЕ КАК ПлательщикКБЕ,
	|	ПлатежноеПоручениеИсходящее.СчетОрганизации КАК СчетОрганизации,
	|	ПлатежноеПоручениеИсходящее.СчетОрганизации.НомерСчета КАК НомерСчетаОрганизации,
	|	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк.БИК КАК ПлательщикБанкБИК,
	|	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк.Наименование КАК ПлательщикБанкНаименование,
	|	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк.Город КАК ПлательщикБанкГород,
	|	ПлатежноеПоручениеИсходящее.СчетОрганизации.Банк.КоррСчет КАК ПлательщикБанкИИК,
	|	ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.СчетОрганизации.ТекстКорреспондента КАК СТРОКА(1000)) КАК ОрганизацияТекстКорреспондента,
	|	ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.СчетОрганизации.ТекстНазначения КАК СТРОКА(1000)) КАК ОрганизацияТекстНазначения,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручениеИсходящее.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ЮрЛицо)
	|			ТОГДА ""ЮрЛицо""
	|		ИНАЧЕ ""ФизЛицо""
	|	КОНЕЦ КАК ТипКонтрагента,
	|	ПлатежноеПоручениеИсходящее.Контрагент КАК ЭлементКонтрагент,
	|	ПлатежноеПоручениеИсходящее.Контрагент КАК Контрагент,
	|	ПлатежноеПоручениеИсходящее.Контрагент.ИдентификационныйКодЛичности КАК КонтрагентБИН_ИНН,
	|	ЕСТЬNULL(ПлатежноеПоручениеИсходящее.Контрагент.СтранаРезидентства.КодАльфа2, """") КАК ПолучательКодСтраныРегистрации,
	|	ВЫБОР
	|		КОГДА ПлатежноеПоручениеИсходящее.Контрагент.ЮрФизЛицо = ЗНАЧЕНИЕ(Перечисление.ЮрФизЛицо.ФизЛицо)
	|					И ПлатежноеПоручениеИсходящее.Контрагент.КБЕ = """"
	|			ТОГДА ВЫБОР
	|					КОГДА ПлатежноеПоручениеИсходящее.Контрагент.СтранаРезидентства.КодАльфа2 <> ""KZ""
	|						ТОГДА ""29""
	|					ИНАЧЕ ""19""
	|				КОНЕЦ
	|		ИНАЧЕ ПлатежноеПоручениеИсходящее.Контрагент.КБЕ
	|	КОНЕЦ КАК КонтрагентКБЕ,
	|	ПлатежноеПоручениеИсходящее.СчетКонтрагента КАК СчетКонтрагента,
	|	ПлатежноеПоручениеИсходящее.СчетКонтрагента.НомерСчета КАК НомерСчетаКонтрагента,
	|	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк.Наименование КАК ПолучательБанкНаименование,
	|	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк.Город КАК ПолучательБанкГород,
	|	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк.БИК КАК ПолучательБанкБИК,
	|	ПлатежноеПоручениеИсходящее.СчетКонтрагента.Банк.КоррСчет КАК ПолучательБанкИИК,
	|	ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.СчетКонтрагента.ТекстКорреспондента КАК СТРОКА(1000)) КАК КонтрагентТекстКорреспондента,
	|	ВЫРАЗИТЬ(ПлатежноеПоручениеИсходящее.СчетКонтрагента.ТекстНазначения КАК СТРОКА(1000)) КАК КонтрагентТекстНазначения,
	|	ПлатежноеПоручениеИсходящее.СчетБанкаПосредника.Банк.КоррСчет КАК БанкПосредникИИК,
	|	ПлатежноеПоручениеИсходящее.СчетБанкаПосредника.Банк.БИК КАК БанкПосредникБИК,
	|	ПлатежноеПоручениеИсходящее.СчетБанкаПосредника.Банк.Наименование КАК БанкПосредникБанкНаименование,
	|	ПлатежноеПоручениеИсходящее.СчетБанкаПосредника.НомерСчета КАК БанкПосредникНомерСчета,
	|	ПлатежноеПоручениеИсходящее.БанкПосредник.Наименование КАК БанкПосредникНаименование,
	|	ПлатежноеПоручениеИсходящее.ДатаПолученияТовара КАК ДатаПолученияТовара,
	|	ПлатежноеПоручениеИсходящее.КодНазначенияПлатежа КАК КодНазначенияПлатежа,
	|	ПлатежноеПоручениеИсходящее.КодБК КАК КодБК,
	|	ПлатежноеПоручениеИсходящее.ДатаВалютирования КАК ДатаВалютирования,
	|	ПлатежноеПоручениеИсходящее.ВидОперации КАК ВидОперации,
	|	ПлатежноеПоручениеИсходящее.ВалютаДокумента КАК ВалютаДокумента, 
	|	ПлатежноеПоручениеИсходящее.СтруктурноеПодразделениеОтправитель КАК СтруктурноеПодразделениеОтправитель,
	|	ПлатежноеПоручениеИсходящее.Организация КАК Организация
	|
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее КАК ПлатежноеПоручениеИсходящее
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_СрезПоследнихГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО (ГражданствоФизЛиц.ФизЛицо = ПлатежноеПоручениеИсходящее.Организация.ИндивидуальныйПредприниматель)
	|			И (ГражданствоФизЛиц.Ссылка = ПлатежноеПоручениеИсходящее.Ссылка)
	|ГДЕ
	|	ПлатежноеПоручениеИсходящее.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА ОПВПеречислениеВФондыПенсионныеВзносы.ФизЛицо ССЫЛКА Справочник.Контрагенты
	|			ТОГДА ОПВПеречислениеВФондыПенсионныеВзносы.ФизЛицо.ФизЛицо
	|		ИНАЧЕ ОПВПеречислениеВФондыПенсионныеВзносы.ФизЛицо
	|	КОНЕЦ КАК ФизЛицо,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОПВПеречислениеВФонды.ПеречислениеОбязательныхПенсионныхВзносов)
	|			ТОГДА ""OPVC""
	|		КОГДА ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийОПВПеречислениеВФонды.ПеречислениеОбязательныхПенсионныхВзносовРаботодателя)
	|			ТОГДА ""OPVE""
	|		ИНАЧЕ ""OPVP""
	|	КОНЕЦ КАК ВидНалога,
	|	ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов.Ссылка КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ОПВПеречислениеВФондыПенсионныеВзносы
	|ИЗ
	|	Документ.ОПВПеречислениеВФонды.ПенсионныеВзносы КАК ОПВПеречислениеВФондыПенсионныеВзносы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручениеИсходящее.ПеречислениеПенсионныхВзносов КАК ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов
	|		ПО ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка = ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов.Документ
	|ГДЕ
	|	ПлатежноеПоручениеИсходящееПеречислениеПенсионныхВзносов.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ФИОФизЛиц.Период) КАК Период,
	|	ФИОФизЛиц.ФизЛицо КАК ФизЛицо,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка КАК док,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПериодыФИОПенсионныеВзносы
	|ИЗ
	|	ВТ_ОПВПеречислениеВФондыПенсионныеВзносы КАК ОПВПеречислениеВФондыПенсионныеВзносы
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО ОПВПеречислениеВФондыПенсионныеВзносы.ФизЛицо = ФИОФизЛиц.ФизЛицо
	|			И ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка.Дата >= ФИОФизЛиц.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ФИОФизЛиц.ФизЛицо,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.ПлатежныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодыФИО.док КАК док,
	|	ВТ_ПериодыФИО.ФизЛицо КАК ФизЛицо,
	|	ФИОФизЛиц.Фамилия КАК Фамилия,
	|	ФИОФизЛиц.Имя КАК Имя,
	|	ФИОФизЛиц.Отчество КАК Отчество,
	|	ЕСТЬNULL(ГражданствоФизЛиц.Страна.КодАльфа2, """") КАК КодАльфа2,
	|	ВТ_ПериодыФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ФИОПенсионныеВзносы
	|ИЗ
	|	ВТ_ПериодыФИОПенсионныеВзносы КАК ВТ_ПериодыФИО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ФИОФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ФИОФизЛиц.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ГражданствоФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ГражданствоФизЛиц.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ФИО.ФизЛицо КАК ФизЛицо,
	|	ВТ_ФИО.Фамилия КАК Фамилия,
	|	ВТ_ФИО.Имя КАК Имя,
	|	ВТ_ФИО.Отчество КАК Отчество,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.ФизЛицо.ИдентификационныйКодЛичности КАК ФизЛицоИдентификационныйКодЛичности,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.ФизЛицо.ДатаРождения КАК ФизЛицоДатаРождения,
	|	ВТ_ФИО.КодАльфа2 КАК КодСтраныРегистрации,
	|	"""" КАК РасчСчет,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
	|	ОПВПеречислениеВФондыПенсионныеВзносы.Сумма КАК Сумма, 
	|	ОПВПеречислениеВФондыПенсионныеВзносы.ВидНалога КАК ВидНалога,
	|	ВТ_ФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПеречислениеВФондыПенсионныеВзносы
	|ИЗ
	|	ВТ_ОПВПеречислениеВФондыПенсионныеВзносы КАК ОПВПеречислениеВФондыПенсионныеВзносы
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ФИОПенсионныеВзносы КАК ВТ_ФИО
	|		ПО ОПВПеречислениеВФондыПенсионныеВзносы.Ссылка = ВТ_ФИО.док
	|			И ОПВПеречислениеВФондыПенсионныеВзносы.ФизЛицо = ВТ_ФИО.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ФИОФизЛиц.Период) КАК Период,
	|	ФИОФизЛиц.ФизЛицо КАК ФизЛицо,
	|	ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка КАК док,
	|	ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПериодыФИОПодотчет
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее.ПеречислениеВПодотчет КАК ПлатежноеПоручениеИсходящееПеречислениеВПодотчет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.ФизЛицо = ФИОФизЛиц.ФизЛицо
	|			И ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка.Дата >= ФИОФизЛиц.Период
	|ГДЕ
	|	ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка В(&Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ФИОФизЛиц.ФизЛицо,
	|	ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка,
	|	ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодыФИО.док КАК док,
	|	ВТ_ПериодыФИО.ФизЛицо КАК ФизЛицо,
	|	ФИОФизЛиц.Фамилия КАК Фамилия,
	|	ФИОФизЛиц.Имя КАК Имя,
	|	ФИОФизЛиц.Отчество КАК Отчество,
	|	ЕСТЬNULL(ГражданствоФизЛиц.Страна.КодАльфа2, """") КАК КодАльфа2,
	|	ВТ_ПериодыФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ФИОПодотчет
	|ИЗ
	|	ВТ_ПериодыФИОПодотчет КАК ВТ_ПериодыФИО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ФИОФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ФИОФизЛиц.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ГражданствоФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ГражданствоФизЛиц.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.ФизЛицо, ВТ_ФИО.ФизЛицо) КАК ФизЛицо,
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.Фамилия, ВТ_ФИО.Фамилия) КАК Фамилия,
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.Имя, ВТ_ФИО.Имя) КАК Имя,
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.Отчество, ВТ_ФИО.Отчество) КАК Отчество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КартСчета.НеЯвляетсяВладельцемСчета, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ВладелецСчета.ИдентификационныйКодЛичности, """")
	|		ИНАЧЕ ЕСТЬNULL(ФизическиеЛица.ИдентификационныйКодЛичности, """")
	|	КОНЕЦ КАК ИИН,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КартСчета.НеЯвляетсяВладельцемСчета, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ВладелецСчета.ДатаРождения, ДАТАВРЕМЯ(1, 1, 1))
	|		ИНАЧЕ ЕСТЬNULL(ФизическиеЛица.ДатаРождения, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ДатаРождения,
	|	ВТ_ФИО.КодАльфа2 КАК КодСтраныРегистрации,
	|	ЕСТЬNULL(КартСчета.НомерСчета, """") КАК РасчСчет,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодПлатежа,
	|	ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.СуммаПлатежа КАК Сумма,
	|	ВТ_ФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПеречислениеВПодотчет
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее.ПеречислениеВПодотчет КАК ПлатежноеПоручениеИсходящееПеречислениеВПодотчет
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручениеИсходящее КАК ПлатежноеПоручениеИсходящее
	|		ПО ПлатежноеПоручениеИсходящее.Ссылка = ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ФИОПодотчет КАК ВТ_ФИО
	|		ПО ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.Ссылка = ВТ_ФИО.док
	|			И ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.ФизЛицо = ВТ_ФИО.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КартСчета КАК КартСчета
	|		ПО КартСчета.Ссылка = ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.НомерКартСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ВладелецСчета
	|		ПО ВладелецСчета.Ссылка = КартСчета.ФизЛицоВладелецСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ФизическиеЛица.Ссылка = ПлатежноеПоручениеИсходящееПеречислениеВПодотчет.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФИОФизЛицСПериодомДействия КАК ФИОФизЛицСПериодомДействия
	|		ПО КартСчета.НеЯвляетсяВладельцемСчета = ИСТИНА
	|			И КартСчета.ФизЛицоВладелецСчета = ФИОФизЛицСПериодомДействия.ФизЛицо
	|			И ПлатежноеПоручениеИсходящее.Дата >= ФИОФизЛицСПериодомДействия.Период
	|			И ПлатежноеПоручениеИсходящее.Дата < ФИОФизЛицСПериодомДействия.ПериодПо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ФИОФизЛиц.Период) КАК Период,
	|	ФИОФизЛиц.ФизЛицо КАК ФизЛицо,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка КАК док,
	|	ПлатежноеПоручениеИсходящееПеречислениеЗаработнойПлаты.Ссылка КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПериодыФИОЗарплата
	|ИЗ
	|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацийЗарплата
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручениеИсходящее.ПеречислениеЗаработнойПлаты КАК ПлатежноеПоручениеИсходящееПеречислениеЗаработнойПлаты
	|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = ПлатежноеПоручениеИсходящееПеречислениеЗаработнойПлаты.Ведомость
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = ФИОФизЛиц.ФизЛицо
	|			И ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка.Дата >= ФИОФизЛиц.Период
	|ГДЕ
	|	ПлатежноеПоручениеИсходящееПеречислениеЗаработнойПлаты.Ссылка В(&Ссылка)
	|
	|СГРУППИРОВАТЬ ПО
	|	ФИОФизЛиц.ФизЛицо,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка,
	|	ПлатежноеПоручениеИсходящееПеречислениеЗаработнойПлаты.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодыФИО.док КАК док,
	|	ВТ_ПериодыФИО.ФизЛицо КАК ФизЛицо,
	|	ФИОФизЛиц.Фамилия КАК Фамилия,
	|	ФИОФизЛиц.Имя КАК Имя,
	|	ФИОФизЛиц.Отчество КАК Отчество,
	|	ЕСТЬNULL(ГражданствоФизЛиц.Страна.КодАльфа2, """") КАК КодАльфа2,
	|	ВТ_ПериодыФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ФИОЗарплата
	|ИЗ
	|	ВТ_ПериодыФИОЗарплата КАК ВТ_ПериодыФИО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ФИОФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ФИОФизЛиц.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ГражданствоФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ГражданствоФизЛиц.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.ФизЛицо, ВТ_ФИО.ФизЛицо) КАК ФизЛицо,
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.Фамилия, ВТ_ФИО.Фамилия) КАК Фамилия,
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.Имя, ВТ_ФИО.Имя) КАК Имя,
	|	ЕСТЬNULL(ФИОФизЛицСПериодомДействия.Отчество, ВТ_ФИО.Отчество) КАК Отчество,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КартСчета.НеЯвляетсяВладельцемСчета, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ВладелецСчета.ИдентификационныйКодЛичности, """")
	|		ИНАЧЕ ЕСТЬNULL(ФизическиеЛица.ИдентификационныйКодЛичности, """")
	|	КОНЕЦ КАК ИИН,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(КартСчета.НеЯвляетсяВладельцемСчета, ЛОЖЬ) = ИСТИНА
	|			ТОГДА ЕСТЬNULL(ВладелецСчета.ДатаРождения, ДАТАВРЕМЯ(1, 1, 1))
	|		ИНАЧЕ ЕСТЬNULL(ФизическиеЛица.ДатаРождения, ДАТАВРЕМЯ(1, 1, 1))
	|	КОНЕЦ КАК ФизлицоДатаРождения,
	|	ВТ_ФИО.КодАльфа2 КАК КодСтраныРегистрации,
	|	ЕСТЬNULL(КартСчета.НомерСчета, """") КАК НомерКартСчетаНомерСчета,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ПериодПлатежа,
	|	ЗарплатаКВыплатеОрганизацийЗарплата.СуммаКВыплате КАК СуммаКВыплате,
	|	ВТ_ФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПеречислениеЗарплата
	|ИЗ
	|	Документ.ЗарплатаКВыплатеОрганизаций.Зарплата КАК ЗарплатаКВыплатеОрганизацийЗарплата
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ЗарплатаКВыплатеОрганизаций КАК ЗарплатаКВыплатеОрганизаций
	|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = ЗарплатаКВыплатеОрганизаций.Ссылка
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ФИОЗарплата КАК ВТ_ФИО
	|		ПО ЗарплатаКВыплатеОрганизацийЗарплата.Ссылка = ВТ_ФИО.док
	|			И ЗарплатаКВыплатеОрганизацийЗарплата.Физлицо = ВТ_ФИО.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.КартСчета КАК КартСчета
	|		ПО КартСчета.Ссылка = ЗарплатаКВыплатеОрганизацийЗарплата.НомерКартСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ВладелецСчета
	|		ПО ВладелецСчета.Ссылка = КартСчета.ФизЛицоВладелецСчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ФизическиеЛица КАК ФизическиеЛица
	|		ПО ФизическиеЛица.Ссылка = ЗарплатаКВыплатеОрганизацийЗарплата.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ФИОФизЛицСПериодомДействия КАК ФИОФизЛицСПериодомДействия
	|		ПО КартСчета.НеЯвляетсяВладельцемСчета = ИСТИНА
	|			И КартСчета.ФизЛицоВладелецСчета = ФИОФизЛицСПериодомДействия.ФизЛицо
	|			И ЗарплатаКВыплатеОрганизаций.Дата >= ФИОФизЛицСПериодомДействия.Период
	|			И ЗарплатаКВыплатеОрганизаций.Дата < ФИОФизЛицСПериодомДействия.ПериодПо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	СОПеречислениеВФондыСоциальныеОтчисления.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА СОПеречислениеВФондыСоциальныеОтчисления.ФизЛицо ССЫЛКА Справочник.Контрагенты
	|			ТОГДА СОПеречислениеВФондыСоциальныеОтчисления.ФизЛицо.ФизЛицо
	|		ИНАЧЕ СОПеречислениеВФондыСоциальныеОтчисления.ФизЛицо
	|	КОНЕЦ КАК ФизЛицо,
	|	ВЫБОР
	|		КОГДА СОПеречислениеВФондыСоциальныеОтчисления.Ссылка.ВидОперации В (ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСОПеречислениеВФонды.ПеречислениеВзносовОСМС), ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСОПеречислениеВФонды.ПеречислениеОтчисленийОСМС))
	|			ТОГДА ВЫБОР
	|					КОГДА СОПеречислениеВФондыСоциальныеОтчисления.МесяцПериода = ДАТАВРЕМЯ(1, 1, 1)
	|						ТОГДА НАЧАЛОПЕРИОДА(СОПеречислениеВФондыСоциальныеОтчисления.Ссылка.ПериодРегистрации, МЕСЯЦ)
	|					ИНАЧЕ НАЧАЛОПЕРИОДА(СОПеречислениеВФондыСоциальныеОтчисления.МесяцПериода, МЕСЯЦ)
	|				КОНЕЦ
	|		ИНАЧЕ НАЧАЛОПЕРИОДА(СОПеречислениеВФондыСоциальныеОтчисления.Ссылка.ПериодРегистрации, МЕСЯЦ)
	|	КОНЕЦ КАК ПериодРегистрации,
	|	СОПеречислениеВФондыСоциальныеОтчисления.Сумма КАК Сумма,
	|	ВЫБОР
	|		КОГДА СОПеречислениеВФондыСоциальныеОтчисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСОПеречислениеВФонды.ПеречислениеОбязательныхСоциальныхОтчислений)
	|			ТОГДА ""OPVS""
	|		КОГДА СОПеречислениеВФондыСоциальныеОтчисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСОПеречислениеВФонды.ПеречислениеВзносовОСМС)
	|			ТОГДА ""OPVM""
	|		КОГДА СОПеречислениеВФондыСоциальныеОтчисления.Ссылка.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийСОПеречислениеВФонды.ПеречислениеОтчисленийОСМС)
	|			ТОГДА ""OPVR""
	|	КОНЕЦ КАК ВидНалога,
	|	ПлатежноеПоручениеИсходящееПеречислениеСоциальныхОтчислений.Ссылка КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_СОПеречислениеВФондыСоциальныеОтчисления
	|ИЗ
	|	Документ.СОПеречислениеВФонды.СоциальныеОтчисления КАК СОПеречислениеВФондыСоциальныеОтчисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручениеИсходящее.ПеречислениеСоциальныхОтчислений КАК ПлатежноеПоручениеИсходящееПеречислениеСоциальныхОтчислений
	|		ПО СОПеречислениеВФондыСоциальныеОтчисления.Ссылка = ПлатежноеПоручениеИсходящееПеречислениеСоциальныхОтчислений.Документ
	|ГДЕ
	|	ПлатежноеПоручениеИсходящееПеречислениеСоциальныхОтчислений.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(ФИОФизЛиц.Период) КАК Период,
	|	ФИОФизЛиц.ФизЛицо КАК ФизЛицо,
	|	СОПеречислениеВФондыСоциальныеОтчисления.Ссылка КАК док,
	|	СОПеречислениеВФондыСоциальныеОтчисления.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПериодыФИОСоциальныеОтчисления
	|ИЗ
	|	ВТ_СОПеречислениеВФондыСоциальныеОтчисления КАК СОПеречислениеВФондыСоциальныеОтчисления
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО СОПеречислениеВФондыСоциальныеОтчисления.ФизЛицо = ФИОФизЛиц.ФизЛицо
	|			И СОПеречислениеВФондыСоциальныеОтчисления.Ссылка.Дата >= ФИОФизЛиц.Период
	|
	|СГРУППИРОВАТЬ ПО
	|	ФИОФизЛиц.ФизЛицо,
	|	СОПеречислениеВФондыСоциальныеОтчисления.Ссылка,
	|	СОПеречислениеВФондыСоциальныеОтчисления.ПлатежныйДокумент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПериодыФИО.док КАК док,
	|	ВТ_ПериодыФИО.ФизЛицо КАК ФизЛицо,
	|	ФИОФизЛиц.Фамилия КАК Фамилия,
	|	ФИОФизЛиц.Имя КАК Имя,
	|	ФИОФизЛиц.Отчество КАК Отчество,
	|	ЕСТЬNULL(ГражданствоФизЛиц.Страна.КодАльфа2, """") КАК КодАльфа2,
	|	ВТ_ПериодыФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ФИОСоциальныеОтчисления
	|ИЗ
	|	ВТ_ПериодыФИОСоциальныеОтчисления КАК ВТ_ПериодыФИО
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.ФИОФизЛиц КАК ФИОФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ФИОФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ФИОФизЛиц.ФизЛицо
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ГражданствоФизЛиц КАК ГражданствоФизЛиц
	|		ПО ВТ_ПериодыФИО.Период = ГражданствоФизЛиц.Период
	|			И ВТ_ПериодыФИО.ФизЛицо = ГражданствоФизЛиц.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ФИО.ФизЛицо КАК ФизЛицо,
	|	ВТ_ФИО.Фамилия КАК Фамилия,
	|	ВТ_ФИО.Имя КАК Имя,
	|	ВТ_ФИО.Отчество КАК Отчество,
	|	СОПеречислениеВФондыСоциальныеОтчисления.ФизЛицо.ИдентификационныйКодЛичности КАК ФизЛицоИдентификационныйКодЛичности,
	|	СОПеречислениеВФондыСоциальныеОтчисления.ФизЛицо.ДатаРождения КАК ФизЛицоДатаРождения,
	|	ВТ_ФИО.КодАльфа2 КАК КодСтраныРегистрации,
	|	"""" КАК РасчСчет,
	|	СОПеречислениеВФондыСоциальныеОтчисления.ПериодРегистрации КАК ПериодРегистрации,
	|	СОПеречислениеВФондыСоциальныеОтчисления.Сумма КАК Сумма,
	|	СОПеречислениеВФондыСоциальныеОтчисления.ВидНалога КАК ВидНалога,
	|	ВТ_ФИО.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПеречислениеСоциальныеОтчисления
	|ИЗ
	|	ВТ_СОПеречислениеВФондыСоциальныеОтчисления КАК СОПеречислениеВФондыСоциальныеОтчисления
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ФИОСоциальныеОтчисления КАК ВТ_ФИО
	|		ПО СОПеречислениеВФондыСоциальныеОтчисления.Ссылка = ВТ_ФИО.док
	|			И СОПеречислениеВФондыСоциальныеОтчисления.ФизЛицо = ВТ_ФИО.ФизЛицо
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.Ссылка КАК Ссылка,
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.Ссылка.ПериодРегистрации КАК ПериодРегистрации,
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.СуммаПлатежа КАК Сумма,
	|	ПлатежноеПоручениеИсходящееПеречислениеПоИсполнительнымЛистам.Ссылка КАК ПлатежныйДокумент,
	|	ИсполнительныйЛист.Физлицо КАК ФактическийПлательщик,
	|	ИсполнительныйЛист.Физлицо.ИдентификационныйКодЛичности КАК ФактическийПлательщикИНН
	|ПОМЕСТИТЬ ВТ_ИЛПеречислениеПолучателямИсполнительныеЛисты
	|ИЗ
	|	Документ.ИЛПеречислениеПолучателям.ИсполнительныеЛисты КАК ИЛПеречислениеПолучателямИсполнительныеЛисты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ПлатежноеПоручениеИсходящее.ПеречислениеПоИсполнительнымЛистам КАК ПлатежноеПоручениеИсходящееПеречислениеПоИсполнительнымЛистам
	|		ПО ИЛПеречислениеПолучателямИсполнительныеЛисты.Ссылка = ПлатежноеПоручениеИсходящееПеречислениеПоИсполнительнымЛистам.Документ
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.ИсполнительныйЛист КАК ИсполнительныйЛист
	|		ПО (ИЛПеречислениеПолучателямИсполнительныеЛисты.ДокументОснование.Ссылка = ИсполнительныйЛист.Ссылка)
	|ГДЕ
	|	ПлатежноеПоручениеИсходящееПеречислениеПоИсполнительнымЛистам.Ссылка В(&Ссылка)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.ФактическийПлательщик КАК ФактическийПлательщик,
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.ФактическийПлательщикИНН КАК ФактическийПлательщикИНН,
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.ПериодРегистрации КАК ПериодРегистрации,
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.Сумма КАК Сумма,
	|	ИЛПеречислениеПолучателямИсполнительныеЛисты.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ПОМЕСТИТЬ ВТ_ПеречислениеПолучателямИсполнительныеЛисты
	|ИЗ
	|	ВТ_ИЛПеречислениеПолучателямИсполнительныеЛисты КАК ИЛПеречислениеПолучателямИсполнительныеЛисты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.ФизЛицо КАК ФизЛицо,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.Фамилия КАК Фамилия,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.Имя КАК Имя,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.Отчество КАК Отчество,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.ФизЛицоИдентификационныйКодЛичности КАК ИНН,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.ФизЛицоДатаРождения КАК ДатаРождения,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.КодСтраныРегистрации КАК КодСтраныРегистрации,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.РасчСчет КАК РасчСчет,
	|	"""" КАК ФактическийПлательщик,
	|	"""" КАК ФактическийПлательщикИНН,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.ПериодРегистрации КАК ПериодПлатежа,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.Сумма КАК Сумма,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.ВидНалога КАК ВидНалога,
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы.ПлатежныйДокумент КАК ПлатежныйДокумент
	|ИЗ
	|	ВТ_ПеречислениеВФондыПенсионныеВзносы КАК ВТ_ПеречислениеВФондыПенсионныеВзносы
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПеречислениеВПодотчет.ФизЛицо,
	|	ВТ_ПеречислениеВПодотчет.Фамилия,
	|	ВТ_ПеречислениеВПодотчет.Имя,
	|	ВТ_ПеречислениеВПодотчет.Отчество,
	|	ВТ_ПеречислениеВПодотчет.ИИН,
	|	ВТ_ПеречислениеВПодотчет.ДатаРождения,
	|	ВТ_ПеречислениеВПодотчет.КодСтраныРегистрации,
	|	ВТ_ПеречислениеВПодотчет.РасчСчет,
	|	"""" КАК ФактическийПлательщик,
	|	"""" КАК ФактическийПлательщикИНН,
	|	ВТ_ПеречислениеВПодотчет.ПериодПлатежа,
	|	ВТ_ПеречислениеВПодотчет.Сумма,
	|	"""",
	|	ВТ_ПеречислениеВПодотчет.ПлатежныйДокумент
	|ИЗ
	|	ВТ_ПеречислениеВПодотчет КАК ВТ_ПеречислениеВПодотчет
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПеречислениеЗарплата.ФизЛицо,
	|	ВТ_ПеречислениеЗарплата.Фамилия,
	|	ВТ_ПеречислениеЗарплата.Имя,
	|	ВТ_ПеречислениеЗарплата.Отчество,
	|	ВТ_ПеречислениеЗарплата.ИИН,
	|	ВТ_ПеречислениеЗарплата.ФизлицоДатаРождения,
	|	ВТ_ПеречислениеЗарплата.КодСтраныРегистрации,
	|	ВТ_ПеречислениеЗарплата.НомерКартСчетаНомерСчета,
	|	"""" КАК ФактическийПлательщик,
	|	"""" КАК ФактическийПлательщикИНН,
	|	ВТ_ПеречислениеЗарплата.ПериодПлатежа,
	|	ВТ_ПеречислениеЗарплата.СуммаКВыплате,
	|	"""",
	|	ВТ_ПеречислениеЗарплата.ПлатежныйДокумент
	|ИЗ
	|	ВТ_ПеречислениеЗарплата КАК ВТ_ПеречислениеЗарплата
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	ВТ_ПеречислениеСоциальныеОтчисления.ФизЛицо,
	|	ВТ_ПеречислениеСоциальныеОтчисления.Фамилия,
	|	ВТ_ПеречислениеСоциальныеОтчисления.Имя,
	|	ВТ_ПеречислениеСоциальныеОтчисления.Отчество,
	|	ВТ_ПеречислениеСоциальныеОтчисления.ФизЛицоИдентификационныйКодЛичности,
	|	ВТ_ПеречислениеСоциальныеОтчисления.ФизЛицоДатаРождения,
	|	ВТ_ПеречислениеСоциальныеОтчисления.КодСтраныРегистрации,
	|	ВТ_ПеречислениеСоциальныеОтчисления.РасчСчет,
	|	"""",
	|	"""",
	|	ВТ_ПеречислениеСоциальныеОтчисления.ПериодРегистрации,
	|	ВТ_ПеречислениеСоциальныеОтчисления.Сумма,
	|	ВТ_ПеречислениеСоциальныеОтчисления.ВидНалога,
	|	ВТ_ПеречислениеСоциальныеОтчисления.ПлатежныйДокумент
	|ИЗ
	|	ВТ_ПеречислениеСоциальныеОтчисления КАК ВТ_ПеречислениеСоциальныеОтчисления
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ
	|	"""",
	|	"""",
	|	"""",
	|	"""",
	|	""000000000000"",
	|	"""",
	|	"""",
	|	"""",
	|	ВТ_ПеречислениеПолучателямИсполнительныеЛисты.ФактическийПлательщик,
	|	ВТ_ПеречислениеПолучателямИсполнительныеЛисты.ФактическийПлательщикИНН,
	|	ВТ_ПеречислениеПолучателямИсполнительныеЛисты.ПериодРегистрации,
	|	ВТ_ПеречислениеПолучателямИсполнительныеЛисты.Сумма,
	|	"""",
	|	ВТ_ПеречислениеПолучателямИсполнительныеЛисты.ПлатежныйДокумент
	|ИЗ
	|	ВТ_ПеречислениеПолучателямИсполнительныеЛисты КАК ВТ_ПеречислениеПолучателямИсполнительныеЛисты";
	
	МассивОперацийПеречисленияВБюджет = Новый Массив;
	МассивОперацийПеречисленияВБюджет.Добавить(Перечисления.ВидыОперацийППИсходящее.ПеречислениеНалога);
	МассивОперацийПеречисленияВБюджет.Добавить(Перечисления.ВидыОперацийППИсходящее.ПеречислениеНДССИзмененнымСрокомУплаты);
	
	Запрос.УстановитьПараметр("Ссылка", МассивСсылок);
	Запрос.УстановитьПараметр("ОперацииПеречисленияВБюджет", МассивОперацийПеречисленияВБюджет);

	РезультатыЗапроса = Запрос.ВыполнитьПакет();
	
	ТаблицаПлатежноеПоручение = РезультатыЗапроса[2].Выгрузить(); //0
	ТаблицаСписокСотрудников = РезультатыЗапроса[19].Выгрузить(); //15//17//21
	
	Для каждого ПлатежноеПоручение Из ТаблицаПлатежноеПоручение Цикл
		
		// Порядок документов в МассивСсылок может не соответствовать порядку документов в ТаблицаДокументов,
		// поэтому из массива деревьев ДанныеДляЗаполнения данные выбираем по индексу
		ДеревоДокумента = ДанныеДляЗаполнения[МассивСсылок.Найти(ПлатежноеПоручение.Ссылка)];
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Дата", ПлатежноеПоручение.Дата);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Сумма", ПлатежноеПоручение.Сумма);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Организация", ПлатежноеПоручение.Организация);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "СтруктурноеПодразделениеОтправитель", ПлатежноеПоручение.СтруктурноеПодразделениеОтправитель);
		ПечатныйНомерДокумента = "";
		ЭлектронноеВзаимодействиеПереопределяемый.ПолучитьПечатныйНомерДокумента(ПлатежноеПоручение.Ссылка, ПечатныйНомерДокумента);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Номер", ПечатныйНомерДокумента);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.ВидОплаты", ПлатежноеПоручение.ВидОплаты);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.НазначениеПлатежа", ПлатежноеПоручение.НазначениеПлатежа);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.ВалютаДокумента", ПлатежноеПоручение.ВалютаДокумента);
		
		// Плательщик.
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.Наименование", ПлатежноеПоручение.ТекстПлательщика);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.ИНН", ПлатежноеПоручение.ПлательщикБИН_ИНН);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.КБЕ", ПлатежноеПоручение.ПлательщикКБЕ);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.РасчСчет", ПлатежноеПоручение.НомерСчетаОрганизации);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.ФактическийПлательщик", ПлатежноеПоручение.ФактическийПлательщик);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.ПлательщикКодСтраныРегистрации", ПлатежноеПоручение.ПлательщикКодСтраныРегистрации);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.Банк.БИК", ПлатежноеПоручение.ПлательщикБанкБИК);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.Банк.Наименование", ПлатежноеПоручение.ПлательщикБанкНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.Банк.Город", ПлатежноеПоручение.ПлательщикБанкГород);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлательщика.Банк.КоррСчет", ПлатежноеПоручение.ПлательщикБанкИИК);
		
		// Получатель.
		Контрагент = ПлатежноеПоручение.Контрагент;
		Если НЕ ЗначениеЗаполнено(Контрагент) И ПлатежноеПоручение.ВидОперации = Перечисления.ВидыОперацийППИсходящее.ПереводНаДругойСчет Тогда
			Контрагент = Справочники.Организации.КонтрагентСоответствующийОрганизации(ПлатежноеПоручение.Организация);
		КонецЕсли;
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "Получатель", Контрагент);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.ТипКонтрагента", ПлатежноеПоручение.ТипКонтрагента);

		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.Наименование", ПлатежноеПоручение.ТекстПолучателя);
		Если НЕ ЗначениеЗаполнено(ПлатежноеПоручение.КонтрагентБИН_ИНН) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.ИНН", СокрЛП(Контрагент.ИдентификационныйКодЛичности));
		Иначе
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.ИНН", СокрЛП(ПлатежноеПоручение.КонтрагентБИН_ИНН));
		КонецЕсли;
		Если НЕ ЗначениеЗаполнено(ПлатежноеПоручение.КонтрагентКБЕ) Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.КБЕ", СокрЛП(Контрагент.КБЕ));
		Иначе
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.КБЕ", СокрЛП(ПлатежноеПоручение.КонтрагентКБЕ));
		КонецЕсли;
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.РасчСчет", ПлатежноеПоручение.НомерСчетаКонтрагента);
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.Банк.БИК", ПлатежноеПоручение.ПолучательБанкБИК);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.Банк.Наименование", ПлатежноеПоручение.ПолучательБанкНаименование);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.Банк.Город", ПлатежноеПоручение.ПолучательБанкГород);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.Банк.КоррСчет", ПлатежноеПоручение.ПолучательБанкИИК);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПолучателя.ПолучательКодСтраныРегистрации", ПлатежноеПоручение.ПолучательКодСтраныРегистрации);
	
		// Платежи в бюджет.
		Если ПлатежноеПоручение.ПеречислениеВБюджет Тогда
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ПлатежиВБюджет.ПоказательКБК", ПлатежноеПоручение.КодБК);
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.ПеречислениеНалога", Истина);
		Иначе
			ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.ПеречислениеНалога", Ложь);
		КонецЕсли;
		
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.КодНазначенияПлатежа", ПлатежноеПоручение.КодНазначенияПлатежа);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.ДатаВалютирования", ПлатежноеПоручение.ДатаВалютирования);
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "РеквизитыПлатежа.ДатаПолученияТовара", ПлатежноеПоручение.ДатаПолученияТовара);
		
		// Списочные платежи.
		ПараметрыОтбора = Новый Структура;
		ПараметрыОтбора.Вставить("ПлатежныйДокумент", ПлатежноеПоручение.Ссылка);
		
		Если ТаблицаСписокСотрудников.Количество() <> 0 Тогда
			СписокСотрудников = ТаблицаСписокСотрудников.Скопировать(ПараметрыОтбора);
		Иначе
			СписокСотрудников = новый ТаблицаЗначений;
		КонецЕсли;
		ЭлектронноеВзаимодействие.ЗаполнитьЗначениеРеквизитаВДереве(ДеревоДокумента, "ПереданСписокСотрудников", СписокСотрудников.Количество() <> 0);
		Если СписокСотрудников.Количество() <> 0 Тогда
			ЭлектронноеВзаимодействие.ЗагрузитьТаблицуВДерево(ДеревоДокумента, СписокСотрудников, "СписокСотрудников");
		КонецЕсли; 
		
	КонецЦикла;
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Платежное требование.
//
// Параметры:
//  МассивСсылок - Массив - содержит ссылки на документы информационной базы, на основании которых будут созданы электронные документы.
//  ДанныеДляЗаполнения - Массив - содержит пустые деревья значений, которые необходимо заполнить данными.
//           Дерево значений повторяет структуру макета ПлатежноеТребование из обработки ОбменСБанками.
//           Если по какому-либо документу не удалось получить данные, то текст ошибки необходимо поместить вместо дерева значений.
//           ВНИМАНИЕ! Порядок элементов массива ДанныеДляЗаполнения соответствует порядку элементов массива МассивСсылок.
//
Процедура ЗаполнитьДанныеПлатежныхТребований(МассивСсылок, ДанныеДляЗаполнения) Экспорт
		
КонецПроцедуры

// Подготавливает данные для электронного документа типа Поручение на перевод валюты.
//
// Параметры:
//  МассивСсылок - Массив - содержит ссылки на документы информационной базы, на основании которых будут созданы электронные документы.
//  ДанныеДляЗаполнения - Массив - содержит пустые деревья значений, которые необходимо заполнить данными.
//           Дерево значений повторяет структуру макета ПоручениеНаПереводВалюты из обработки ОбменСБанками.
//           Если по какому-либо документу не удалось получить данные, то текст ошибки необходимо поместить вместо дерева значений.
//           ВНИМАНИЕ! Порядок элементов массива ДанныеДляЗаполнения соответствует порядку элементов массива МассивСсылок.
//
//@skip-warning
Процедура ЗаполнитьДанныеПорученийНаПереводВалюты(МассивСсылок, ДанныеДляЗаполнения) Экспорт
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Поручение на покупку валюты.
//
// Параметры:
//  МассивСсылок - Массив - содержит ссылки на документы информационной базы, на основании которых будут созданы электронные документы.
//  ДанныеДляЗаполнения - Массив - содержит пустые деревья значений, которые необходимо заполнить данными.
//           Дерево значений повторяет структуру макета ПоручениеНаПокупкуВалюты из обработки ОбменСБанками.
//           Если по какому-либо документу не удалось получить данные, то текст ошибки необходимо поместить вместо дерева значений.
//           ВНИМАНИЕ! Порядок элементов массива ДанныеДляЗаполнения соответствует порядку элементов массива МассивСсылок.
//
//@skip-warning
Процедура ЗаполнитьДанныеПорученийНаПокупкуВалюты(МассивСсылок, ДанныеДляЗаполнения) Экспорт
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Поручение на продажу валюты.
//
// Параметры:
//  МассивСсылок - Массив - содержит ссылки на документы информационной базы, на основании которых будут созданы электронные документы.
//  ДанныеДляЗаполнения - Массив - содержит пустые деревья значений, которые необходимо заполнить данными.
//           Дерево значений повторяет структуру макета ПоручениеНаПродажуВалюты из обработки ОбменСБанками.
//           Если по какому-либо документу не удалось получить данные, то текст ошибки необходимо поместить вместо дерева значений.
//           ВНИМАНИЕ! Порядок элементов массива ДанныеДляЗаполнения соответствует порядку элементов массива МассивСсылок.
//
//@skip-warning
Процедура ЗаполнитьДанныеПорученийНаПродажуВалюты(МассивСсылок, ДанныеДляЗаполнения) Экспорт
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Распоряжение на обязательную продажу валюты.
//
// Параметры:
//  МассивСсылок - Массив - содержит ссылки на документы информационной базы, на основании которых будут созданы электронные документы.
//  ДанныеДляЗаполнения - Массив - содержит пустые деревья значений, которые необходимо заполнить данными.
//           Дерево значений повторяет структуру макета РаспоряжениеНаОбязательнуюПродажуВалюты из обработки ОбменСБанками.
//           Если по какому-либо документу не удалось получить данные, то текст ошибки необходимо поместить вместо дерева значений.
//           ВНИМАНИЕ! Порядок элементов массива ДанныеДляЗаполнения соответствует порядку элементов массива МассивСсылок.
//
//@skip-warning
Процедура ЗаполнитьДанныеРаспоряженийНаОбязательнуюПродажуВалюты(МассивСсылок, ДанныеДляЗаполнения) Экспорт
	
КонецПроцедуры

// Подготавливает данные для электронного документа типа Справка о подтверждающих документах.
//
// Параметры:
//  МассивСсылок - Массив - содержит ссылки на документы информационной базы, на основании которых будут созданы электронные документы.
//  ДанныеДляЗаполнения - Массив - содержит пустые деревья значений, которые необходимо заполнить данными.
//           Дерево значений повторяет структуру макета СправкаОПодтверждающихДокументах из обработки ОбменСБанками.
//           Если по какому-либо документу не удалось получить данные, то текст ошибки необходимо поместить вместо дерева значений.
//           ВНИМАНИЕ! Порядок элементов массива ДанныеДляЗаполнения соответствует порядку элементов массива МассивСсылок.
//
//@skip-warning
Процедура ЗаполнитьДанныеСправокОПодтверждающихДокументах(МассивСсылок, ДанныеДляЗаполнения) Экспорт
	
КонецПроцедуры

// Вызывается при получении уведомления о зачислении валюты
//
// Параметры:
//  ДеревоРазбора - ДеревоЗначений - дерево данных, соответствующее макету Обработки.ОбменСБанками.УведомлениеОЗачислении
//  НовыйДокументСсылка - ДокументСсылка - ссылка на созданный документ на основании данных электронного документа.
//
//@skip-warning
Процедура ПриПолученииУведомленияОЗачислении(ДеревоРазбора, НовыйДокументСсылка) Экспорт
	
КонецПроцедуры

// Включает тестовый режим обмена в банком.
// При включении тестового режима возможно ручное указание URL сервера для получения настроек обмена.
//
// Параметры:
//    ИспользуетсяТестовыйРежим - Булево - признак использования тестового режима.
//
Процедура ПроверитьИспользованиеТестовогоРежима(ИспользуетсяТестовыйРежим) Экспорт
	
КонецПроцедуры

// Событие возникает при получении выписки из регламентного задания или при синхронизации.
// Необходимо создать документы в информационной базе для отражения произведенных по счету операций.
// Для получения данных выписки в удобном формате можно использовать следующие процедуры:
//  - ОбменСБанками.ПолучитьДанныеВыпискиБанкаДеревоЗначений()
//  - ОбменСБанками.ПолучитьДанныеВыпискиБанкаТекстовыйФормат() - только для рублевых выписок.
//
// Параметры:
//  СообщениеОбмена - ДокументСсылка.СообщениеОбменСБанками - ссылка на сообщение обмена, содержащий выписку банка.
//
Процедура ПриПолученииВыписки(СообщениеОбмена) Экспорт
	
КонецПроцедуры

#Область ЗарплатныйПроект

// Вызывается для формирования XML файла в прикладном решении.
//
// Параметры:
//    ОбъектДляВыгрузки - ДокументСсылка - ссылка на документ, на основании которого будет сформирован ЭД.
//    ИмяФайла - Строка - имя сформированного файла.
//    АдресФайла - Строка - АдресВременногоХранилища, содержит двоичные данные файла.
//
//@skip-warning
Процедура ПриФормированииXMLФайла(ОбъектДляВыгрузки, ИмяФайла, АдресФайла) Экспорт
	
КонецПроцедуры

// Формирует табличный документ на основании файла XML для визуального отображения электронного документа.
//
// Параметры:
//  ИмяФайла - Строка - полный путь к файлу XML
//  ТабличныйДокумент - ТабличныйДокумент - возвращаемое значение, визуальное отображение данных файла.
//
//@skip-warning
Процедура ЗаполнитьТабличныйДокумент(Знач ИмяФайла, ТабличныйДокумент) Экспорт
	
КонецПроцедуры

// Вызывается при получении файла из банка.
//
// Параметры:
//  АдресДанныхФайла - Строка - адрес временного хранилища с двоичными данными файла.
//  ИмяФайла - Строка - формализованное имя файла данных.
//  ОбъектВладелец - ДокументСсылка - (возвращаемый параметр) ссылка на документ, который был создан на основании ЭД.
//  ДанныеОповещения - Структура - (возвращаемый параметр) данные для вызова метода Оповестить на клиенте.
//                 * Ключ - Строка - имя события.
//                 * Значение - Произвольный - параметр сообщения.
//@skip-warning
Процедура ПриПолученииXMLФайла(АдресДанныхФайла, ИмяФайла, ОбъектВладелец, ДанныеОповещения) Экспорт
	
КонецПроцедуры

// Вызывается при изменении состояния электронного документооборота.
//
// Параметры:
//  СсылкаНаОбъект - ДокументСсылка - владелец электронного документооборота;
//  СостояниеЭД - ПеречислениеСсылка.СостоянияОбменСБанками - новое состояние электронного документооборота.
//
//@skip-warning
Процедура ПриИзмененииСостоянияЭД(СсылкаНаОбъект, СостояниеЭД) Экспорт
	
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

