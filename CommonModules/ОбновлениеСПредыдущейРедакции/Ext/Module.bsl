///////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ ОБНОВЛЕНИЯ 2.0 - 3.0

///////////////////////////////////////////////////////////////////////////////
// Блок обработчиков перехода с 2.0

Функция ЭтоПереходСРедакции20() Экспорт
	
	Если НЕ ПервыйЗапускИнформационнойБазы() 
		ИЛИ ОбщегоНазначенияПовтИсп.РазделениеВключено() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Константы.УдалитьНомерВерсииКонфигурации.Получить())
		И ОбновлениеИнформационнойБазы.ВерсияИБ(Метаданные.Имя)="0.0.0.0" Тогда
		
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

Процедура ОчиститьВерсиюПодсистемыБСППриПереходеСБК20() Экспорт
	
	Если ЭтоПереходСРедакции20() Тогда
		
		ВерсииПодсистем = РегистрыСведений.ВерсииПодсистем.СоздатьМенеджерЗаписи();
		ВерсииПодсистем.ИмяПодсистемы = "СтандартныеПодсистемы";
		ВерсииПодсистем.Активность = Истина;
		ВерсииПодсистем.Версия = "1.0.0.0";
		ВерсииПодсистем.Записать(Истина);
		
		ВерсииПодсистем = РегистрыСведений.ВерсииПодсистем.СоздатьМенеджерЗаписи();
		ВерсииПодсистем.ИмяПодсистемы = Метаданные.Имя;
		ВерсииПодсистем.ЭтоОсновнаяКонфигурация = Истина;
		ВерсииПодсистем.Активность = Истина;
		ВерсииПодсистем.Версия = "0.0.0.0";
		ВерсииПодсистем.Записать(Истина);
		
	КонецЕсли;
	
КонецПроцедуры

Процедура УстановитьПоследнююВерсиюОтображенияИзмененийСистемыПриПереходеС20() Экспорт
	
	Если НЕ ЭтоПереходСРедакции20() Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		// Анонимным пользователям новое в версии не показываем
		Возврат;
	КонецЕсли;
		
	ОбщегоНазначения.ХранилищеОбщихНастроекСохранить("ОбновлениеИБ",
			"ПоследняяВерсияОтображенияИзмененийСистемы", "2.0.1.1", , ИмяПользователя());
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// РАСПРЕДЕЛЕННАЯ ИНФОРМАЦИОННАЯ БАЗА: ОБНОВЛЕНИЕ С БП 2.0

Процедура ОбновитьРИБПриПереходе20На30() Экспорт 
	
	Если ЗначениеЗаполнено(Константы.УдалитьНомерВерсииКонфигурации.Получить()) 
		И ПланыОбмена.ГлавныйУзел() <> Неопределено Тогда 
		// Переход с версии 2.0
		Константы.НастройкаПодчиненногоУзлаРИБЗавершена.Установить(Истина);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПервыйЗапускИнформационнойБазы()
	
	Возврат НЕ Константы.ИспользоватьРазделениеПоОбластямДанных.Получить()
		И НЕ Константы.НеИспользоватьРазделениеПоОбластямДанных.Получить()
		И НЕ Константы.ЭтоАвтономноеРабочееМесто.Получить();
		
КонецФункции

