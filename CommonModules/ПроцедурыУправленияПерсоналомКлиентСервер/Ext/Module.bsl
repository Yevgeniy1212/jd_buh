////////////////////////////////////////////////////////////////////////////////
// ПроцедурыУправленияПерсоналомКлиентСервер
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

//Функция раскладывает строку с данными о месте рождения на элементы структуры
//
// Параметры
//  СтрокаМестоРождения - строка - текстовое представление места рождения
//  ВерхнийРегистр 		- булево - преобразовать в верхний регистр
//
// Возвращаемое значение
//  Структура - возвращается структура наименований по типам населенных пунктов
//
Функция РазложитьМестоРождения(Знач СтрокаМестоРождения, ВерхнийРегистр = Истина) Экспорт

	НаселенныйПункт		= "";
	Район				= "";
	Область				= "";
	Страна				= "";
	МассивМестоРождения	= СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(?(ВерхнийРегистр, Врег(СтрокаМестоРождения), СтрокаМестоРождения));
	ЭлементовВМассиве	= МассивМестоРождения.Количество();   
	
	Если ЭлементовВМассиве	>	0	тогда
		НаселенныйПункт	=	СокрЛП(МассивМестоРождения[0]);
	КонецЕсли;
	
	Если ЭлементовВМассиве	>	1	тогда
		Район	=	СокрЛП(МассивМестоРождения[1]);
	КонецЕсли;
	
	Если ЭлементовВМассиве	>	2	тогда
		Область	=	СокрЛП(МассивМестоРождения[2]);
	КонецЕсли;
	
	Если ЭлементовВМассиве	>	3	тогда
		Страна	=	СокрЛП(МассивМестоРождения[3]);
	КонецЕсли;

	СтруктураВозврата = Новый Структура;
	СтруктураВозврата.Вставить("НаселенныйПункт",НаселенныйПункт);
	СтруктураВозврата.Вставить("Район",Район);
	СтруктураВозврата.Вставить("Область",Область);
	СтруктураВозврата.Вставить("Страна",Страна);
	
	Возврат СтруктураВозврата;
	
КонецФункции // РазложитьМестоРождения()

//Возвращает строковое представление места рождения
//
Функция ПредставлениеМестаРождения(Знач СтрокаМестоРождения) Экспорт

	СтруктураМестоРождения = РазложитьМестоРождения(СтрокаМестоРождения, Ложь);
	
	Результат = Новый Массив;
	Если ЗначениеЗаполнено(СтруктураМестоРождения.НаселенныйПункт) Тогда
		Результат.Добавить(НСтр("ru = 'Населенный пункт: '") + СокрЛП(СтруктураМестоРождения.НаселенныйПункт));
	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураМестоРождения.Район) Тогда
		Результат.Добавить(?(Результат.Количество(), НСтр("ru = 'район: '")  , НСтр("ru = 'Район: '"))   + СокрЛП(СтруктураМестоРождения.Район));
	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураМестоРождения.Область) Тогда
		Результат.Добавить(?(Результат.Количество(), НСтр("ru = 'область: '"), НСтр("ru = 'Область: '")) + СокрЛП(СтруктураМестоРождения.Область));
	КонецЕсли;
	Если ЗначениеЗаполнено(СтруктураМестоРождения.Страна) Тогда
		Результат.Добавить(?(Результат.Количество(), НСтр("ru = 'страна: '") , НСтр("ru = 'Страна: '"))  + СокрЛП(СтруктураМестоРождения.Страна));
	КонецЕсли;
	
	Возврат СтрСоединить(Результат, ", ")
	
КонецФункции // ПредставлениеМестаРождения()

// Функция определяет пол физлица по его отчеству. 
//
Функция УстановитьПол(ОтчествоРаботника) Экспорт

	Если НРег(Прав(ОтчествоРаботника, 2)) = "ич" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ПолФизическихЛиц.Мужской");
	ИначеЕсли НРег(Прав(ОтчествоРаботника, 2)) = "на" Тогда
		Возврат ПредопределенноеЗначение("Перечисление.ПолФизическихЛиц.Женский");
	КонецЕсли; 

	Возврат ПредопределенноеЗначение("Перечисление.ПолФизическихЛиц.ПустаяСсылка");

КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ