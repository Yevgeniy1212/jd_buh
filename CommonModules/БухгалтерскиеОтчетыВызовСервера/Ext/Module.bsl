
Функция ЕстьЭлементФормы(Форма, ИмяЭлемента)
	
	Возврат Форма.Элементы.Найти(ИмяЭлемента) <> Неопределено;
	
КонецФункции

Функция КоличествоПоказателей(ПараметрыОтчета) Экспорт 
	
	КоличествоПоказателей = 0;
	Для Каждого ИмяПоказателя Из ПараметрыОтчета.НаборПоказателей Цикл
		Если ПараметрыОтчета["Показатель" + ИмяПоказателя] И ИмяПоказателя <> "РазвернутоеСальдо" Тогда
			КоличествоПоказателей = КоличествоПоказателей + 1;
		КонецЕсли;
	КонецЦикла;
	
	Возврат КоличествоПоказателей;
	
КонецФункции

Процедура ЗаполнитьГруппировку(ПолеВыбраннойГруппировки, Группировка) Экспорт
	
	ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	ПолеГруппировки.Использование  = Истина;
	ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных(ПолеВыбраннойГруппировки.Поле);
	Если ПолеВыбраннойГруппировки.ТипГруппировки = 1 Тогда
		ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
	ИначеЕсли ПолеВыбраннойГруппировки.ТипГруппировки = 2 Тогда
		ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия;
	Иначе
		ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
	КонецЕсли;
	Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
	Группировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
	
КонецПроцедуры

Процедура ДобавитьГруппировки(ПараметрыОтчета, КомпоновщикНастроек) Экспорт
	
	Если ТипЗнч(КомпоновщикНастроек) = Тип("КомпоновщикНастроекКомпоновкиДанных") Тогда
		СтруктураНастроек = КомпоновщикНастроек.Настройки.Структура;
	Иначе
		СтруктураНастроек = КомпоновщикНастроек;
	КонецЕсли;
	
	Для Каждого ЭлементСтруктуры Из СтруктураНастроек Цикл
		Группировка = ЭлементСтруктуры;
		Если Группировка.Имя = "Группировка" Тогда // Заполнить все поля группировки
			
			Если ТипЗнч(Группировка) = Тип("ГруппировкаКомпоновкиДанных") Тогда
				Родитель = Группировка.Родитель;
				ПерваяГруппировка = Истина;
				Индекс = Родитель.Структура.Индекс(Группировка);
				Родитель.Структура.Удалить(Группировка);
				Для Каждого ПолеВыбраннойГруппировки Из ПараметрыОтчета.Группировка Цикл 
					Если ПолеВыбраннойГруппировки.Использование Тогда
						Если ПерваяГруппировка Тогда
							Группировка = Родитель.Структура.Вставить(Индекс, Тип("ГруппировкаКомпоновкиДанных"));
							ПерваяГруппировка = Ложь;
						Иначе
							Группировка = Группировка.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
						КонецЕсли;
						
						ПолеГруппировки = Группировка.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
						ПолеГруппировки.Использование  = Истина;
						ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных(ПолеВыбраннойГруппировки.Поле);
						Если ПолеВыбраннойГруппировки.ТипГруппировки = 1 Тогда
							ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Иерархия;
						ИначеЕсли ПолеВыбраннойГруппировки.ТипГруппировки = 2 Тогда
							ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.ТолькоИерархия;
						Иначе
							ПолеГруппировки.ТипГруппировки = ТипГруппировкиКомпоновкиДанных.Элементы;
						КонецЕсли;
						Группировка.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
						Группировка.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;		
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьДополнительныеПоля(ПараметрыОтчета, КомпоновщикНастроек = Неопределено, ГруппировкаОтчета = Неопределено) Экспорт
	
	Если ПараметрыОтчета.Свойство("РазмещениеДополнительныхПолей") Тогда
		Если ПараметрыОтчета.РазмещениеДополнительныхПолей = 0 Тогда
			РасположениеРеквизитов 	= РасположениеРеквизитовКомпоновкиДанных.ВместеСВладельцем;
			РасположениеПолей 		= РасположениеПоляКомпоновкиДанных.Вместе;
		ИначеЕсли ПараметрыОтчета.РазмещениеДополнительныхПолей = 1 Тогда
			РасположениеРеквизитов  = РасположениеРеквизитовКомпоновкиДанных.Отдельно;
			РасположениеПолей       = РасположениеПоляКомпоновкиДанных.Авто;
		КонецЕсли;
	Иначе
		РасположениеРеквизитов  = РасположениеРеквизитовКомпоновкиДанных.ВместеСВладельцем;
		РасположениеПолей 		= РасположениеПоляКомпоновкиДанных.Авто;
	КонецЕсли;
	
	Если ГруппировкаОтчета <> Неопределено И ТипЗнч(ГруппировкаОтчета) = Тип("ГруппаВыбранныхПолейКомпоновкиДанных") Тогда
		ГруппировкаОтчета.Расположение = РасположениеПолей;	
	КонецЕсли;	
	
	Если КомпоновщикНастроек <> Неопределено Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(КомпоновщикНастроек, "РасположениеРеквизитов", РасположениеРеквизитов); 
	Иначе	
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(ПараметрыОтчета.НастройкиКомпоновкиДанных, "РасположениеРеквизитов", РасположениеРеквизитов); 
	КонецЕсли;
			
	Для Каждого ПолеГруппировки Из ПараметрыОтчета.ДополнительныеПоля Цикл 
		Если ПолеГруппировки.Использование Тогда
			Если ГруппировкаОтчета <> Неопределено Тогда
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ГруппировкаОтчета, ПолеГруппировки.Поле);
			Иначе	
				Если КомпоновщикНастроек <> Неопределено Тогда
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(КомпоновщикНастроек, ПолеГруппировки.Поле);
				Иначе	
					БухгалтерскиеОтчетыКлиентСервер.ДобавитьВыбранноеПоле(ПараметрыОтчета.НастройкиКомпоновкиДанных, ПолеГруппировки.Поле);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ДобавитьГруппировкуПоПериоду(ПараметрыОтчета, Структура) Экспорт
	
	Если ПараметрыОтчета.Периодичность > 0 Тогда
		Если ТипЗнч(Структура) = Тип("ГруппировкаКомпоновкиДанных") Тогда 
			Структура = Структура.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
		ИначеЕсли ТипЗнч(Структура) = Тип("ГруппировкаТаблицыКомпоновкиДанных") Тогда
			Структура = Структура.Структура.Добавить();
		КонецЕсли;
		ПолеГруппировки = Структура.ПоляГруппировки.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
		ПолеГруппировки.Использование  = Истина;
		ПолеГруппировки.Поле           = Новый ПолеКомпоновкиДанных(?(ПараметрыОтчета.Периодичность = 2, "Регистратор", "Период"));		
		Структура.Выбор.Элементы.Добавить(Тип("АвтоВыбранноеПолеКомпоновкиДанных"));
		Структура.Порядок.Элементы.Добавить(Тип("АвтоЭлементПорядкаКомпоновкиДанных"));	
		Если ПараметрыОтчета.Периодичность = 2 Тогда
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(Структура.Отбор, "Регистратор", 0, ВидСравненияКомпоновкиДанных.Заполнено);
			БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(Структура, "ВыводитьОтбор", ТипВыводаТекстаКомпоновкиДанных.НеВыводить);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ДобавитьОтборПоОрганизации(ПараметрыОтчета, КомпоновщикНастроек, ВПользовательскиеНастройки = Истина) Экспорт
	
	Если ЗначениеЗаполнено(ПараметрыОтчета.Организация) Тогда
		Если ПараметрыОтчета.ВключатьОбособленныеПодразделения Тогда
			Отбор = КомпоновщикНастроек.Настройки.Отбор;
			Если ВПользовательскиеНастройки Тогда
				Для Каждого ЭлементНастройки Из КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл	
					Если ЭлементНастройки.ИдентификаторПользовательскойНастройки = КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки Тогда
						Отбор = ЭлементНастройки;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;	
			ГруппаОтборОрганизация = Отбор.Элементы.Добавить(Тип("ГруппаЭлементовОтбораКомпоновкиДанных"));
			ГруппаОтборОрганизация.Представление = "###ОтборПоОрганизацииСОП###";
			ГруппаОтборОрганизация.ТипГруппы     = ТипГруппыЭлементовОтбораКомпоновкиДанных.ГруппаИли;
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаОтборОрганизация, "Организация"                    , ПараметрыОтчета.Организация);
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ГруппаОтборОрганизация, "Организация.ГоловнаяОрганизация", ПараметрыОтчета.Организация);
		Иначе
			НовыйОтбор = БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(КомпоновщикНастроек, "Организация", ПараметрыОтчета.Организация,,, ВПользовательскиеНастройки);
			НовыйОтбор.Представление = "###ОтборПоОрганизации###"; 
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьМакетШапки(МакетКомпоновки, Тело = Неопределено, ТипМакета = "Заголовок") Экспорт
	
	ЕстьПустойМакет = Ложь;
	
	Если Тело = Неопределено Тогда
		Тело = МакетКомпоновки.Тело;
	КонецЕсли;
	
	Если Тело.Количество() > 0 Тогда
		Если ТипМакета = "Заголовок" Тогда
			НачальныйИндекс = 0;
			КонечныйИндекс  = Тело.Количество();
			ИтераторПрямой  = Истина;
		ИначеЕсли ТипМакета = "Подвал" Тогда 
			НачальныйИндекс = Тело.Количество() - 1;
			КонечныйИндекс  = 0;
			ИтераторПрямой  = Ложь;
		КонецЕсли;
		
		Индекс = НачальныйИндекс;
		Пока Индекс <> КонечныйИндекс Цикл
			Элемент = Тело[Индекс];
			Если ТипЗнч(Элемент) = Тип("МакетОбластиМакетаКомпоновкиДанных") Тогда
				Если ЕстьПустойМакет Тогда
					ЕстьПустойМакет = Ложь;
				Иначе
					Возврат МакетКомпоновки.Макеты[Элемент.Макет];
				КонецЕсли;
			ИначеЕсли ТипЗнч(Элемент) = Тип("ДиаграммаМакетаКомпоновкиДанных") Тогда
				ЕстьПустойМакет = Истина;
			ИначеЕсли ТипЗнч(Элемент) = Тип("ТаблицаМакетаКомпоновкиДанных") Тогда
				ЕстьПустойМакет = Истина;
			КонецЕсли;
			
			Если ИтераторПрямой Тогда
				Индекс = Индекс + 1;
			Иначе
				Индекс = Индекс - 1;
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьМакетПодвала(МакетКомпоновки, Тело = Неопределено) Экспорт
	
	Если Тело = Неопределено Тогда
		Тело = МакетКомпоновки.Тело;
	КонецЕсли;
	
	Для Каждого Элемент Из Тело Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппировкаМакетаКомпоновкиДанных") Тогда
			Если Не ПустаяСтрока(Элемент.МакетПодвала) Тогда
				Возврат МакетКомпоновки.Макеты[Элемент.МакетПодвала];
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;	
	
	Возврат Неопределено;
	
КонецФункции

Функция ПолучитьМакетГруппировкиПоПолюГруппировки(МакетКомпоновки, ПолеГруппировки, ИскатьВДетальныхЗаписях = Ложь, ТипМакета = "Заголовок") Экспорт
	
	МассивМакетов = Новый Массив;
	
	ОбойтиТелоМакетаКомпоновки(МакетКомпоновки, МакетКомпоновки.Тело, МассивМакетов, ПолеГруппировки, ТипМакета, ИскатьВДетальныхЗаписях);	
	
	Возврат МассивМакетов;
	
КонецФункции

Процедура ОбойтиТелоМакетаКомпоновки(МакетКомпоновки, Тело, МассивМакетов, ПолеГруппировки, ТипМакета, ИскатьВДетальныхЗаписях = Ложь) 
	
	Для Каждого Элемент Из Тело Цикл
		Если ТипЗнч(Элемент) = Тип("ГруппировкаМакетаКомпоновкиДанных") Тогда
			Для Каждого ЭлементГруппировки Из Элемент.Группировка Цикл
				Если Найти(ЭлементГруппировки.ИмяПоля, ПолеГруппировки) = 1 Тогда 
					МакетТело = ПолучитьМакетШапки(МакетКомпоновки, Элемент.Тело, ТипМакета);
					Если МакетТело <> Неопределено Тогда
						МассивМакетов.Добавить(МакетТело);  
					КонецЕсли;
					МакетТелоИерархии = ПолучитьМакетШапки(МакетКомпоновки, Элемент.ТелоИерархии, ТипМакета);
					Если МакетТелоИерархии <> Неопределено Тогда
						МассивМакетов.Добавить(МакетТелоИерархии);
					КонецЕсли;
				КонецЕсли; 
				ОбойтиТелоМакетаКомпоновки(МакетКомпоновки, Элемент.Тело, МассивМакетов, ПолеГруппировки, ТипМакета, ИскатьВДетальныхЗаписях);
			КонецЦикла;
		КонецЕсли;
		Если ИскатьВДетальныхЗаписях Тогда
			Если ТипЗнч(Элемент) = Тип("ЗаписиМакетаКомпоновкиДанных") Тогда
				Если Элемент.Имя = ПолеГруппировки Тогда
					МассивМакетов.Добавить(ПолучитьМакетШапки(МакетКомпоновки, Элемент.Тело));	
				КонецЕсли;
			КонецЕсли;			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура УстановитьНастройкиПоУмолчанию(ФормаОтчета) Экспорт
	
	Отчет = ФормаОтчета.Отчет;
	
	ОсновнаяОрганизация = Справочники.Организации.ОрганизацияПоУмолчанию();
	
	Если Отчет.Свойство("НачалоПериода") Тогда
		Отчет.НачалоПериода = НачалоМесяца(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	Если Отчет.Свойство("КонецПериода") Тогда
		Отчет.КонецПериода  = КонецМесяца(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	
	Если Отчет.Свойство("Налогоплательщик") Тогда
		Отчет.Налогоплательщик = ОсновнаяОрганизация;
	КонецЕсли;
	
	Если (Отчет.Свойство("ПредставлениеСпискаОрганизаций") ИЛИ Отчет.Свойство("ПредставлениеСпискаСтруктурныхЕдиниц"))
		И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СписокСтруктурныхЕдиниц") Тогда
		
		ФормаОтчета.СписокСтруктурныхЕдиниц.Очистить();
		
		Если Отчет.Свойство("Налогоплательщик")
			И ЗначениеЗаполнено(Отчет.Налогоплательщик)
			И Отчет.Свойство("ПредставлениеСпискаСтруктурныхЕдиниц") Тогда
			
			Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ПеречислениеРазделыНалоговогоУчета")
				И ЗначениеЗаполнено(ФормаОтчета.ПеречислениеРазделыНалоговогоУчета) Тогда
				ПеречислениеРазделыНалоговогоУчета = ФормаОтчета.ПеречислениеРазделыНалоговогоУчета;
			Иначе
				ПеречислениеРазделыНалоговогоУчета = Неопределено;
			КонецЕсли;
			
			ФормаОтчета.СписокСтруктурныхЕдиниц = ПроцедурыНалоговогоУчетаВызовСервераПовтИсп.СформироватьСписокСтруктурныхЕдиниц(ПеречислениеРазделыНалоговогоУчета,, Отчет.Налогоплательщик);
			
		Иначе
			Если ЗначениеЗаполнено(ОсновнаяОрганизация) Тогда
				ФормаОтчета.СписокСтруктурныхЕдиниц.Добавить(ОсновнаяОрганизация);
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
	Если Отчет.Свойство("ПредставлениеСпискаПодразделений") И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СписокПодразделений") Тогда
		Если ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями") Тогда
			ФормаОтчета.СписокПодразделений.Очистить();
			СтруктурноеПодразделение = ПользователиБКВызовСервераПовтИсп.ПолучитьЗначениеПоУмолчанию(Пользователи.ТекущийПользователь(), "ОсновноеСтруктурноеПодразделениеОрганизации");
			Если ТипЗнч(СтруктурноеПодразделение) = Тип("СправочникСсылка.Организации") Тогда 
				СтруктурноеПодразделение = Справочники.ПодразделенияОрганизаций.ПустаяСсылка();
			КонецЕсли;
			Если СтруктурноеПодразделение <> Неопределено Тогда
				ФормаОтчета.СписокПодразделений.Добавить(СтруктурноеПодразделение);
			КонецЕсли;
		КонецЕсли;
		Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СписокВладельцевГоловныхПодразделений") Тогда
			Если ФормаОтчета.СписокПодразделений.НайтиПоЗначению(Справочники.ПодразделенияОрганизаций.ПустаяСсылка()) <> Неопределено
				И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СписокСтруктурныхЕдиниц") И ФормаОтчета.СписокСтруктурныхЕдиниц.Количество() > 0 Тогда
				ФормаОтчета.СписокВладельцевГоловныхПодразделений.Добавить(ФормаОтчета.СписокСтруктурныхЕдиниц[0].Значение);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

	Если Отчет.Свойство("ПоказательБУ") Тогда
		Отчет.ПоказательБУ = Истина;
	КонецЕсли;
	
	Если Отчет.Свойство("СальдоНаНачалоДт") Тогда
		Отчет.СальдоНаНачалоДт   = Истина;	
		Отчет.СальдоНаНачалоКт   = Истина;
		Отчет.СальдоНаКонецДт    = Истина;
		Отчет.СальдоНаКонецКт    = Истина;
		Отчет.ОборотыЗаПериодДт  = Истина;
		Отчет.ОборотыЗаПериодКт  = Истина;
	КонецЕсли;
	
	Если Отчет.Свойство("ОборотыСоСчетамиДт") Тогда
		Отчет.ОборотыСоСчетамиДт = Истина;
		Отчет.ОборотыСоСчетамиКт = Истина;
	КонецЕсли;
	
	ИдентификаторОбъекта = БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(ФормаОтчета);
	Если ИдентификаторОбъекта = "ОборотноСальдоваяВедомостьТиповой"
		ИЛИ ИдентификаторОбъекта = "ОтчетОбъект.ОборотноСальдоваяВедомостьНалоговый" Тогда
		Если Отчет.ДополнительныеПоля.НайтиСтроки(Новый Структура("Поле", "Счет.Наименование")).Количество() = 0 Тогда
			НоваяСтрока = ФормаОтчета.Отчет.ДополнительныеПоля.Добавить();
			НоваяСтрока.Представление = НСтр("ru = 'Выводить наименование счета'");
			НоваяСтрока.Поле          = "Счет.Наименование";
			НоваяСтрока.Использование = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	// Установка начальных значений Группировки
	Если Отчет.Свойство("Группировка") Тогда
		Отчет.Группировка.Очистить();
		Для Каждого ЭлементСтруктуры Из Отчет.КомпоновщикНастроек.Настройки.Структура Цикл
			Если ТипЗнч(ЭлементСтруктуры) = Тип("ДиаграммаКомпоновкиДанных") Тогда
				Для Каждого Серия Из ЭлементСтруктуры.Серии Цикл
					Если Серия.Имя = "Группировка" Тогда
						ЗаполнитьГруппировкиИзНастроек(Отчет.КомпоновщикНастроек, Серия, Отчет.Группировка);
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Для Каждого Точка Из ЭлементСтруктуры.Точки Цикл
					Если Точка.Имя = "Группировка" Тогда
						ЗаполнитьГруппировкиИзНастроек(Отчет.КомпоновщикНастроек, Точка, Отчет.Группировка);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ТаблицаКомпоновкиДанных") Тогда
				Для Каждого Колонка Из ЭлементСтруктуры.Колонки Цикл
					Если Колонка.Имя = "Группировка" Тогда
						ЗаполнитьГруппировкиИзНастроек(Отчет.КомпоновщикНастроек, Колонка, Отчет.Группировка);
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Для Каждого Строка Из ЭлементСтруктуры.Строки Цикл
					Если Строка.Имя = "Группировка" Тогда
						ЗаполнитьГруппировкиИзНастроек(Отчет.КомпоновщикНастроек, Строка, Отчет.Группировка);
						Прервать;
					КонецЕсли;
				КонецЦикла;
			ИначеЕсли ТипЗнч(ЭлементСтруктуры) = Тип("ГруппировкаКомпоновкиДанных") Тогда
				Если ЭлементСтруктуры.Имя = "Группировка" Тогда
					Отчет.Группировка.Очистить();
					ЗаполнитьГруппировкиИзНастроек(Отчет.КомпоновщикНастроек, ЭлементСтруктуры, Отчет.Группировка);
					Прервать;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если Отчет.Свойство("Интервалы") Тогда
		Отчет.Интервалы.Очистить();
		
		НоваяСтрока = Отчет.Интервалы.Добавить();
		НоваяСтрока.Значение      = 7;
		НоваяСтрока.Представление = НСтр("ru = 'До 7 дней'");
		
		НоваяСтрока = Отчет.Интервалы.Добавить();
		НоваяСтрока.Значение      = 15;
		НоваяСтрока.Представление = НСтр("ru = 'От 8 до 15 дней'");
		
		НоваяСтрока = Отчет.Интервалы.Добавить();
		НоваяСтрока.Значение      = 30;
		НоваяСтрока.Представление = НСтр("ru = 'От 16 до 30 дней'");
		
		НоваяСтрока = Отчет.Интервалы.Добавить();
		НоваяСтрока.Значение      = 60;
		НоваяСтрока.Представление = НСтр("ru = 'От 31 до 60 дней'");
		
		НоваяСтрока = Отчет.Интервалы.Добавить();
		НоваяСтрока.Значение      = 90;
		НоваяСтрока.Представление = НСтр("ru = 'От 61 до 90 дней'");
	КонецЕсли;
	
	Если Отчет.Свойство("Период") Тогда
		Отчет.Период = ОбщегоНазначения.ТекущаяДатаПользователя();
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьЗаголовок") Тогда
		ФормаОтчета.ВыводитьЗаголовок = Истина;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьПодписи") Тогда
		ФормаОтчета.ВыводитьПодписи = Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьПодписиРуководителей") Тогда
		ФормаОтчета.ВыводитьПодписи = Ложь;
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьДиаграмму") Тогда
		ФормаОтчета.ВыводитьДиаграмму = Истина;
	КонецЕсли;
	
	Если Отчет.Свойство("РазмещениеДополнительныхПолей") Тогда
		Отчет.РазмещениеДополнительныхПолей = ПолучитьРазмещениеДополнительныхПолей(Отчет.КомпоновщикНастроек);
	КонецЕсли;
	
	Если Отчет.Свойство("Группировка") И Отчет.Свойство("ДополнительныеПоля") Тогда
		ЗаполнитьДополнительныеПоляИзНастроек(Отчет.КомпоновщикНастроек, Отчет.ДополнительныеПоля, Отчет.Группировка);
	КонецЕсли;
	
	Если Отчет.Свойство("ПоказательПоступление") Тогда
		Отчет.ПоказательПоступление = Истина;
	КонецЕсли;
	Если Отчет.Свойство("ПоказательРасход") Тогда
		Отчет.ПоказательРасход = Истина;
	КонецЕсли;
	
	Если Отчет.Свойство("ПоказательЗадолженность") Тогда
		Отчет.ПоказательЗадолженность = Истина;
	КонецЕсли;
	Если Отчет.Свойство("ПоказательПросроченнаяЗадолженность") Тогда
		Отчет.ПоказательПросроченнаяЗадолженность = Истина;
	КонецЕсли;
	
КонецПроцедуры

Функция ПолучитьРазмещениеДополнительныхПолей(КомпоновщикНастроек) 
	
	РасположениеРеквизитов = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметрВывода(КомпоновщикНастроек, "РасположениеРеквизитов");	
	
	Если РасположениеРеквизитов.Использование = Истина
		И РасположениеРеквизитов.Значение = РасположениеРеквизитовКомпоновкиДанных.Отдельно Тогда
		Возврат 1;
	Иначе
		Возврат 0;
	КонецЕсли;
	
КонецФункции

Процедура ЗаполнитьДополнительныеПоляИзНастроек(КомпоновщикНастроек, ДополнительныеПоля, Группировка)
	
	Если Группировка.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	ДополнительныеПоля.Очистить();
	Для Каждого ВыбранноеПоле Из КомпоновщикНастроек.Настройки.Выбор.Элементы Цикл
		Если ТипЗнч(ВыбранноеПоле) = Тип("ВыбранноеПолеКомпоновкиДанных") 
			И Найти(Строка(ВыбранноеПоле.Поле), ".") > 0 Тогда
			Поле = Строка(ВыбранноеПоле.Поле);
			МассивПолей = СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Поле, ".");
			
			Если Группировка.НайтиСтроки(Новый Структура("Поле", МассивПолей[0])).Количество() > 0 Тогда
				НоваяСтрока = ДополнительныеПоля.Добавить();
				НоваяСтрока.Использование = ВыбранноеПоле.Использование;
				НоваяСтрока.Поле          = Поле;
				НоваяСтрока.Представление = БухгалтерскиеОтчетыКлиентСервер.ПолучитьСвойствоПоля(КомпоновщикНастроек, НоваяСтрока.Поле);
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьГруппировкиИзНастроек(КомпоновщикНастроек, Структура, Группировка)
	
	Если Структура.ПоляГруппировки.Элементы.Количество() > 0 Тогда
        Поле = Строка(Структура.ПоляГруппировки.Элементы[0].Поле);
		Если Структура.Структура.Количество() > 0 Тогда
			ЗаполнитьГруппировкиИзНастроек(КомпоновщикНастроек, Структура.Структура[0], Группировка);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Функция СкопироватьПараметрСхемыКомпоновкиДанных(СхемаКомпоновкиДанных, Приемник, Источник, ЗначениеПриемника = Неопределено) Экспорт
	
	Если ТипЗнч(СхемаКомпоновкиДанных) = Тип("СхемаКомпоновкиДанных") Тогда
		Параметры = СхемаКомпоновкиДанных.Параметры;
		
		НовыйПараметр = Параметры.Найти(Приемник);
		Если НовыйПараметр = Неопределено Тогда
			НовыйПараметр = Параметры.Добавить();
		КонецЕсли;
		
		ПараметрИсточник = Параметры.Найти(Источник);
		
		Если ПараметрИсточник = Неопределено Тогда
			Возврат Неопределено;
		Иначе
			ЗаполнитьЗначенияСвойств(НовыйПараметр, ПараметрИсточник);
			НовыйПараметр.Имя = Приемник;
			Если ЗначениеПриемника <> Неопределено Тогда
				НовыйПараметр.Значение = ЗначениеПриемника;
			КонецЕсли;
		КонецЕсли;
		
		Возврат НовыйПараметр;
	Иначе 
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция ПолучитьТипДополненияПоИнтервалу(Интервал = 0) Экспорт
	
	ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.БезДополнения;
	
	Если Интервал = 6 Тогда
		ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.День;
	ИначеЕсли Интервал = 7 Тогда
		ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Неделя;
	ИначеЕсли Интервал = 8 Тогда
		ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Декада;
	ИначеЕсли Интервал = 9 Тогда
		ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Месяц;
	ИначеЕсли Интервал = 10 Тогда
		ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Квартал;
	ИначеЕсли Интервал = 11 Тогда
		ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Полугодие;
	ИначеЕсли Интервал = 12 Тогда
		ТипДополнения = ТипДополненияПериодаКомпоновкиДанных.Год;
	КонецЕсли;
	
	Возврат ТипДополнения;	
	
КонецФункции

Функция ВычислитьСуммуВыделенныхЯчеекТабличногоДокумента(Знач Результат, КэшВыделеннойОбласти) Экспорт
	
	Сумма = 0;
	Для Каждого КлючИЗначение Из КэшВыделеннойОбласти Цикл
		СтруктураАдресВыделеннойОбласти = КлючИЗначение.Значение;
		Для ИндексСтрока = СтруктураАдресВыделеннойОбласти.Верх По СтруктураАдресВыделеннойОбласти.Низ Цикл
			Для ИндексКолонка = СтруктураАдресВыделеннойОбласти.Лево По СтруктураАдресВыделеннойОбласти.Право Цикл
				Попытка
					Ячейка = Результат.Область(ИндексСтрока, ИндексКолонка, ИндексСтрока, ИндексКолонка);
					Если Ячейка.Видимость = Истина Тогда
						Если Ячейка.СодержитЗначение И ТипЗнч(Ячейка.Значение) = Тип("Число") Тогда
							Сумма = Сумма + Ячейка.Значение;
						ИначеЕсли ЗначениеЗаполнено(Ячейка.Текст) Тогда
							Сумма = Сумма + Число(СтрЗаменить(Ячейка.Текст, " ", ""));
						КонецЕсли;
					КонецЕсли;
				Исключение
				КонецПопытки;
			КонецЦикла;
		КонецЦикла;
	КонецЦикла;
	
	КэшВыделеннойОбласти.Вставить("Сумма", Сумма);
	
	Возврат Сумма;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Функции и процедуры обеспечения формирования бухгалтерских отчетов.
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Процедура СформироватьОтчет(ПараметрыОтчета, АдресХранилища) Экспорт
	
	ВыводитьПолностью = Истина;
	
	Отказ = Ложь;
	ДанныеРасшифровкиОбъект = Неопределено;
	ПараметрыИсполненияОтчета = Неопределено;
	
	Если ПараметрыОтчета.Свойство("ОтчетОбъект") И ПараметрыОтчета.ОтчетОбъект <> Неопределено Тогда
		МенеджерОтчета = ПараметрыОтчета.ОтчетОбъект;
	Иначе
		МенеджерОтчета = Отчеты[ПараметрыОтчета.ИдентификаторОтчета];
	КонецЕсли;
	
	Попытка
		ПараметрыИсполненияОтчета = МенеджерОтчета.ПолучитьПараметрыИсполненияОтчета();
	Исключение
		// Запись в журнал регистрации не требуется
	КонецПопытки;
	
	ИспользоватьВнешниеНаборыДанных            = Ложь;
	ИспользоватьПриВыводеЗаголовка             = Ложь;
	ИспользоватьПриВыводеПодвала               = Ложь;
	ИспользоватьПередКомпоновкойМакета         = Ложь;
	ИспользоватьПослеКомпоновкиМакета          = Ложь;
	ИспользоватьПередВыводомЭлементаРезультата = Ложь;
	ИспользоватьПослеВыводаРезультата          = Ложь;
	ИспользоватьДанныеРасшифровки              = Истина;
	
	
	Если ПараметрыИсполненияОтчета <> Неопределено Тогда
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьВнешниеНаборыДанных") Тогда
			ИспользоватьВнешниеНаборыДанных = ПараметрыИсполненияОтчета.ИспользоватьВнешниеНаборыДанных;
		КонецЕсли;
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьПриВыводеЗаголовка") Тогда
			ИспользоватьПриВыводеЗаголовка = ПараметрыИсполненияОтчета.ИспользоватьПриВыводеЗаголовка;
		КонецЕсли;
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьПриВыводеПодвала") Тогда
			ИспользоватьПриВыводеПодвала = ПараметрыИсполненияОтчета.ИспользоватьПриВыводеПодвала;
		КонецЕсли;
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьПередКомпоновкойМакета") Тогда
			ИспользоватьПередКомпоновкойМакета = ПараметрыИсполненияОтчета.ИспользоватьПередКомпоновкойМакета;
		КонецЕсли;
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьПослеКомпоновкиМакета") Тогда
			ИспользоватьПослеКомпоновкиМакета = ПараметрыИсполненияОтчета.ИспользоватьПослеКомпоновкиМакета;
		КонецЕсли;
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьПередВыводомЭлементаРезультата") Тогда
			ИспользоватьПередВыводомЭлементаРезультата = ПараметрыИсполненияОтчета.ИспользоватьПередВыводомЭлементаРезультата;
		КонецЕсли;
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьПослеВыводаРезультата") Тогда
			ИспользоватьПослеВыводаРезультата = ПараметрыИсполненияОтчета.ИспользоватьПослеВыводаРезультата;
		КонецЕсли;
		Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьДанныеРасшифровки") Тогда
			ИспользоватьДанныеРасшифровки = ПараметрыИсполненияОтчета.ИспользоватьДанныеРасшифровки;
		КонецЕсли;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрыОтчета.СхемаКомпоновкиДанных) = Тип("Строка") Тогда
		Если ЭтоАдресВременногоХранилища(ПараметрыОтчета.СхемаКомпоновкиДанных) Тогда
			СхемаКомпоновкиДанных = ПолучитьИзВременногоХранилища(ПараметрыОтчета.СхемаКомпоновкиДанных);
		КонецЕсли;
	Иначе
		СхемаКомпоновкиДанных = ПараметрыОтчета.СхемаКомпоновкиДанных;
	КонецЕсли;
	
	КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
	КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыОтчета.НастройкиКомпоновкиДанных);
	КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));
	
	ВыводитьЗаголовок = ?(ПараметрыОтчета.Свойство("ВыводитьЗаголовок") <> Ложь, ПараметрыОтчета.ВыводитьЗаголовок, Истина);
	ВыводитьПодписи   = ?(ПараметрыОтчета.Свойство("ВыводитьПодписи")   <> Ложь, ПараметрыОтчета.ВыводитьПодписи  , Истина);
	
	ВыводитьПодписиРуководителей = ?(ПараметрыОтчета.Свойство("ВыводитьПодписиРуководителей") <> Ложь,
	                                 ПараметрыОтчета.ВыводитьПодписиРуководителей, Ложь);
	
	Результат = Новый ТабличныйДокумент;
	
	Если ВыводитьЗаголовок Тогда
		Если ИспользоватьПриВыводеЗаголовка Тогда
			МенеджерОтчета.ПриВыводеЗаголовка(ПараметрыОтчета, Результат);
		Иначе
			ВывестиЗаголовокОтчета(ПараметрыОтчета, КомпоновщикНастроек, Результат);
		КонецЕсли;
	КонецЕсли;
	
	Если ВыводитьПолностью Тогда
		
		Если ИспользоватьПередКомпоновкойМакета Тогда
			// Очистка пользовательских настроек
			КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
			КомпоновщикНастроек.Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
			КомпоновщикНастроек.Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";

			МенеджерОтчета.ПередКомпоновкойМакета(ПараметрыОтчета, СхемаКомпоновкиДанных, КомпоновщикНастроек);
		КонецЕсли;
		КомпоновщикНастроек.Восстановить();
		
		НастройкиДляКомпоновкиМакета = КомпоновщикНастроек.ПолучитьНастройки();
		
		Если ПараметрыОтчета.Свойство("СвойМакетОформления") И ПараметрыОтчета.СвойМакетОформления
			И ПараметрыОтчета.Свойство("МакетОформления") И ЗначениеЗаполнено(ПараметрыОтчета.МакетОформления) Тогда
			СвойМакетОформления = МенеджерОтчета.ПолучитьМакет(ПараметрыОтчета.МакетОформления);
		Иначе
			УстановитьМакетОформленияОтчета(ПараметрыОтчета, НастройкиДляКомпоновкиМакета);
			СвойМакетОформления = Неопределено;
		КонецЕСли;
		
		//Сгенерируем макет компоновки данных при помощи компоновщика макета
		КомпоновщикМакета = Новый КомпоновщикМакетаКомпоновкиДанных;
		
		Попытка
			
			//В качестве схемы компоновки будет выступать схема самого отчета
			//В качестве настроек отчета - текущие настройки отчета
			//Данные расшифровки будем помещать в ДанныеРасшифровки
			Если ИспользоватьДанныеРасшифровки Тогда 
				МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиДляКомпоновкиМакета, ДанныеРасшифровкиОбъект, СвойМакетОформления);
			Иначе
				МакетКомпоновки = КомпоновщикМакета.Выполнить(СхемаКомпоновкиДанных, НастройкиДляКомпоновкиМакета,, СвойМакетОформления);
			КонецЕсли;
			
			Если ПараметрыОтчета.Свойство("КоличествоВыводимыхЗаписейВДиаграмме") Тогда
				СхемаКомпоновкиДанных.НаборыДанных[0].Запрос =
					СтрЗаменить(СхемаКомпоновкиДанных.НаборыДанных[0].Запрос, Врег("Первые ") + ПараметрыОтчета.КоличествоВыводимыхЗаписейВДиаграмме, Врег("Первые 7"));
			КонецЕсли;
			
			//Вызываем событие отчета
			Если ИспользоватьПослеКомпоновкиМакета Тогда
				МенеджерОтчета.ПослеКомпоновкиМакета(ПараметрыОтчета, МакетКомпоновки);
			КонецЕсли;
			
			Если ИспользоватьВнешниеНаборыДанных Тогда
				ВнешниеНаборыДанных = МенеджерОтчета.ПолучитьВнешниеНаборыДанных(ПараметрыОтчета, МакетКомпоновки);
			КонецЕсли;
		
			//Создадим и инициализируем процессор компоновки
			ПроцессорКомпоновки = Новый ПроцессорКомпоновкиДанных;
			Если ВнешниеНаборыДанных = Неопределено Тогда
				ПроцессорКомпоновки.Инициализировать(МакетКомпоновки,, ДанныеРасшифровкиОбъект, Истина);
			Иначе
				ПроцессорКомпоновки.Инициализировать(МакетКомпоновки, ВнешниеНаборыДанных, ДанныеРасшифровкиОбъект, Истина);
			КонецЕсли;	
			
						
			//Создадим и инициализируем процессор вывода результата
			ПроцессорВывода = Новый ПроцессорВыводаРезультатаКомпоновкиДанныхВТабличныйДокумент;
			ПроцессорВывода.УстановитьДокумент(Результат);
			
			//Обозначим начало вывода
			ПроцессорВывода.НачатьВывод();
			
			Если ИспользоватьПередВыводомЭлементаРезультата Тогда
				//Основной цикл вывода отчета
				Пока Истина Цикл
					
					//Получим следующий элемент результата компоновки
					ЭлементРезультата = ПроцессорКомпоновки.Следующий();
					
					Если ЭлементРезультата = Неопределено Тогда
						//Следующий элемент не получен - заканчиваем цикл вывода
						Прервать;
					Иначе
						
						Отказ = Ложь;
						
						МенеджерОтчета.ПередВыводомЭлементаРезультата(ПараметрыОтчета, МакетКомпоновки, ДанныеРасшифровкиОбъект, ЭлементРезультата, Отказ);
						
						Если Не Отказ Тогда
							//Элемент получен - выведем его при помощи процессора вывода
							ПроцессорВывода.ВывестиЭлемент(ЭлементРезультата);
						КонецЕсли;	
					КонецЕсли;
				КонецЦикла;
				
				//Завершение вывода отчета
				ПроцессорВывода.ЗакончитьВывод();
			Иначе
				ПроцессорВывода.Вывести(ПроцессорКомпоновки, Истина);
			КонецЕсли;
			
			//Если ПараметрыОтчета.Свойство("КоличествоВыводимыхЗаписейВДиаграмме") Тогда
			//	СхемаКомпоновкиДанных.НаборыДанных[0].Запрос =
			//		СтрЗаменить(СхемаКомпоновкиДанных.НаборыДанных[0].Запрос, Врег("Первые ") + ПараметрыОтчета.КоличествоВыводимыхЗаписейВДиаграмме, Врег("Первые 7"));
			//КонецЕсли;
			
			ДанныеДляРасшифровки = Новый Структура("Объект, ДанныеРасшифровки", ПараметрыОтчета, ДанныеРасшифровкиОбъект); 
			ДанныеРасшифровки = ПоместитьВоВременноеХранилище(ДанныеДляРасшифровки, ПараметрыОтчета.ДанныеРасшифровки);

			Если ПараметрыОтчета.Свойство("НастройкиКомпоновкиДанных") Тогда
				ПараметрыОтчета.НастройкиКомпоновкиДанных = НастройкиДляКомпоновкиМакета;
			КонецЕсли;
			
		Исключение
			// Запись в журнал регистрации не требуется
			ИнформацияОбОшибке = ИнформацияОбОшибке();
			Пока ИнформацияОбОшибке.Причина <> Неопределено Цикл
				ИнформацияОбОшибке = ИнформацияОбОшибке.Причина;
			КонецЦикла;
			ТекстСообщения = НСтр("ru = 'Отчет не сформирован!'") + Символы.ПС + ИнформацияОбОшибке.Описание;
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
			Отказ = Истина;
		КонецПопытки;
	КонецЕсли;
	
	Если Не Отказ Тогда
		Если ВыводитьПодписи Тогда
			Если ИспользоватьПриВыводеПодвала Тогда 
				МенеджерОтчета.ПриВыводеПодвала(ПараметрыОтчета, Результат);
			Иначе
				ВыводПодписейОтчета(ПараметрыОтчета, Результат);
			КонецЕсли;
		КонецЕсли;
		
		Если ИспользоватьПослеВыводаРезультата Тогда
			МенеджерОтчета.ПослеВыводаРезультата(ПараметрыОтчета, Результат);
		Иначе
			ОбработкаРезультатаОтчета(ПараметрыОтчета.ИдентификаторОтчета, Результат);
		КонецЕсли;
	КонецЕсли;
	
	// Если по каким-либо причинам отчет не был сформирован, адрес расшифровки оставляем прежним,
	// чтобы использовать его повторно при следующем формировании отчета.
	Если Отказ Тогда
		ДанныеРасшифровки = ПараметрыОтчета.ДанныеРасшифровки;
	КонецЕсли;
	
	ПоместитьВоВременноеХранилище(Новый Структура("Результат,ДанныеРасшифровки", Результат, ДанныеРасшифровки), АдресХранилища);
	
КонецПроцедуры

Процедура ВывестиЗаголовокОтчета(ПараметрыОтчета, КомпоновщикНастроек, Результат, ПланСчетов = "Типовой") Экспорт
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	ОбластьЗаголовок        = Макет.ПолучитьОбласть("ОбластьЗаголовок");
	ОбластьОписаниеНастроек = Макет.ПолучитьОбласть("ОписаниеНастроек");
	ОбластьОрганизация      = Макет.ПолучитьОбласть("Организация");
	
	// Организация
	Если ПараметрыОтчета.Свойство("СписокСтруктурныхЕдиниц") И ЗначениеЗаполнено(ПараметрыОтчета.СписокСтруктурныхЕдиниц) Тогда
		ТекстОрганизация = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ПараметрыОтчета.СписокСтруктурныхЕдиниц);
		ОбластьОрганизация.Параметры.НазваниеОрганизации = ТекстОрганизация;
		Результат.Вывести(ОбластьОрганизация);
	КонецЕсли;
			
	Если ПараметрыОтчета.Свойство("ОтчетОбъект") Тогда
		МенеджерОтчета   = ПараметрыОтчета.ОтчетОбъект;
		МетаданныеОтчета = ПараметрыОтчета.ОтчетОбъект.Метаданные();
	Иначе
		МенеджерОтчета   = Отчеты[ПараметрыОтчета.ИдентификаторОтчета];
		МетаданныеОтчета = Метаданные.Отчеты[ПараметрыОтчета.ИдентификаторОтчета];
	КонецЕсли;
	
	// Текст заголовка
	ОбластьЗаголовок.Параметры.ЗаголовокОтчета = МенеджерОтчета.ПолучитьТекстЗаголовка(ПараметрыОтчета);
	Результат.Вывести(ОбластьЗаголовок);
	
	// Наименование и БИН налогоплательщика для расшифровки регл.отчетов
	Если ПараметрыОтчета.Свойство("РежимРасшифровки")
		И ПараметрыОтчета.РежимРасшифровки 
		И ПараметрыОтчета.ИдентификаторОтчета = "УниверсальныйОтчетПоМетаданным" 
		И ПараметрыОтчета.Свойство("Налогоплательщик") 
		И ЗначениеЗаполнено(ПараметрыОтчета.Налогоплательщик)Тогда
		СведенияОНалогоплательщике = ОбщегоНазначенияБКВызовСервера.СведенияОЮрФизЛице(ПараметрыОтчета.Налогоплательщик, ПараметрыОтчета.КонецПериода);
		ОбластьЗаголовокРасшифровка = Макет.ПолучитьОбласть("ОбластьЗаголовокРасшифровка");
		ОбластьЗаголовокРасшифровка.Параметры.Заполнить(СведенияОНалогоплательщике);
		ОбластьЗаголовокРасшифровка.Параметры.Период = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПредставлениеПериода(НачалоДня(ПараметрыОтчета.НачалоПериода), КонецДня(ПараметрыОтчета.КонецПериода), Истина);
		Результат.Вывести(ОбластьЗаголовокРасшифровка);
	КонецЕсли;

	ИдентификаторОбъекта = "ОтчетОбъект." + ПараметрыОтчета.ИдентификаторОтчета;
	
	// Показатели отчета
	ПоказыватьВыводимыеДанные = Ложь;
	ТекстВыводимыеДанные = "";
	КоличествоПоказателей = 0;
	
	Если ПараметрыОтчета.Свойство("НаборПоказателей") Тогда
		Для Каждого ИмяПоказателя Из ПараметрыОтчета.НаборПоказателей Цикл
			Если ПараметрыОтчета["Показатель" + ИмяПоказателя] И ИмяПоказателя <> "РазвернутоеСальдо" Тогда
				ТекстВыводимыеДанные = ТекстВыводимыеДанные + МетаданныеОтчета.Реквизиты["Показатель" + ИмяПоказателя].Синоним + ", ";
				КоличествоПоказателей = КоличествоПоказателей + 1;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	ТекстВыводимыеДанные = Лев(ТекстВыводимыеДанные, СтрДлина(ТекстВыводимыеДанные) - 2);
	
	Если КоличествоПоказателей = 1 Тогда
		ПоказыватьВыводимыеДанные = Истина;
	КонецЕсли;
	Если ПоказыватьВыводимыеДанные Тогда 
		ОбластьОписаниеНастроек.Параметры.ИмяНастроекОтчета      = НСтр("ru = 'Выводимые данные:'");
		ОбластьОписаниеНастроек.Параметры.ОписаниеНастроекОтчета = ТекстВыводимыеДанные;
		Результат.Вывести(ОбластьОписаниеНастроек);
	КонецЕсли;
	
	// Отбор
	ТекстОтбор = Строка(КомпоновщикНастроек.Настройки.Отбор);
	ТекстПодразделение = "";
	
	ЕстьОтборПоПодразделению = Ложь;
	Для Каждого ЭлементОтбора Из КомпоновщикНастроек.Настройки.Отбор.Элементы Цикл
		Если ТипЗнч(ЭлементОтбора) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
			Если ЭлементОтбора.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") 
				И ЭлементОтбора.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии Тогда 
				ЕстьОтборПоПодразделению = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Если Не ЕстьОтборПоПодразделению Тогда 
		Если ПараметрыОтчета.Свойство("СписокПодразделений") И ЗначениеЗаполнено(ПараметрыОтчета.СписокПодразделений) Тогда
			ТекстПодразделение = НСтр("ru = 'Подразделение В группе ""%1""'");
			ТекстПодразделение = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			                     	ТекстПодразделение, БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ПараметрыОтчета.СписокПодразделений));
		КонецЕсли;
		Если Не ПустаяСтрока(ТекстОтбор) И Не ПустаяСтрока(ТекстПодразделение) Тогда
			ТекстОтбор = ТекстПодразделение + НСтр("ru = ' И '") + ТекстОтбор;
		ИначеЕсли ПустаяСтрока(ТекстОтбор) И Не ПустаяСтрока(ТекстПодразделение) Тогда
			ТекстОтбор = ТекстПодразделение;
		КонецЕсли;
	КонецЕсли;
	
	ТекстОтбор = СтрЗаменить(ТекстОтбор, "И ###ОтборПоОрганизации###", "");
	ТекстОтбор = СтрЗаменить(ТекстОтбор, "###ОтборПоОрганизации### И", "");
	ТекстОтбор = СтрЗаменить(ТекстОтбор, "###ОтборПоОрганизации###", "");
	ТекстОтбор = СокрЛП(ТекстОтбор);
	
	Если Не ПустаяСтрока(ТекстОтбор) Тогда
		ОбластьОписаниеНастроек.Параметры.ИмяНастроекОтчета      = НСтр("ru = 'Отбор:'");
		ОбластьОписаниеНастроек.Параметры.ОписаниеНастроекОтчета = ТекстОтбор;
		Результат.Вывести(ОбластьОписаниеНастроек);
	КонецЕсли;
	
	Если ИдентификаторОбъекта <>  ("ОтчетОбъект.ОборотноСальдоваяВедомость" + ПланСчетов) 
		И ИдентификаторОбъекта <> ("ОтчетОбъект.КарточкаСчета" + ПланСчетов)
		И ИдентификаторОбъекта <> ("ОтчетОбъект.КарточкаСубконто" + ПланСчетов)
		И ИдентификаторОбъекта <> ("ОтчетОбъект.СверкаДанныхБУ_НУ")
        И ИдентификаторОбъекта <> ("ОтчетОбъект.ОтчетПоПроводкам" + ПланСчетов) Тогда
		// Сортировка
		ТекстСортировка = "";
		Для Каждого СтрокаПорядок Из КомпоновщикНастроек.Настройки.Порядок.Элементы Цикл
			Если СтрокаПорядок.Использование Тогда
				ТекстСортировка = ТекстСортировка + БухгалтерскиеОтчетыКлиентСервер.ПолучитьСвойствоПоля(КомпоновщикНастроек, СтрокаПорядок.Поле);
				ТекстСортировка = ТекстСортировка + " " + Строка(СтрокаПорядок.ТипУпорядочивания) + ", ";
			КонецЕсли;
		КонецЦикла;
		ТекстСортировка = Лев(ТекстСортировка, СтрДлина(ТекстСортировка) - 2);
		
		Если Не ПустаяСтрока(ТекстСортировка) Тогда
			ОбластьОписаниеНастроек.Параметры.ИмяНастроекОтчета      = НСтр("ru = 'Сортировка:'");
			ОбластьОписаниеНастроек.Параметры.ОписаниеНастроекОтчета = ТекстСортировка;
			Результат.Вывести(ОбластьОписаниеНастроек);
		КонецЕсли;
	КонецЕсли;
	
	Результат.Область("R1:R" + Результат.ВысотаТаблицы).Имя = "Заголовок";
	Результат.Области.Заголовок.Видимость = ПараметрыОтчета.ВыводитьЗаголовок;
	
КонецПроцедуры

Процедура УстановитьМакетОформленияОтчета(ПараметрыОтчета, НастройкаКомпоновкиДанных) Экспорт
	
	Если ПараметрыОтчета.Свойство("МакетОформления") И ЗначениеЗаполнено(ПараметрыОтчета.МакетОформления) Тогда
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(НастройкаКомпоновкиДанных, "МакетОформления", ПараметрыОтчета.МакетОформления);
	Иначе
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(НастройкаКомпоновкиДанных, "МакетОформления", "МакетОформленияОтчетов");  // "МакетОформленияОтчетовЗеленый" "green" "Main"
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаРезультатаОтчета(ИдентификаторОтчета, Результат) Экспорт
	
	// Выводим надписи вертикально, если количество точек диаграмм больше 9
	Для Каждого Рисунок Из Результат.Рисунки Цикл
		Попытка
			Если ТипЗнч(Рисунок.Объект) = Тип("Диаграмма") Тогда
				Рисунок.Объект.ОбластьПостроения.ВертикальныеМетки  = (Рисунок.Объект.Точки.Количество() > 9);
				Рисунок.Объект.ОбластьПостроения.ШкалаЗначений.ФорматПодписей = "ЧГ=3,0";
				Рисунок.Объект.ОбластьЛегенды.Рамка = Новый Рамка(ТипРамкиЭлементаУправления.Одинарная, 1);
				
				Рисунок.Объект.ПоложениеПодписейШкалыЗначенийИзмерительнойДиаграммы = ПоложениеПодписейШкалыЗначенийИзмерительнойДиаграммы.НаШкале;
				Рисунок.Объект.ПодписиШкалыЗначенийИзмерительнойДиаграммыВдольШкалы = Истина;
				Рисунок.Объект.ТолщинаШкалыИзмерительнойДиаграммы                   = 3;
				Рисунок.Объект.ФорматЗначенийВПодписях                              = "ЧДЦ=2; ЧГ=3,0";
				
				Рисунок.Объект.ОписаниеПалитрыЦветов.ПалитраЦветов = ПалитраЦветовДиаграммы.Палитра32;
				Рисунок.Объект.ОбластьПостроения.ШкалаЗначений.ОтображениеЗаголовка = ОтображениеЗаголовкаШкалыДиаграммы.НеОтображать;
				Рисунок.Объект.ПорядокСерийВЛегенде = ПорядокСерийВЛегендеДиаграммы.Прямой;
			КонецЕсли;
		Исключение
		КонецПопытки;
	КонецЦикла;
	
	//Удалим строки с высотой равной 1
	Индекс = Результат.ВысотаТаблицы;
	Пока Индекс > 0 Цикл
		ИндексСтроки = "R" + Формат(Индекс, "ЧГ=0");
		Если Результат.Область(ИндексСтроки).ВысотаСтроки = 1 Тогда
			Результат.УдалитьОбласть(Результат.Область(ИндексСтроки), ТипСмещенияТабличногоДокумента.ПоВертикали);
		КонецЕсли;
		Индекс = Индекс - 1;
	КонецЦикла;
		
	Результат.АвтоМасштаб = Истина;
	
	Результат.ПолеСлева   = 5;
	Результат.ПолеСправа  = 5;
	
	Результат.КлючПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_" + ИдентификаторОтчета;
		
	УправлениеКолонтитулами.УстановитьКолонтитулы(
		Результат, Метаданные.Отчеты[ИдентификаторОтчета].Синоним, Пользователи.ТекущийПользователь());

КонецПроцедуры

// Проверяет необходимость выполнения обработки проверки заполнения.
// 
// Параметры:
//  ОтчетОбъект - ОтчетОбъект.* - Проверяемый отчет.
// 
// Возвращаемое значение:
//  Булево - Истина, если проверка не требуется, Ложь в противном случае.
//
Функция ПропуститьПроверкуЗаполнения(ОтчетОбъект) Экспорт
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ОтчетОбъект, "КомпоновщикНастроек") Тогда
		
		ДополнительныеСвойства = ОтчетОбъект.КомпоновщикНастроек.Настройки.ДополнительныеСвойства;
		
		Если ДополнительныеСвойства.Свойство("ПропуститьПроверкуЗаполнения")
			И ДополнительныеСвойства.ПропуститьПроверкуЗаполнения Тогда
			Возврат Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Ложь;
	
КонецФункции

Процедура ОбработкаПроверкиЗаполнения(ОтчетОбъект, Отказ, Проверки = Неопределено) Экспорт
	
	Если ПропуститьПроверкуЗаполнения(ОтчетОбъект) Тогда
		Возврат;
	КонецЕсли;
	
	Если Проверки = Неопределено Тогда
		Проверки = Новый Структура("КорректностьПериода, ВыборПоказателя", Истина, Истина);
	КонецЕсли;
	
	Если Проверки.Свойство("КорректностьПериода") И Проверки.КорректностьПериода Тогда
		Если ЗначениеЗаполнено(ОтчетОбъект.КонецПериода) И ОтчетОбъект.НачалоПериода > ОтчетОбъект.КонецПериода Тогда
			ТекстСообщения = НСтр("ru = 'Дата начала периода не может быть больше даты конца периода'");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Отчет.НачалоПериода",, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Проверки.Свойство("ВыборПоказателя") И Проверки.ВыборПоказателя Тогда
		КоличествоПоказателей = 0;
		ПервыйПоказатель = "";
		НаборПоказателей = Отчеты[ОтчетОбъект.Метаданные().Имя].ПолучитьНаборПоказателей();
		Для Каждого ЭлементНабора Из НаборПоказателей Цикл
			КоличествоПоказателей = КоличествоПоказателей + (ОтчетОбъект["Показатель" + ЭлементНабора] И ЭлементНабора <> "РазвернутоеСальдо");
			Если ПустаяСтрока(ПервыйПоказатель) Тогда
				ПервыйПоказатель = "Показатель" + ЭлементНабора;
			КонецЕсли;
		КонецЦикла;
		
		Если КоличествоПоказателей = 0 И НаборПоказателей.Количество() > 0 Тогда
			ТекстСообщения = НСтр("ru = 'Не выбран показатель(и)'");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Отчет." + ПервыйПоказатель,, Отказ);
		КонецЕсли;
	КонецЕсли;
	
	Если Проверки.Свойство("СписокОрганизаций") И Проверки.СписокОрганизаций Тогда
		
		Если НЕ ЗначениеЗаполнено(ОтчетОбъект.ПредставлениеСпискаОрганизаций) Тогда
			ТекстСообщения = НСтр("ru = 'Не указана организация.'");
			
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Отчет.ПредставлениеСпискаОрганизаций",, Отказ);
		КонецЕсли;
		
	КонецЕсли;
	
	Если Проверки.Свойство("СписокВидовСубконто") И Проверки.СписокВидовСубконто Тогда
		МаксКоличествоСубконто = ПроцедурыБухгалтерскогоУчета.МаксимальноеКоличествоСубконто();
		Если ОтчетОбъект.СписокВидовСубконто.Количество() > МаксКоличествоСубконто Тогда
			ТекстСообщения = НСтр("ru = 'Выбрано слишком много видов субконто, максимально допустимо %1'");
			ОбщегоНазначения.СообщитьПользователю(СтрШаблон(ТекстСообщения, МаксКоличествоСубконто),, "Отчет.СписокВидовСубконто",, Отказ);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

Функция ПолучитьКопиюОписанияМакета(ОписаниеМакета, ФормаЗаписи = Неопределено) Экспорт
	
	Если ФормаЗаписи = Неопределено Тогда
		ФормаЗаписи = "{http://v8.1c.ru/8.1/data-composition-system/composition-template}";
	КонецЕсли;
	
	ЗаписьXML = Новый ЗаписьXML();
	ЗаписьXML.УстановитьСтроку();
	СериализаторXDTO.ЗаписатьXML(ЗаписьXML, ОписаниеМакета, "item", ФормаЗаписи);
	СтрокаXML = ЗаписьXML.Закрыть();
	
	ЧтениеXML = Новый ЧтениеXML();
	ЧтениеXML.УстановитьСтроку(СтрокаXML);
	
	Возврат СериализаторXDTO.ПрочитатьXML(ЧтениеXML, ТипЗнч(ОписаниеМакета));
	
КонецФункции

Процедура ПриСохраненииПользовательскихНастроекНаСервере(ФормаОтчета, Настройки, СохранятьТолькоРеквизиты = Ложь, СохраняемыеРеквизитыФормы = Неопределено) Экспорт
	
	ОтчетОбъект = ФормаОтчета.РеквизитФормыВЗначение("Отчет");
	
	ОтчетМетаданные = ОтчетОбъект.Метаданные();
	
	Если НЕ СохранятьТолькоРеквизиты Тогда
		ТекущиеНастройки = ОтчетОбъект.КомпоновщикНастроек.Настройки;
		
		// Очистка пользовательских настроек
		ТекущиеНастройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
		ТекущиеНастройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
		ТекущиеНастройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
		
		// Установка пользовательских настроек
		ТекущиеНастройки.Отбор.ИдентификаторПользовательскойНастройки              = "Отбор";
		ТекущиеНастройки.Порядок.ИдентификаторПользовательскойНастройки            = "Порядок";
		ТекущиеНастройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "УсловноеОформление";
	КонецЕсли;

	// Сохранение реквизитов отчета
	ДополнительныеСвойства = Новый Структура;
	Для Каждого Реквизит Из ОтчетМетаданные.Реквизиты Цикл
		Если Реквизит.Имя <> "РежимРасшифровки" Тогда
			ДополнительныеСвойства.Вставить(Реквизит.Имя, ОтчетОбъект[Реквизит.Имя]);
		КонецЕсли;
	КонецЦикла;
	Для Каждого Реквизит Из ОтчетМетаданные.ТабличныеЧасти Цикл
		ДополнительныеСвойства.Вставить(Реквизит.Имя, ОтчетОбъект[Реквизит.Имя].Выгрузить());
	КонецЦикла;
	
	// Сохранение реквизитов формы
	Если ЕстьРеквизитФормы(ФормаОтчета, "ВыводитьЗаголовок") Тогда
		ДополнительныеСвойства.Вставить("ВыводитьЗаголовок", ФормаОтчета.ВыводитьЗаголовок);
	КонецЕсли;
	Если ЕстьРеквизитФормы(ФормаОтчета, "ВыводитьПодписиРуководителей") Тогда
		ДополнительныеСвойства.Вставить("ВыводитьПодписиРуководителей", ФормаОтчета.ВыводитьПодписиРуководителей);
	КонецЕсли;
	Если ЕстьРеквизитФормы(ФормаОтчета, "ВыводитьПодписи") Тогда
		ДополнительныеСвойства.Вставить("ВыводитьПодписи", ФормаОтчета.ВыводитьПодписи);
	КонецЕсли;
	Если ЕстьРеквизитФормы(ФормаОтчета, "ВыводитьДиаграмму") Тогда
		ДополнительныеСвойства.Вставить("ВыводитьДиаграмму", ФормаОтчета.ВыводитьДиаграмму);
	КонецЕсли;
	Если ЕстьРеквизитФормы(ФормаОтчета, "МакетОформления") Тогда
		ДополнительныеСвойства.Вставить("МакетОформления", ФормаОтчета.МакетОформления);
	КонецЕсли;
	
	Если ЕстьРеквизитФормы(ФормаОтчета, "СкрыватьНастройкиПриФормированииОтчета") Тогда
		ДополнительныеСвойства.Вставить("СкрыватьНастройкиПриФормированииОтчета", ФормаОтчета.СкрыватьНастройкиПриФормированииОтчета);
	КонецЕсли;
	
	Если ЕстьЭлементФормы(ФормаОтчета, "ГруппаПанельНастроек") Тогда
		Если Настройки.ДополнительныеСвойства.Свойство("ПанельНастроекСкрытаАвтоматически")
			И Настройки.ДополнительныеСвойства.ПанельНастроекСкрытаАвтоматически = Истина Тогда
			ДополнительныеСвойства.Вставить("ВидимостьПанелиНастроек", Истина);
		Иначе
			ДополнительныеСвойства.Вставить("ВидимостьПанелиНастроек", ФормаОтчета.Элементы.ГруппаПанельНастроек.Видимость);
		КонецЕсли;
	КонецЕсли;
	
	Если ЕстьРеквизитФормы(ФормаОтчета, "СписокСтруктурныхЕдиниц") Тогда
		ДополнительныеСвойства.Вставить("СписокСтруктурныхЕдиниц", ФормаОтчета.СписокСтруктурныхЕдиниц);
	КонецЕсли;
	
	Если ЕстьРеквизитФормы(ФормаОтчета, "СписокПодразделений") Тогда
		ДополнительныеСвойства.Вставить("СписокПодразделений", ФормаОтчета.СписокПодразделений);
	КонецЕсли;
	
	Если ЕстьРеквизитФормы(ФормаОтчета, "СписокВладельцевГоловныхПодразделений") Тогда
		ДополнительныеСвойства.Вставить("СписокВладельцевГоловныхПодразделений", ФормаОтчета.СписокВладельцевГоловныхПодразделений);
	КонецЕсли;
	
	Если СохраняемыеРеквизитыФормы <> Неопределено И ТипЗнч(СохраняемыеРеквизитыФормы) = Тип("Массив") Тогда
		Для Каждого РеквизитФормы Из СохраняемыеРеквизитыФормы Цикл
			ДополнительныеСвойства.Вставить(РеквизитФормы, ФормаОтчета[РеквизитФормы]);
		КонецЦикла;
	КонецЕсли;
	
	Если (ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ОткрытИзРассылки") И ФормаОтчета.ОткрытИзРассылки) Тогда
		
		Настройки.ДополнительныеСвойства.Вставить("ДанныеОтчетаРассылка", Новый ХранилищеЗначения(ДополнительныеСвойства));

		Если Настройки.ДополнительныеСвойства.Свойство("ДанныеОтчета") Тогда
			Настройки.ДополнительныеСвойства.Удалить("ДанныеОтчета");
		КонецЕсли;
	
		ФормаОтчета.ЗначениеВРеквизитФормы(ОтчетОбъект, "Отчет");

	Иначе
		Настройки.ДополнительныеСвойства.Вставить("ДанныеОтчета", Новый ХранилищеЗначения(ДополнительныеСвойства));
		Если Настройки.ДополнительныеСвойства.Свойство("ДанныеОтчетаРассылка") Тогда
			Настройки.ДополнительныеСвойства.Удалить("ДанныеОтчетаРассылка");
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриЗагрузкеПользовательскихНастроекНаСервере(ФормаОтчета, Настройки, ЗагружатьТолькоРеквизиты = Ложь, ЗагружаемыеРеквизитыФормы = Неопределено) Экспорт

	Если Настройки = Неопределено Тогда
		// Установка настроек по умолчанию
		УстановитьНастройкиПоУмолчанию(ФормаОтчета);		
	Иначе 

		ОткрытИзРассылки = Ложь;
		Если (ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ОткрытИзРассылки") И ФормаОтчета.ОткрытИзРассылки) Тогда
		    ДанныеОтчета = "ДанныеОтчетаРассылка";
			ОткрытИзРассылки = ФормаОтчета.ОткрытИзРассылки;
			ПараметрыПериода = Новый Структура("ПериодОтчета, НачалоПериода, КонецПериода, Период");
		Иначе
		    ДанныеОтчета = "ДанныеОтчета";
			ПараметрыПериода = Новый Структура;
		КонецЕсли;
		
		Если Настройки.ДополнительныеСвойства.Свойство(ДанныеОтчета) Тогда
			ДоступныеОрганизации   = ОбщегоНазначенияБКВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
			ДоступныеПодразделения = БухгалтерскиеОтчеты.ПолучитьСписокДоступныхПодразделений().ВыгрузитьКолонку("Подразделение");
			ДополнительныеСвойства = Настройки.ДополнительныеСвойства[ДанныеОтчета].Получить();
			Для Каждого ЭлементСтруктуры Из ДополнительныеСвойства Цикл
				// Восстановление реквизитов отчета
				Если ОткрытИзРассылки И ПараметрыПериода.Свойство(ЭлементСтруктуры.Ключ) Тогда
					Продолжить;
				КонецЕсли;
				Если ФормаОтчета.Отчет.Свойство(ЭлементСтруктуры.Ключ) Тогда
					Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("ТаблицаЗначений") Тогда
						ФормаОтчета.Отчет[ЭлементСтруктуры.Ключ].Очистить();
						ОбщегоНазначенияКлиентСервер.ДополнитьТаблицу(ЭлементСтруктуры.Значение, ФормаОтчета.Отчет[ЭлементСтруктуры.Ключ]);
						Продолжить;
					ИначеЕсли ЭлементСтруктуры.Ключ <> "РежимРасшифровки" Тогда
						ФормаОтчета.Отчет[ЭлементСтруктуры.Ключ] = ЭлементСтруктуры.Значение;
					КонецЕсли;
				КонецЕсли;
				
				// Восстановление реквизитов формы
				Если ЭлементСтруктуры.Ключ = "ВыводитьЗаголовок" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьЗаголовок") Тогда
					ФормаОтчета.ВыводитьЗаголовок = ЭлементСтруктуры.Значение;
				ИначеЕсли ЭлементСтруктуры.Ключ = "ВыводитьПодписи" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьПодписи") Тогда
					ФормаОтчета.ВыводитьПодписи = ЭлементСтруктуры.Значение;
				ИначеЕсли ЭлементСтруктуры.Ключ = "ВыводитьПодписиРуководителей" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьПодписиРуководителей") Тогда
					ФормаОтчета.ВыводитьПодписиРуководителей = ЭлементСтруктуры.Значение;
				ИначеЕсли ЭлементСтруктуры.Ключ = "ВыводитьДиаграмму" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьДиаграмму") Тогда
					ФормаОтчета.ВыводитьДиаграмму = ЭлементСтруктуры.Значение;
				ИначеЕсли ЭлементСтруктуры.Ключ = "МакетОформления" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "МакетОформления") Тогда
					ФормаОтчета.МакетОформления = ЭлементСтруктуры.Значение;
					БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметрВывода(
						ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки, 
						"МакетОформления", ФормаОтчета.МакетОформления);
				КонецЕсли;
				Если ЭлементСтруктуры.Ключ = "СкрыватьНастройкиПриФормированииОтчета" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СкрыватьНастройкиПриФормированииОтчета") Тогда
					ФормаОтчета.СкрыватьНастройкиПриФормированииОтчета = ЭлементСтруктуры.Значение;		
				КонецЕсли;
				Если ЭлементСтруктуры.Ключ = "ВидимостьПанелиНастроек" И НЕ ЕстьЭлементФормы(ФормаОтчета, "РазделыОтчета") Тогда
					Если ЕстьЭлементФормы(ФормаОтчета, "ПанельНастроек") Тогда
						БухгалтерскиеОтчетыКлиентСервер.ИзменитьЗаголовокКнопкиПанельНастроек(
							ФормаОтчета.Элементы.ПанельНастроек, ДополнительныеСвойства.ВидимостьПанелиНастроек);
					КонецЕсли;
					Если ЕстьЭлементФормы(ФормаОтчета, "ГруппаПанельНастроек") Тогда
 						ФормаОтчета.Элементы.ГруппаПанельНастроек.Видимость = ДополнительныеСвойства.ВидимостьПанелиНастроек;
					КонецЕсли;
				КонецЕсли;
				Если ЭлементСтруктуры.Ключ = "СписокСтруктурныхЕдиниц" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СписокСтруктурныхЕдиниц") Тогда
					ФормаОтчета.СписокСтруктурныхЕдиниц = ЭлементСтруктуры.Значение.Скопировать();
					// Доступ к организации мог быть изменен с момента сохранения настроек.
					// Недоступную организацию исключим из состава настроек.
					Для Каждого СтруктурнаяЕдиница Из ЭлементСтруктуры.Значение Цикл
						Если ДоступныеОрганизации.Найти(СтруктурнаяЕдиница.Значение) = Неопределено Тогда
							НайденныйЭлемент = ФормаОтчета.СписокСтруктурныхЕдиниц.НайтиПоЗначению(СтруктурнаяЕдиница.Значение);
							Если НайденныйЭлемент <> Неопределено Тогда
							    ФормаОтчета.СписокСтруктурныхЕдиниц.Удалить(НайденныйЭлемент);
							КонецЕсли;							
						КонецЕсли;
					КонецЦикла;
					Если ФормаОтчета.Отчет.Свойство("ПредставлениеСпискаОрганизаций") Тогда
						ФормаОтчета.Отчет.ПредставлениеСпискаОрганизаций = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ФормаОтчета.СписокСтруктурныхЕдиниц);
					КонецЕсли;
					Если ФормаОтчета.Отчет.Свойство("ПредставлениеСпискаСтруктурныхЕдиниц") Тогда
						ФормаОтчета.Отчет.ПредставлениеСпискаСтруктурныхЕдиниц = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ФормаОтчета.СписокСтруктурныхЕдиниц);
					КонецЕсли;
				КонецЕсли;
				Если ЭлементСтруктуры.Ключ = "СписокПодразделений" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СписокПодразделений") Тогда
					ФормаОтчета.СписокПодразделений = ЭлементСтруктуры.Значение.Скопировать();
					// В списке подразделений оставим доступные подразделения.
					Для Каждого Подразделение Из ЭлементСтруктуры.Значение Цикл
						Если ДоступныеПодразделения.Найти(Подразделение.Значение) = Неопределено Тогда
							НайденныйЭлемент = ФормаОтчета.СписокПодразделений.НайтиПоЗначению(Подразделение.Значение);
							Если НайденныйЭлемент <> Неопределено Тогда
							    ФормаОтчета.СписокПодразделений.Удалить(НайденныйЭлемент);
							КонецЕсли;							
						КонецЕсли;
					КонецЦикла;
					Если ФормаОтчета.Отчет.Свойство("ПредставлениеСпискаПодразделений") Тогда
						ФормаОтчета.Отчет.ПредставлениеСпискаПодразделений = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ФормаОтчета.СписокПодразделений);
					КонецЕсли;
				КонецЕсли;
				Если ЭлементСтруктуры.Ключ = "СписокВладельцевГоловныхПодразделений" И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "СписокВладельцевГоловныхПодразделений") Тогда
					ФормаОтчета.СписокВладельцевГоловныхПодразделений = ЭлементСтруктуры.Значение.Скопировать();
					// В списке владельцев головных подразделений оставим доступные организации.
					Для Каждого ВладелецГоловного Из ЭлементСтруктуры.Значение Цикл
						Если ДоступныеОрганизации.Найти(ВладелецГоловного.Значение) = Неопределено Тогда
							НайденныйЭлемент = ФормаОтчета.СписокВладельцевГоловныхПодразделений.НайтиПоЗначению(ВладелецГоловного.Значение);
							Если НайденныйЭлемент <> Неопределено Тогда
							    ФормаОтчета.СписокВладельцевГоловныхПодразделений.Удалить(НайденныйЭлемент);
							КонецЕсли;							
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
			Если НЕ ДополнительныеСвойства.Свойство("ВыводитьДиаграмму") И ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "ВыводитьДиаграмму") Тогда
				ФормаОтчета.ВыводитьДиаграмму = Истина;
			КонецЕсли;
		КонецЕсли;
		Если Не ЗагружатьТолькоРеквизиты Тогда
			ТекущиеНастройки = ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки;
			
			// Установка пользовательских настроек
			ТекущиеНастройки.Отбор.ИдентификаторПользовательскойНастройки              = "Отбор";
			ТекущиеНастройки.Порядок.ИдентификаторПользовательскойНастройки            = "Порядок";
			ТекущиеНастройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "УсловноеОформление";
			
			// Перенос пользовательских настроек в основные
			ФормаОтчета.Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(Настройки);
			ФормаОтчета.Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(ФормаОтчета.Отчет.КомпоновщикНастроек.ПолучитьНастройки());
			
			// Очистка пользовательских настроек
			ТекущиеНастройки = ФормаОтчета.Отчет.КомпоновщикНастроек.Настройки;
			ТекущиеНастройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
			ТекущиеНастройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
			ТекущиеНастройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
		КонецЕсли;		
	КонецЕсли;
	
	Если ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ФормаОтчета, "МинимальныйВидПериода") Тогда
		МинимальныйВидПериода = ФормаОтчета.МинимальныйВидПериода;
	Иначе
		МинимальныйВидПериода = Неопределено;
	КонецЕсли;
	
	ИдентификаторОбъекта = БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(ФормаОтчета);
	Если ИдентификаторОбъекта = "ОборотноСальдоваяВедомостьТиповой"
		ИЛИ ИдентификаторОбъекта = "ОтчетОбъект.ОборотноСальдоваяВедомостьНалоговый" Тогда
		Если ФормаОтчета.Отчет.ДополнительныеПоля.НайтиСтроки(Новый Структура("Поле", "Счет.Наименование")).Количество() = 0 Тогда
			НоваяСтрока = ФормаОтчета.Отчет.ДополнительныеПоля.Добавить();
			НоваяСтрока.Представление = НСтр("ru = 'Выводить наименование счета'");
			НоваяСтрока.Поле          = "Счет.Наименование";
			НоваяСтрока.Использование = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Если ЗагружаемыеРеквизитыФормы <> Неопределено
		И ТипЗнч(ЗагружаемыеРеквизитыФормы) = Тип("Массив")
		И ДополнительныеСвойства <> Неопределено Тогда
		
		Для Каждого РеквизитФормы Из ЗагружаемыеРеквизитыФормы Цикл
			Если ДополнительныеСвойства.Свойство(РеквизитФормы) Тогда
				ФормаОтчета[РеквизитФормы] = ДополнительныеСвойства[РеквизитФормы];
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
		
КонецПроцедуры

// Возвращает доступное поле по полю компоновки
Функция ПолучитьДоступноеПолеПоПолюКомпоновкиДанных(ПолеКомпоновкиДанных, ОбластьПоиска) Экспорт
	
	Если ТипЗнч(ПолеКомпоновкиДанных) = Тип("Строка") Тогда
		ПолеПоиска = Новый ПолеКомпоновкиДанных(ПолеКомпоновкиДанных);
	Иначе
		ПолеПоиска = ПолеКомпоновкиДанных;
	КонецЕсли;
	
	Если ТипЗнч(ОбластьПоиска) = Тип("КомпоновщикНастроекКомпоновкиДанных")
	 Или ТипЗнч(ОбластьПоиска) = Тип("ДанныеРасшифровкиКомпоновкиДанных")
	 Или ТипЗнч(ОбластьПоиска) = Тип("НастройкиВложенногоОбъектаКомпоновкиДанных") Тогда
		Возврат ОбластьПоиска.Настройки.ДоступныеПоляВыбора.НайтиПоле(ПолеПоиска);
	Иначе
		Возврат ОбластьПоиска.НайтиПоле(ПолеПоиска);
	КонецЕсли;
	
КонецФункции

// Возвращает массив, по которому следует расшифровать отчет
Функция ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровки, ТекущийОтчет = Неопределено, ВключатьРесурсы = Ложь) Экспорт
	
	МассивПолейРасшифровки = Новый Массив;
	
	Если ТипЗнч(Расшифровка) <> Тип("ИдентификаторРасшифровкиКомпоновкиДанных") 
	   И ТипЗнч(Расшифровка) <> Тип("ДанныеРасшифровкиКомпоновкиДанных") Тогда
		Возврат МассивПолейРасшифровки;
	КонецЕсли;
	
	Если ТекущийОтчет = Неопределено Тогда
		ТекущийОтчет = ДанныеРасшифровки;
	КонецЕсли;
	
	// Добавим поля родительских группировок
	ДобавитьРодителей(ДанныеРасшифровки.Элементы[Расшифровка], ТекущийОтчет, МассивПолейРасшифровки, ВключатьРесурсы);
	
	Количество = МассивПолейРасшифровки.Количество();
	Для Индекс = 1 По Количество Цикл
		ОбратныйИндекс = Количество - Индекс;
		Для ИндексВнутри = 0 По ОбратныйИндекс - 1 Цикл
			Если МассивПолейРасшифровки[ОбратныйИндекс].Поле = МассивПолейРасшифровки[ИндексВнутри].Поле Тогда
				МассивПолейРасшифровки.Удалить(ОбратныйИндекс);
				Прервать;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	// Добавим отбор, установленный в отчете
	Для каждого ЭлементОтбора Из ТекущийОтчет.Настройки.Отбор.Элементы Цикл
		Если Не ЭлементОтбора.Использование Тогда
			Продолжить;
		КонецЕсли;
		МассивПолейРасшифровки.Добавить(ЭлементОтбора);
	КонецЦикла;
	
	Возврат МассивПолейРасшифровки;
	
КонецФункции

Функция ПолучитьПараметрыРасшифровкиОтчета(Адрес, ИдентификаторОбъекта, Расшифровка) Экспорт
	
	ПараметрыРасшифровки = Новый Структура;
	
	ДанныеОбъекта = ПолучитьИзВременногоХранилища(Адрес);
	
	Если ИдентификаторОбъекта = "ГлавнаяКнигаТиповой" Тогда 
		ОтчетОбъект       = ДанныеОбъекта;
		ДанныеРасшифровки = ДанныеОбъекта.ДанныеРасшифровки;
	Иначе 		
		ОтчетОбъект       = ДанныеОбъекта.Объект;
		ДанныеРасшифровки = ДанныеОбъекта.ДанныеРасшифровки;
	КонецЕсли;
	
	Если ОтчетОбъект.Свойство("ОтчетОбъект") И ОтчетОбъект.ОтчетОбъект <> Неопределено Тогда
		МенеджерОтчета = ОтчетОбъект.ОтчетОбъект;
	Иначе
		МенеджерОтчета = Отчеты[ИдентификаторОбъекта];
	КонецЕсли;
	
	ПараметрыИсполненияОтчета = МенеджерОтчета.ПолучитьПараметрыИсполненияОтчета();
	Если ПараметрыИсполненияОтчета.Свойство("ИспользоватьРасширенныеПараметрыРасшифровки") 
		И ПараметрыИсполненияОтчета.ИспользоватьРасширенныеПараметрыРасшифровки Тогда
		
		МенеджерОтчета.ЗаполнитьПараметрыРасшифровкиОтчета(Адрес, Расшифровка, ПараметрыРасшифровки);
		Возврат ПараметрыРасшифровки;
	КонецЕсли;
	
	Если Найти(ИдентификаторОбъекта, "Налоговый") <> 0
		ИЛИ ИдентификаторОбъекта = "СправкаРасчетВычетовПоНалогам" Тогда 
		ПланСчетов = "Налоговый";
	Иначе 
		ПланСчетов = "Типовой";
	КонецЕсли;

	Если ИдентификаторОбъекта = ("КарточкаСчета" + ПланСчетов)
		ИЛИ ИдентификаторОбъекта = ("КарточкаСубконто" + ПланСчетов)
		ИЛИ ИдентификаторОбъекта = ("ОтчетПоПроводкам" + ПланСчетов) Тогда
		
		ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
		ПараметрыРасшифровки.Вставить("Значение"     , ДанныеРасшифровки.Элементы[Расшифровка].ПолучитьПоля()[0].Значение);
		
	Иначе
		ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Ложь);
		
		ЕстьПоказатель      = Ложь;
		ЕстьКорЗначение     = Ложь;
		ЕстьСчетБУ          = Ложь; // Для отчета Сверка данных БУ и НУ
		ЕстьРегистратор     = Ложь; // Для отчета Сверка данных БУ и НУ
		ЕстьСубконто1       = Ложь; // Для отчета Сверка данных БУ и НУ
		
		ПервыйЭлемент       = Неопределено;
		Счет                = Неопределено;
		СчетБУ              = Неопределено;
		СчетНУ              = Неопределено;
		
		Если ИдентификаторОбъекта = "ГлавнаяКнигаТиповой" Тогда
			Если ТипЗнч(Расшифровка) = Тип("ПланСчетовСсылка.Типовой") Тогда
				ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
				ПараметрыРасшифровки.Вставить("Значение"     , Расшифровка);
			Иначе 
				МассивПолей = Расшифровка;
				
				ЕстьПоказатель = Истина;
				ПервыйЭлемент = Истина;
				Для Каждого ЭлементСписка Из Расшифровка Цикл
					Для Каждого ЭлементСоответствия Из ЭлементСписка.Значение Цикл
						Если ЭлементСоответствия.Ключ = "КорСчет" Тогда
							ЕстьКорЗначение = Истина;
						КонецЕсли;
						Если ЭлементСоответствия.Ключ = "Счет" Тогда
							Счет = ЭлементСоответствия.Значение;
						КонецЕсли;
					КонецЦикла;
				КонецЦикла;
			КонецЕсли;
			
		Иначе
			КомпоновщикНастроек = Новый КомпоновщикНастроекКомпоновкиДанных;
			КомпоновщикНастроек.ЗагрузитьНастройки(ДанныеРасшифровки.Настройки);
			КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(ДанныеОбъекта.Объект.СхемаКомпоновкиДанных));
			
			МассивПолей = ПолучитьМассивПолейРасшифровки(Расшифровка, ДанныеРасшифровки, КомпоновщикНастроек, Истина);
			
			// обработка порядка субконто в счетах детализации
			// (в отчетах есть таблицы детализации корсчетов)
			Если ИдентификаторОбъекта = ("ОборотноСальдоваяВедомость" + ПланСчетов)
				ИЛИ ИдентификаторОбъекта = ("АнализСчета" + ПланСчетов) Тогда
				
				Если ИдентификаторОбъекта = ("АнализСчета" + ПланСчетов) Тогда
					ИмяТаблицыДетализации = "ГруппировкаКор";
				Иначе
					ИмяТаблицыДетализации = "Группировка";
				КонецЕсли;
				
				ПараметрыОтбораСтрок = Новый Структура("Использование", Истина);
				ДетализацияПоСчетам = ОтчетОбъект[ИмяТаблицыДетализации].Скопировать(ПараметрыОтбораСтрок);
				
				Если ДетализацияПоСчетам.Количество() > 0 Тогда
				
					СчетРасшифровки = Неопределено;
					МассивСубконтоРасшифровки = Новый Массив;
					
					Для Каждого ЭлементРасшифровки Из МассивПолей Цикл
						Если ТипЗнч(ЭлементРасшифровки) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
							Если Найти(ЭлементРасшифровки.Поле, "Субконто") > 0 Тогда
								МассивСубконтоРасшифровки.Добавить(ЭлементРасшифровки);
							КонецЕсли;
							Если ЭлементРасшифровки.Поле = "Счет" Тогда
								СчетРасшифровки = ЭлементРасшифровки.Значение;
								Прервать;
							КонецЕсли;
						КонецЕсли;
					КонецЦикла;
					
					Если СчетРасшифровки <> Неопределено
						И МассивСубконтоРасшифровки.Количество() > 0 Тогда
						
						ДанныеСчета = ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.ПолучитьСвойстваСчета(СчетРасшифровки);
						ТаблицаСубконтоСчета = Новый ТаблицаЗначений;
						
						Если ДанныеСчета.КоличествоСубконто > 0 Тогда
							
							ТаблицаСубконтоСчета.Колонки.Добавить("ВидСубконто");
							ТаблицаСубконтоСчета.Колонки.Добавить("СубконтоРасшифровки");
							
							Для Индекс = 1 По ДанныеСчета.КоличествоСубконто Цикл
								СтрокаТаблицыСубконто = ТаблицаСубконтоСчета.Добавить();
								СтрокаТаблицыСубконто.ВидСубконто = ДанныеСчета["ВидСубконто" + Индекс];
							КонецЦикла;
							
						КонецЕсли;
						
						СтрокаДетализацииСчета = ДетализацияПоСчетам.Найти(СчетРасшифровки, "Счет");
						
						Если СтрокаДетализацииСчета <> Неопределено Тогда
							
							СтрокаПоСубконто = СтрокаДетализацииСчета.ПоСубконто;
							
							СписокСубконтоДетализации = Новый СписокЗначений;
							Если НЕ ПустаяСтрока(СтрокаПоСубконто) Тогда		
								КоличествоСубконто = СтрДлина(СтрокаПоСубконто) / 2;
								Для Индекс = 1 По КоличествоСубконто Цикл
									Если Сред(СтрокаПоСубконто, Индекс*2 - 1, 1) = "+" Тогда
										ИндексСубконто = Сред(СтрокаПоСубконто, Индекс*2, 1);
										СписокСубконтоДетализации.Добавить(ДанныеСчета["ВидСубконто" + ИндексСубконто]);
									КонецЕсли;
								КонецЦикла;
							КонецЕсли;	

							Для Каждого СубконтоСчета Из ТаблицаСубконтоСчета Цикл
								СубконтоДетализации = СписокСубконтоДетализации.НайтиПоЗначению(СубконтоСчета.ВидСубконто);
								Если СубконтоДетализации <> Неопределено Тогда
									СубконтоСчета.СубконтоРасшифровки = "Субконто" + (СписокСубконтоДетализации.Индекс(СубконтоДетализации) + 1);
								КонецЕсли;
							КонецЦикла;
								
							МассивСубконтоДляУдаления = Новый Массив;
							Для Каждого СубконтоРасшифровки Из МассивСубконтоРасшифровки Цикл
								НайденнаяСтрока = ТаблицаСубконтоСчета.Найти(СубконтоРасшифровки.Поле, "СубконтоРасшифровки");
								Если НайденнаяСтрока <> Неопределено Тогда
									ТипыЗначенийСубконто = НайденнаяСтрока.ВидСубконто.ТипЗначения;
									Если ТипыЗначенийСубконто.СодержитТип(ТипЗнч(СубконтоРасшифровки.Значение)) Тогда
										СубконтоРасшифровки.Поле = "Субконто" + (ТаблицаСубконтоСчета.Индекс(НайденнаяСтрока) + 1);
									КонецЕсли;
								Иначе
									МассивСубконтоДляУдаления.Добавить(СубконтоРасшифровки);
								КонецЕсли;
							КонецЦикла;
								
							Для Каждого СубконтоДляУдаления Из МассивСубконтоДляУдаления Цикл
								ИндексДляУдаления = МассивПолей.Найти(СубконтоДляУдаления);
								МассивПолей.Удалить(ИндексДляУдаления);
							КонецЦикла;
							
						КонецЕсли;
						
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
			Для Индекс = 0 по МассивПолей.ВГраница() Цикл
				Элемент = МассивПолей[Индекс];
				Если ТипЗнч(Элемент) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
					Если ПервыйЭлемент = Неопределено И Элемент.Поле <> "Показатель" Тогда 
						ПервыйЭлемент = Элемент;
					КонецЕсли;
					Если Элемент.Поле = "Показатель" Тогда
						ЕстьПоказатель = Истина;
					КонецЕсли;
					Если Найти(Элемент.Поле, "Кор") = 1 Тогда
						ЕстьКорЗначение = Истина;
					КонецЕсли;
					Если Элемент.Поле = "Счет" Тогда
						Счет = Элемент.Значение;
					КонецЕсли;
					Если Элемент.Поле = "СчетНУ" Тогда
						Счет 	= Элемент.Значение;
						СчетНУ 	= Элемент.Значение;
					КонецЕсли;				
					Если Элемент.Поле = "СчетБУ" Тогда
						СчетБУ 		= Элемент.Значение;
						ЕстьСчетБУ 	= Истина;
					КонецЕсли;
					Если Элемент.Поле = "ДокументДвижения" Тогда
						Регистратор   = Элемент.Значение;
						ЕстьРегистратор = Истина;
					КонецЕсли;
					Если Элемент.Поле = "Субконто1" Тогда
						ЕстьСубконто1 = Истина;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			
			Если  ИдентификаторОбъекта = "СверкаДанныхБУ_НУ" Тогда
				Если ЕстьСчетБУ Тогда
					ПланСчетов = "Типовой";
					Счет       = СчетБУ;
				Иначе
					ПланСчетов = "Налоговый";
					Счет       = СчетНУ;
				КонецЕсли;			
			КонецЕсли;
		
		КонецЕсли;
		
		ПредставлениеПланаСчетов = ?(ПланСчетов = "Налоговый", НСтр("ru = '(налоговый) '"), "");
		
		ПредставлениеСверкиБУНУПоРегистраторам = НСтр("ru = 'Расшифровать по первичным документам'");
		ПредставлениеСверкиБУНУПоСубконто      = НСтр("ru = 'Расшифровать по субконто'");
		ПредставлениеДляСверкаДанныхБУ_НУ      = НСтр("ru = 'ОСВ по счету %1'");
		ПредставлениеДляОСВПоСчету             = НСтр("ru = 'ОСВ по счету %1'");
		ПредставлениеДляКарточкиСчета          = НСтр("ru = 'Карточка счета %1'");
		ПредставлениеДляАнализаСчета           = НСтр("ru = 'Анализ счета %1'");
		ПредставлениеДляОборотыСчета           = НСтр("ru = 'Обороты счета %1'");
		ПредставлениеДляОборотыСчетаПоМесяцам  = НСтр("ru = 'Обороты счета %1 по месяцам'");
		ПредставлениеДляОборотыСчетаПоДням     = НСтр("ru = 'Обороты счета %1 по дням'");
		ПредставлениеДляКарточкиСубконто       = НСтр("ru = 'Карточка субконто'") + ПредставлениеПланаСчетов;
		ПредставлениеДляОтчетаПоПроводкам      = НСтр("ru = 'Отчет по проводкам'") + ПредставлениеПланаСчетов;
		ПредставлениеОткрытьОбъект             = НСтр("ru = 'Открыть ""%1""'");
		
		ПредставлениеДляСверкаДанныхБУ_НУ = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДляСверкаДанныхБУ_НУ, ПредставлениеПланаСчетов + Счет);
		ПредставлениеДляОСВПоСчету = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДляОСВПоСчету, ПредставлениеПланаСчетов + Счет);  
		ПредставлениеДляКарточкиСчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДляКарточкиСчета, ПредставлениеПланаСчетов + Счет);  
		ПредставлениеДляАнализаСчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДляАнализаСчета, ПредставлениеПланаСчетов + Счет); 
		ПредставлениеДляОборотыСчета = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДляОборотыСчета, ПредставлениеПланаСчетов + Счет);
		ПредставлениеДляОборотыСчетаПоМесяцам = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДляОборотыСчетаПоМесяцам, ПредставлениеПланаСчетов + Счет); 
		ПредставлениеДляОборотыСчетаПоДням = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеДляОборотыСчетаПоДням, ПредставлениеПланаСчетов + Счет);
				
		Если ЕстьПоказатель Тогда
			УстановитьВсеПоказатели = Ложь;
		Иначе
			УстановитьВсеПоказатели = Истина;
		КонецЕсли;
		
		СписокПунктовМеню = Новый СписокЗначений;
		
		Если ПервыйЭлемент <> Неопределено Тогда
			Если ИдентификаторОбъекта = ("ОборотноСальдоваяВедомость" + ПланСчетов) ИЛИ ИдентификаторОбъекта = ("СверкаДанныхБУ_НУ") Тогда
				Если Не ЕстьПоказатель И ЗначениеЗаполнено(ПервыйЭлемент.Значение)  И Не БухгалтерскиеОтчетыКлиентСервер.ПростойТип(ПервыйЭлемент.Значение) Тогда
					Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеОткрытьОбъект, ПервыйЭлемент.Значение);  
					
					СписокПунктовМеню.Добавить(ПервыйЭлемент.Значение, Представление);
				КонецЕсли;
				
				Если  ИдентификаторОбъекта = ("СверкаДанныхБУ_НУ") Тогда
					Если ЕстьРегистратор Тогда
						// Просто открываем регистратор
						ПараметрыРасшифровки.Вставить("ОткрытьОбъект", Истина);
						ПараметрыРасшифровки.Вставить("Значение"     , Регистратор);
						Возврат ПараметрыРасшифровки;
					Иначе
						Если НЕ ЕстьСубконто1 Тогда
							СписокПунктовМеню.Добавить("СверкаДанныхБУ_НУ", ПредставлениеСверкиБУНУПоСубконто);						
						КонецЕсли;
						СписокПунктовМеню.Добавить("СверкаДанныхБУ_НУ", ПредставлениеСверкиБУНУПоРегистраторам);
					КонецЕсли;	
				КонецЕсли;
				
				СписокПунктовМеню.Добавить("ОборотноСальдоваяВедомостьПоСчету" + ПланСчетов, ПредставлениеДляОСВПоСчету);
				СписокПунктовМеню.Добавить("КарточкаСчета" + ПланСчетов                    , ПредставлениеДляКарточкиСчета);
				СписокПунктовМеню.Добавить("АнализСчета"   + ПланСчетов                    , ПредставлениеДляАнализаСчета);
				СписокПунктовМеню.Добавить("ОборотыСчета"  + ПланСчетов + "ПоМесяцам"      , ПредставлениеДляОборотыСчетаПоМесяцам);
				СписокПунктовМеню.Добавить("ОборотыСчета"  + ПланСчетов + "ПоДням"         , ПредставлениеДляОборотыСчетаПоДням);
				
			Иначе
				Если ЕстьПоказатель Тогда
					// Если расшифровывается показатель, то необходимо сразу формировать отчет
					Если ИдентификаторОбъекта = "АнализСубконто" + ПланСчетов Тогда
						Если Счет <> Неопределено Тогда 
							СписокПунктовМеню.Добавить("КарточкаСчета" + ПланСчетов   , ПредставлениеДляКарточкиСчета);
						Иначе
							СписокПунктовМеню.Добавить("КарточкаСубконто" + ПланСчетов, ПредставлениеДляКарточкиСубконто);	
						КонецЕсли;
					ИначеЕсли ИдентификаторОбъекта = "АнализСчета" + ПланСчетов Тогда
						Если ЕстьКорЗначение Тогда
							СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
						Иначе
							СписокПунктовМеню.Добавить("КарточкаСчета" + ПланСчетов   , ПредставлениеДляКарточкиСчета);
						КонецЕсли;
					ИначеЕсли ИдентификаторОбъекта = "ОборотноСальдоваяВедомостьПоСчету" + ПланСчетов Тогда
						СписокПунктовМеню.Добавить("КарточкаСчета" + ПланСчетов   , ПредставлениеДляКарточкиСчета);
					ИначеЕсли ИдентификаторОбъекта = "ОборотыМеждуСубконто" + ПланСчетов Тогда
						СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
					ИначеЕсли ИдентификаторОбъекта = "ОборотыСчета" + ПланСчетов Тогда
						СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
					ИначеЕсли ИдентификаторОбъекта = "СводныеПроводкиТиповой" Тогда
						СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
					ИначеЕсли ИдентификаторОбъекта = "ГлавнаяКнигаТиповой" Тогда
						СписокПунктовМеню.Добавить("ОборотыСчета" + ПланСчетов, ПредставлениеДляОборотыСчета);
						Если ЕстьКорЗначение Тогда
							СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
						КонецЕсли;						
					КонецЕсли;
				Иначе
					Если ИдентификаторОбъекта = "СводныеПроводкиТиповой" Тогда
						СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
						УстановитьВсеПоказатели = Истина;
					ИначеЕсли ИдентификаторОбъекта = ("ШахматнаяВедомостьТиповой") Тогда
						СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
					Иначе 
						Если ЗначениеЗаполнено(ПервыйЭлемент.Значение) И Не БухгалтерскиеОтчетыКлиентСервер.ПростойТип(ПервыйЭлемент.Значение) Тогда
							Представление = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ПредставлениеОткрытьОбъект, ПервыйЭлемент.Значение);
							
							СписокПунктовМеню.Добавить(ПервыйЭлемент.Значение, Представление);
						КонецЕсли;
						Если ИдентификаторОбъекта = "АнализСубконто" + ПланСчетов Тогда
							Если Счет <> Неопределено Тогда 
								СписокПунктовМеню.Добавить("КарточкаСчета" + ПланСчетов, ПредставлениеДляКарточкиСчета);
							Иначе
								СписокПунктовМеню.Добавить("КарточкаСубконто" + ПланСчетов, ПредставлениеДляКарточкиСубконто);
							КонецЕсли;	
						ИначеЕсли ИдентификаторОбъекта = "АнализСчета" + ПланСчетов Тогда
							Если ЕстьКорЗначение Тогда 
								СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
							Иначе
								СписокПунктовМеню.Добавить("КарточкаСчета" + ПланСчетов, ПредставлениеДляКарточкиСчета);
							КонецЕсли;
						ИначеЕсли ИдентификаторОбъекта = "ОборотноСальдоваяВедомостьПоСчету" + ПланСчетов Тогда
							СписокПунктовМеню.Добавить("КарточкаСчета" + ПланСчетов, ПредставлениеДляКарточкиСчета);
						ИначеЕсли ИдентификаторОбъекта = "ОборотыМеждуСубконто" + ПланСчетов Тогда
							СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
						ИначеЕсли ИдентификаторОбъекта = "ОборотыСчета" + ПланСчетов Тогда				
							СписокПунктовМеню.Добавить("ОтчетПоПроводкам" + ПланСчетов, ПредставлениеДляОтчетаПоПроводкам);
						ИначеЕсли 	ИдентификаторОбъекта = ("СправкаРасчетВычетовПоНалогам") Тогда
							СписокПунктовМеню.Добавить("КарточкаСубконто" + ПланСчетов, ПредставлениеДляКарточкиСубконто);
							СписокПунктовМеню.Добавить("АнализСчета" + ПланСчетов, ПредставлениеДляАнализаСчета);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;	
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
				
	НастройкиРасшифровки = Новый Структура;   
	Если СписокПунктовМеню <> Неопределено Тогда
		Для Каждого ПунктМеню Из СписокПунктовМеню Цикл
			Если ТипЗнч(ПунктМеню.Значение) = Тип("Строка") Тогда
				НастройкиРасшифровки.Вставить(ПунктМеню.Значение, ПолучитьНастройкиДляРасшифровки(ПунктМеню.Значение, ИдентификаторОбъекта, МассивПолей, ОтчетОбъект, УстановитьВсеПоказатели, ПланСчетов));
			КонецЕсли;
		КонецЦикла;
		
		ДанныеОбъекта.Вставить("НастройкиРасшифровки", НастройкиРасшифровки);
		Адрес = ПоместитьВоВременноеХранилище(ДанныеОбъекта, Адрес);
		
		ПараметрыРасшифровки.Вставить("СписокПунктовМеню", СписокПунктовМеню);
	КонецЕсли;

	Возврат ПараметрыРасшифровки;
	
КонецФункции

Функция ПолучитьНастройкиДляРасшифровки(ИДРасшифровки, ИдентификаторОбъекта, МассивПолей, ОтчетОбъект, УстановитьВсеПоказатели, ПланСчетов)
	
	ПоддержкаРаботыСоСтруктурнымиПодразделениями = ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями");
	
	Счет              = Неопределено;
	
	ЕстьСчет          = Истина;
	ЕстьСубконто3     = Ложь; // Для отчета Сверка данных БУ и НУ
	ЕстьПодразделение = Ложь;
	Субконто3		  = Неопределено;
	
	КорСчет           = Неопределено;
	ЕстьКорЗначение   = Ложь;
	Период            = Неопределено;
	Периодичность     = Неопределено;
	
	БухТипРесурса       = Неопределено; // Для отчетов АнализСчета и ОборотыСчета
	ИндексБухТипРесурса = Неопределено; // Для отчетов АнализСчета и ОборотыСчета
	
	Если ИдентификаторОбъекта = "ГлавнаяКнигаТиповой" Тогда
		УстановитьВсеПоказатели = Ложь;
		Показатель = "БУ";
		Для Каждого ЭлементСписка Из МассивПолей Цикл
			Для Каждого ЭлементСоответствия Из ЭлементСписка.Значение Цикл
				Если ЭлементСоответствия.Ключ = "Счет" Тогда
					Счет = ЭлементСоответствия.Значение;
					ЕстьСчет = Истина;
				ИначеЕсли ЭлементСоответствия.Ключ = "КорСчет" Тогда
					КорСчет = ЭлементСоответствия.Значение;
					ЕстьКорЗначение = Истина;
				ИначеЕсли ЭлементСоответствия.Ключ = "Отбор" Тогда
					Для Каждого ЭлементОтбора Из ЭлементСоответствия.Значение Цикл
						Если ЭлементОтбора.Ключ = "Период" Тогда
							Период = ЭлементОтбора.Значение;
						КонецЕсли;
					КонецЦикла;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	Иначе
		Для Индекс = 0 по МассивПолей.ВГраница() Цикл
			Элемент = МассивПолей[Индекс];
			Если ТипЗнч(Элемент) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
				Если Найти(Элемент.Поле, "Кор") = 1 Тогда
					ЕстьКорЗначение = Истина;
				КонецЕсли;
				Если Элемент.Поле = "КорСчет" Тогда
					КорСчет = Элемент.Значение;
				КонецЕсли;
				Если Элемент.Поле = "Счет" Тогда
					Счет = Элемент.Значение;
				КонецЕсли;
				Если Элемент.Поле = "СчетНУ" Тогда
					Счет 	= Элемент.Значение;
					СчетНУ 	= Элемент.Значение;
				КонецЕсли;
				Если Элемент.Поле = "СчетБУ" Тогда
					Счет    = Элемент.Значение;
					СчетБУ  = Элемент.Значение;
				КонецЕсли;
				Если Элемент.Поле = "Период" Тогда
					Период = Элемент.Значение;
				КонецЕсли;
				Если Элемент.Поле = "Субконто3" Тогда
					Субконто3 = Элемент.Значение;
					ЕстьСубконто3 	= Истина;
				КонецЕсли;
				
				Если Элемент.Поле = "БухТипРесурса" Тогда
					БухТипРесурса = Элемент.Значение;
					ИндексБухТипРесурса = Индекс;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если БухТипРесурса <> Неопределено И ИндексБухТипРесурса <> Неопределено Тогда
		МассивПолей.Удалить(ИндексБухТипРесурса);
	КонецЕсли;
	
	ДобавитьОтборПоВидСубконто    = Истина;
	ДобавитьОтборПоВидКорСубконто = Истина;
	
	ПользовательскиеНастройки  = Новый ПользовательскиеНастройкиКомпоновкиДанных;
	ПользовательскиеОтборы     = ПользовательскиеНастройки.Элементы.Добавить(Тип("ОтборКомпоновкиДанных"));
	ДополнительныеСвойства     = ПользовательскиеНастройки.ДополнительныеСвойства;
	ДополнительныеСвойства.Вставить("ПараметрыВыводаРасшифровки", Новый Структура);
	ПараметрыВыводаРасшифровки = ДополнительныеСвойства.ПараметрыВыводаРасшифровки;
	
	ПользовательскиеОтборы.ИдентификаторПользовательскойНастройки = "Отбор";
	
	Если ИДРасшифровки = "КарточкаСубконто" + ПланСчетов Тогда
		Если ИдентификаторОбъекта = "СправкаРасчетВычетовПоНалогам" Тогда
			СписокВидовСубконто  = Новый СписокЗначений;
			СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоТиповые.НалогиСборыОтчисления);
			СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоТиповые.ВидыПлатежейВБюджетИФонды);
			Если ЕстьСубконто3 Тогда
				// включена аналитика по налоговым комитетам на счетах налогов
				СписокВидовСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоТиповые.Контрагенты);
			КонецЕсли;
			ДополнительныеСвойства.Вставить("СписокВидовСубконто", СписокВидовСубконто.Скопировать());
		Иначе
			ДополнительныеСвойства.Вставить("СписокВидовСубконто", ОтчетОбъект.СписокВидовСубконто.Скопировать());
		КонецЕсли;
	ИначеЕсли ИДРасшифровки = "АнализСчета" + ПланСчетов Тогда
		Если Счет = Неопределено И ИдентификаторОбъекта = "СправкаРасчетВычетовПоНалогам" Тогда
			Для Каждого Элемент Из МассивПолей Цикл
				Если ТипЗнч(Элемент) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
					Если Элемент.Поле = "Субконто1" И ТипЗнч(Элемент.Значение) = Тип("СправочникСсылка.НалогиСборыОтчисления") Тогда
						Если Элемент.Значение = Справочники.НалогиСборыОтчисления.ОбязательныеСоциальныеОтчисления  Тогда
							ДополнительныеСвойства.Вставить("Счет", ПланыСчетов.Налоговый.ОбязательстваПоСоциальномуСтрахованию);
						ИначеЕсли  Элемент.Значение = Справочники.НалогиСборыОтчисления.ОтчисленияОбязательноеСоциальноеМедицинскоеСтрахование Тогда
							ДополнительныеСвойства.Вставить("Счет", ПланыСчетов.Налоговый.ОбязательстваПоОтчислениямОСМС);
						ИначеЕсли  Элемент.Значение = Справочники.НалогиСборыОтчисления.ОбязательныеПенсионныеВзносы Тогда
							ДополнительныеСвойства.Вставить("Счет", ПланыСчетов.Налоговый.ОбязательстваПоПенсионнымОтчислениям);
						Иначе
							ДополнительныеСвойства.Вставить("Счет", ПланыСчетов.Налоговый.РасчетыСБюджетомПоНалогамИОтчислениям);
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
		КонецЕсли;
		ДополнительныеСвойства.Вставить("ПоПодразделамКорСчетов", Истина);
	ИначеЕсли ИДРасшифровки = "ОборотыСчета" + ПланСчетов + "ПоДням" Тогда
		ДополнительныеСвойства.Вставить("Периодичность", 6);
	ИначеЕсли ИДРасшифровки = "ОборотыСчета" + ПланСчетов + "ПоМесяцам" Тогда
		ДополнительныеСвойства.Вставить("Периодичность", 9);
	ИначеЕсли ИДРасшифровки = "СверкаДанныхБУ_НУ" Тогда
		ДополнительныеСвойства.Вставить("ПоСубсчетам", Истина);
	КонецЕсли;
	
	ДополнительныеСвойства.Вставить("РежимРасшифровки", Истина);
	
	Если ИДРасшифровки = "КарточкаСубконто" + ПланСчетов
		ИЛИ ИДРасшифровки = "ОтчетПоПроводкам" + ПланСчетов
		ИЛИ ИДРасшифровки = "СверкаДанныхБУ_НУ" Тогда
		ЕстьСчет = Ложь;
	ИначеЕсли ИдентификаторОбъекта = "СверкаДанныхБУ_НУ" Тогда
		Если Плансчетов = "Типовой" Тогда
			ДополнительныеСвойства.Вставить("Счет", СчетБУ);
		Иначе
			ДополнительныеСвойства.Вставить("Счет", СчетНУ);
		КонецЕсли;
	ИначеЕсли ИДРасшифровки = "КарточкаСчета" + ПланСчетов Тогда
		Если Счет = Неопределено И ИдентификаторОбъекта = "ОборотноСальдоваяВедомостьПоСчету" + ПланСчетов Тогда 
			ДополнительныеСвойства.Вставить("Счет", ОтчетОбъект.Счет);
		Иначе
			ДополнительныеСвойства.Вставить("Счет", Счет);
		КонецЕсли;					
	Иначе
		Если НЕ (ИДРасшифровки = "АнализСчета" + ПланСчетов И ИдентификаторОбъекта = "СправкаРасчетВычетовПоНалогам" И Счет = Неопределено) Тогда
			ДополнительныеСвойства.Вставить("Счет", Счет);
		КонецЕсли;
	КонецЕсли;

	Если ИдентификаторОбъекта = "ГлавнаяКнигаТиповой" Тогда
		ДополнительныеСвойства.Вставить("СписокСтруктурныхЕдиниц", ОтчетОбъект.СписокСтруктурныхЕдиниц);
		ПредставлениеСпискаОрганизаций = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ОтчетОбъект.СписокСтруктурныхЕдиниц);
		ДополнительныеСвойства.Вставить("ПредставлениеСпискаОрганизаций", ПредставлениеСпискаОрганизаций);
		Если ИДРасшифровки = "ОборотыСчетаТиповой" Тогда
			ДополнительныеСвойства.Вставить("Счет", Счет);
			Если ЕстьКорЗначение Тогда
				БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "КорСчет", КорСчет, ВидСравненияКомпоновкиДанных.ВИерархии);	
			КонецЕсли;
		ИначеЕсли ИДРасшифровки = "ОтчетПоПроводкамТиповой" Тогда
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СчетДт", Счет, ВидСравненияКомпоновкиДанных.ВИерархии);
			БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "СчетКт", КорСчет, ВидСравненияКомпоновкиДанных.ВИерархии);
		КонецЕсли;
	Иначе
		ВладелецГоловногоПодразделения = Справочники.Организации.ПустаяСсылка();
		Для каждого Отбор из МассивПолей Цикл
			Если ТипЗнч(Отбор) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
				Если Отбор.Поле = "СтруктурноеПодразделение" тогда
					Отбор.Поле = "Подразделение";
				КонецЕсли;
				Если Отбор.Поле = "Организация" И ТипЗнч(Отбор.Значение) = Тип("СправочникСсылка.Организации") тогда
					ВладелецГоловногоПодразделения = Отбор.Значение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
		
		ГруппировкаПоОрганизации   = Неопределено;
		ГруппировкаПоПодразделению = Неопределено;
		
		СписокВладельцевГоловныхПодразделений = Новый СписокЗначений;
		СписокСтруктурныхЕдиниц = Новый СписокЗначений;
		СписокПодразделений = Новый СписокЗначений;
		
		Если ИДРасшифровки = "АнализСчета" + ПланСчетов И ИдентификаторОбъекта = "СправкаРасчетВычетовПоНалогам" Тогда
			Для Индекс = 0 По МассивПолей.ВГраница() Цикл
				Элемент = МассивПолей[Индекс];
				Если ТипЗнч(Элемент) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") Тогда
					Если Элемент.Поле = "Субконто1" И 
						(Элемент.Значение = Справочники.НалогиСборыОтчисления.ОбязательныеСоциальныеОтчисления
						ИЛИ Элемент.Значение = Справочники.НалогиСборыОтчисления.ОтчисленияОбязательноеСоциальноеМедицинскоеСтрахование) Тогда
						ИндексНекорректноеСубконто = Индекс;
					КонецЕсли;
				КонецЕсли;
			КонецЦикла;
			Если  ИндексНекорректноеСубконто <> Неопределено Тогда
				МассивПолей.Удалить(ИндексНекорректноеСубконто);
			КонецЕсли;
		КонецЕсли;

		СписокПолейОтборов = Новый Массив;
		Для каждого Отбор из МассивПолей Цикл
			Если ТипЗнч(Отбор) = Тип("ЗначениеПоляРасшифровкиКомпоновкиДанных") тогда
				Если Отбор.Значение = NULL тогда
					Продолжить;
				КонецЕсли;
				Если Отбор.Поле = "Счет" И (ИдентификаторОбъекта = "АнализСчета" + ПланСчетов ИЛИ ИдентификаторОбъекта = "ОборотыСчета" + ПланСчетов) Тогда
					ЗначениеСчет = ?(Счет.Уровень() > ОтчетОбъект.Счет.Уровень(), Счет, ОтчетОбъект.Счет); 
					Если БухТипРесурса <> Неопределено И ИДРасшифровки = "ОтчетПоПроводкам" + ПланСчетов Тогда
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле + БухТипРесурса, ЗначениеСчет, ВидСравненияКомпоновкиДанных.ВИерархии);	
						Если БухТипРесурса = "Дт" Тогда
							ПараметрыВыводаРасшифровки.Вставить("ВыводитьКт", Ложь);
						ИначеЕсли БухТипРесурса = "Кт" Тогда
							ПараметрыВыводаРасшифровки.Вставить("ВыводитьДт", Ложь);
						КонецЕсли;
					Иначе
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, ЗначениеСчет, ВидСравненияКомпоновкиДанных.ВИерархии);	
					КонецЕсли;
				ИначеЕсли (Отбор.Поле = "Счет" ИЛИ Отбор.Поле = "КорСчет") И НЕ ЕстьСчет Тогда
					Если Отбор.Поле = "КорСчет" И (Не ПустаяСтрока(БухТипРесурса))
						И (БухТипРесурса = "Дт" ИЛИ БухТипРесурса = "Кт") И ИДРасшифровки = "ОтчетПоПроводкам" + ПланСчетов Тогда
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Счет" + ?(БухТипРесурса = "Дт", "Кт", "Дт"), Отбор.Значение, ВидСравненияКомпоновкиДанных.ВИерархии);	
					Иначе
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение, ВидСравненияКомпоновкиДанных.ВИерархии);		
					КонецЕсли;
				ИначеЕсли Отбор.Поле = "Счет" И ЕстьСчет Тогда	
				ИначеЕсли Найти(Отбор.Поле, "Субконто") = 1 тогда
					Если ИдентификаторОбъекта = "ОборотыМеждуСубконто" + ПланСчетов Тогда
						Если Счет <> Неопределено Тогда 
							ИндексСубконто = Сред(Отбор.Поле, 9, 1); 
							ПозицияСубконто = Найти(Отбор.Поле, "Субконто");
							ИндексСубконтоОтчет = Счет.ВидыСубконто.Найти(ОтчетОбъект.СписокВидовСубконто[Число(ИндексСубконто) - 1].Значение, "ВидСубконто").НомерСтроки;
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Субконто" + ИндексСубконтоОтчет, Отбор.Значение, ?(Отбор.Иерархия, ВидСравненияКомпоновкиДанных.ВИерархии, ВидСравненияКомпоновкиДанных.Равно));
						Иначе
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение, ?(Отбор.Иерархия, ВидСравненияКомпоновкиДанных.ВИерархии, ВидСравненияКомпоновкиДанных.Равно));
							ИндексСубконто = Число(Сред(Отбор.Поле, 9));
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Вид" + Отбор.Поле, ОтчетОбъект.СписокВидовСубконто[ИндексСубконто - 1].Значение);
						КонецЕсли;
						ДобавитьОтборПоВидСубконто = Ложь;
					ИначеЕсли ИдентификаторОбъекта = "АнализСубконто" + ПланСчетов И ИДРасшифровки = "КарточкаСчета" + ПланСчетов Тогда
						ИндексСубконто = Сред(Отбор.Поле, 9, 1); 
						ПозицияСубконто = Найти(Отбор.Поле, "Субконто");
						НайденнаяСтрока = Счет.ВидыСубконто.Найти(ОтчетОбъект.СписокВидовСубконто[Число(ИндексСубконто) - 1].Значение, "ВидСубконто");
						Если НайденнаяСтрока <> Неопределено Тогда
							ИндексСубконтоОтчет = НайденнаяСтрока.НомерСтроки;
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, СтрЗаменить(Строка(Отбор.Поле), "Субконто" + ИндексСубконто, "Субконто" + ИндексСубконтоОтчет), Отбор.Значение);
						КонецЕсли;
					ИначеЕсли (ИдентификаторОбъекта = "АнализСчета" + ПланСчетов ИЛИ ИдентификаторОбъекта = "ОборотыСчета" + ПланСчетов)
							И (Не ПустаяСтрока(БухТипРесурса)) И (БухТипРесурса = "Дт" ИЛИ БухТипРесурса = "Кт") И ИДРасшифровки = "ОтчетПоПроводкам" + ПланСчетов Тогда
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, СтрЗаменить(Отбор.Поле, "Субконто", "Субконто" + БухТипРесурса), Отбор.Значение, ?(Отбор.Иерархия, ВидСравненияКомпоновкиДанных.ВИерархии, ВидСравненияКомпоновкиДанных.Равно));	
					Иначе
						ПозицияСубконто = Найти(Отбор.Поле, "Субконто");
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Сред(Отбор.Поле, ПозицияСубконто, СтрДлина(Отбор.Поле) - ПозицияСубконто + 1), Отбор.Значение);
					КонецЕсли;
					СписокПолейОтборов.Добавить(Отбор.Поле);
				ИначеЕсли Найти(Отбор.Поле, "КорСубконто") = 1 тогда
					Если ИдентификаторОбъекта = "ОборотыМеждуСубконто" + ПланСчетов Тогда
						Если КорСчет <> Неопределено Тогда
							ИндексКорСубконто = Сред(Отбор.Поле, 12, 1); 
							ПозицияКорСубконто = Найти(Отбор.Поле, "КорСубконто");
							ИндексКорСубконтоОтчет = КорСчет.ВидыСубконто.Найти(ОтчетОбъект.СписокВидовКорСубконто[Число(ИндексКорСубконто) - 1].Значение, "ВидСубконто").НомерСтроки;
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "КорСубконто" + ИндексКорСубконтоОтчет, Отбор.Значение, ?(Отбор.Иерархия, ВидСравненияКомпоновкиДанных.ВИерархии, ВидСравненияКомпоновкиДанных.Равно));	
						Иначе
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение, ?(Отбор.Иерархия, ВидСравненияКомпоновкиДанных.ВИерархии, ВидСравненияКомпоновкиДанных.Равно));
							ИндексСубконто = Число(Сред(Отбор.Поле, 12));
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "Вид" + Отбор.Поле, ОтчетОбъект.СписокВидовКорСубконто[ИндексСубконто - 1].Значение);
						КонецЕсли;
						ДобавитьОтборПоВидКорСубконто = Ложь;
					Иначе
						ПозицияСубконто = Найти(Отбор.Поле, "КорСубконто");
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Сред(Отбор.Поле, ПозицияСубконто, СтрДлина(Отбор.Поле) - ПозицияСубконто + 1), Отбор.Значение);
					КонецЕсли;
				ИначеЕсли Отбор.Поле = "Подразделение" тогда
					ГруппировкаПоПодразделению = Отбор.Значение;
					Если ПоддержкаРаботыСоСтруктурнымиПодразделениями Тогда
						СписокПодразделений.Очистить();
						Если ЗначениеЗаполнено(Отбор.Значение) Тогда
							СписокПодразделений.Добавить(Отбор.Значение, "");
						Иначе
							СписокВладельцевГоловныхПодразделений.Очистить();
							Если ЗначениеЗаполнено(ВладелецГоловногоПодразделения) Тогда
								СписокПодразделений.Добавить(Отбор.Значение, НСтр("ru = 'Головное подразделение'"));
								СписокВладельцевГоловныхПодразделений.Добавить(ВладелецГоловногоПодразделения);
								ДополнительныеСвойства.Вставить("СписокВладельцевГоловныхПодразделений", СписокВладельцевГоловныхПодразделений);
							Иначе
								Для Каждого СтруктурнаяЕдиница Из ОтчетОбъект.СписокСтруктурныхЕдиниц Цикл
									СписокПодразделений.Добавить(Отбор.Значение, НСтр("ru = 'Головное подразделение'"));
									СписокВладельцевГоловныхПодразделений.Добавить(СтруктурнаяЕдиница.Значение);
								КонецЦикла;
								ДополнительныеСвойства.Вставить("СписокВладельцевГоловныхПодразделений", СписокВладельцевГоловныхПодразделений);
							КонецЕсли;
						КонецЕсли;
						ДополнительныеСвойства.Вставить("СписокПодразделений", СписокПодразделений);
						ПредставлениеСпискаПодразделений = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(СписокПодразделений);
						ДополнительныеСвойства.Вставить("ПредставлениеСпискаПодразделений", ПредставлениеСпискаПодразделений);
					Иначе
						Если (ИдентификаторОбъекта = "АнализСчета" + ПланСчетов ИЛИ ИдентификаторОбъекта = "ОборотыСчета" + ПланСчетов)
							И (Не ПустаяСтрока(БухТипРесурса)) И (БухТипРесурса = "Дт" ИЛИ БухТипРесурса = "Кт") Тогда
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле + БухТипРесурса, Отбор.Значение);
						Иначе
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение);
						КонецЕсли;
					КонецЕсли;
					ЕстьПодразделение = Истина;
				ИначеЕсли Отбор.Поле = "Организация" Тогда
					ГруппировкаПоОрганизации = Отбор.Значение;
					СписокСтруктурныхЕдиниц.Очистить();
					СписокСтруктурныхЕдиниц.Добавить(Отбор.Значение);
					ДополнительныеСвойства.Вставить("СписокСтруктурныхЕдиниц", СписокСтруктурныхЕдиниц);
					ПредставлениеСпискаОрганизаций = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(СписокСтруктурныхЕдиниц);
					ДополнительныеСвойства.Вставить("ПредставлениеСпискаОрганизаций", ПредставлениеСпискаОрганизаций);
				ИначеЕсли Отбор.Поле = "Показатель" Тогда 
					Показатель = Отбор.Значение;
				ИначеЕсли Отбор.Поле = "СчетБУ" ИЛИ Отбор.Поле = "СчетНУ" И ИдентификаторОбъекта = "СверкаДанныхБУ_НУ"
					И (ИДРасшифровки = "КарточкаСчета" + ПланСчетов ИЛИ ИДРасшифровки = "АнализСчета" + ПланСчетов ИЛИ ИДРасшифровки = "ОборотноСальдоваяВедомостьПоСчету" + ПланСчетов
						ИЛИ ИДРасшифровки = "ОборотыСчета" + ПланСчетов + "ПоМесяцам" ИЛИ ИДРасшифровки = "ОборотыСчета" + ПланСчетов + "ПоДням") Тогда
				ИначеЕсли Отбор.Поле = "Период" ИЛИ Отбор.Поле = "Регистратор" Тогда
				Иначе
					Если Отбор.Иерархия Тогда
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение, ВидСравненияКомпоновкиДанных.ВИерархии);
					Иначе
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.Поле, Отбор.Значение);
					КонецЕсли;
				КонецЕсли;	
			ИначеЕсли ТипЗнч(Отбор) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				Если СписокПолейОтборов.Найти(Строка(Отбор.ЛевоеЗначение)) = Неопределено Тогда
					Если Найти(Отбор.ЛевоеЗначение, "Субконто") = 1 И ИдентификаторОбъекта = "АнализСубконто" + ПланСчетов И ИДРасшифровки = "КарточкаСчета" + ПланСчетов Тогда
						ИндексСубконто = Сред(Отбор.ЛевоеЗначение, 9, 1); 
						ПозицияСубконто = Найти(Отбор.ЛевоеЗначение, "Субконто");
						НайденнаяСтрока = Счет.ВидыСубконто.Найти(ОтчетОбъект.СписокВидовСубконто[Число(ИндексСубконто) - 1].Значение, "ВидСубконто");
						Если НайденнаяСтрока <> Неопределено Тогда
							ИндексСубконтоОтчет = НайденнаяСтрока.НомерСтроки;
							БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, СтрЗаменить(Строка(Отбор.ЛевоеЗначение), "Субконто" + ИндексСубконто, "Субконто" + ИндексСубконтоОтчет), Отбор.ПравоеЗначение, Отбор.ВидСравнения);
						КонецЕсли;
					ИначеЕсли Отбор.Представление = "###ОтборПоОрганизации###" Тогда
						ДополнительныеСвойства.Вставить("СписокСтруктурныхЕдиниц", Отбор.ПравоеЗначение);
						ПредставлениеСпискаОрганизаций = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(Отбор.ПравоеЗначение);
						ДополнительныеСвойства.Вставить("ПредставлениеСпискаОрганизаций", ПредставлениеСпискаОрганизаций);
					ИначеЕсли Отбор.Представление = "###ОтборПоВидуУчета###" Тогда
						ДополнительныеСвойства.Вставить("ВидУчета", Отбор.ПравоеЗначение);
					ИначеЕсли Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") И ЕстьПодразделение Тогда
					ИначеЕсли Отбор.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Подразделение") 
						И Отбор.ВидСравнения = ВидСравненияКомпоновкиДанных.ВИерархии
						И ПоддержкаРаботыСоСтруктурнымиПодразделениями Тогда
						ДополнительныеСвойства.Вставить("Подразделение", Отбор.ПравоеЗначение);
					Иначе
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, Отбор.ЛевоеЗначение, Отбор.ПравоеЗначение, Отбор.ВидСравнения);
					КонецЕсли;
				КонецЕсли;
			ИначеЕсли ТипЗнч(Отбор) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
				
				Если Отбор.Представление = "###ОтборПоОрганизации###" Тогда
					
					Если (ГруппировкаПоОрганизации = Неопределено) Тогда 
						ДополнительныеСвойства.Вставить("СписокСтруктурныхЕдиниц", ОтчетОбъект.СписокСтруктурныхЕдиниц);
						ПредставлениеСпискаОрганизаций = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ОтчетОбъект.СписокСтруктурныхЕдиниц);
						ДополнительныеСвойства.Вставить("ПредставлениеСпискаОрганизаций", ПредставлениеСпискаОрганизаций);
					КонецЕсли;
					
					Если (ГруппировкаПоПодразделению = Неопределено) Тогда
						ДополнительныеСвойства.Вставить("СписокПодразделений", ОтчетОбъект.СписокПодразделений);
						ПредставлениеСпискаПодразделений = БухгалтерскиеОтчетыКлиентСервер.ВыгрузитьСписокВСтроку(ОтчетОбъект.СписокПодразделений);
						ДополнительныеСвойства.Вставить("ПредставлениеСпискаПодразделений", ПредставлениеСпискаПодразделений);
						ДополнительныеСвойства.Вставить("СписокВладельцевГоловныхПодразделений", ОтчетОбъект.СписокВладельцевГоловныхПодразделений);
					КонецЕсли;
					
				КонецЕсли;				
				
			КонецЕсли;
		КонецЦикла;

		Если ИдентификаторОбъекта = "ОборотыМеждуСубконто" + ПланСчетов Тогда
			Если ДобавитьОтборПоВидСубконто Тогда
				Индекс = 1;
				Для Каждого ВидСубконто Из ОтчетОбъект.СписокВидовСубконто Цикл
					Если ЗначениеЗаполнено(ВидСубконто.Значение) Тогда
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "ВидСубконто" + Индекс, ВидСубконто.Значение);
						Индекс = Индекс + 1;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
			Если ДобавитьОтборПоВидКорСубконто Тогда
				Индекс = 1;
				Для Каждого ВидСубконто Из ОтчетОбъект.СписокВидовКорСубконто Цикл
					Если ЗначениеЗаполнено(ВидСубконто.Значение) Тогда
						БухгалтерскиеОтчетыКлиентСервер.ДобавитьОтбор(ПользовательскиеОтборы, "ВидКорСубконто" + Индекс, ВидСубконто.Значение);
						Индекс = Индекс + 1;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЕсли;
					
	КонецЕсли;
	
	Если Период <> Неопределено И Период <> Null Тогда
		ДополнительныеСвойства.Вставить("НачалоПериода", Период);
		ДополнительныеСвойства.Вставить("КонецПериода" , БухгалтерскиеОтчетыКлиентСервер.КонецПериода(Период, ?(Периодичность = Неопределено, ОтчетОбъект.Периодичность, Периодичность)));
	Иначе
		ДополнительныеСвойства.Вставить("НачалоПериода", ОтчетОбъект.НачалоПериода);
		ДополнительныеСвойства.Вставить("КонецПериода" , ОтчетОбъект.КонецПериода);
	КонецЕсли;
	
	// Настройка показателей
	Если УстановитьВсеПоказатели И ОтчетОбъект.Свойство("НаборПоказателей") Тогда
		Если ИдентификаторОбъекта = "СверкаДанныхБУ_НУ" Тогда
			Если ИДРасшифровки = "СверкаДанныхБУ_НУ" Тогда
				Для Каждого ИмяПоказателя Из ОтчетОбъект.НаборПоказателей Цикл
					ДополнительныеСвойства.Вставить("Показатель" + ИмяПоказателя , ОтчетОбъект["Показатель" + ИмяПоказателя]);
				КонецЦикла;
			Иначе
				ДополнительныеСвойства.Вставить("ПоказательБУ", ОтчетОбъект.ПоказательБУ);
				ДополнительныеСвойства.Вставить("ПоказательНУ", ОтчетОбъект.ПоказательНУ ИЛИ ОтчетОбъект.ПоказательНУНУ);
				ДополнительныеСвойства.Вставить("ПоказательПР", ОтчетОбъект.ПоказательНУПР);
				ДополнительныеСвойства.Вставить("ПоказательВР", ОтчетОбъект.ПоказательНУВР);
			КонецЕсли;
		Иначе
			Для Каждого ИмяПоказателя Из ОтчетОбъект.НаборПоказателей Цикл
				ДополнительныеСвойства.Вставить("Показатель" + ИмяПоказателя , ОтчетОбъект["Показатель" + ИмяПоказателя]);
			КонецЦикла;
			Если ОтчетОбъект.НаборПоказателей.Найти("Количество") = Неопределено Тогда
				Если ЕстьСчет И ЗначениеЗаполнено(Счет) И Счет.Количественный Тогда
					ДополнительныеСвойства.Вставить("ПоказательКоличество", Истина);
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
	Иначе
		Если Показатель = "Контроль" Тогда // Для СверкаДанныхБУ_НУ
			ДополнительныеСвойства.Вставить("ПоказательБУ", ОтчетОбъект.ПоказательБУ);
			Если ИДРасшифровки = "СверкаДанныхБУ_НУ" Тогда 
				ДополнительныеСвойства.Вставить("ПоказательНУ"      , ОтчетОбъект.ПоказательНУ);
				ДополнительныеСвойства.Вставить("ПоказательНУНУ"    , ОтчетОбъект.ПоказательНУНУ);
				ДополнительныеСвойства.Вставить("ПоказательНУПР"    , ОтчетОбъект.ПоказательНУПР);
				ДополнительныеСвойства.Вставить("ПоказательНУВР"    , ОтчетОбъект.ПоказательНУВР);
				ДополнительныеСвойства.Вставить("ПоказательКонтроль", ОтчетОбъект.ПоказательКонтроль);
			Иначе
				ДополнительныеСвойства.Вставить("ПоказательНУ", ОтчетОбъект.ПоказательНУ ИЛИ ОтчетОбъект.ПоказательНУНУ);
				ДополнительныеСвойства.Вставить("ПоказательПР", ОтчетОбъект.ПоказательНУПР);
				ДополнительныеСвойства.Вставить("ПоказательВР", ОтчетОбъект.ПоказательНУВР);
			КонецЕсли;
		ИначеЕсли Показатель <> Неопределено Тогда
			ДополнительныеСвойства.Вставить("Показатель" + Показатель , Истина);
		КонецЕсли;
	КонецЕсли;
	
	Если ИдентификаторОбъекта = "СправкаРасчетВычетовПоНалогам" Тогда
		ДополнительныеСвойства.Вставить("ПоказательНУ", Истина);
		ДополнительныеСвойства.Вставить("ПоказательПР", Истина);
		ДополнительныеСвойства.Вставить("ПоказательВР", Истина);
	ИначеЕсли ИдентификаторОбъекта = "ШахматнаяВедомостьТиповой" Тогда
		ДополнительныеСвойства.Вставить("ПоказательБУ", Истина);
		ДополнительныеСвойства.Вставить("ПоказательВалютнаяСумма", ОтчетОбъект.ПоВалютам);
	КонецЕсли;
	
	Возврат ПользовательскиеНастройки;
	
КонецФункции

Процедура ПриСозданииНаСервере(Форма, Отказ, СтандартнаяОбработка) Экспорт
	
	Отчет     = Форма.Отчет;
	Параметры = Форма.Параметры;
	Если Параметры.Свойство("РежимРасшифровки") Тогда 
		Отчет.РежимРасшифровки = Параметры.РежимРасшифровки;
	КонецЕсли;
	
	Если ЕстьЭлементФормы(Форма, "ВидПериода") Тогда
		ВыборПериодаКлиентСервер.ЗаполнитьСписокВыбораВидПериода(Перечисления.ДоступныеПериодыОтчета.День,
			Форма.Элементы.ВидПериода.СписокВыбора, Форма.ВидПериода);
	КонецЕсли;
	
	Если ЕстьРеквизитФормы(Форма, "ПоддержкаРаботыСоСтруктурнымиПодразделениями") Тогда
		Форма.ПоддержкаРаботыСоСтруктурнымиПодразделениями = ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями");
	КонецЕсли;
	
	Если ЕстьРеквизитФормы(Форма, "УчетПоВсемОрганизациям") Тогда
		Форма.УчетПоВсемОрганизациям = ПользователиБКВызовСервераПовтИсп.ПолучитьЗначениеПоУмолчанию(ПользователиКлиентСервер.ТекущийПользователь(), "УчетПоВсемОрганизациям");
	КонецЕсли;
	
	Если Параметры.Свойство("ВидРасшифровки") Тогда 
		// 1 - из временного хранилища, 2 - из параметров формы
		Если Параметры.ВидРасшифровки = 1 Тогда
			ДанныеОбъекта = ПолучитьИзВременногоХранилища(Параметры.АдресНастроек);
			ПользовательскиеНастройки = ДанныеОбъекта.НастройкиРасшифровки[Параметры.ИДРасшифровки];
		ИначеЕсли Параметры.ВидРасшифровки = 2 Тогда
			ПользовательскиеНастройки = Параметры.ПользовательскиеНастройки;
		КонецЕсли;
		
		Если ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("КлючВарианта") 
			И ЗначениеЗаполнено(ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта)Тогда
			Форма.УстановитьТекущийВариант(ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта);
		КонецЕсли;
		
		РеквизитыФормы = РеквизитыФормы(Форма);
		
		Если Параметры.Свойство("СформироватьПриОткрытии") И Параметры.СформироватьПриОткрытии = Истина 
			и РеквизитыФормы.Свойство("КомпоновщикИнициализирован") И Не Форма.КомпоновщикИнициализирован Тогда
			Если РеквизитыФормы.Свойство("КлючТекущегоВарианта") Тогда
				ИнициализацияКомпоновщикаНастроек(Форма, , Форма.КлючТекущегоВарианта);
			Иначе
				ИнициализацияКомпоновщикаНастроек(Форма);
			КонецЕсли;
		КонецЕсли;
		
		// Установка пользовательских настроек
		Отчет.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "Отбор";
		Отчет.КомпоновщикНастроек.Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "Порядок";
		Отчет.КомпоновщикНастроек.Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "УсловноеОформление";
		
		Если ДанныеОбъекта <> Неопределено И ДанныеОбъекта.Свойство("Объект") Тогда
			Если ДанныеОбъекта.Объект.Свойство("СписокСтруктурныхЕдиниц") И Не ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("СписокСтруктурныхЕдиниц") Тогда
				ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("СписокСтруктурныхЕдиниц", ДанныеОбъекта.Объект.СписокСтруктурныхЕдиниц);
			КонецЕсли;
			Если ДанныеОбъекта.Объект.Свойство("СписокПодразделений") И Не ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("СписокПодразделений") Тогда
				ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("СписокПодразделений", ДанныеОбъекта.Объект.СписокПодразделений);
			КонецЕсли;
			Если ДанныеОбъекта.Объект.Свойство("СписокВладельцевГоловныхПодразделений") И Не ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("СписокВладельцевГоловныхПодразделений") Тогда
				ПользовательскиеНастройки.ДополнительныеСвойства.Вставить("СписокВладельцевГоловныхПодразделений", ДанныеОбъекта.Объект.СписокВладельцевГоловныхПодразделений);
			КонецЕсли;
			Если ТипЗнч(ДанныеОбъекта.ДанныеРасшифровки) = Тип("ДанныеРасшифровкиКомпоновкиДанных") Тогда
				ПользовательскийОтбор = Неопределено;
				Для Каждого ЭлементПользовательскихНастроек Из ПользовательскиеНастройки.Элементы Цикл
					Если ТипЗнч(ЭлементПользовательскихНастроек) = Тип("ОтборКомпоновкиДанных") Тогда
						ПользовательскийОтбор = ЭлементПользовательскихНастроек;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				Если ПользовательскийОтбор <> Неопределено Тогда
					ПараметрСчет = Неопределено;
					ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("Счет", ПараметрСчет);
					ПараметрВидыСубконто = Неопределено;
					ДанныеОбъекта.Объект.Свойство("СписокВидовСубконто", ПараметрВидыСубконто);
					ДополнитьОтборИзОтбора(ПользовательскийОтбор, ДанныеОбъекта.ДанныеРасшифровки.Настройки.Отбор, Отчет.КомпоновщикНастроек.Настройки.ДоступныеПоляОтбора, ПараметрСчет, ПараметрВидыСубконто);
				КонецЕсли;
			КонецЕсли;
			Если Параметры.ИДРасшифровки = "Продажи" Тогда
				Параметры.КлючВарианта = ДанныеОбъекта.Объект.КлючТекущегоВарианта;
				Параметры.ПредставлениеВарианта = ДанныеОбъекта.Объект.СхемаКомпоновкиДанных.ВариантыНастроек[ДанныеОбъекта.Объект.КлючТекущегоВарианта].Представление;
				Форма.КлючТекущегоВарианта = Параметры.КлючВарианта;
				Форма.ПредставлениеТекущегоВарианта = Параметры.ПредставлениеВарианта;
			КонецЕсли;
		КонецЕсли;
		
		// Загрузка пользовательских настроек в компоновщик настроек отчета
		Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(ПользовательскиеНастройки);
		
		// Получение основных настроек компоновщика настроек, с учетом пользовательских настроек
		Настройки = Отчет.КомпоновщикНастроек.ПолучитьНастройки();
		
		// Загрузка основных настроек
		Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
		
		// Очистка пользовательских настроек
		Отчет.КомпоновщикНастроек.Настройки.Отбор.ИдентификаторПользовательскойНастройки              = "";
		Отчет.КомпоновщикНастроек.Настройки.Порядок.ИдентификаторПользовательскойНастройки            = "";
		Отчет.КомпоновщикНастроек.Настройки.УсловноеОформление.ИдентификаторПользовательскойНастройки = "";
		
		// Установка значений реквизитов отчета
		Для Каждого ЭлементСтруктуры Из ПользовательскиеНастройки.ДополнительныеСвойства Цикл
			Если Отчет.Свойство(ЭлементСтруктуры.Ключ) Тогда
				// Табличные части отчета могут быть переданы в виде массива структур
				Если ТипЗнч(ЭлементСтруктуры.Значение) = Тип("Массив") Тогда
					Отчет[ЭлементСтруктуры.Ключ].Очистить();
					Для Каждого СтрокаСведений Из ЭлементСтруктуры.Значение Цикл
						НоваяСтрока = Отчет[ЭлементСтруктуры.Ключ].Добавить();
						ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаСведений);
					КонецЦикла;
				Иначе
					Отчет[ЭлементСтруктуры.Ключ] = ЭлементСтруктуры.Значение;
				КонецЕсли;
			КонецЕсли;
			Если ЭлементСтруктуры.Ключ = "СписокСтруктурныхЕдиниц" И ЕстьРеквизитФормы(Форма, "СписокСтруктурныхЕдиниц") Тогда
				Форма.СписокСтруктурныхЕдиниц = ЭлементСтруктуры.Значение;
			КонецЕсли;
			Если ЭлементСтруктуры.Ключ = "СписокПодразделений" И ЕстьРеквизитФормы(Форма, "СписокПодразделений") Тогда
				Форма.СписокПодразделений = ЭлементСтруктуры.Значение;
			КонецЕсли;
			Если ЭлементСтруктуры.Ключ = "СписокВладельцевГоловныхПодразделений" И ЕстьРеквизитФормы(Форма, "СписокВладельцевГоловныхПодразделений") Тогда
				Форма.СписокВладельцевГоловныхПодразделений = ЭлементСтруктуры.Значение;
			КонецЕсли;
			
			//Для отчета оборачиваемость товаров Начало
			Если ЭлементСтруктуры.Ключ = "РасчетныйПоказательРасшифровки" И ЕстьРеквизитФормы(Форма, "РасчетныйПоказательРасшифровки") Тогда
				Форма.РасчетныйПоказательРасшифровки = ЭлементСтруктуры.Значение;
			КонецЕсли;
			
			Если ЭлементСтруктуры.Ключ = "ПоказательРасшифровки" И ЕстьРеквизитФормы(Форма, "ПоказательРасшифровки") Тогда
				Форма.ПоказательРасшифровки = ЭлементСтруктуры.Значение;
			КонецЕсли;
			//Для отчета оборачиваемость товаров Конец
			
		КонецЦикла;
		
		Если ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("ПараметрыВыводаРасшифровки") Тогда
			ПараметрыВыводаРасшифровки = ПользовательскиеНастройки.ДополнительныеСвойства.ПараметрыВыводаРасшифровки;
			Для Каждого ЭлементСтруктуры Из ПараметрыВыводаРасшифровки Цикл
				БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(Отчет.КомпоновщикНастроек, ЭлементСтруктуры.Ключ, ЭлементСтруктуры.Значение);
			КонецЦикла;
		КонецЕсли;
		
		Если ПользовательскиеНастройки.ДополнительныеСвойства.Свойство("КлючВарианта") Тогда
			Параметры.КлючВарианта = ПользовательскиеНастройки.ДополнительныеСвойства.КлючВарианта;
		КонецЕсли;
	КонецЕсли;

	Если ЕстьРеквизитФормы(Форма, "ОписанияТиповВидовСубконто") Тогда
		Форма.ОписанияТиповВидовСубконто = Новый Структура;
		Форма.ОписанияТиповВидовСубконто.Вставить("Номенклатура"                  , ПланыВидовХарактеристик.ВидыСубконтоТиповые.Номенклатура.ТипЗначения);
		Форма.ОписанияТиповВидовСубконто.Вставить("Склад"                         , ПланыВидовХарактеристик.ВидыСубконтоТиповые.Склады.ТипЗначения);
		Форма.ОписанияТиповВидовСубконто.Вставить("Контрагент"                    , ПланыВидовХарактеристик.ВидыСубконтоТиповые.Контрагенты.ТипЗначения);
		Форма.ОписанияТиповВидовСубконто.Вставить("ДоговорКонтрагента"            , ПланыВидовХарактеристик.ВидыСубконтоТиповые.Договоры.ТипЗначения);
		Форма.ОписанияТиповВидовСубконто.Вставить("Партия"                        , ПланыВидовХарактеристик.ВидыСубконтоТиповые.Партии.ТипЗначения);
		Форма.ОписанияТиповВидовСубконто.Вставить("ДокументРасчетовСКонтрагентами", ПланыВидовХарактеристик.ВидыСубконтоТиповые.ДокументыРасчетовСКонтрагентами.ТипЗначения);
	КонецЕсли;
	
	ОтчетОбъект = Форма.РеквизитФормыВЗначение("Отчет");
	Если Не ЗначениеЗаполнено(Форма.СхемаКомпоновкиДанных) Тогда
		Форма.СхемаКомпоновкиДанных = ПоместитьВоВременноеХранилище(ОтчетОбъект.СхемаКомпоновкиДанных, Форма.УникальныйИдентификатор);
	КонецЕсли;
	Форма.ДанныеРасшифровки = ПоместитьВоВременноеХранилище(Форма.ДанныеРасшифровки, Форма.УникальныйИдентификатор);
	
	Если ЕстьРеквизитФормы(Форма, "НаборПоказателей") Тогда
		Если СтроковыеФункцииКлиентСервер.РазложитьСтрокуВМассивПодстрок(Форма.ИмяФормы, ".")[0] = "Отчет" Тогда
			МенеджерОтчета = Отчеты[БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(Форма)];
		Иначе
			МенеджерОтчета = ОтчетОбъект;
		КонецЕсли;
		Форма.НаборПоказателей = Новый ФиксированныйМассив(МенеджерОтчета.ПолучитьНаборПоказателей());
	КонецЕсли;
	
	ПараметрМакетОформления = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметрВывода(Отчет.КомпоновщикНастроек.Настройки, "МакетОформления");
	СписокМакетовОформления = БухгалтерскиеОтчетыВызовСервераПовтИсп.ПолучитьСписокМакетовОформления();
	Для Каждого ЭлементСписка Из СписокМакетовОформления Цикл
		Если ЕстьЭлементФормы(Форма, "МакетОформления") Тогда
			Форма.Элементы.МакетОформления.СписокВыбора.Добавить(ЭлементСписка.Значение, ЭлементСписка.Представление);
		КонецЕсли;
	КонецЦикла;
	Если ЕстьРеквизитФормы(Форма, "МакетОформления") Тогда
		Если ПараметрМакетОформления.Использование И Не ПустаяСтрока(ПараметрМакетОформления.Значение) Тогда
			Форма.МакетОформления = ПараметрМакетОформления.Значение;
		Иначе
			ОсновнойМакетОтчетов = СписокМакетовОформления.НайтиПоЗначению("МакетОформленияОтчетов");
			Если ОсновнойМакетОтчетов <> Неопределено Тогда
				Форма.МакетОформления = ОсновнойМакетОтчетов.Значение;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
		
	Если Отчет.РежимРасшифровки Тогда
		СтандартнаяОбработка = Ложь;
		Попытка
			Форма.ИзменениеСхемыКомпоновкиДанныхНаСервере();
		Исключение
			// Запись в журнал регистрации не требуется
		КонецПопытки;
		Если Параметры.Свойство("ЗаполняемыеНастройки") Тогда
			Попытка
				Форма.ЗаполнитьНастройкамиПоУмолчанию(Параметры.ЗаполняемыеНастройки);
			Исключение
				// Запись в журнал регистрации не требуется
			КонецПопытки;
		КонецЕсли;
		Если ЕстьРеквизитФормы(Форма, "ВыводитьЗаголовок") Тогда
			Форма.ВыводитьЗаголовок = Истина;
		КонецЕсли;
		Форма.СформироватьОтчетНаСервере();
	Иначе
		УстановитьНастройкиПоУмолчанию(Форма);
	КонецЕсли;
	
КонецПроцедуры

Функция РеквизитыФормы(Форма)
	
	РеквизитыФормы = Новый Структура();
	
	Для Каждого Реквизит Из Форма.ПолучитьРеквизиты() Цикл
		
		РеквизитыФормы.Вставить(Реквизит.Имя);
		
	КонецЦикла;
	
	Возврат РеквизитыФормы;
	
КонецФункции

// Инициализирует компоновщик настроек отчета.
//
// Параметры:
//	Форма - ФормаКлиентскогоПриложения - Форма отчета.
//	ОрганизацияИзменилась - Булево - При вызове инициализации из-за изменения организации необходимо передать Истина.
//	ИмяВариантаНастроек - Строка - Имя варианта настроек.
//
Процедура ИнициализацияКомпоновщикаНастроек(Форма, ОрганизацияИзменилась = Ложь, ИмяВариантаНастроек = "") Экспорт
	
	Форма.КомпоновщикИнициализирован = Истина;
	
	Форма.Элементы.НастройкиОтчета.Видимость = Истина;
	
	ИмяОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(Форма);
	
	Схема = Отчеты[ИмяОтчета].ПолучитьМакет("СхемаКомпоновкиДанных");
	
	Если ПустаяСтрока(ИмяВариантаНастроек) Тогда
		ИмяВариантаНастроек = ИмяОтчета;
	КонецЕсли;
	
	ВариантНастроек = Схема.ВариантыНастроек.Найти(ИмяВариантаНастроек);
	
	Если ВариантНастроек <> Неопределено Тогда
		Настройки = ВариантНастроек.Настройки;
	Иначе
		Настройки = Схема.НастройкиПоУмолчанию;
	КонецЕсли;
	
	Форма.Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(Настройки);
	Форма.СхемаКомпоновкиДанных = ПоместитьВоВременноеХранилище(Схема, Форма.УникальныйИдентификатор);
	Форма.Отчет.КомпоновщикНастроек.Инициализировать(Новый ИсточникДоступныхНастроекКомпоновкиДанных(Форма.СхемаКомпоновкиДанных));
	
	ТекущиеПользовательскиеНастройки = Неопределено;
	
	Если ЭтоАдресВременногоХранилища(Форма.ПользовательскиеНастройки) Тогда
		ТекущиеПользовательскиеНастройки = ПолучитьИзВременногоХранилища(Форма.ПользовательскиеНастройки);
	КонецЕсли;
	
КонецПроцедуры

// Тип - определяет для кого надо получить настройку: 1 - покупатель, 2 - поставщик
Функция ПолучитьСписокСчетовИсключаемыхИзРасчетаЗадолженности(Тип = 1) Экспорт
	
	Возврат ХранилищеОбщихНастроек.Загрузить(ПолучитьЗначениеКлючаОбъектаПоТипу(Тип, "СчетаИсключаемые"));
	
КонецФункции

// Тип - определяет для кого надо получить настройку: 1 - покупатель, 2 - поставщик
Процедура СохранитьСписокСчетовИсключаемыхИзРасчетаЗадолженности(СписокСчетов, Тип = 1) Экспорт
	
	Если Тип = 1 Тогда
		Описание    = НСтр("ru = 'Счета, исключаемые из расчета задолженности покупателей'");
	ИначеЕсли Тип = 2 Тогда
		Описание    = НСтр("ru = 'Счета, исключаемые из расчета задолженности поставщикам'");
	Иначе
		Описание    = НСтр("ru = 'Счета, исключаемые из расчета'");
	КонецЕсли;
	
	ОписаниеНастроек = Новый ОписаниеНастроек;
	ОписаниеНастроек.Представление = Описание;
	
	ХранилищеОбщихНастроек.Сохранить(ПолучитьЗначениеКлючаОбъектаПоТипу(Тип, "СчетаИсключаемые"),, СписокСчетов, ОписаниеНастроек);
	
КонецПроцедуры

Процедура ВыводПодписейОтчета(ПараметрыОтчета, Результат) Экспорт
	
	Если ПараметрыОтчета.Свойство("ВыводитьПодписиРуководителей") И ПараметрыОтчета.ВыводитьПодписиРуководителей Тогда
		ВыводПодписейРуководителей(ПараметрыОтчета, Результат);
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("ВыводитьПодписи") И ПараметрыОтчета.ВыводитьПодписи Тогда 
		Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
		ОбластьПодписи = Макет.ПолучитьОбласть("Подписи");
		
		Если ПараметрыОтчета.Свойство("ПримечаниеПодписи") И ЗначениеЗаполнено(ПараметрыОтчета.ПримечаниеПодписи) Тогда
			ОбластьПодписи.Параметры.ТекстПримечание = ПараметрыОтчета.ПримечаниеПодписи;
		КонецЕсли;
		
		Результат.Вывести(ОбластьПодписи);
		
		Результат.Области.Подписи.Видимость = ПараметрыОтчета.ВыводитьПодписи;
	КонецЕсли;

	// Наименование и БИН налогоплательщика для расшифровки регл.отчетов
	Если ПараметрыОтчета.Свойство("РежимРасшифровки")
		И ПараметрыОтчета.РежимРасшифровки 
		И ПараметрыОтчета.ИдентификаторОтчета = "УниверсальныйОтчетПоМетаданным" 
		И ПараметрыОтчета.Свойство("Налогоплательщик") 
		И ЗначениеЗаполнено(ПараметрыОтчета.Налогоплательщик)Тогда
		ОбластьПодписи.Очистить();
		Результат.Области.Подписи.Видимость = Ложь;
		ОтветЛица = ОбщегоНазначенияБКВызовСервера.ОтветственныеЛицаОрганизаций(ПараметрыОтчета.Налогоплательщик, ПараметрыОтчета.КонецПериода);
		ОбластьПодписиОтветственныйЗаРегистры = Макет.ПолучитьОбласть("ПодписиОтветственныйЗаРегистры");
		ОбластьПодписиОтветственныйЗаРегистры.Параметры.ОтветственныйЗаРегистры = ОтветЛица.ОтветственныйЗаРегистры;
		ОбластьПодписиОтветственныйЗаРегистры.Параметры.ОтветственныйЗаРегистрыДолжность = ОтветЛица.ОтветственныйЗаРегистрыДолжность;
		Результат.Вывести(ОбластьПодписиОтветственныйЗаРегистры);
	КонецЕсли;

КонецПроцедуры

Функция ДобавитьРодителей(ЭлементРасшифровки, ТекущийОтчет, МассивПолейРасшифровки, ВключатьРесурсы = Ложь)  Экспорт
	
	Если ТипЗнч(ЭлементРасшифровки) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		Для каждого Поле Из ЭлементРасшифровки.ПолучитьПоля() Цикл
			ДоступноеПоле = ПолучитьДоступноеПолеПоПолюКомпоновкиДанных(Новый ПолеКомпоновкиДанных(Поле.Поле), ТекущийОтчет);
			Если ДоступноеПоле = Неопределено Тогда
				Продолжить;
			КонецЕсли;
			Если Не ВключатьРесурсы И ДоступноеПоле.Ресурс Тогда
				Продолжить;
			КонецЕсли;
			МассивПолейРасшифровки.Добавить(Поле);
		КонецЦикла;
	КонецЕсли;
	Для каждого Родитель Из ЭлементРасшифровки.ПолучитьРодителей() Цикл
		ДобавитьРодителей(Родитель, ТекущийОтчет, МассивПолейРасшифровки, ВключатьРесурсы);
	КонецЦикла;
	
КонецФункции

Функция ПолучитьПараметрыРасшифровкиСпециализированногоОтчета(АдресДанныхРасшифровки, ИдентификаторОбъекта, Расшифровка) Экспорт
	
	ПараметрыРасшифровки = Новый Структура;
	
	ДанныеОбъекта = ПолучитьИзВременногоХранилища(АдресДанныхРасшифровки);
	Объект = ДанныеОбъекта.Объект;
	ДанныеРасшифровки = ДанныеОбъекта.ДанныеРасшифровки;
	СхемаКомпоновкиДанных = Объект.СхемаКомпоновкиДанных;

	//Создадим и инициализируем обработчик расшифровки
	ОбработкаРасшифровки = Новый ОбработкаРасшифровкиКомпоновкиДанных(ДанныеРасшифровки, Новый ИсточникДоступныхНастроекКомпоновкиДанных(СхемаКомпоновкиДанных));

	Возврат ПараметрыРасшифровки;
	
КонецФункции

// Тип - определяет для кого надо получить настройку: 1 - покупатель, 2 - поставщик
Функция ПолучитьСписокСчетовПользователяДляРасчетаЗадолженности(Тип = 1) Экспорт
	
	Возврат ХранилищеОбщихНастроек.Загрузить(ПолучитьЗначениеКлючаОбъектаПоТипу(Тип, "СчетаПользователя"));
	
КонецФункции

// Тип - определяет для кого надо получить настройку: 1 - покупатель, 2 - поставщик
Процедура СохранитьСписокСчетовПользователяДляРасчетаЗадолженности(СписокСчетов, Тип = 1) Экспорт
	
	Если Тип = 1 Тогда
		Описание    = НСтр("ru = 'Счета, добавленные пользователем для расчета задолженности покупателей'");
	ИначеЕсли Тип = 2 Тогда
		Описание    = НСтр("ru = 'Счета, добавленные пользователем для расчета задолженности поставщикам'");
	КонецЕсли;
	
	ОписаниеНастроек = Новый ОписаниеНастроек;
	ОписаниеНастроек.Представление = Описание;
	
	ХранилищеОбщихНастроек.Сохранить(ПолучитьЗначениеКлючаОбъектаПоТипу(Тип, "СчетаПользователя"),, СписокСчетов, ОписаниеНастроек);
	
КонецПроцедуры

Функция ВозвратитьМассивСчетовДтКтЗадолженности(КредиторскаяЗадолженность, Краткосрочная = Истина, Долгосрочная = Ложь) Экспорт
	
	ПС = ПланыСчетов["Типовой"];
	СчетаДебиторскойЗадолж = Новый Массив;
	
	Если КредиторскаяЗадолженность Тогда
		Если Краткосрочная Тогда
			Счет3310 = ПС.КраткосрочнаяЗадолженностьПоставщикамИПодрядчикам;
			Счет3320 = ПС.КраткосрочнаяКредиторскаяЗадолженностьДочернимОрганизациям;
			Счет3330 = ПС.КраткосрочнаяКредиторскаяЗадолженностьАссоциированнымИСовместнымОрганизациям;
			Счет3340 = ПС.КраткосрочнаяКредиторскаяЗадолженностьФилиаламИСтруктурнымПодразделениям;
			Счет3350 = ПС.КраткосрочнаяЗадолженностьПоОплатеТруда;
			Счет3360 = ПС.КраткосрочнаяЗадолженностьПоАренде;
			Счет3370 = ПС.ТекущаяЧастьДолгосрочнойКредиторскойЗадолженности;
			Счет3380 = ПС.КраткосрочныеВознагражденияКВыплате;
			Счет3390 = ПС.ПрочаяКраткосрочнаяКредиторскаяЗадолженность_ ;
			
			Счет1710 = ПС.КраткосрочныеАвансыВыданные;
			Счет3540 = ПС.КраткосрочныеОбязательстваПоДоговорам;
			Счет3550 = ПС.ДолговойКомпонентКомбинированногоКраткосрочногоФинансовогоИнструмента;
			
			СчетаДебиторскойЗадолж.Добавить(Счет3310);
			СчетаДебиторскойЗадолж.Добавить(Счет3320);
			СчетаДебиторскойЗадолж.Добавить(Счет3330);
			СчетаДебиторскойЗадолж.Добавить(Счет3340);
			СчетаДебиторскойЗадолж.Добавить(Счет3350);
			СчетаДебиторскойЗадолж.Добавить(Счет3360);
			СчетаДебиторскойЗадолж.Добавить(Счет3370);
			СчетаДебиторскойЗадолж.Добавить(Счет3380);
			СчетаДебиторскойЗадолж.Добавить(Счет3390);
			СчетаДебиторскойЗадолж.Добавить(Счет1710);			
			СчетаДебиторскойЗадолж.Добавить(Счет3540);
			СчетаДебиторскойЗадолж.Добавить(Счет3550);
		КонецЕсли;
		
		Если Долгосрочная Тогда
			
			Счет4110 = ПС.ДолгосрочнаяЗадолженностьПоставщикамИПодрядчикам;
			Счет4120 = ПС.ДолгосрочнаяКредиторскаяЗадолженностьДочернимОрганизациям;
			Счет4130 = ПС.ДолгосрочнаяКредиторскаяЗадолженностьАссоциированнымИСовместнымОрганизациям;
			Счет4140 = ПС.ДолгосрочнаяКредиторскаяЗадолженностьФилиаламИСтруктурнымПодразделениям;
			Счет4150 = ПС.ДолгосрочнаяЗадолженностьПоАренде;
			Счет4160 = ПС.ДолгосрочныеВознагражденияКВыплате;
			Счет4170 = ПС.ПрочаяДолгосрочнаяКредиторскаяЗадолженность_;
			
			Счет2910 = ПС.ДолгосрочныеАвансыВыданные;
			Счет4430 = ПС.ДолгосрочныеОбязательстваПоДоговорам;
			
			СчетаДебиторскойЗадолж.Добавить(Счет4110);
			СчетаДебиторскойЗадолж.Добавить(Счет4120);
			СчетаДебиторскойЗадолж.Добавить(Счет4130);
			СчетаДебиторскойЗадолж.Добавить(Счет4140);
			СчетаДебиторскойЗадолж.Добавить(Счет4150);
			СчетаДебиторскойЗадолж.Добавить(Счет4160);
			СчетаДебиторскойЗадолж.Добавить(Счет4170);
			СчетаДебиторскойЗадолж.Добавить(Счет2910);			
			СчетаДебиторскойЗадолж.Добавить(Счет4430);			
		КонецЕсли; 	
		
	Иначе
		// *** Дебиторская
		//если краткосрочная
		Если Краткосрочная Тогда
			Счет1210  = ПС.КраткосрочнаяДебиторскаяЗадолженностьПокупателейИЗаказчиков;
			Счет1220  = ПС.КраткосрочнаяДебиторскаяЗадолженностьДочернихОрганизаций;
			Счет1230  = ПС.КраткосрочнаяДебиторскаяЗадолженностьАссоциированныхИСовместныхОрганизаций;
			Счет1240  = ПС.КраткосрочнаяДебиторскаяЗадолженностьФилиаловИСтруктурныхПодразделений;
			
			Счет1250  = ПС.КраткосрочнаяДебиторскаяЗадолженностьРаботников;
			Счет1260  = ПС.КраткосрочнаяДебиторскаяЗадолженностьПоАренде;
			Счет1270  = ПС.КраткосрочныеВознагражденияКПолучению ;
			Счет1280  = ПС.ПрочаяКраткосрочнаяДебиторскаяЗадолженность_; 
			Счет1290  = ПС.РезервПоСомнительнымТребованиям;
			
			Счет3510  = ПС.КраткосрочныеАвансыПолученные;
			Счет1730  = ПС.КраткосрочныеАктивыПоДоговорам;
						
			СчетаДебиторскойЗадолж.Добавить(Счет1210);
			СчетаДебиторскойЗадолж.Добавить(Счет1220);
			СчетаДебиторскойЗадолж.Добавить(Счет1230);
			СчетаДебиторскойЗадолж.Добавить(Счет1240);
			СчетаДебиторскойЗадолж.Добавить(Счет1250);
			СчетаДебиторскойЗадолж.Добавить(Счет1260);
			СчетаДебиторскойЗадолж.Добавить(Счет1270);
			СчетаДебиторскойЗадолж.Добавить(Счет1280);
			СчетаДебиторскойЗадолж.Добавить(Счет1290); 
			СчетаДебиторскойЗадолж.Добавить(Счет3510); 
			СчетаДебиторскойЗадолж.Добавить(Счет1730); 				
			
		КонецЕсли;
		//долгосрочная задолженность
		Если Долгосрочная Тогда
			
			Счет2110  = ПС.ДолгосрочнаяЗадолженностьПокупателейИЗаказчиков;
			Счет2120  = ПС.ДолгосрочнаяДебиторскаяЗадолженностьДочернихОрганизаций;
			Счет2130  = ПС.ДолгосрочнаяДебиторскаяЗадолженностьАссоциированныхИСовместныхОрганизаций;
			Счет2140  = ПС.ДолгосрочнаяДебиторскаяЗадолженностьФилиаловИСтруктурныхПодразделений;
			
			Счет2150  = ПС.ДолгосрочнаяДебиторскаяЗадолженностьРаботников;
			Счет2160  = ПС.ДолгосрочнаяДебиторскаяЗадолженностьПоАренде;
			Счет2170  = ПС.ДолгосрочныеВознагражденияКПолучению ;
			Счет2180  = ПС.ПрочаяДолгосрочнаяДебиторскаяЗадолженность_; 
			
			Счет4410  = ПС.ДолгосрочныеАвансыПолученные;
			Счет2940  = ПС.ДолгосрочныеАктивыПоДоговорам;
			Счет2960  = ПС.ЗатратыПоДоговорам;
		
			СчетаДебиторскойЗадолж.Добавить(Счет2110);
			СчетаДебиторскойЗадолж.Добавить(Счет2120);
			СчетаДебиторскойЗадолж.Добавить(Счет2130);
			СчетаДебиторскойЗадолж.Добавить(Счет2140);
			СчетаДебиторскойЗадолж.Добавить(Счет2150);
			СчетаДебиторскойЗадолж.Добавить(Счет2160);
			СчетаДебиторскойЗадолж.Добавить(Счет2170);
			СчетаДебиторскойЗадолж.Добавить(Счет2180);
			СчетаДебиторскойЗадолж.Добавить(Счет1290); 		
			СчетаДебиторскойЗадолж.Добавить(Счет4410);
			СчетаДебиторскойЗадолж.Добавить(Счет2940); 		
			СчетаДебиторскойЗадолж.Добавить(Счет2960); 	
		КонецЕсли;	
		
	КонецЕсли;
	
	Возврат СчетаДебиторскойЗадолж;

КонецФункции // ВозвратитьМассивСчетов

// Функция добавляет набор данных - запрос в указанную в параметре коллекцию наборов данных
Функция ДобавитьНаборДанныхЗапрос(НаборыДанных, ИсточникДанных, ИмяНабораДанных = "НаборДанных1") Экспорт
	
	НаборДанных = НаборыДанных.Добавить(Тип("НаборДанныхЗапросСхемыКомпоновкиДанных"));
	НаборДанных.Имя = ИмяНабораДанных;
	НаборДанных.ИсточникДанных = ИсточникДанных.Имя;
	Возврат НаборДанных;
	
КонецФункции

// Добавляет в набор данных поле набора данных
Функция ДобавитьПолеНабораДанных(НаборДанных, Поле, Заголовок, ПутьКДанным = Неопределено) Экспорт
	
	Если ПутьКДанным = Неопределено Тогда
		ПутьКДанным = Поле;
	КонецЕсли;
	
	ПолеНабораДанных = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
	ПолеНабораДанных.Поле        = Поле;
	ПолеНабораДанных.Заголовок   = Заголовок;
	ПолеНабораДанных.ПутьКДанным = ПутьКДанным;
	Возврат ПолеНабораДанных;
	
КонецФункции

// Добавляет в набор данных поля периода Период секунда, минута, час ....
Функция ДобавитьПоляПериодаВНаборДанных(НаборДанных) Экспорт
	
	СписокПериодов = Новый СписокЗначений;
	СписокПериодов.Добавить("ПериодСекунда",   НСтр("ru = 'Период секунда'"));
	СписокПериодов.Добавить("ПериодМинута",    НСтр("ru = 'Период минута'"));
	СписокПериодов.Добавить("ПериодЧас",       НСтр("ru = 'Период час'"));
	СписокПериодов.Добавить("ПериодДень",      НСтр("ru = 'Период день'"));
	СписокПериодов.Добавить("ПериодНеделя",    НСтр("ru = 'Период неделя'"));
	СписокПериодов.Добавить("ПериодДекада",    НСтр("ru = 'Период декада'"));
	СписокПериодов.Добавить("ПериодМесяц",     НСтр("ru = 'Период месяц'"));
	СписокПериодов.Добавить("ПериодКвартал",   НСтр("ru = 'Период квартал'"));
	СписокПериодов.Добавить("ПериодПолугодие", НСтр("ru = 'Период полугодие'"));
	СписокПериодов.Добавить("ПериодГод",       НСтр("ru = 'Период год'"));
	
	ИмяПапки = "Периоды";
	СписокПолейНабораДанных = Новый СписокЗначений;
	ПапкаПолейНабораДанных = НаборДанных.Поля.Добавить(Тип("ПапкаПолейНабораДанныхСхемыКомпоновкиДанных"));
	ПапкаПолейНабораДанных.Заголовок   = ИмяПапки;
	ПапкаПолейНабораДанных.ПутьКДанным = ИмяПапки;
	
	Для каждого Период Из СписокПериодов Цикл
		ПолеНабораДанных = НаборДанных.Поля.Добавить(Тип("ПолеНабораДанныхСхемыКомпоновкиДанных"));
		ПолеНабораДанных.Поле        = Период.Значение;
		ПолеНабораДанных.Заголовок   = Период.Представление;
		ПолеНабораДанных.ПутьКДанным = ИмяПапки + "." + Период.Значение;
		СписокПолейНабораДанных.Добавить(ПолеНабораДанных);
	КонецЦикла;
	
	Возврат СписокПолейНабораДанных;
	
КонецФункции

// Функция добавляет поле итога в схему компоновки данных. Если параметр Выражение не указан, используется Сумма(ПутьКДанным)
Функция ДобавитьПолеИтога(СхемаКомпоновкиДанных, ПутьКДанным, Выражение = Неопределено) Экспорт
	
	Если Выражение = Неопределено Тогда
		Выражение = "Сумма(" + ПутьКДанным + ")";
	КонецЕсли;
	
	ПолеИтога = СхемаКомпоновкиДанных.ПоляИтога.Добавить();
	ПолеИтога.ПутьКДанным = ПутьКДанным;
	ПолеИтога.Выражение = Выражение;
	Возврат ПолеИтога;
	
КонецФункции

Функция ЭтоДетальнаяЗапись(ДанныеРасшифровки, Расшифровка) Экспорт
	
	ЭтоДетальнаяЗапись = Ложь;
	Элемент = ДанныеРасшифровки.Элементы[Расшифровка];
	Если ТипЗнч(Элемент) = Тип("ЭлементРасшифровкиКомпоновкиДанныхПоля") Тогда
		Элементы = Элемент.ПолучитьРодителей();
		Если Элементы.Количество() > 0 Тогда
			Элемент = Элементы[0];
			Если ТипЗнч(Элемент) = Тип("ЭлементРасшифровкиКомпоновкиДанныхГруппировка") Тогда
				ЭтоДетальнаяЗапись = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;
	Возврат ЭтоДетальнаяЗапись;
	
КонецФункции

// Вызывается из обработчика события "ПередЗагрузкойНастроекВКомпоновщик" при инициализации варианта отчета средствами БСП.
//
// Параметры:
//  ОтчетОбъект - ОтчетОбъект - Объект отчета.
//  Контекст - Структура - Контекст инициализации варианта очтета.
//  КлючСхемы - Строка - Ключ схемы компоновки данных.
//  КлючВарианта - Строка - Ключ варианта отчета.
//  НовыеНастройкиКД - НастройкиКомпоновкиДанных - Настройки, с которыми будет выполнена инициализация варианта отчета.
//  НовыеПользовательскиеНастройкиКД - ПользовательскиеНастройкиКомпоновкиДанных - Настройки, с которыми будет выполнена инициализация варианта отчета.
//
Процедура ПередЗагрузкойНастроекВКомпоновщик(ОтчетОбъект, Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД, ЗаполняемыеНастройки = Неопределено) Экспорт
	
	ОткрытИзРассылки = Неопределено;
	РежимВариантаОтчета = ПолучитьРежимВыполненияОтчета(ОтчетОбъект.Метаданные());
	Если ТипЗнч(Контекст) = Тип("Структура") Тогда
		Контекст.Вставить("РежимВариантаОтчета", РежимВариантаОтчета);
	ИначеЕсли ТипЗнч(Контекст) = Тип("ФормаКлиентскогоПриложения") И ЕстьРеквизитФормы(Контекст, "ОткрытИзРассылки") Тогда
		ОткрытИзРассылки = Контекст.ОткрытИзРассылки;
	КонецЕсли;
	
	Если НовыеНастройкиКД = Неопределено ИЛИ НовыеПользовательскиеНастройкиКД = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТипЗнч(Контекст) = Тип("Структура") Тогда
		ОткрытИзРассылки = 
			?(НовыеНастройкиКД.ДополнительныеСвойства.Свойство("ОткрытИзРассылки"), НовыеНастройкиКД.ДополнительныеСвойства.ОткрытИзРассылки, Истина);
	КонецЕсли;

	Если НЕ РежимВариантаОтчета Тогда
		ПередЗагрузкойНастроекВКомпоновщикРежимОбычный(ОтчетОбъект, Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД, ЗаполняемыеНастройки);  //БухгалтерскиеОтчетыВызовСервера
	ИначеЕсли ОткрытИзРассылки = Истина Тогда
		ПередЗагрузкойНастроекВКомпоновщикРежимВариантаОтчета(ОтчетОбъект, Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД, ЗаполняемыеНастройки);  //БухгалтерскиеОтчетыВызовСервера
	КонецЕсли;
	
	НовыеНастройкиКД.ДополнительныеСвойства.Вставить("ОткрытИзРассылки", ОткрытИзРассылки);
	НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Вставить("ОткрытИзРассылки", ОткрытИзРассылки);

КонецПроцедуры

Процедура ПередЗагрузкойНастроекВКомпоновщикРежимВариантаОтчета(ОтчетОбъект, Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД, ЗаполняемыеНастройки = Неопределено) Экспорт
	
    НастройкиОтчета         = Неопределено;
	ПараметрНастройкиОтчета = Неопределено;
	
	ПараметрНастройкиОтчета = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(НовыеНастройкиКД, "НастройкиОтчета");
	
	Если ПараметрНастройкиОтчета = Неопределено Тогда
		НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Свойство("НастройкиОтчета", ПараметрНастройкиОтчета);
	Иначе
		ПараметрНастройкиОтчета = ПараметрНастройкиОтчета.Значение;
	КонецЕсли;
	
	Если ТипЗнч(ПараметрНастройкиОтчета) = Тип("ХранилищеЗначения") Тогда
		НастройкиОтчета = ПараметрНастройкиОтчета.Получить();
	ИначеЕсли ТипЗнч(ПараметрНастройкиОтчета) = Тип("Структура") Тогда
		НастройкиОтчета = ПараметрНастройкиОтчета;
	КонецЕсли;
	
	Если НовыеНастройкиКД <> Неопределено Тогда
		НовыеНастройкиКД.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗаполнения", Истина);
	КонецЕсли;
		
	// Инициализируем пользовательские настройки вручную, если в них отсутствует параметр "НастройкиОтчета".
	// Такая ситуация возникает при добавлении варианта отчета в рассылку в основной форме элемента справочника "РассылкиОтчетов".
	Если НастройкиОтчета = Неопределено Тогда
		
		ИмяОтчета = ОтчетОбъект.Метаданные().ПолноеИмя();
		
		ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(НовыеНастройкиКД);
		
		// Получим пользовательские настройки из системного хранилища.
		КлючОбъекта = ИмяОтчета + "/" + КлючВарианта + "/ТекущиеПользовательскиеНастройки";
		ПользовательскиеНастройки = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(КлючОбъекта,, Неопределено);
		
		МенеджерОтчета  = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяОтчета);
		ПараметрыОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();
		НастройкиОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();
		
		// Добавим временные параметры, необходимые для заполнения настроек.
		ПараметрыОтчета.Вставить("Отчет",     ОбъектОтчетаВСтруктуру(ОтчетОбъект));
		ПараметрыОтчета.Вставить("ИмяФормы",  ИмяОтчета);
		ПараметрыОтчета.Вставить("Параметры", Новый Структура);
		ПараметрыОтчета.Вставить("Элементы",  Новый Массив);
		ПараметрыОтчета.Вставить("ИдентификаторОтчета", ОтчетОбъект.Метаданные().Имя);
		
		Если ПользовательскиеНастройки = Неопределено Тогда
			БухгалтерскиеОтчетыВызовСервера.УстановитьНастройкиПоУмолчанию(ПараметрыОтчета);
			Если ЗаполняемыеНастройки <> Неопределено Тогда
				//ОтчетОбъект.ЗаполнитьНастройкамиПоУмолчанию(ЗаполняемыеНастройки, ПараметрыОтчета.Отчет, ПараметрыОтчета);
			КонецЕсли;
		Иначе
			ПриЗагрузкеПользовательскихНастроекНаСервере(ПараметрыОтчета, ПользовательскиеНастройки, Истина);
		КонецЕсли;
		
		// Удалим служебные свойства.
		ПараметрыОтчета.Удалить("ИмяФормы");
		ПараметрыОтчета.Удалить("Параметры");
		ПараметрыОтчета.Удалить("Элементы");
		
		Если ПараметрыОтчета.ИдентификаторОтчета = "ЗадолженностьПокупателейПоСрокамДолга" ИЛИ ПараметрыОтчета.ИдентификаторОтчета = "ЗадолженностьПоставщикамПоСрокамДолга" Тогда
			ПараметрыОтчета.Отчет.КомпоновщикНастроек.ЗагрузитьНастройки(НовыеНастройкиКД);
			ПараметрыОтчета.Отчет.КомпоновщикНастроек.ЗагрузитьПользовательскиеНастройки(НовыеПользовательскиеНастройкиКД);
			Отчеты[ПараметрыОтчета.ИдентификаторОтчета].ЗаполнитьПоляВСоответствииСоСпискомИнтервалов(
				ПараметрыОтчета, ПараметрыОтчета.Отчет.СхемаКомпоновкиДанных, ПараметрыОтчета.Отчет.КомпоновщикНастроек, Истина);
			НовыеНастройкиКД = ПараметрыОтчета.Отчет.КомпоновщикНастроек.ПолучитьНастройки();
			НовыеПользовательскиеНастройкиКД = ПараметрыОтчета.Отчет.КомпоновщикНастроек.ПользовательскиеНастройки;
		КонецЕсли;
		
		// Часть настроек по умолчанию осталась незаполненной в структуре "ПараметрыОтчета", заполним их.
		ЗаполнитьЗначенияСвойств(ПараметрыОтчета, ПараметрыОтчета.Отчет);
		
		// Удалим служебные свойства.
		ПараметрыОтчета.Удалить("Отчет");
		
		// Скопируем настройки по умолчанию в структуру настроек, сохраняемых в рассылке.
		ЗаполнитьЗначенияСвойств(НастройкиОтчета, ПараметрыОтчета);

		// Отборы, порядок и условное оформление хранятся в настройках КД.
		НастройкиОтчета.Вставить("НастройкиКомпоновкиДанных", НовыеНастройкиКД);
		
		// Установим значение параметра схемы КД, в котором хранятся все настройки отчета.
		// Этот параметр используется в рассылке при формировании экземпляра отчета.
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			НовыеНастройкиКД, "НастройкиОтчета", Новый ХранилищеЗначения(НастройкиОтчета, Новый СжатиеДанных(9)), Истина);
			
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			ОтчетОбъект.КомпоновщикНастроек, "НастройкиОтчета", Новый ХранилищеЗначения(НастройкиОтчета, Новый СжатиеДанных(9)), Истина);
			
		НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Вставить("НастройкиОтчета", Новый ХранилищеЗначения(НастройкиОтчета, Новый СжатиеДанных(9)));
		
	КонецЕсли;
			
КонецПроцедуры

Процедура ПередЗагрузкойНастроекВКомпоновщикРежимОбычный(ОтчетОбъект, Контекст, КлючСхемы, КлючВарианта, НовыеНастройкиКД, НовыеПользовательскиеНастройкиКД, ЗаполняемыеНастройки = Неопределено) Экспорт

	ПараметрНастройкиОтчета = Неопределено;
	НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Свойство("НастройкиОтчета", ПараметрНастройкиОтчета);
	
	ПараметрДанныеОтчетаРассылка = Неопределено;
	НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Свойство("ДанныеОтчетаРассылка", ПараметрДанныеОтчетаРассылка);
	
	// Инициализируем пользовательские настройки вручную, если в них отсутствует параметр "НастройкиОтчета".
	// Такая ситуация возникает при добавлении варианта отчета в рассылку в основной форме элемента справочника "РассылкиОтчетов".
	Если ПараметрНастройкиОтчета = Неопределено ИЛИ ПараметрДанныеОтчетаРассылка = Неопределено Тогда
		
		ИмяОтчета = ОтчетОбъект.Метаданные().ПолноеИмя();
		
		ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(НовыеНастройкиКД);
		
		// Получим пользовательские настройки из системного хранилища.
		КлючОбъекта = ИмяОтчета + "/" + КлючВарианта;
		ПользовательскиеНастройки = ОбщегоНазначения.ХранилищеСистемныхНастроекЗагрузить(КлючОбъекта + "/ТекущиеПользовательскиеНастройки",, Неопределено);
		
		Если ПользовательскиеНастройки <> Неопределено Тогда
			ДополнительныеСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
			Если ДополнительныеСвойства.Свойство("ДанныеОтчета") И ТипЗнч(ДополнительныеСвойства.ДанныеОтчета) = Тип("ХранилищеЗначения") Тогда
				НастройкиОтчета_БК = ДополнительныеСвойства.ДанныеОтчета.Получить();
				НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Вставить("ДанныеОтчетаРассылка", ДополнительныеСвойства.ДанныеОтчета);
			КонецЕсли;
		КонецЕсли;
		
		МенеджерОтчета  = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ИмяОтчета);
		ПараметрыОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();
		НастройкиОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();

		// Добавим временные параметры, необходимые для заполнения настроек.
		ПараметрыОтчета.Вставить("Отчет"    , ОбъектОтчетаВСтруктуру(ОтчетОбъект));
		ПараметрыОтчета.Вставить("ИмяФормы" , ИмяОтчета);
		ПараметрыОтчета.Вставить("Параметры", Новый Структура);
		ПараметрыОтчета.Вставить("Элементы" , Новый Массив);
		ПараметрыОтчета.Вставить("ИдентификаторОтчета", ОтчетОбъект.Метаданные().Имя);
		
		Если ПользовательскиеНастройки = Неопределено Тогда

			УстановитьНастройкиПоУмолчанию(ПараметрыОтчета);

			Если ЗаполняемыеНастройки <> Неопределено Тогда
				ОтчетОбъект.ЗаполнитьНастройкамиПоУмолчанию(ЗаполняемыеНастройки, ПараметрыОтчета.Отчет);
			КонецЕсли;
			
			Если ПараметрыОтчета.Отчет.Свойство("Период") И ТипЗнч(ПараметрыОтчета.Отчет.Период) = Тип("Дата") Тогда
				ОтчетПериод = ПараметрыОтчета.Отчет.Период;
				ПараметрыОтчета.Дата2 = ?(ОтчетПериод <> НачалоМесяца(ОтчетПериод), НачалоМесяца(ОтчетПериод), ДобавитьМесяц(НачалоМесяца(ОтчетПериод), -1));
				ПараметрыОтчета.Дата3 = ?(НачалоМесяца(ОтчетПериод) <> НачалоГода(ОтчетПериод), НачалоГода(ОтчетПериод), ДобавитьМесяц(НачалоГода(ОтчетПериод), -12));
			КонецЕсли;
			
			ДанныеОтчетаРассылка = ОбщегоНазначенияКлиентСервер.СкопироватьСтруктуру(НастройкиОтчета);
			ДанныеОтчетаРассылка.Удалить("ДанныеРасшифровки");
			ДанныеОтчетаРассылка.Удалить("НастройкиКомпоновкиДанных");
			ДанныеОтчетаРассылка.Удалить("ОткрытИзРассылки");
			ДанныеОтчетаРассылка.Удалить("СхемаКомпоновкиДанных");
			ДанныеОтчетаРассылка.Удалить("РежимРасшифровки");
			ДанныеОтчетаРассылка.Удалить("ИзмененыНастройкиВариант");
			ЗаполнитьЗначенияСвойств(ДанныеОтчетаРассылка, ПараметрыОтчета);
			ЗаполнитьЗначенияСвойств(ДанныеОтчетаРассылка, ПараметрыОтчета.Отчет);
			
			НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Вставить("ДанныеОтчетаРассылка", Новый ХранилищеЗначения(ДанныеОтчетаРассылка, Новый СжатиеДанных(9)));
		Иначе
			ПараметрыОтчета.Вставить("ОткрытИзРассылки", Истина);
			ПриЗагрузкеПользовательскихНастроекНаСервере(ПараметрыОтчета, ПользовательскиеНастройки, Истина);
		КонецЕсли;
		
		// Удалим служебные свойства.
		ПараметрыОтчета.Удалить("ИмяФормы");
		ПараметрыОтчета.Удалить("Параметры");
		ПараметрыОтчета.Удалить("Элементы");
		
		// Часть настроек по умолчанию осталась незаполненной в структуре "ПараметрыОтчета", заполним их.
		ЗаполнитьЗначенияСвойств(ПараметрыОтчета, ПараметрыОтчета.Отчет);
		
		// Удалим служебные свойства.
		ПараметрыОтчета.Удалить("Отчет");
		
		// Скопируем настройки по умолчанию в структуру настроек, сохраняемых в рассылке.
		ЗаполнитьЗначенияСвойств(НастройкиОтчета, ПараметрыОтчета);
		
		// Отборы, порядок и условное оформление хранятся в настройках КД.
		НастройкиОтчета.Вставить("НастройкиКомпоновкиДанных", НовыеНастройкиКД);
		
		// Установим значение параметра схемы КД, в котором хранятся все настройки отчета.
		// Этот параметр используется в рассылке при формировании экземпляра отчета.
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			НовыеНастройкиКД, "НастройкиОтчета", Новый ХранилищеЗначения(НастройкиОтчета, Новый СжатиеДанных(9)), Истина);
			
		БухгалтерскиеОтчетыКлиентСервер.УстановитьПараметр(
			ОтчетОбъект.КомпоновщикНастроек, "НастройкиОтчета", Новый ХранилищеЗначения(НастройкиОтчета, Новый СжатиеДанных(9)), Истина);
			
		НовыеПользовательскиеНастройкиКД.ДополнительныеСвойства.Вставить("НастройкиОтчета", Новый ХранилищеЗначения(НастройкиОтчета, Новый СжатиеДанных(9)));
		
	КонецЕсли;
	
	НовыеНастройкиКД.ДополнительныеСвойства.Вставить("ПропуститьПроверкуЗаполнения", ПараметрНастройкиОтчета <> Неопределено);
	
КонецПроцедуры

// Вызывается из обработчика события "ПриКомпоновкеРезультата".
//
// Параметры:
//  ОтчетОбъект - ОтчетОбъект - Объект отчета.
//  ДокументРезультат - ТабличныйДокумент - Табличный документ для вывода результата формирования отчета.
//  ДанныеРасшифровки - ДанныеРасшифровкиКомпоновкиДанных - Данные расшифровки.
//
Процедура ОбработкаСобытияПриКомпоновкеРезультата(ОтчетОбъект, ДокументРезультат, ДанныеРасшифровки) Экспорт
	
	ДокументРезультат.Очистить();
	
	// Параметры формирования отчета.
	ПараметрыОтчета = ЗаполнитьПараметрыОтчетаИзНастроекКомпоновщика(ОтчетОбъект, ОтчетОбъект.КомпоновщикНастроек);
	
	Если ПараметрыОтчета = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОтчетМетаданные = ОтчетОбъект.Метаданные();
	МенеджерОтчета = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОтчетМетаданные.ПолноеИмя());
	
	Если ПараметрыОтчета.Интервалы <> Неопределено
		И ПараметрыОтчета.Интервалы.Количество() > 0
		И ОтчетМетаданные.ТабличныеЧасти.Найти("Интервалы") <> Неопределено Тогда

		МенеджерОтчета.ЗаполнитьПоляВСоответствииСоСпискомИнтервалов(
			ПараметрыОтчета, ПараметрыОтчета.СхемаКомпоновкиДанных, ОтчетОбъект.КомпоновщикНастроек, Истина);
			
		ОтчетОбъект.КомпоновщикНастроек.Настройки.Выбор.ИдентификаторПользовательскойНастройки = "";
		ПараметрыОтчета.НастройкиКомпоновкиДанных = ОтчетОбъект.КомпоновщикНастроек.ПолучитьНастройки();

	КонецЕсли;
	
	// Формирование отчета и помещение результата во временное хранилище.
	АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено);
	СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
	
	Если ПараметрыОтчета.Свойство("НастройкиКомпоновкиДанных") Тогда
		ОтчетОбъект.КомпоновщикНастроек.ЗагрузитьНастройки(ПараметрыОтчета.НастройкиКомпоновкиДанных);
	КонецЕсли;
	
	// Получение результатов формирования и копирование результатов в параметр "ДокументРезультат".
	ДанныеОтчета = ПолучитьИзВременногоХранилища(АдресХранилища);
	ДокументРезультат.Вывести(ДанныеОтчета.Результат);
	
	ДанныеРасшифровки = ДанныеОтчета.ДанныеРасшифровки;
	
	// Для рассылки отчета. Отчет рассылается всегда, даже если он пустой.
	ДопСвойства = ОтчетОбъект.КомпоновщикНастроек.ПользовательскиеНастройки.ДополнительныеСвойства;
	ДопСвойства.Вставить("ОтчетПустой", Ложь);
	
	// Для рассылки отчета. Используется для заполнения параметров шаблонов текста письма, отправляемого в рассылке.
	ДопСвойства.Вставить("ПредставлениеОтчета", МенеджерОтчета.ПолучитьТекстЗаголовка(ПараметрыОтчета));
	
КонецПроцедуры

Функция ЗаполнитьПараметрыОтчетаИзНастроекКомпоновщика(ОтчетОбъект, КомпоновщикНастроекКД)
	
	ДополнительныеСвойства = КомпоновщикНастроекКД.ПользовательскиеНастройки.ДополнительныеСвойства;
	ДанныеОтчета = "";
	ЗначениеНастройкиОтчета = Неопределено;
	Если ДополнительныеСвойства.Свойство("ДанныеОтчетаРассылка") Тогда
	    ДанныеОтчета = "ДанныеОтчетаРассылка";
	ИначеЕсли ДополнительныеСвойства.Свойство("ДанныеОтчета") Тогда
	    ДанныеОтчета = "ДанныеОтчета";
	ИначеЕсли ДополнительныеСвойства.Свойство("НастройкиОтчета") Тогда
	    ДанныеОтчета = "НастройкиОтчета";
	Иначе
		Возврат Неопределено;
	КонецЕсли;	
	Если ДополнительныеСвойства.Свойство(ДанныеОтчета) Тогда
		Если ТипЗнч(ДополнительныеСвойства[ДанныеОтчета]) = Тип("Структура") Тогда
			ЗначениеНастройкиОтчета = ДополнительныеСвойства[ДанныеОтчета];
		ИначеЕсли ТипЗнч(ДополнительныеСвойства[ДанныеОтчета]) = Тип("ХранилищеЗначения") Тогда
			ЗначениеНастройкиОтчета = ДополнительныеСвойства[ДанныеОтчета].Получить();
		КонецЕсли;
	КонецЕсли;	

	// Если в схеме КД нет служебного параметра "НастройкиОтчета" или он не инициализирован, то ничего не делаем.
	Если ЗначениеНастройкиОтчета = Неопределено Тогда
		Возврат Неопределено;
	КонецЕсли;
	
	МенеджерОтчета = ОбщегоНазначения.МенеджерОбъектаПоПолномуИмени(ОтчетОбъект.Метаданные().ПолноеИмя());
	
	ПараметрыОтчета = МенеджерОтчета.ПустыеПараметрыКомпоновкиОтчета();
	
	КомпоновщикНастроекКД.ЗагрузитьПользовательскиеНастройки(КомпоновщикНастроекКД.ПользовательскиеНастройки);
	
	ПараметрыОтчета.Вставить("СхемаКомпоновкиДанных",     ОтчетОбъект.СхемаКомпоновкиДанных);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета",       ОтчетОбъект.Метаданные().Имя);
	ПараметрыОтчета.Вставить("НастройкиКомпоновкиДанных", КомпоновщикНастроекКД.ПолучитьНастройки());
	
	КомпоновщикНастроекКД.ЗагрузитьНастройки(КомпоновщикНастроекКД.ПолучитьНастройки());
	
	ЗаполнитьЗначенияСвойств(ПараметрыОтчета, ЗначениеНастройкиОтчета);
	
	// Проверим наличие дополнительных параметров формирования отчета, которые зависят от каких-либо условий.
	// Определяются в модуле объекта отчета.
	ДопСвойства = КомпоновщикНастроекКД.Настройки.ДополнительныеСвойства;
	Если ДопСвойства.Свойство("ДополнительныеПараметрыОтчета") Тогда
		ОбщегоНазначенияКлиентСервер.ДополнитьСтруктуру(ПараметрыОтчета, ДопСвойства.ДополнительныеПараметрыОтчета, Истина);
	КонецЕсли;
	ОткрытИзРассылки = Ложь;
	Если ДопСвойства.Свойство("ОткрытИзРассылки") Тогда
		ОткрытИзРассылки = ДопСвойства.ОткрытИзРассылки;
		ПараметрыОтчета.Вставить("ОткрытИзРассылки", ОткрытИзРассылки);
	КонецЕсли;
	
	УстановкаПериодаОтчетаРассылка(ПараметрыОтчета, КомпоновщикНастроекКД.Настройки);
	
	Возврат ПараметрыОтчета;
	
КонецФункции

Функция ОбъектОтчетаВСтруктуру(Отчет) Экспорт
	
	МетаданныеОбъекта = Отчет.Метаданные();
	
	Структура = Новый Структура;
	
	Для Каждого Реквизит Из МетаданныеОбъекта.Реквизиты Цикл
		Структура.Вставить(Реквизит.Имя, Отчет[Реквизит.Имя]);
	КонецЦикла;
	
	Для Каждого ТабличнаяЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		Структура.Вставить(ТабличнаяЧасть.Имя, Отчет[ТабличнаяЧасть.Имя].Выгрузить());
	КонецЦикла;
	
	// КомпоновщикНастроек и СхемаКомпоновкиДанных всегда присутствуют в экземпляре объекта отчета
	Структура.Вставить("КомпоновщикНастроек",   Отчет.КомпоновщикНастроек);
	Структура.Вставить("СхемаКомпоновкиДанных", Отчет.СхемаКомпоновкиДанных);
	
	Возврат Структура;
	
КонецФункции

Функция ПолучитьРежимВыполненияОтчета(ОтчетМетаданные) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ОтчетыВРежимеВариантыОтчетов.Отчет,
		|	ОтчетыВРежимеВариантыОтчетов.Пользователь
		|ИЗ
		|	РегистрСведений.ОтчетыВРежимеВариантыОтчетов КАК ОтчетыВРежимеВариантыОтчетов
		|ГДЕ
		|	ОтчетыВРежимеВариантыОтчетов.Отчет = &Отчет
		|	И ОтчетыВРежимеВариантыОтчетов.Пользователь = &Пользователь";
	
	Запрос.УстановитьПараметр("Отчет", ОтчетМетаданные.ПолноеИмя());
	Запрос.УстановитьПараметр("Пользователь", Пользователи.ТекущийПользователь());
	
	РезультатЗапроса = Запрос.Выполнить().Выбрать();
	
	Если РезультатЗапроса.Следующий() Тогда
		Возврат Истина;
	Иначе
		СписокОтчетов = Новый СписокЗначений;
		ОтчетыВызовСервераБК.СписокОтчетовВсегдаВРежимеВариантыотчетов(СписокОтчетов);
		Если СписокОтчетов.НайтиПоЗначению(ОтчетМетаданные.Имя) <> Неопределено Тогда
			// Отчет всегда работает в режиме "Варианты отчетов"
		    Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

Процедура УстановкаПериодаОтчетаРассылка(НастройкиОтчета, ПользовательскиеНастройки) Экспорт
	
	ДопСвойства = ПользовательскиеНастройки.ДополнительныеСвойства;
	ОткрытИзРассылки = ?(ДопСвойства.Свойство("ОткрытИзРассылки"), ДопСвойства.ОткрытИзРассылки, Ложь);
	
	Если ТипЗнч(НастройкиОтчета) = Тип("Структура") И ОткрытИзРассылки Тогда
		ПараметрПериод = БухгалтерскиеОтчетыКлиентСервер.ПолучитьПараметр(ПользовательскиеНастройки, "ПериодОтчета");
		Если ПараметрПериод <> Неопределено И ПараметрПериод.Использование
			И ТипЗнч(ПараметрПериод.Значение) = Тип("СтандартныйПериод")
			И ОткрытИзРассылки Тогда
			НастройкиОтчета.Вставить("ПериодОтчета" , ПараметрПериод.Значение);
			НастройкиОтчета.Вставить("НачалоПериода", ПараметрПериод.Значение.ДатаНачала);
			НастройкиОтчета.Вставить("КонецПериода" , КонецДня(ПараметрПериод.Значение.ДатаОкончания));
			НастройкиОтчета.Вставить("Период"       , КонецДня(ПараметрПериод.Значение.ДатаОкончания));
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

// Отправка отчетов по электронной почте

Функция ПараметрыОтправкиПисьма(ВыбранныеПараметры, ПараметрыОтчета) Экспорт
	
	СписокВложений = ПоместитьТабличныеДокументыВоВременноеХранилище(ВыбранныеПараметры, ПараметрыОтчета);
	
	// Контроль уникальности имен.
	ШаблонИмениФайла = "%1%2.%3";
	ИспользованныеИменаФайлов = Новый Соответствие;
	Для Каждого Вложение Из СписокВложений Цикл
		ИмяФайла = Вложение.Представление;
		НомерИспользования = ?(ИспользованныеИменаФайлов[ИмяФайла] <> Неопределено,
			ИспользованныеИменаФайлов[ИмяФайла] + 1, 1);
		ИспользованныеИменаФайлов.Вставить(ИмяФайла, НомерИспользования);
		Если НомерИспользования > 1 Тогда
			Файл = Новый Файл(ИмяФайла);
			ИмяФайла = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ШаблонИмениФайла,
				Файл.ИмяБезРасширения, " (" + НомерИспользования + ")", Файл.Расширение);
		КонецЕсли;
		Вложение.Представление = ИмяФайла;
	КонецЦикла;
	
	ПараметрыОтправки = Новый Структура;
	ПараметрыОтправки.Вставить("Тема"    , ПараметрыОтчета.Заголовок);
	ПараметрыОтправки.Вставить("Вложения", СписокВложений);
	ПараметрыОтправки.Вставить("УдалятьФайлыПослеОтправки", Истина);
	
	Возврат ПараметрыОтправки;
	
КонецФункции

Функция ПоместитьТабличныеДокументыВоВременноеХранилище(ПереданныеНастройки, ПараметрыОтчета)
	Перем ЗаписьZipФайла, ИмяАрхива;
	
	НастройкиСохранения = Новый Структура;
	НастройкиСохранения.Вставить("ФорматыСохранения", Новый Массив);
	НастройкиСохранения.Вставить("УпаковатьВАрхив"  , Ложь);
	НастройкиСохранения.Вставить("ПереводитьИменаФайловВТранслит", Ложь);
	
	ЗаполнитьЗначенияСвойств(НастройкиСохранения, ПереданныеНастройки);
	
	ТабличныйДокумент = ПолучитьИзВременногоХранилища(ПараметрыОтчета.ТабличныйДокумент);
	
	Вложения = Новый Массив;
	
	// подготовка архива
	Если НастройкиСохранения.УпаковатьВАрхив Тогда
		ИмяАрхива = ПолучитьИмяВременногоФайла("zip");
		ЗаписьZipФайла = Новый ЗаписьZipФайла(ИмяАрхива);
	КонецЕсли;
	
	// подготовка временной папки
	ИмяВременнойПапки = ПолучитьИмяВременногоФайла();
	СоздатьКаталог(ИмяВременнойПапки);
	ИспользованныеИменаФайлов = Новый Соответствие;
	
	ВыбранныеФорматыСохранения = НастройкиСохранения.ФорматыСохранения;
	ПереводитьИменаФайловВТранслит = НастройкиСохранения.ПереводитьИменаФайловВТранслит;
	ТаблицаФорматов = УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента();
	
	Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
		ТипФайла = ТипФайлаТабличногоДокумента[ВыбранныйФормат];
		НастройкиФормата = ТаблицаФорматов.НайтиСтроки(Новый Структура("ТипФайлаТабличногоДокумента", ТипФайла))[0];
		ИмяФайла = ПараметрыОтчета.Заголовок;
		ИмяФайла = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайла);
		
		Если ПереводитьИменаФайловВТранслит Тогда
			ИмяФайла = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(ИмяФайла);
		КонецЕсли;
		
		ИмяФайла = ИмяФайла + "." + НастройкиФормата.Расширение;
		
		ПолноеИмяФайла = УникальноеИмяФайла(ОбщегоНазначенияКлиентСервер.ДобавитьКонечныйРазделительПути(ИмяВременнойПапки) + ИмяФайла);
		ТабличныйДокумент.Записать(ПолноеИмяФайла, ТипФайла);
		
		Если ТипФайла = ТипФайлаТабличногоДокумента.HTML Тогда
			ВставитьКартинкиВHTML(ПолноеИмяФайла);
		КонецЕсли;
		
		Если ЗаписьZipФайла <> Неопределено Тогда 
			ЗаписьZipФайла.Добавить(ПолноеИмяФайла);
		Иначе
			ДвоичныеДанные = Новый ДвоичныеДанные(ПолноеИмяФайла);
			ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ПараметрыОтчета.УникальныйИдентификатор);
			ОписаниеФайла = Новый Структура;
			ОписаниеФайла.Вставить("Представление", ИмяФайла);
			ОписаниеФайла.Вставить("АдресВоВременномХранилище", ПутьВоВременномХранилище);
			Если ТипФайла = ТипФайлаТабличногоДокумента.ANSITXT Тогда
				ОписаниеФайла.Вставить("Кодировка", "windows-1251");
			КонецЕсли;
			Вложения.Добавить(ОписаниеФайла);
		КонецЕсли;
	КонецЦикла;
	
	// Если архив подготовлен, записываем и помещаем его во временное хранилище.
	Если ЗаписьZipФайла <> Неопределено Тогда
		
		ИмяФайлаДляАрхива = ПараметрыОтчета.Заголовок;
		ИмяФайлаДляАрхива = ОбщегоНазначенияКлиентСервер.ЗаменитьНедопустимыеСимволыВИмениФайла(ИмяФайлаДляАрхива);
		
		Если ПереводитьИменаФайловВТранслит Тогда
			ИмяФайлаДляАрхива = СтроковыеФункцииКлиентСервер.СтрокаЛатиницей(ИмяФайлаДляАрхива);
		КонецЕсли;
		
		ИмяФайлаДляАрхива = ИмяФайлаДляАрхива + ".zip";
		
		ЗаписьZipФайла.Записать();
		ФайлАрхива = Новый Файл(ИмяАрхива);
		ДвоичныеДанные = Новый ДвоичныеДанные(ИмяАрхива);
		ПутьВоВременномХранилище = ПоместитьВоВременноеХранилище(ДвоичныеДанные, ПараметрыОтчета.УникальныйИдентификатор);
		ОписаниеФайла = Новый Структура;
		ОписаниеФайла.Вставить("Представление", ИмяФайлаДляАрхива);
		ОписаниеФайла.Вставить("АдресВоВременномХранилище", ПутьВоВременномХранилище);
		Вложения.Добавить(ОписаниеФайла);
		
	КонецЕсли;
	
	УдалитьФайлы(ИмяВременнойПапки);
	Если ЗначениеЗаполнено(ИмяАрхива) Тогда
		УдалитьФайлы(ИмяАрхива);
	КонецЕсли;
	
	Возврат Вложения;
	
КонецФункции

Процедура ВставитьКартинкиВHTML(ИмяФайлаHTML)
	
	ТекстовыйДокумент = Новый ТекстовыйДокумент();
	ТекстовыйДокумент.Прочитать(ИмяФайлаHTML, КодировкаТекста.UTF8);
	ТекстHTML = ТекстовыйДокумент.ПолучитьТекст();
	
	ФайлHTML = Новый Файл(ИмяФайлаHTML);
	
	ИмяПапкиКартинок = ФайлHTML.ИмяБезРасширения + "_files";
	ПутьКПапкеКартинок = СтрЗаменить(ФайлHTML.ПолноеИмя, ФайлHTML.Имя, ИмяПапкиКартинок);
	
	// Ожидается, что в папке будут только картинки.
	ФайлыКартинок = НайтиФайлы(ПутьКПапкеКартинок, "*");
	
	Для Каждого ФайлКартинки Из ФайлыКартинок Цикл
		КартинкаТекстом = Base64Строка(Новый ДвоичныеДанные(ФайлКартинки.ПолноеИмя));
		КартинкаТекстом = "data:image/" + Сред(ФайлКартинки.Расширение,2) + ";base64," + Символы.ПС + КартинкаТекстом;
		
		ТекстHTML = СтрЗаменить(ТекстHTML, ИмяПапкиКартинок + "\" + ФайлКартинки.Имя, КартинкаТекстом);
	КонецЦикла;
		
	ТекстовыйДокумент.УстановитьТекст(ТекстHTML);
	ТекстовыйДокумент.Записать(ИмяФайлаHTML, КодировкаТекста.UTF8);
	
КонецПроцедуры

Функция УникальноеИмяФайла(ИмяФайла)
	
	Файл = Новый Файл(ИмяФайла);
	ИмяБезРасширения = Файл.ИмяБезРасширения;
	Расширение = Файл.Расширение;
	Папка = Файл.Путь;
	
	Счетчик = 1;
	Пока Файл.Существует() Цикл
		Счетчик = Счетчик + 1;
		Файл = Новый Файл(Папка + ИмяБезРасширения + " (" + Счетчик + ")" + Расширение);
	КонецЦикла;
	
	Возврат Файл.ПолноеИмя;

КонецФункции


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ЕстьРеквизитФормы(Форма, ИмяРеквизита)
	
	Для Каждого РеквизитФормы Из Форма.ПолучитьРеквизиты() Цикл
		Если ВРег(РеквизитФормы.Имя) = ВРег(ИмяРеквизита) Тогда
			Возврат Истина;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Ложь;
	
КонецФункции

Функция ПолучитьЗначениеКлючаОбъектаПоТипу(Тип, Вариант)
	
	Если Вариант = "СчетаИсключаемые" Тогда
		Если Тип = 1 Тогда
			КлючОбъекта = "Отчеты.СчетаИсключаемыеИзРасчетаЗадолженностиПокупателей";
		ИначеЕсли Тип = 2 Тогда
			КлючОбъекта = "Отчеты.СчетаИсключаемыеИзРасчетаЗадолженностиПоставщикам";
		ИначеЕсли Тип = 3 Тогда
			КлючОбъекта = "Отчеты.СчетаИсключаемыеИзРасчета";
		КонецЕсли;
	КонецЕсли;
	
	Если Вариант = "СчетаПользователя" Тогда
		Если Тип = 1 Тогда
			КлючОбъекта = "Отчеты.СчетаПользователяДляРасчетаЗадолженностиПокупателей";
		ИначеЕсли Тип = 2 Тогда
			КлючОбъекта = "Отчеты.СчетаПользователяДляРасчетаЗадолженностиПоставщикам";
		КонецЕсли;
	КонецЕсли;
	
	Возврат КлючОбъекта;
	
КонецФункции

Процедура ВыводПодписейРуководителей(ПараметрыОтчета, Результат)
	
	Макет = ПолучитьОбщийМакет("ОбщиеОбластиСтандартногоОтчета");
	ОбластьПодписи = Макет.ПолучитьОбласть("ПодписиРуководителей");
	
	ВыбиратьОрганизацию = Ложь;
	Попытка
		СписокСтруктурныхЕдиниц = ПараметрыОтчета.СписокСтруктурныхЕдиниц;
		Если СписокСтруктурныхЕдиниц.Количество() > 0 Тогда
			ВыбиратьОрганизацию = Истина;
		КонецЕсли;
	Исключение
	КонецПопытки;

	Если ВыбиратьОрганизацию Тогда
		Если СписокСтруктурныхЕдиниц.Количество() = 1 Тогда
			Организация = СписокСтруктурныхЕдиниц[0].Значение;
		Иначе
			Организация = Неопределено;
		КонецЕсли;	
	Иначе
		Организация = Неопределено;
	КонецЕсли;
	
	Если Организация = Неопределено Тогда
		ОбластьПодписи.Параметры.РукРасшифровкаПодписи     = "";	
		ОбластьПодписи.Параметры.РукДолжность              = "";
		ОбластьПодписи.Параметры.ГлавБухРасшифровкаПодписи = "";	
		ОбластьПодписи.Параметры.ГлавБухДолжность          = "";
	Иначе
		Руководители = ОбщегоНазначенияБКВызовСервера.ОтветственныеЛицаОрганизаций(Организация, ТекущаяДатаСеанса());
		ОбластьПодписи.Параметры.РукРасшифровкаПодписи     = Руководители.Руководитель;	
		ОбластьПодписи.Параметры.РукДолжность              = Руководители.РуководительДолжность;
		ОбластьПодписи.Параметры.ГлавБухРасшифровкаПодписи = Руководители.ГлавныйБухгалтер;	
		ОбластьПодписи.Параметры.ГлавБухДолжность          = Руководители.ГлавныйБухгалтерДолжность;
	КонецЕсли;
	
	Если ПараметрыОтчета.Свойство("ПримечаниеПодписиРуководителей") И ЗначениеЗаполнено(ПараметрыОтчета.ПримечаниеПодписиРуководителей) Тогда
		ОбластьПодписи.Параметры.ТекстПримечание = ПараметрыОтчета.ПримечаниеПодписиРуководителей;
	КонецЕсли;

	Результат.Вывести(ОбластьПодписи);
	
КонецПроцедуры

Функция ЭлементыОтборовИдентичны(ЭлементОтбора1, ЭлементОтбора2, ДополнениеОтбора = Неопределено)
	
	Если ТипЗнч(ЭлементОтбора1) <> ТипЗнч(ЭлементОтбора2) Тогда
		Возврат Ложь
	КонецЕсли;
	
	Если ТипЗнч(ЭлементОтбора1) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
		
		Если ЭлементОтбора1.Элементы.Количество() <> ЭлементОтбора2.Элементы.Количество() Тогда
			Возврат Ложь
		КонецЕсли;
		
		ЭлементыГруппы2 = Новый СписокЗначений;
		Для Каждого ЭлементГруппы2 Из ЭлементОтбора2.Элементы Цикл
			ЭлементыГруппы2.Добавить(ЭлементГруппы2);
		КонецЦикла;
		
		Для Каждого ЭлементГруппы1 Из ЭлементОтбора1.Элементы Цикл
			Для Каждого ЭлементГруппы2 Из ЭлементыГруппы2 Цикл
				Если ЭлементыОтборовИдентичны(ЭлементГруппы1, ЭлементГруппы2.Значение) Тогда
					ЭлементыГруппы2.Удалить(ЭлементГруппы2);
					Прервать;
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
		
		Возврат ЭлементыГруппы2.Количество() = 0
		
	КонецЕсли;
	
	ЛевоеЗначение = ЭлементОтбора2.ЛевоеЗначение;
	Если ТипЗнч(ДополнениеОтбора) = Тип("Структура") Тогда
		Если ДополнениеОтбора.Свойство("ЛевоеЗначение") Тогда
			ЛевоеЗначение = ДополнениеОтбора.ЛевоеЗначение;
		КонецЕсли;
	КонецЕсли;
	
	Возврат
	ЭлементОтбора1.Использование = ЭлементОтбора2.Использование И
	ЭлементОтбора1.ВидСравнения = ЭлементОтбора2.ВидСравнения  И
	ЭлементОтбора1.ЛевоеЗначение = ЛевоеЗначение И
	ЭлементОтбора1.ПравоеЗначение = ЭлементОтбора2.ПравоеЗначение
	
КонецФункции

// Дополняет основной отбор элементами из второго с проверкой на дублирование
//
// Параметры:
//  ОсновнойОтбор - ОтборКомпоновкиДанных
//  ДобавляемыйОтбор - ОтборКомпоновкиДанных
// 
Процедура ДополнитьОтборИзОтбора(ОсновнойОтбор, ДобавляемыйОтбор, ДоступныеПоляОтбора, Счет = Неопределено, ВидыСубконто = Неопределено, ИсключатьОтборПоОрганизации = Истина)
	
	Для Каждого ЭлементДобавляемогоОтбора Из ДобавляемыйОтбор.Элементы Цикл
		Если ЭлементДобавляемогоОтбора.Использование И (Не ИсключатьОтборПоОрганизации Или ЭлементДобавляемогоОтбора.Представление <> "###ОтборПоОрганизации###") Тогда
			
			Если ДоступныеПоляОтбора.НайтиПоле(ЭлементДобавляемогоОтбора.ЛевоеЗначение) <> Неопределено Тогда
				ДополнениеОтбора = Новый Структура;
				Если Счет <> Неопределено И ВидыСубконто <> Неопределено Тогда
					ДобавляемоеЛевоеЗначение = Строка(ЭлементДобавляемогоОтбора.ЛевоеЗначение);
					Если СтрНачинаетсяС(ДобавляемоеЛевоеЗначение, "Субконто") Тогда
						НомерСубконто = Прав(ДобавляемоеЛевоеЗначение, 1);
						Если СтрНайти("123", НомерСубконто) Тогда
							ИндексВидаСубконто = Число(НомерСубконто) - 1;
							Если ИндексВидаСубконто < ВидыСубконто.Количество() Тогда
								СтрокаСубконто = Счет.ВидыСубконто.Найти(ВидыСубконто[ИндексВидаСубконто].Значение);
								Если СтрокаСубконто <> Неопределено Тогда
									ДополнениеОтбора.Вставить("ЛевоеЗначение", Новый ПолеКомпоновкиДанных("Субконто" + СтрокаСубконто.НомерСтроки));
								КонецЕсли;
							КонецЕсли;
						КонецЕсли;
					КонецЕсли;
				КонецЕсли;
				
				ДобавлятьЭлементОтбора = Истина;
				Для Каждого ЭлементОсновногоОтбора Из ОсновнойОтбор.Элементы Цикл
					Если ЭлементыОтборовИдентичны(ЭлементОсновногоОтбора, ЭлементДобавляемогоОтбора, ДополнениеОтбора) Тогда
						ДобавлятьЭлементОтбора = Ложь;
						Прервать;
					КонецЕсли;
				КонецЦикла;
				
				Если ДобавлятьЭлементОтбора Тогда
					НовыйЭлементОтбора = ОсновнойОтбор.Элементы.Добавить(ТипЗнч(ЭлементДобавляемогоОтбора));
					ЗаполнитьЗначенияСвойств(НовыйЭлементОтбора, ЭлементДобавляемогоОтбора);
					ЗаполнитьЗначенияСвойств(НовыйЭлементОтбора, ДополнениеОтбора);
					Если ТипЗнч(НовыйЭлементОтбора) = Тип("ГруппаЭлементовОтбораКомпоновкиДанных") Тогда
						ДополнитьОтборИзОтбора(НовыйЭлементОтбора, ЭлементДобавляемогоОтбора, ДоступныеПоляОтбора, Счет, ВидыСубконто);
					КонецЕсли;
				КонецЕсли;
			КонецЕсли;
			
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры
