#Область Штрихкода

Функция ПечатьШтрихкодовОС(ОписаниеКоманды) Экспорт
	
	АдресВХранилище =
		УправлениеПечатьюБКВызовСервера.ДанныеДляПечатиШтрихкодовОС(
			ОписаниеКоманды.Идентификатор,
			ОписаниеКоманды.ОбъектыПечати,
			ОписаниеКоманды.ДополнительныеПараметры);
			
	Если АдресВХранилище <> Неопределено Тогда 
		ОткрытьФорму(
			"Обработка.ПечатьШтрихкодов.Форма.ФормаПечатиОС",
			Новый Структура("АдресВХранилище, НазначениеШаблона", АдресВХранилище, Неопределено),
			ОписаниеКоманды.Форма,
			Новый УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;	
	
КонецФункции

Функция ПечатьШтрихкодовНоменклатуры(ОписаниеКоманды) Экспорт
	
	АдресВХранилище =
		УправлениеПечатьюБКВызовСервера.ДанныеДляПечатиШтрихкодовНоменклатуры(
			ОписаниеКоманды.Идентификатор,
			ОписаниеКоманды.ОбъектыПечати);
			
	Если АдресВХранилище <> Неопределено Тогда 
		ОткрытьФорму(
			"Обработка.ПечатьШтрихкодов.Форма.ФормаПечатиНоменклатуры",
			Новый Структура("АдресВХранилище, НазначениеШаблона", АдресВХранилище, Неопределено),
			ОписаниеКоманды.Форма,
			Новый УникальныйИдентификатор,,,,РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	КонецЕсли;	
	
КонецФункции


#КонецОбласти

Функция ВыполнитьКомандуПечати(ОписаниеКоманды) Экспорт
	
	Если ОписаниеКоманды.Свойство("ЗаголовокФормы") И ЗначениеЗаполнено(ОписаниеКоманды.ЗаголовокФормы) Тогда
		ПараметрыПечати = Новый Структура("ЗаголовокФормы", Строка(ОписаниеКоманды.ЗаголовокФормы));
	Иначе
		ПараметрыПечати = ПолучитьЗаголовокПечатнойФормы(ОписаниеКоманды.ОбъектыПечати);
	КонецЕсли;
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор, ОписаниеКоманды.ОбъектыПечати,
		ОписаниеКоманды.Форма, ПараметрыПечати);
	
КонецФункции
	
Функция ВыполнитьКомандуПечатиСчетаФактуры(ОписаниеКоманды) Экспорт
	
	Если Найти(ОписаниеКоманды.Идентификатор, "СчетФактураВыданный") <> 0 Тогда
		ВидСчетаФактуры = "СчетФактураВыданный";
	Иначе
		ВидСчетаФактуры = "СчетФактураПолученный";
	КонецЕсли;
		
	МассивСчетовФактур = Новый Массив;
	Если ОписаниеКоманды.ОбъектыПечати.Количество() = 1 Тогда
		СчетФактура = УчетНДСИАкцизаВызовСервера.НайтиПодчиненныйДокумент(ВидСчетаФактуры, ОписаниеКоманды.ОбъектыПечати[0]);
		МассивСчетовФактур.Добавить(СчетФактура);
	КонецЕсли;

	ПараметрыПечати = ПолучитьЗаголовокПечатнойФормы(МассивСчетовФактур);
	
	Если ОписаниеКоманды.Свойство("ДополнительныеПараметры") Тогда
		ПараметрыПечати.Вставить("ДополнительныеПараметры", ОписаниеКоманды.ДополнительныеПараметры);
	КонецЕсли; 
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ОписаниеКоманды.МенеджерПечати, ОписаниеКоманды.Идентификатор, ОписаниеКоманды.ОбъектыПечати, 
		ОписаниеКоманды.Форма, ПараметрыПечати);
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Процедуры для команд печати

Функция ПолучитьЗаголовокПечатнойФормы(ПараметрКоманды) Экспорт 
	
	Если Тип(ПараметрКоманды) = Тип("Массив") И ПараметрКоманды.Количество() = 1 Тогда 
		Возврат Новый Структура("ЗаголовокФормы", Строка(ПараметрКоманды[0]));
	Иначе
		Возврат Новый Структура;
	КонецЕсли;
	
КонецФункции
