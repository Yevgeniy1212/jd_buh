////////////////////////////////////////////////////////////////////////////////
// УправлениеЦенообразованиемВызовСервера: 
//  
////////////////////////////////////////////////////////////////////////////////

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

Функция ПолучитьЦеныНоменклатуры(СтруктураЗначений, МассивНоменклатуры, ДатаДокумента, Контрагент, Организация) Экспорт
	
	// Дастанем нужные цены запросом
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивНоменклатуры",	МассивНоменклатуры);
	Запрос.УстановитьПараметр("Дата",				КонецДня(ДатаДокумента));
	Запрос.УстановитьПараметр("ТипЦен",				СтруктураЗначений.ТипЦен);
	Запрос.УстановитьПараметр("Организация",		Организация);
	 
	Запрос.Текст = "ВЫБРАТЬ
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура КАК Номенклатура,
	|	ЦеныНоменклатурыСрезПоследних.Номенклатура.СтавкаНДС КАК СтавкаНДС,
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен       КАК ТипЦен,
	|	ЦеныНоменклатурыСрезПоследних.Валюта       КАК ВалютаЦены,
	|	ЦеныНоменклатурыСрезПоследних.Цена         КАК Цена,
	|	КурсыВалютСрезПоследних.Курс               КАК КурсВалютыЦены,
	|	КурсыВалютСрезПоследних.Кратность          КАК КратностьВалютыЦены,
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен.ЦенаВключаетНДС          КАК ЦенаВключаетНДС,
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен.ПорядокОкругления        КАК ПорядокОкругления,
	|	ЦеныНоменклатурыСрезПоследних.ТипЦен.ОкруглятьВБольшуюСторону КАК ОкруглятьВБольшуюСторону
	|ИЗ
	|	РегистрСведений.ЦеныНоменклатуры.СрезПоследних(&Дата, Номенклатура В (&МассивНоменклатуры) И ТипЦен = &ТипЦен И Организация = &Организация) КАК ЦеныНоменклатурыСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.КурсыВалют.СрезПоследних(&Дата, ) КАК КурсыВалютСрезПоследних
	|		ПО ЦеныНоменклатурыСрезПоследних.Валюта = КурсыВалютСрезПоследних.Валюта";

	ВыборкаЗапроса = Запрос.Выполнить().Выбрать();
	
	СоответствиеДанныхНоменклатуры = Новый Соответствие;
	Пока ВыборкаЗапроса.Следующий() Цикл
		СтруктураДанныхНоменклатуры = Новый Структура("СтавкаНДС, ТипЦен, ВалютаЦены, Цена, КурсВалютыЦены, КратностьВалютыЦены, ЦенаВключаетНДС, ПорядокОкругления, ОкруглятьВБольшуюСторону");
		
		ЗаполнитьЗначенияСвойств(СтруктураДанныхНоменклатуры, ВыборкаЗапроса);
		СоответствиеДанныхНоменклатуры.Вставить(ВыборкаЗапроса.Номенклатура, СтруктураДанныхНоменклатуры);
	КонецЦикла;	
	
	Возврат СоответствиеДанныхНоменклатуры;

КонецФункции

Функция ПолучитьСведенияОбУчетнойПолитикеОрганизации(Организация, ДатаДокумента) Экспорт 
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	УчетнаяПолитикаНалоговыйУчетСрезПоследних.Организация КАК Организация,
	               |	УчетнаяПолитикаНалоговыйУчетСрезПоследних.ОрганизацияЯвляетсяПлательщикомНДС КАК ОрганизацияЯвляетсяПлательщикомНДС,
	               |	УчетнаяПолитикаНалоговыйУчетСрезПоследних.ОрганизацияЯвляетсяПлательщикомАкциза КАК ОрганизацияЯвляетсяПлательщикомАкциза
	               |ИЗ
	               |	РегистрСведений.УчетнаяПолитикаНалоговыйУчет.СрезПоследних(&ДатаДокумента, Организация = &Организация) КАК УчетнаяПолитикаНалоговыйУчетСрезПоследних";
	
	Запрос.УстановитьПараметр("ДатаДокумента", ДатаДокумента);
	Запрос.УстановитьПараметр("Организация", Организация);
	
	Выборка = запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		СведенияОбУчетнойПолитикеОрганизации = Новый Структура();
		СведенияОбУчетнойПолитикеОрганизации.Вставить("ЯвляетсяПлательщикомНДС", 	Выборка.ОрганизацияЯвляетсяПлательщикомНДС);
		СведенияОбУчетнойПолитикеОрганизации.Вставить("ЯвляетсяПлательщикомАкциза", Выборка.ОрганизацияЯвляетсяПлательщикомАкциза);
	КонецЦикла;
	
	Возврат СведенияОбУчетнойПолитикеОрганизации;
	
КонецФункции

Функция ПолучитьВалютуВзаиморасчетов(ДоговорКонтрагента) Экспорт
	Возврат ДоговорКонтрагента.ВалютаВзаиморасчетов;
КонецФункции