
#Область ПрограммныйИнтерфейс

// Вызывается из события ПриЗаписи справочника Пользователи.
//
// Параметры:
//  Ссылка - СправочникСсылка.Пользователи - Записываемый пользователь.
//  ЭтоНовый - Булево - Истина, если записывается новый пользователь. Ложь в противном случае.
//
Процедура ПриЗаписиПользователя(Знач Ссылка, Знач ЭтоНовый) Экспорт
	
	Если ЭтоНовый Тогда
		ЗаписатьНастройкиПоУмолчаниюРегистрСведенийНастройкиПользователей(Ссылка);
	КонецЕсли;
	
КонецПроцедуры

Функция КонтактнаяИнформацияПользователяБК(Пользователь, СтруктураКИ = Неопределено) Экспорт
	
	Если СтруктураКИ = Неопределено Тогда
		СтруктураКИ = Новый Структура;
		СтруктураКИ.Вставить("Телефон", "");
		СтруктураКИ.Вставить("АдресЭлектроннойПочты", "");
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Пользователь) Тогда
		
		Запрос = Новый Запрос;
		Запрос.Текст = 
		"ВЫБРАТЬ
		|	КонтактнаяИнформация.Вид КАК Вид,
		|	КонтактнаяИнформация.Представление
		|ИЗ
		|	РегистрСведений.КонтактнаяИнформация КАК КонтактнаяИнформация
		|ГДЕ
		|	КонтактнаяИнформация.Объект = &Объект
		|	И КонтактнаяИнформация.Вид В(&МассивВидовКИ)";
		
		Запрос.УстановитьПараметр("Объект", Пользователь);
		
		АдресЭлектроннойПочты = Справочники.ВидыКонтактнойИнформации.СлужебныйАдресЭлектроннойПочтыПользователя;
		Телефон               = Справочники.ВидыКонтактнойИнформации.СлужебныйТелефонПользователя;
		
		МассивВидовКИ = Новый Массив;
		МассивВидовКИ.Добавить(АдресЭлектроннойПочты);
		МассивВидовКИ.Добавить(Телефон);	
		Запрос.УстановитьПараметр("МассивВидовКИ", МассивВидовКИ);
			
		Выборка = Запрос.Выполнить().Выбрать();
		
		Пока Выборка.Следующий() Цикл
			
			Если Выборка.Вид = АдресЭлектроннойПочты Тогда
				СтруктураКИ.Вставить("АдресЭлектроннойПочты", СокрЛП(Выборка.Представление));
			ИначеЕсли Выборка.Вид = Телефон Тогда
				СтруктураКИ.Вставить("Телефон", СокрЛП(Выборка.Представление));
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат СтруктураКИ;
	
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Записывает для указанного пользователя настройки в регистр сведений НастройкиПользователей.
// Все существующие настройки удаляются.
// Устанавливает настройки: ПоказыватьВДокументахСчетаУчета, ОсновнойОтветственный, УчетПоВсемОрганизациям.
// Остальные настройки записываются со значениями по умолчанию.
//
// Параметры:
//  Пользователь - СправочникСсылка.Пользователи - Пользователь, для которого будут записаны настройки.
//
Процедура ЗаписатьНастройкиПоУмолчаниюРегистрСведенийНастройкиПользователей(Знач Пользователь)
	
	//////////////////////////////////////////////////////////////////////////////
	// Заполнить все возможные настройки пользователя пустыми значениями.
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	НастройкиПользователей.Ссылка КАК Настройка,
	|	НастройкиПользователей.ТипЗначения
	|ИЗ
	|	ПланВидовХарактеристик.НастройкиПользователей КАК НастройкиПользователей
	|ГДЕ
	|	НастройкиПользователей.Предопределенный = ИСТИНА
	|	И НастройкиПользователей.ЭтоГруппа = ЛОЖЬ";
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	НаборЗаписей = РегистрыСведений.НастройкиПользователей.СоздатьНаборЗаписей();
	НаборЗаписей.Отбор.Пользователь.Установить(Пользователь, Истина);
	
	Пока Выборка.Следующий() Цикл
		
		Запись = НаборЗаписей.Добавить();
		Запись.Пользователь = Пользователь;
		Запись.Настройка = Выборка.Настройка;
		
		МассивТипов = Выборка.ТипЗначения.Типы();
		
		Если МассивТипов.Количество() = 1 Тогда
			
			ТипЗначения = МассивТипов[0];
			
			// С помощью встроенной функции Новый не могут создаваться значения примитивных типов.	
			Если ТипЗначения = Тип("Булево") Тогда
				ПустоеЗначение = Ложь;	
			ИначеЕсли ТипЗначения = Тип("Число") Тогда
				ПустоеЗначение = 0;	
			ИначеЕсли ТипЗначения = Тип("Строка") Тогда
				ПустоеЗначение = "";	
			ИначеЕсли ТипЗначения = Тип("Дата") Тогда
				ПустоеЗначение = '0001-01-01';	
			Иначе
				ПустоеЗначение = Новый(ТипЗначения);	
			КонецЕсли;
			
		Иначе
			
			ПустоеЗначение = Неопределено;	
			
		КонецЕсли;
		
		Запись.Значение = ПустоеЗначение;
		
	КонецЦикла;
	
	//////////////////////////////////////////////////////////////////////////////
	// Заполнить все возможные настройки пользователя пустыми значениями.
	
	Для Каждого Запись Из НаборЗаписей Цикл
		Если Запись.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ПоказыватьВДокументахСчетаУчета Тогда
			Запись.Значение = Истина;
		ИначеЕсли Запись.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.ОсновнойОтветственный Тогда
			Запись.Значение = Пользователь;
		ИначеЕсли Запись.Настройка = ПланыВидовХарактеристик.НастройкиПользователей.УчетПоВсемОрганизациям Тогда
			Если ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоОрганизаций") Тогда
				Запись.Значение = Истина;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	НаборЗаписей.Записать(Истина);
	
КонецПроцедуры

#КонецОбласти