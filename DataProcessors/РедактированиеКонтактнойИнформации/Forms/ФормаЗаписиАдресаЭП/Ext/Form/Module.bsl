
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Параметры.ЛокальнаяСтруктураЗаписи) Тогда
		ЗаполнитьЗначенияСвойств(Объект, Параметры.ЛокальнаяСтруктураЗаписи);
	КонецЕсли;
	
	Объект.Тип = Параметры.ТипКИ;
	
	Объект.ДоступностьОбъекта = Параметры.ДоступностьОбъекта;
	Объект.ЗаписыватьВРегистр = Параметры.ЗаписыватьВРегистр;
	
	Если Объект.Вид = Неопределено Тогда
		Объект.Вид = Справочники.ВидыКонтактнойИнформации.ПустаяСсылка();
	КонецЕсли;
	
	РедактируетсяНаборЗаписей = Параметры.РедактируетсяНаборЗаписей;
	
	ВидОбъектаКИ = КонтактнаяИнформацияКлиентСерверПовтИсп.ВидОбъектаКИ(Объект.Объект);

	Элементы.ОбъектКонтактнойИнформации.Видимость = Объект.ДоступностьОбъекта;
	
	Если НЕ ЗначениеЗаполнено(Объект.Объект) И Объект.ДоступностьОбъекта Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ОбъектКонтактнойИнформации;
	ИначеЕсли НЕ ЗначениеЗаполнено(Объект.Вид) Тогда
		ЭтаФорма.ТекущийЭлемент = Элементы.ВидКонтактнойИнформации;
	Иначе
		ЭтаФорма.ТекущийЭлемент = Элементы.Представление;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	Оповещение = Новый ОписаниеОповещения("ПодтвердитьИЗакрыть", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(Оповещение, Отказ, ЗавершениеРаботы);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ОбъектКонтактнойИнформацииПриИзменении(Элемент)
	
	ВидОбъектаКИ = КонтактнаяИнформацияКлиентСерверПовтИсп.ВидОбъектаКИ(Объект.Объект);

	Если НЕ ЗначениеЗаполнено(Объект.Объект) И ТипЗнч(Объект.Вид) = Тип("СправочникСсылка.ВидыКонтактнойИнформации") Тогда
		
		Объект.Вид = ПредопределенноеЗначение("Справочник.ВидыКонтактнойИнформации.ПустаяСсылка");
		ВидОбъектаКИ = Неопределено;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПредставлениеПриИзменении(Элемент)
	Объект.Представление = СокрЛП(Объект.Представление);
КонецПроцедуры

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(
		Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)
	ПодтвердитьИЗакрыть();
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Модифицированность = Ложь;
	Закрыть();

КонецПроцедуры

&НаКлиенте
Процедура ПроверитьКорректность(Команда)
	
	СписокОшибок = ОшибкиЗаполненияПредставления(Объект.Представление);
	Если СписокОшибок.Количество()>0 Тогда
		СообщитьОбОшибкахЗаполнения(СписокОшибок);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПодтвердитьИЗакрыть(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	
	// При немодифицированности работает "отмена"
	
	Если Модифицированность Тогда
		
		Если НЕ ПроверитьЗаполнение() Тогда
			Возврат;
		КонецЕсли;
		
		Результат = РезультатВыбора();
		
		СброситьМодифицированностьПриВыборе();
		
	Иначе
		
		Результат = Неопределено;
		
	КонецЕсли;
	
	Если (МодальныйРежим Или ЗакрыватьПриВыборе) И Открыта() Тогда
		СброситьМодифицированностьПриВыборе();
		Закрыть(Результат);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СброситьМодифицированностьПриВыборе()
	
	Модифицированность = Ложь;
	
КонецПроцедуры

&НаСервере
Функция РезультатВыбора()
	
	Если НЕ РедактируетсяНаборЗаписей Тогда
		СтруктураЗаписи = КонтактнаяИнформацияКлиентСервер.ПолучитьСтруктуруЗаписиРегистра(Объект);
		СтруктураЗаписи.Вставить("ДобавленаНоваяКИ", Ложь);
	ИначеЕсли РедактируетсяНаборЗаписей Тогда
		СтруктураЗаписи = КонтактнаяИнформацияКлиентСервер.ПолучитьСтруктуруЗаписиРегистра(Объект);
		СтруктураЗаписи.Вставить("ДобавленаНоваяКИ", Истина);
	КонецЕсли;
	
	Если Объект.ЗаписыватьВРегистр Тогда
		МенеджерЗаписи = РегистрыСведений.КонтактнаяИнформация.СоздатьМенеджерЗаписи();
		ЗаполнитьЗначенияСвойств(МенеджерЗаписи, Объект);
		
		Попытка
			МенеджерЗаписи.Записать(Ложь);
		Исключение
			ТекстСообщения = НСтр("ru = 'Не удалось записать данные о контактной информации по причине: %1.'");
			ВызватьИсключение СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОписаниеОшибки());
		КонецПопытки;
	КонецЕсли; 
	
	Возврат СтруктураЗаписи;
	
КонецФункции

// Возвращает список ошибок заполнения в виде списка значений:
//      Представление   - описание ошибки
//      Значение        - XPath для поля
&НаКлиентеНаСервереБезКонтекста
Функция ОшибкиЗаполненияПредставления(Представление) 
	СписокОшибок = Новый СписокЗначений();
	Если НЕ ЗначениеЗаполнено(Представление) Тогда
		СписокОшибок.Добавить("Представление", НСтр("ru = 'Не указан адрес электронной почты'"));
	ИначеЕсли ЗначениеЗаполнено(Представление) И НЕ ОбщегоНазначенияКлиентСервер.АдресЭлектроннойПочтыСоответствуетТребованиям(Представление, Истина) Тогда 
		СписокОшибок.Добавить("Представление", НСтр("ru = 'Адрес электронной почты не соответствует требованиям стандартов'"));
	КонецЕсли;
	
	Возврат СписокОшибок;
КонецФункции    

// Сообщает об ошибках заполнения по результату функции ОшибкиЗаполненияПредставленияСервер
&НаКлиенте
Процедура СообщитьОбОшибкахЗаполнения(СписокОшибок)
	
	Если СписокОшибок.Количество()=0 Тогда
		ПоказатьПредупреждение(, НСтр("ru='Адрес электронной почты введен корректно.'"));
		Возврат;
	КонецЕсли;
	
	ОчиститьСообщения();
	
	Для Каждого Элемент Из СписокОшибок Цикл
		ОбщегоНазначенияКлиент.СообщитьПользователю(Элемент.Представление,,,
			Элемент.Значение);
	КонецЦикла;
	
КонецПроцедуры    
