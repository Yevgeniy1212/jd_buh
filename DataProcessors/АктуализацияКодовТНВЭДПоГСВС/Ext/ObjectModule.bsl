
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

Функция АктуализироватьКодыТНВЭД(Знач ПараметрыВызова, Знач АдресХранилища) Экспорт

	НеобработанныеСтроки = ПараметрыВызова.ТаблицаАктуализации.СкопироватьКолонки();
	
	Для Каждого ТекСтрока Из ПараметрыВызова.ТаблицаАктуализации Цикл
		Если ТекСтрока.Пометка И Не ТекСтрока.Номенклатура.Пустая() И Не ПустаяСтрока(ТекСтрока.НовыйКодТНВЭД) Тогда
				
			НачатьТранзакцию();
			
			Попытка
				
				Блокировка = Новый БлокировкаДанных;
				ЭлементБлокировки = Блокировка.Добавить(ТекСтрока.Номенклатура.Метаданные().ПолноеИмя());
				ЭлементБлокировки.УстановитьЗначение("Ссылка", ТекСтрока.Номенклатура);
				Блокировка.Заблокировать();
				
				НоменклатураОбъект = ТекСтрока.Номенклатура.ПолучитьОбъект();
				Если НоменклатураОбъект = Неопределено Тогда
					ОтменитьТранзакцию();
					Продолжить;
				КонецЕсли;
				
				НоменклатураОбъект.КодТНВЭД = ТекСтрока.НовыйКодТНВЭД;
				
				ОбновлениеИнформационнойБазы.ЗаписатьДанные(НоменклатураОбъект);
				
				ЗафиксироватьТранзакцию();
				
			Исключение
				
				ОтменитьТранзакцию();
				ТекстСообщения = НСтр("ru = 'Не удалось обработать %1 по причине: %2'"); 			
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ТекСтрока.Номенклатура, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
								
				ЗаписьЖурналаРегистрации(ОбновлениеИнформационнойБазы.СобытиеЖурналаРегистрации(), УровеньЖурналаРегистрации.Предупреждение,
					ТекСтрока.Номенклатура.Метаданные(), ТекСтрока.Номенклатура, ТекстСообщения);
			
				ЗаполнитьЗначенияСвойств(НеобработанныеСтроки.Добавить(), ТекСтрока);
				
			КонецПопытки;
			
		Иначе
			ЗаполнитьЗначенияСвойств(НеобработанныеСтроки.Добавить(), ТекСтрока);
		КонецЕсли;
	КонецЦикла;
	
	ПоместитьВоВременноеХранилище(НеобработанныеСтроки, АдресХранилища);
	
	Возврат Истина

КонецФункции

#КонецЕсли
