
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПутьДляВыгрузки = Параметры.ПутьДляВыгрузки;
	
	Если СокрЛП(ПутьДляВыгрузки)="" Тогда
				
		ПутьДляВыгрузки = ХранилищеНастроекДанныхФорм.Загрузить("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.НастройкаПараметровСохраненияРегламентированногоОтчета",
										                        "ПутьДляВыгрузкиРегламентированныхОтчетов");
				
	КонецЕсли;
	
	// заполнение списка форматов
	Для Каждого ФорматСохранения Из УправлениеПечатью.НастройкиФорматовСохраненияТабличногоДокумента() Цикл
		ВыбранныеФорматыСохранения.Добавить(ФорматСохранения.ТипФайлаТабличногоДокумента, Строка(ФорматСохранения.Ссылка), Ложь, ФорматСохранения.Картинка);
	КонецЦикла;
	
	ЕстьОтмеченныеФорматы       = Ложь;
	ФорматыСохраненияИзНастроек = ХранилищеНастроекДанныхФорм.Загрузить("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.НастройкаПараметровСохраненияРегламентированногоОтчета",
										                        "ФорматыСохраненияТабличногоДокумента");
	Если ФорматыСохраненияИзНастроек <> Неопределено Тогда
		ЕстьОтмеченныеФорматы = Истина;
		Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл 
			ФорматИзНастроек = ФорматыСохраненияИзНастроек.Найти(ВыбранныйФормат.Значение);
			Если ФорматИзНастроек <> Неопределено Тогда
				ВыбранныйФормат.Пометка = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
	Если НЕ ЕстьОтмеченныеФорматы Тогда
		ВыбранныеФорматыСохранения[0].Пометка = Истина; // по умолчанию выбран только первый формат из списка
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Если (ВРЕГ(ПутьДляВыгрузки) = "A:\") ИЛИ (СокрЛП(ПутьДляВыгрузки) = "") Тогда
		Переключатель = 1;
			
		Дискета = Элементы.Дискета.СписокВыбора.Получить(0).Значение;
	ИначеЕсли ВРЕГ(ПутьДляВыгрузки) = "B:\" Тогда
		Переключатель = 1;
		Дискета = Элементы.Дискета.СписокВыбора.Получить(1).Значение;
	Иначе
		Переключатель = 2;
	КонецЕсли;
	ИзменениеВариантаВыгрузки();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПереключательПриИзменении(Элемент)
	
	ИзменениеВариантаВыгрузки();
	
КонецПроцедуры

&НаКлиенте
Процедура ПутьДляВыгрузкиНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПутьДляВыгрузкиНачалоВыбораЗавершение", ЭтотОбъект);
	ТекстПредупреждения = НСтр("ru='Не удалось подключить расширение работы с файлами!
		|Выбор каталога невозможен.'");
	ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(ОписаниеОповещения, , ТекстПредупреждения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Сохранить(Команда)
	
	ОписаниеОповещения = Новый ОписаниеОповещения("СохранитьЗавершение", ЭтотОбъект);
	ТекстПредупреждения = НСтр("ru='Не удалось подключить расширение работы с файлами!
		|Сохранение файла выгрузки невозможно.'");
	ОбщегоНазначенияКлиент.ПроверитьРасширениеРаботыСФайламиПодключено(ОписаниеОповещения, , ТекстПредупреждения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ИзменениеВариантаВыгрузки()

	Если Переключатель = 2 Тогда
		Элементы.Дискета.Доступность = Ложь;
		Элементы.ПутьДляВыгрузки.Доступность = Истина;
	Иначе
		Элементы.Дискета.Доступность = Истина;
		Элементы.ПутьДляВыгрузки.Доступность = Ложь;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПутьДляВыгрузкиНачалоВыбораЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат = Ложь Тогда
		Возврат
	КонецЕсли;
	
	ДиалогВыбораКаталога = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.ВыборКаталога);
	ДиалогВыбораКаталога.Заголовок = НСтр("ru = 'Укажите каталог'");
	ДиалогВыбораКаталога.Каталог   = ПутьДляВыгрузки;
	Если ДиалогВыбораКаталога.Выбрать() Тогда
		РазделительПутиОС = ПолучитьРазделительПути();
		ПутьДляВыгрузки = ДиалогВыбораКаталога.Каталог
						+ ?(Прав(ДиалогВыбораКаталога.Каталог, 1) <> РазделительПутиОС, РазделительПутиОС, "");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СохранитьЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> Неопределено И Результат = Ложь Тогда
		ЗакрытьФормуЗавершение(ДополнительныеПараметры);
		Возврат;
	КонецЕсли;

	Если Переключатель = 1 Тогда
		ПутьДляВыгрузки = Дискета;
	КонецЕсли;
	
	Если Лев(ПутьДляВыгрузки, 2) <> "\\" Тогда
		
		// Если при указании пути для выгрузки вручную не указан концевой слэш - добавляем его.
		РазделительПутиОС = ПолучитьРазделительПути();
		Если Прав(ПутьДляВыгрузки, 1) <> РазделительПутиОС Тогда
			ПутьДляВыгрузки = ПутьДляВыгрузки + РазделительПутиОС;
		КонецЕсли;
		
		Кат = Новый Файл(ПутьДляВыгрузки + "NUL");
		
		Если НЕ Кат.Существует() Тогда
			
			Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Нет доступа к каталогу ""%1"".'"), ПутьДляВыгрузки);
			
			Если Переключатель = 1 Тогда
				Текст = Текст + Символы.ПС + НСтр("ru = 'Вставьте дискету в дисковод!'");
			Иначе
				Текст = Текст + Символы.ПС + НСтр("ru = 'Проверьте корректность имени каталога выгрузки!'");
			КонецЕсли;
			
			ПоказатьПредупреждение(,Текст);
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;
	
	ФорматыСохранения = Новый Массив;
	
	Для Каждого ВыбранныйФормат Из ВыбранныеФорматыСохранения Цикл
		Если ВыбранныйФормат.Пометка Тогда
			ФорматыСохранения.Добавить(ВыбранныйФормат.Значение);
		КонецЕсли;
	КонецЦикла;
	
	Если ФорматыСохранения.Количество() = 0 Тогда
		ПоказатьПредупреждение(,НСтр("ru = 'Необходимо указать как минимум один из предложенных форматов.'"));
		Возврат;
	КонецЕсли;

	Кат = Новый Файл(ПутьДляВыгрузки);
	Если Кат.Существует() и Кат.ЭтоКаталог() Тогда
		СохранитьЗначениеНаСервере(ФорматыСохранения);
		
		РезультатВыбора = Новый Структура;
		РезультатВыбора.Вставить("ФорматыСохранения", ФорматыСохранения);
		РезультатВыбора.Вставить("ПутьДляВыгрузки",   ПутьДляВыгрузки);
		
		ЭтаФорма.Закрыть(РезультатВыбора);
	Иначе
		ПоказатьПредупреждение(,НСтр("ru='Имя каталога задано неверно! Проверьте правильность указания имени каталога!'"));
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьФормуЗавершение(ДополнительныеПараметры) Экспорт
	
	ЭтаФорма.Закрыть(Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьЗначениеНаСервере(ФорматыСохранения)
	
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.НастройкаПараметровСохраненияРегламентированногоОтчета",
										  "ПутьДляВыгрузкиРегламентированныхОтчетов", 
										  ?(Переключатель = 1, Дискета, ПутьДляВыгрузки));
										  
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.НастройкаПараметровСохраненияРегламентированногоОтчета",
										  "ФорматыСохраненияТабличногоДокумента", 
										  ФорматыСохранения);
										  
КонецПроцедуры

