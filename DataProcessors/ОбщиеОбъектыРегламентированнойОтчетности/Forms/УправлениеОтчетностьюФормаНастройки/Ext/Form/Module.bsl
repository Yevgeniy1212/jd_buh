
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ИнтервалАвтосохранения = РегламентированнаяОтчетностьВызовСервера.ЗначениеИнтервалаАвтосохраненияРегламентированнойОтчетности();

	ИспользоватьАвтосохранение = ИнтервалАвтосохранения <> 0;
	УправлениеФормой(ЭтотОбъект);
	
	Если ИнтервалАвтосохранения = 0 Тогда
		ИнтервалАвтосохранения  = 10;
	КонецЕсли;

	НеОтображатьНапоминания   = ХранилищеНастроекДанныхФорм.Загрузить("Обработка.КалендарьБухгалтера.Форма.ФормаКалендаря", "КалендарьБухгалтера_НеОтображатьНапоминания");
	ЗапоминатьВыбранныйПериод = ХранилищеНастроекДанныхФорм.Загрузить("Обработка.КалендарьБухгалтера.Форма.ФормаКалендаря", "КалендарьБухгалтера_ЗапоминатьВыбранныйПериод");
	
	ИнтервалНапоминаний = РегламентированнаяОтчетностьВызовСервера.ЗначениеИнтервалаНапоминанийРегламентированнойОтчетности();

	мЗадаватьВопросПриЗакрытии = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если ЗавершениеРаботы Тогда
		Возврат;	
	КонецЕсли;
	
	Если Модифицированность И мЗадаватьВопросПриЗакрытии Тогда
		Отказ = Истина;
		ОписаниеОповещения = Новый ОписаниеОповещения("ПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(ОписаниеОповещения, НСтр("ru = 'Сохранить настройки?'"), РежимДиалогаВопрос.ДаНетОтмена);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура СохранитьИЗакрыть(Команда)
	
	Если НЕ СохранитьНастройки() Тогда
		Возврат;
	КонецЕсли;
	
	мЗадаватьВопросПриЗакрытии = Ложь;
	Закрыть(Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	Элементы.ИнтервалАвтосохранения.Доступность = Форма.ИспользоватьАвтосохранение;
	Элементы.НадписьМинут.Доступность           = Форма.ИспользоватьАвтосохранение;

КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытиемЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Если СохранитьНастройки() Тогда
			мЗадаватьВопросПриЗакрытии = Ложь;
			Закрыть(Истина);
		КонецЕсли;
	ИначеЕсли Ответ = КодВозвратаДиалога.Нет Тогда
		мЗадаватьВопросПриЗакрытии = Ложь;
		Закрыть();
	КонецЕсли;

КонецПроцедуры

&НаСервере
Функция СохранитьНастройки()
	
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.ОбщиеОбъектыРегламентированнойОтчетности.Форма.УправлениеОтчетностьюФормаНастройки", "ФормаРеглИФинОтчетности_ИнтервалАвтосохраненияРегламентированнойОтчетности", ?(ИспользоватьАвтосохранение, ИнтервалАвтосохранения, 0));
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.КалендарьБухгалтера.Форма.ФормаКалендаря", "КалендарьБухгалтера_ИнтервалНапоминанийРегламентированнойОтчетности", ИнтервалНапоминаний);
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.КалендарьБухгалтера.Форма.ФормаКалендаря", "КалендарьБухгалтера_НеОтображатьНапоминания", НеОтображатьНапоминания);
	ХранилищеНастроекДанныхФорм.Сохранить("Обработка.КалендарьБухгалтера.Форма.ФормаКалендаря", "КалендарьБухгалтера_ЗапоминатьВыбранныйПериод", ЗапоминатьВыбранныйПериод);
		
	Возврат Истина;
	
КонецФункции
