
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ВыбранныеНастройкиЗаполнения.ЗагрузитьЗначения(Параметры.ВыбранныеНастройкиЗаполнения.ВыгрузитьЗначения());
	
	ЗаполнитьНастройкиНачальногоЗаполнения();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	Для Каждого СтрокаНастройки Из ТабличноеПолеНастроек Цикл
		СтрокаНастройки.Использовать = Истина;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	Для Каждого СтрокаНастройки Из ТабличноеПолеНастроек Цикл
		СтрокаНастройки.Использовать = Ложь;	
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ОК(Команда)
	
	Если  ВыбранныеНастройкиЗаполнения = Неопределено Тогда
		ВыбранныеНастройкиЗаполнения = Новый СписокЗначений;
	Иначе
		ВыбранныеНастройкиЗаполнения.Очистить();
	КонецЕсли;

	Для Каждого СтрокаНастройки Из ТабличноеПолеНастроек Цикл
		Если СтрокаНастройки.Использовать Тогда
			ВыбранныеНастройкиЗаполнения.Добавить(СтрокаНастройки.НаименованиеОбъектаВКонфигурации);
		КонецЕсли;
	КонецЦикла;

	Закрыть(ВыбранныеНастройкиЗаполнения);	
	
КонецПроцедуры

&НаКлиенте
Процедура Отмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура ЗаполнитьНастройкиНачальногоЗаполнения()
	
	СтруктураЗначенийПараметров = Новый Структура();
	Если НЕ ОбщегоНазначения.РазделениеВключено() Тогда
		СтруктураЗначенийПараметров.Вставить("АдресныйКлассификатор"                  , НСтр("ru = 'Адресный классификатор'"));
	КонецЕсли;
	СтруктураЗначенийПараметров.Вставить("ДополнительнаяКолонкаПечатныхФормДокументов", НСтр("ru = 'Дополнительная колонка печатных форм документов'")); 
	СтруктураЗначенийПараметров.Вставить("ПредставлениеНомераДокументов"			  , НСтр("ru = 'Представление номера документов'"));
	СтруктураЗначенийПараметров.Вставить("СрокОплатыПокупателей"					  , НСтр("ru = 'Срок оплаты покупателей'"));
	СтруктураЗначенийПараметров.Вставить("СрокОплатыПоставщикам"					  , НСтр("ru = 'Срок оплаты поставщикам'"));
	СтруктураЗначенийПараметров.Вставить("ТипЦенПлановойСебестоимостиНоменклатуры"	  , НСтр("ru = 'Тип цен плановой себестоимости номенклатуры'")); 
	СтруктураЗначенийПараметров.Вставить("Банки"									  ,	НСтр("ru = 'Банки'"));
	СтруктураЗначенийПараметров.Вставить("БанковскиеСчета"							  , НСтр("ru = 'Банковские счета'"));
	СтруктураЗначенийПараметров.Вставить("Валюты"									  , НСтр("ru = 'Валюты'"));
	СтруктураЗначенийПараметров.Вставить("ВидыНоменклатуры"							  , НСтр("ru = 'Виды номенклатуры'"));
	СтруктураЗначенийПараметров.Вставить("ВидыПервичныхДокументов"					  , НСтр("ru = 'Виды первичных документов'"));
	СтруктураЗначенийПараметров.Вставить("ГодовыеГрафикиАмортизацииОС"				  , НСтр("ru = 'Годовые графики амортизации ОС'"));
	СтруктураЗначенийПараметров.Вставить("ДокументыУдостоверяющиеЛичность"			  , НСтр("ru = 'Документы удостоверяющие личность'"));
	СтруктураЗначенийПараметров.Вставить("ДолжностиОрганизаций"						  , НСтр("ru = 'Должности организаций'"));
	СтруктураЗначенийПараметров.Вставить("КлассификаторЕдиницИзмерения"				  , НСтр("ru = 'Классификатор единиц измерения'"));
	СтруктураЗначенийПараметров.Вставить("КлассификаторСтранМира"					  , НСтр("ru = 'Классификатор стран мира'"));
	СтруктураЗначенийПараметров.Вставить("НалогиСборыОтчисления"					  , НСтр("ru = 'Налоги сборы отчисления'"));
	СтруктураЗначенийПараметров.Вставить("Номенклатура"								  , НСтр("ru = 'Номенклатура'"));
	СтруктураЗначенийПараметров.Вставить("ОснованияУвольненияИзОрганизации"			  , НСтр("ru = 'Основания увольнения из организации'"));
	СтруктураЗначенийПараметров.Вставить("ПараметрыВыработкиОС"						  , НСтр("ru = 'Параметры выработки ОС'"));
	СтруктураЗначенийПараметров.Вставить("ПричиныСписанияОС"						  , НСтр("ru = 'Причины списания ОС'"));
	СтруктураЗначенийПараметров.Вставить("Резервы"									  , НСтр("ru = 'Резервы'"));
	СтруктураЗначенийПараметров.Вставить("РолиКонтактныхЛиц"						  , НСтр("ru = 'Роли контактных лиц'"));
	СтруктураЗначенийПараметров.Вставить("СобытияОС"								  , НСтр("ru = 'События ОС'"));
	СтруктураЗначенийПараметров.Вставить("ТипыЦенНоменклатуры"						  , НСтр("ru = 'Типы цен номенклатуры'"));
	СтруктураЗначенийПараметров.Вставить("ФизическиеЛица"							  , НСтр("ru = 'Физические лица'"));
	СтруктураЗначенийПараметров.Вставить("ФондыВыплатОрганизаций"					  , НСтр("ru = 'Фонды выплат организаций'"));
	СтруктураЗначенийПараметров.Вставить("СчетаУчетаДенежныхСредств"				  , НСтр("ru = 'Счета учета денежных средств'"));
	СтруктураЗначенийПараметров.Вставить("СчетаУчетаНоменклатуры"					  , НСтр("ru = 'Счета учета номенклатуры'")); 
	СтруктураЗначенийПараметров.Вставить("СтавкиНДС"								  , НСтр("ru = 'Ставки НДС'"));
	СтруктураЗначенийПараметров.Вставить("Склады"									  ,	НСтр("ru = 'Склады'"));
	СтруктураЗначенийПараметров.Вставить("ОсновныеНачисленияОрганизаций"			  , НСтр("ru = 'Основные начисления организаций'"));
	СтруктураЗначенийПараметров.Вставить("СтатьиДвиженияДенежныхСредств"   			  , НСтр("ru = 'Статьи движения денежных средств'"));
		
	Для Каждого Строка Из СтруктураЗначенийПараметров Цикл 
		СтрокаТЧ = ТабличноеПолеНастроек.Добавить();
		СтрокаТЧ.НаименованиеОбъектаВКонфигурации = Строка.Ключ;
		СтрокаТЧ.НаименованиеОбъекта = Строка.Значение;
		СтрокаТЧ.Использовать = ВыбранныеНастройкиЗаполнения.Количество() = 0;
	КонецЦикла;
	
	Если ВыбранныеНастройкиЗаполнения.Количество() <> 0 Тогда
		ПараметрыОтбора = Новый Структура("НаименованиеОбъектаВКонфигурации");
		Для Каждого ВыбраннаяНастройка Из ВыбранныеНастройкиЗаполнения Цикл
			ПараметрыОтбора.НаименованиеОбъектаВКонфигурации = ВыбраннаяНастройка.Значение;
			НайденныеНастройки = ТабличноеПолеНастроек.НайтиСтроки(ПараметрыОтбора);
			Если НайденныеНастройки.Количество() > 0 Тогда
				НайденныеНастройки[0].Использовать = Истина;
			КонецЕсли;
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
