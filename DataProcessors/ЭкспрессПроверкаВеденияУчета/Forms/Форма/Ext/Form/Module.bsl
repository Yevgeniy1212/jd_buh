
&НаКлиенте
Перем ПараметрыОбработчикаОжидания;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ПодготовитьФормуНаСервере();

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	ИБФайловая = СтандартныеПодсистемыКлиентПовтИсп.ПараметрыРаботыКлиента().ИнформационнаяБазаФайловая;
	ПодключитьОбработчикОжидания = Не ИБФайловая И ЗначениеЗаполнено(ИдентификаторЗадания);
	Если ПодключитьОбработчикОжидания Тогда		
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СтруктурноеПодразделениеОрганизацияПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(СтруктурноеПодразделениеОрганизация) Тогда 
		Объект.Организация = Неопределено;
		Объект.СтруктурноеПодразделение = Неопределено;
	Иначе 
		РезультатПроверки = РаботаСДиалогамиКлиент.ПроверитьИзменениеЗначенийОрганизацииСтруктурногоПодразделения(СтруктурноеПодразделениеОрганизация, Объект.Организация, Объект.СтруктурноеПодразделение);
		Если РезультатПроверки.ИзмененаОрганизация ИЛИ РезультатПроверки.ИзмененоСтруктурноеПодразделение Тогда
			СтруктурноеПодразделениеОрганизацияПриИзмененииНаСервере();
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СтруктурноеПодразделениеОрганизацияНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	РаботаСДиалогамиКлиент.СтруктурноеПодразделениеНачалоВыбора(ЭтаФорма, СтандартнаяОбработка, Объект.Организация, Объект.СтруктурноеПодразделение, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаНачПриИзменении(Элемент)
	
	Объект.ДатаНач = НачалоМесяца(Объект.ДатаНач);
	ПериодВПределахГода = НачалоГода(Объект.ДатаНач) = НачалоГода(Объект.ДатаКон);
	
	Если НЕ ПериодВПределахГода Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Период должен находиться в пределах одного года!'"));
		Объект.ДатаКон = КонецГода(Объект.ДатаНач);
	КонецЕсли;
	
	Если Объект.ДатаКон < Объект.ДатаНач Тогда
		Объект.ДатаКон = КонецМесяца(Объект.ДатаНач);
	КонецЕсли;
	
	ПериодПроверкиСтрокой = ПредставлениеПериода(НачалоДня(Объект.ДатаНач), КонецДня(Объект.ДатаКон), "ФП = Истина");
	
	ОбновитьДеревоПроверок();
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаКонПриИзменении(Элемент)
	
	Объект.ДатаКон = КонецМесяца(Объект.ДатаКон);
	ПериодВПределахГода = НачалоГода(Объект.ДатаНач) = НачалоГода(Объект.ДатаКон);
	
	Если НЕ ПериодВПределахГода Тогда
		ПоказатьПредупреждение(, НСтр("ru = 'Период должен находиться в пределах одного года!'"));
		Объект.ДатаНач = НачалоГода(Объект.ДатаКон);
	КонецЕсли;
	
	Если Объект.ДатаНач > Объект.ДатаКон Тогда
		Объект.ДатаНач = НачалоМесяца(Объект.ДатаКон);
	КонецЕсли;
	
	ПериодПроверкиСтрокой = ПредставлениеПериода(НачалоДня(Объект.ДатаНач), КонецДня(Объект.ДатаКон), "ФП = Истина");
	
	ОбновитьДеревоПроверок();
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ДеревоПроверок

&НаКлиенте
Процедура ДеревоПроверокВключитьПриИзменении(Элемент)
	
	ТекущаяСтрока = Элементы.ДеревоПроверок.ТекущиеДанные;
	
	Если ТекущаяСтрока.Включить = 2 Тогда
		ТекущаяСтрока.Включить = 0;
	ИначеЕсли ТекущаяСтрока.Включить = 1 И Не ОрганизацияПлательщикНДС Тогда
		ТекущийРодитель = ТекущаяСтрока.ПолучитьРодителя();
		Если ТекущаяСтрока.Идентификатор = "РазделНДС" Или ТекущийРодитель <> Неопределено И ТекущийРодитель.Идентификатор = "РазделНДС" Тогда
			ЗадатьВопросВключенияРазделаНДС(ТекущаяСтрока)
		КонецЕсли;
	КонецЕсли;
	
	УстановитьПометкиПодчиненных(ТекущаяСтрока);
	УстановитьПометкиРодителей(ТекущаяСтрока);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПроверокПриАктивизацииСтроки(Элемент)
	УстановитьЗаголовокПредметаКонтроля();
КонецПроцедуры

&НаКлиенте
Процедура ДеревоПроверокЗначениеПараметраПриИзменении(Элемент)
	УстановитьЗаголовокПредметаКонтроля();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьЗаголовокПредметаКонтроля()
	
	ТекстПредметаКонтроля = "";
	
	ТекущиеДанные = Элементы.ДеревоПроверок.ТекущиеДанные;
	Если ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.ЭтоПараметр Тогда
			Попытка
				ТекстПредметаКонтроля = Вычислить(СтрЗаменить(ТекущиеДанные.ПредметКонтроля, "%Параметр%", "ТекущиеДанные.ЗначениеПараметра"));
			Исключение
			КонецПопытки;
		Иначе
			ТекстПредметаКонтроля = ТекущиеДанные.ПредметКонтроля;
		КонецЕсли;
	КонецЕсли;
	
	ОписаниеПредметаКонтроля.УстановитьФорматированнуюСтроку(Новый ФорматированнаяСтрока(ТекстПредметаКонтроля, ШрифтОписанияПредметаКонтроля.ПолучитьАбсолютный()));
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыполнитьПроверку(Команда)
	
	ОтключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания");

	РезультатВыполнения = ВыполнитьПроверкуНаСервере();
	Если Не РезультатВыполнения.ЗаданиеВыполнено Тогда
		ДлительныеОперацииКлиент.ИнициализироватьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
		ПодключитьОбработчикОжидания("Подключаемый_ПроверитьВыполнениеЗадания", 1, Истина);
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "ФормированиеОтчета");
	КонецЕсли;
	
	ПодключитьОбработчикОжидания("Подключаемый_ЗакрытьНастройки", 0.1, Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура Настроить(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаНастройки;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПерейтиКОтчету(Команда)
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОтчет;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериод(Команда)
		
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", Объект.ДатаНач, Объект.ДатаКон);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура Уровень1(Команда)
	
	ПоказатьУровеньОтчета(0);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень2(Команда)
	
	ПоказатьУровеньОтчета(1);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень3(Команда)
	
	ПоказатьУровеньОтчета(2);
	
КонецПроцедуры

&НаКлиенте
Процедура Уровень4(Команда)
	
	ПоказатьУровеньОтчета(3);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлаги(Команда)
	
	КорневыеСтроки = Объект.ДеревоПроверок.ПолучитьЭлементы();
	
	Если КорневыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из КорневыеСтроки Цикл
		Если Не ОрганизацияПлательщикНДС И ТекущаяСтрока.Идентификатор = "РазделНДС" И ТекущаяСтрока.Включить <> 1 Тогда
			ЗадатьВопросВключенияРазделаНДС(ТекущаяСтрока)
		КонецЕсли;
		УстановитьПометки("Установить", ТекущаяСтрока);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура СнятьФлаги(Команда)
	
	КорневыеСтроки = Объект.ДеревоПроверок.ПолучитьЭлементы();
	
	Если КорневыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из КорневыеСтроки Цикл
		УстановитьПометки("Снять", ТекущаяСтрока);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура Инвертировать(Команда)
	
	КорневыеСтроки = Объект.ДеревоПроверок.ПолучитьЭлементы();
	
	Если КорневыеСтроки.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Для Каждого ТекущаяСтрока Из КорневыеСтроки Цикл
		Если Не ОрганизацияПлательщикНДС И ТекущаяСтрока.Идентификатор = "РазделНДС" И ТекущаяСтрока.Включить <> 1 Тогда
			ЗадатьВопросВключенияРазделаНДС(ТекущаяСтрока)
		КонецЕсли;
		УстановитьПометки("Инвертировать", ТекущаяСтрока);
	КонецЦикла;

КонецПроцедуры

&НаКлиенте
Процедура НастройкиПоУмолчанию(Команда)
	
	НастройкиПоУмолчаниюНаСервере();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьПроверки(Команда)
	
	Для Каждого Стр Из Объект.ДеревоПроверок.ПолучитьЭлементы() Цикл
		Элементы.ДеревоПроверок.Развернуть(Стр.ПолучитьИдентификатор(), Истина);
	КонецЦикла;
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьРазделыПроверок(Команда)
	
	Для Каждого Стр Из Объект.ДеревоПроверок.ПолучитьЭлементы() Цикл
		Элементы.ДеревоПроверок.Свернуть(Стр.ПолучитьИдентификатор());
	КонецЦикла;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ОбновлятьДеревоПроверок = Объект.ДатаНач <> РезультатВыбора.НачалоПериода Или Объект.ДатаКон <> РезультатВыбора.КонецПериода;
	
	Объект.ДатаКон = РезультатВыбора.КонецПериода;
	Объект.ДатаНач = РезультатВыбора.НачалоПериода;
	
	Если ОбновлятьДеревоПроверок Тогда
		ОбновитьДеревоПроверок();
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПодготовитьФормуНаСервере()
	
	// установим значения по умолчанию
	Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();

	ПоддержкаРаботыСоСтруктурнымиПодразделениями = ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями");
	РаботаСДиалогамиКлиентСервер.УстановитьВидимостьСтруктурногоПодразделения(Объект.Организация, Объект.СтруктурноеПодразделение, СтруктурноеПодразделениеОрганизация, ПоддержкаРаботыСоСтруктурнымиПодразделениями);
	РаботаСДиалогамиКлиентСервер.УстановитьСвойстваЭлементаСтруктурноеПодразделениеОрганизация(Элементы.СтруктурноеПодразделениеОрганизация, Объект.СтруктурноеПодразделение, ПоддержкаРаботыСоСтруктурнымиПодразделениями);
	
	Если Объект.ДатаНач = '00010101' Тогда
		Объект.ДатаНач = НачалоГода(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	Если Объект.ДатаКон = '00010101' Тогда
		Объект.ДатаКон = КонецМесяца(ОбщегоНазначения.ТекущаяДатаПользователя());
	КонецЕсли;
	Объект.ПериодПроверкиСтрокой = ПредставлениеПериода(НачалоДня(Объект.ДатаНач), КонецДня(Объект.ДатаКон), "ФП = Истина");

	УправлениеФормой(ЭтаФорма);
	
	ДеревоПроверок = РеквизитФормыВЗначение("Объект.ДеревоПроверок");

	Обработки.ЭкспрессПроверкаВеденияУчета.ЗаполнитьДеревоПроверокИзМакетаMXL(ДеревоПроверок, Новый Структура("ДатаПроверки", Объект.ДатаНач));
	
	ЗначениеВРеквизитФормы(ДеревоПроверок, "Объект.ДеревоПроверок");
	
	ОрганизацияПлательщикНДС = Истина;
	
	ШрифтОписанияПредметаКонтроля = ШрифтыСтиля.ОбычныйШрифтТекста;
	
КонецПроцедуры

&НаСервере
Процедура ПриИзмененииЗначенияОрганизацииСервер(СтруктураПараметров, СтруктураРезультатаВыполнения)
	
	Если НЕ СтруктураПараметров.ИзмененаОрганизация И НЕ СтруктураПараметров.ИзмененоСтруктурноеПодразделение Тогда
		Возврат;
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура СтруктурноеПодразделениеОрганизацияПриИзмененииНаСервере(СтруктураПараметров = Неопределено, СтруктураРезультатаВыполнения = Неопределено)
	
	Если СтруктураПараметров = Неопределено 
		ИЛИ (СтруктураПараметров.Свойство("НеобходимоИзменитьЗначенияРеквизитовОбъекта") 
			И СтруктураПараметров.НеобходимоИзменитьЗначенияРеквизитовОбъекта) Тогда 
		РаботаСДиалогами.СтруктурноеПодразделениеПриИзменении(СтруктурноеПодразделениеОрганизация, Объект.Организация, Объект.СтруктурноеПодразделение, СтруктураПараметров);
	КонецЕсли;
	
	ПриИзмененииЗначенияОрганизацииСервер(СтруктураПараметров, СтруктураРезультатаВыполнения);
	
	ПроверитьОтключитьРазделНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораСтруктурногоПодразделения(Результат, Параметры) Экспорт
	
	РаботаСДиалогамиКлиент.ПослеВыбораСтруктурногоПодразделения(Результат, Объект.Организация, Объект.СтруктурноеПодразделение, СтруктурноеПодразделениеОрганизация);
	Если Результат.ИзмененаОрганизация ИЛИ Результат.ИзмененоСтруктурноеПодразделение Тогда
		Модифицированность = Истина;
		Результат.Вставить("НеобходимоИзменитьЗначенияРеквизитовОбъекта", Ложь);
		СтруктураРезультатаВыполнения = Неопределено;
		Если Не ЗначениеЗаполнено(ИдентификаторЗадания) Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеАктуальность");
		КонецЕсли;
		
		ПроверитьОтключитьРазделНДС();
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект		= Форма.Объект;
	Элементы	= Форма.Элементы;
	
	Если Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОтчет Тогда
		Элементы.ПерейтиКОтчету.Видимость = Ложь;
		Элементы.Настроить.Видимость      = Истина;
	Иначе
		Элементы.ПерейтиКОтчету.Видимость = Истина;
		Элементы.Настроить.Видимость      = Ложь;
	КонецЕсли;	

КонецПроцедуры

&НаСервере
Функция ВыполнитьПроверкуНаСервере() Экспорт
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Новый Структура("ЗаданиеВыполнено", Истина);
	КонецЕсли;
	
	ИБФайловая = ОбщегоНазначения.ИнформационнаяБазаФайловая();
	
	ДлительныеОперации.ОтменитьВыполнениеЗадания(ИдентификаторЗадания);
	
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
	ПараметрыОтчета = ПодготовитьПараметрыОтчета();
	
	Если ИБФайловая Тогда
		АдресХранилища = ПоместитьВоВременноеХранилище(Неопределено, УникальныйИдентификатор);
		Обработки.ЭкспрессПроверкаВеденияУчета.СформироватьОтчет(ПараметрыОтчета, АдресХранилища);
		РезультатВыполнения = Новый Структура("ЗаданиеВыполнено", Истина);
	Иначе
		РезультатВыполнения = ДлительныеОперации.ЗапуститьВыполнениеВФоне(
			УникальныйИдентификатор,
			"Обработки.ЭкспрессПроверкаВеденияУчета.СформироватьОтчет",
			ПараметрыОтчета,
			БухгалтерскиеОтчетыКлиентСервер.ПолучитьНаименованиеЗаданияВыполненияОтчета(ЭтаФорма));
			
		АдресХранилища       = РезультатВыполнения.АдресХранилища;
		ИдентификаторЗадания = РезультатВыполнения.ИдентификаторЗадания;
	КонецЕсли;
	
	Если РезультатВыполнения.ЗаданиеВыполнено Тогда
		ЗагрузитьПодготовленныеДанные();
	КонецЕсли;
	
	Возврат РезультатВыполнения;
	
КонецФункции

&НаСервере
Функция ПодготовитьПараметрыОтчета()
	
	ВысотаОбластей = Новый Структура;
	
	ПараметрыОтчета = Новый Структура;
	ПараметрыОтчета.Вставить("Организация"        		, Объект.Организация);
	ПараметрыОтчета.Вставить("ДатаНач"                 	, Объект.ДатаНач);
	ПараметрыОтчета.Вставить("ДатаКон"                 	, Объект.ДатаКон);
	ПараметрыОтчета.Вставить("ИсходящиеДанные"          , Объект.ИсходящиеДанные);
	ПараметрыОтчета.Вставить("СтруктурноеПодразделение" , Объект.СтруктурноеПодразделение);
	ПараметрыОтчета.Вставить("ДеревоПроверок"			, РеквизитФормыВЗначение("Объект.ДеревоПроверок"));
	ПараметрыОтчета.Вставить("ВыполнятьПроверки"		, Истина);
	ПараметрыОтчета.Вставить("ПериодПроверкиСтрокой"	, Объект.ПериодПроверкиСтрокой);
	ПараметрыОтчета.Вставить("ВысотаОбластей"			, ВысотаОбластей);
	ПараметрыОтчета.Вставить("ИдентификаторОтчета"     	, БухгалтерскиеОтчетыКлиентСервер.ПолучитьИдентификаторОбъекта(ЭтаФорма));
	
	Возврат ПараметрыОтчета;
	
КонецФункции

&НаСервере
Процедура ЗагрузитьПодготовленныеДанные()

	РезультатВыполнения = ПолучитьИзВременногоХранилища(АдресХранилища);
	Результат.Очистить();
	Результат.Вывести(РезультатВыполнения.Результат);
	
	ИдентификаторЗадания = Неопределено;
	
	ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПроверитьВыполнениеЗадания()
	
	Попытка
		Если ЗаданиеВыполнено(ИдентификаторЗадания) Тогда 
			ЗагрузитьПодготовленныеДанные();
			ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		Иначе
			ДлительныеОперацииКлиент.ОбновитьПараметрыОбработчикаОжидания(ПараметрыОбработчикаОжидания);
			ПодключитьОбработчикОжидания(
				"Подключаемый_ПроверитьВыполнениеЗадания", 
				ПараметрыОбработчикаОжидания.ТекущийИнтервал, 
				Истина);
		КонецЕсли;
	Исключение
		ОбщегоНазначенияКлиентСервер.УстановитьСостояниеПоляТабличногоДокумента(Элементы.Результат, "НеИспользовать");
		ВызватьИсключение;
	КонецПопытки;
	
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ЗакрытьНастройки()
	
	Элементы.ГруппаСтраницы.ТекущаяСтраница = Элементы.ГруппаОтчет;
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЗаданиеВыполнено(ИдентификаторЗадания)
	
	Возврат ДлительныеОперации.ЗаданиеВыполнено(ИдентификаторЗадания);
	
КонецФункции

&НаКлиенте
Процедура УстановитьПометки(ВидКоманды, ТекущаяСтрока)
	
	Если ВидКоманды = "Установить" Тогда
		ТекущаяСтрока.Включить = Истина;
	ИначеЕсли ВидКоманды = "Снять" Тогда
		ТекущаяСтрока.Включить = Ложь;
	ИначеЕсли ВидКоманды = "Инвертировать" Тогда
		ТекущаяСтрока.Включить = НЕ ТекущаяСтрока.Включить;
	КонецЕсли;

	Подчиненные	= ТекущаяСтрока.ПолучитьЭлементы();

	Если Подчиненные.Количество() > 0 Тогда
		Для Каждого Подчиненный из Подчиненные Цикл
			УстановитьПометки(ВидКоманды, Подчиненный);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры 

&НаКлиенте
Процедура УстановитьПометкиРодителей(ТекущаяСтрока)

	Родитель = ТекущаяСтрока.ПолучитьРодителя();
	Если Родитель = Неопределено Тогда
		Возврат;
	КонецЕсли; 

	ТекущееСостояние	= Родитель.Включить;

	НайденыВключенные	= Ложь;
	НайденыВыключенные	= Ложь;
	
	Подчиненные 		= Родитель.ПолучитьЭлементы();

	Для Каждого Строка Из Подчиненные Цикл
		
		Если Строка.Включить = 0 Тогда
			НайденыВыключенные	= Истина;
		ИначеЕсли Строка.Включить = 1 Тогда
			НайденыВключенные	= Истина;
		ИначеЕсли Строка.Включить = 2 Тогда
			НайденыВыключенные	= Истина;
			НайденыВключенные	= Истина;						
		КонецЕсли; 
		
		Если НайденыВключенные И НайденыВыключенные Тогда
			Прервать;
		КонецЕсли;
		
	КонецЦикла;
	
	Если НайденыВключенные И НайденыВыключенные Тогда
		Включить = 2;
	ИначеЕсли НайденыВключенные И (Не НайденыВыключенные) Тогда
		Включить = 1;
	ИначеЕсли (Не НайденыВключенные) И НайденыВыключенные Тогда
		Включить = 0;
	ИначеЕсли (Не НайденыВключенные) И (Не НайденыВыключенные) Тогда
		Включить = 2;
	КонецЕсли;

	Если Включить = ТекущееСостояние Тогда
		Возврат;
	Иначе
		Родитель.Включить = Включить;
		УстановитьПометкиРодителей(Родитель);
	КонецЕсли; 

КонецПроцедуры 

&НаКлиенте
Процедура УстановитьПометкиПодчиненных(ТекущаяСтрока)

	Пометка		= ТекущаяСтрока.Включить;
	Подчиненные	= ТекущаяСтрока.ПолучитьЭлементы();

	Если Подчиненные.Количество() > 0 Тогда
		Для Каждого Подчиненный из Подчиненные Цикл
			Подчиненный.Включить = Пометка;
			УстановитьПометкиПодчиненных(Подчиненный);
		КонецЦикла;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура НастройкиПоУмолчаниюНаСервере()
	
	Объект.Организация = Справочники.Организации.ОрганизацияПоУмолчанию();

	ПоддержкаРаботыСоСтруктурнымиПодразделениями = ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями");
	РаботаСДиалогамиКлиентСервер.УстановитьВидимостьСтруктурногоПодразделения(Объект.Организация, Объект.СтруктурноеПодразделение, СтруктурноеПодразделениеОрганизация, ПоддержкаРаботыСоСтруктурнымиПодразделениями);
	РаботаСДиалогамиКлиентСервер.УстановитьСвойстваЭлементаСтруктурноеПодразделениеОрганизация(Элементы.СтруктурноеПодразделениеОрганизация, Объект.СтруктурноеПодразделение, ПоддержкаРаботыСоСтруктурнымиПодразделениями);
	
	Объект.ДатаНач = НачалоГода(ОбщегоНазначения.ТекущаяДатаПользователя());
	Объект.ДатаКон = КонецМесяца(ОбщегоНазначения.ТекущаяДатаПользователя());
	
	Объект.ПериодПроверкиСтрокой = ПредставлениеПериода(НачалоДня(Объект.ДатаНач), КонецДня(Объект.ДатаКон), "ФП = Истина");

	УправлениеФормой(ЭтаФорма);

	ДеревоПроверок = РеквизитФормыВЗначение("Объект.ДеревоПроверок");

	Обработки.ЭкспрессПроверкаВеденияУчета.ЗаполнитьДеревоПроверокИзМакетаMXL(ДеревоПроверок, Новый Структура("ДатаПроверки", Объект.ДатаНач));
	
	ЗначениеВРеквизитФормы(ДеревоПроверок, "Объект.ДеревоПроверок");
	
	ОрганизацияПлательщикНДС = Истина;
	
	ПроверитьОтключитьРазделНДС();
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьУровеньОтчета(НомерУровня = 0)

	Результат.ПоказатьУровеньГруппировокСтрок(НомерУровня);

КонецПроцедуры

&НаСервере
Процедура ПередЗагрузкойДанныхИзНастроекНаСервере(Настройки)
	
	ДеревоИзНастроек = Настройки.Получить("Объект.ДеревоПроверок");
	Если ДеревоИзНастроек <> Неопределено Тогда
		
		ВосстановитьНастройкиДереваПроверок(ДеревоИзНастроек, Объект.ДеревоПроверок.ПолучитьЭлементы());
		
		Настройки.Удалить("Объект.ДеревоПроверок");
		
		ПроверитьОтключитьРазделНДС();
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ВосстановитьНастройкиДереваПроверок(ДеревоИзНастроек, СтрокиДерева)
	
	СохраненыЗначенияПараметров = ДеревоИзНастроек.Колонки.Найти("ЗначениеПараметра") <> Неопределено;
	
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		
		Подстроки = СтрокаДерева.ПолучитьЭлементы();
		Если Подстроки.Количество() Тогда
			ВосстановитьНастройкиДереваПроверок(ДеревоИзНастроек, Подстроки);
		КонецЕсли;
		
		СтрокаИзНастройки = ДеревоИзНастроек.Строки.Найти(СтрокаДерева.Идентификатор, "Идентификатор", Истина);
		
		Если СтрокаИзНастройки <> Неопределено Тогда
			СтрокаДерева.Включить = СтрокаИзНастройки.Включить;
			Если СохраненыЗначенияПараметров Тогда
				СтрокаДерева.ЗначениеПараметра = СтрокаИзНастройки.ЗначениеПараметра;
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ОбновитьДеревоПроверок()
	
	ДеревоПроверок = РеквизитФормыВЗначение("Объект.ДеревоПроверок");
	
	ДеревоПроверокСохрЗнч = ДеревоПроверок.Скопировать();
	
	Обработки.ЭкспрессПроверкаВеденияУчета.ЗаполнитьДеревоПроверокИзМакетаMXL(ДеревоПроверок, Новый Структура("ДатаПроверки", Объект.ДатаНач));
	
	ЗначениеВРеквизитФормы(ДеревоПроверок, "Объект.ДеревоПроверок");
	
	ВосстановитьНастройкиДереваПроверок(ДеревоПроверокСохрЗнч, Объект.ДеревоПроверок.ПолучитьЭлементы());
	
	ПроверитьОтключитьРазделНДС();
	
КонецПроцедуры

&НаСервере
Процедура ПроверитьОтключитьРазделНДС()
	
	ПредОрганизацияПлательщикНДС = ОрганизацияПлательщикНДС;
	ОрганизацияПлательщикНДС = ПроцедурыНалоговогоУчета.ПолучитьПризнакПлательщикаНДС(Объект.Организация, КонецДня(Объект.ДатаКон));
	Если ПредОрганизацияПлательщикНДС И Не ОрганизацияПлательщикНДС Тогда
		
		ДеревоПроверок = РеквизитФормыВЗначение("Объект.ДеревоПроверок");
		
		РазделНДС = ДеревоПроверок.Строки.Найти("РазделНДС", "Идентификатор");
		Если РазделНДС <> Неопределено И РазделНДС.Включить <> 0 Тогда
			
			РазделНДС.Включить = 0;
			Для Каждого СтрокаРаздела Из РазделНДС.Строки Цикл
				СтрокаРаздела.Включить = 0;
			КонецЦикла;
			
			ЗначениеВРеквизитФормы(ДеревоПроверок, "Объект.ДеревоПроверок");
			
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Выключены проверки раздела ""НДС"", так как организация ""%1"" не является плательщиком НДС.'"), Объект.Организация);
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция НайтиСтрокуПоИдентификатору(Знач Идентификатор)
	
	НайденнаяСтрока = Неопределено;
	
	СтрокиДерева = Объект.ДеревоПроверок.ПолучитьЭлементы();
	Для Каждого СтрокаДерева Из СтрокиДерева Цикл
		Если СтрокаДерева.Идентификатор = Идентификатор Тогда
			НайденнаяСтрока = СтрокаДерева;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	Возврат НайденнаяСтрока
	
КонецФункции

&НаКлиенте
Процедура ЗадатьВопросВключенияРазделаНДС(ТекущаяСтрока)
	
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ТекущаяСтрока", ТекущаяСтрока.ПолучитьИдентификатор());
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ОбработкаОтветаВключенияРазделаНДС", ЭтотОбъект, ДополнительныеПараметры);
	
	ТекстВопроса   = НСтр("ru = 'Проверка операций по НДС для организаций не являющихся плательщиками НДС не предусмотрена. Продолжить?'");
	ТекстЗаголовка = НСтр("ru = 'Включение проверок раздела НДС'");
	
	ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , , ТекстЗаголовка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОтветаВключенияРазделаНДС(ОтветНаВопрос, ДополнительныеПараметры) Экспорт
	
	Если ОтветНаВопрос <> КодВозвратаДиалога.Да Тогда
		ТекущаяСтрока = Объект.ДеревоПроверок.НайтиПоИдентификатору(ДополнительныеПараметры.ТекущаяСтрока);
		Если ТекущаяСтрока <> Неопределено Тогда
			ТекущаяСтрока.Включить = 0;
			УстановитьПометкиПодчиненных(ТекущаяСтрока);
			УстановитьПометкиРодителей(ТекущаяСтрока);
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

