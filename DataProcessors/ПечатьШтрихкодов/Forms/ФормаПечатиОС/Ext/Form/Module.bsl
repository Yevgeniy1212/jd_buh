#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗначенияНастроекПользователя = ПользователиБКВызовСервераПовтИсп.ЗначенияНастроекПользователя(Пользователи.ТекущийПользователь(), Новый Структура("ОсновнойШаблонШтрихкодаОС, ОсновнаяОрганизация"));
	Объект.Организация 			 = ЗначенияНастроекПользователя.ОсновнаяОрганизация;
	Объект.Шаблон 				 = ЗначенияНастроекПользователя.ОсновнойШаблонШтрихкодаОС;
	Объект.ДатаСведений			 = ОбщегоНазначенияБК.ПолучитьРабочуюДату();	
	
	Если Параметры.Свойство("АдресВХранилище") И
		ЭтоАдресВременногоХранилища(Параметры.АдресВХранилище) Тогда	
		СтруктураДанных = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
		
		Объект.ОС.Загрузить(СтруктураДанных.ОС);
		
		Если СтруктураДанных.Свойство("Организация")
				И ЗначениеЗаполнено(СтруктураДанных.Организация) Тогда
			Объект.Организация = СтруктураДанных.Организация;
		КонецЕсли;
		
		Если СтруктураДанных.Свойство("ДатаСведений") 
			И ЗначениеЗаполнено(СтруктураДанных.ДатаСведений) Тогда
			Объект.ДатаСведений = СтруктураДанных.ДатаСведений;	
		КонецЕсли;
		
		ПерезаполнитьДанныеОСНаСервере();
		
	КонецЕсли;	
	
	Если ОбменДаннымиСервер.ЭтоПодчиненныйУзелРИБ()
		И ПолучитьФункциональнуюОпцию("ШтрихкодУстанавливаетсяВГлавномУзлеРИБ") Тогда // В подчиненных узлах РИБ не выполняется
			Элементы.ОССгенерироватьШтрихкода.Доступность 	= Ложь;
	КонецЕсли;
	
	Если Не ПравоДоступа("Изменение",Метаданные.Справочники.ОсновныеСредства) ТОгда
		Элементы.ОССгенерироватьШтрихкода.Доступность 	= Ложь;
	КонецЕсли;
	
	ФормироватьШрихкодТолькоДляОССПустымШтрихкодом = Истина;
	УстановитьОтображениеПредупрежденияПриРедактировании(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ПодборОС(Команда)
	
	ЗаголовокПодбора	 = НСтр("ru = 'Подбор основных средств для печати'");
	
	ПараметрыФормы = Новый Структура;
	
	ПараметрыФормы.Вставить("ДатаРасчетов"				, ТекущаяДата());
	ПараметрыФормы.Вставить("Организация"				, Объект.Организация);
	ПараметрыФормы.Вставить("СтруктурноеПодразделение"	, Неопределено);
	ПараметрыФормы.Вставить("Заголовок"					, ЗаголовокПодбора);
	ПараметрыФормы.Вставить("ВыбиратьВсе"				, Истина);	
	ПараметрыФормы.Вставить("ИмяТаблицы"				, "ОС");
	
	ОткрытьФорму("Справочник.ОсновныеСредства.Форма.ФормаПодбора", ПараметрыФормы, ЭтаФорма, УникальныйИдентификатор);

	ЭтаФорма.Модифицированность = Ложь;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	ОбработкаВыбораНаСервере(ВыбранноеЗначение, ИсточникВыбора.ИмяТаблицы);
КонецПроцедуры

&НаКлиенте
Процедура ОсновныеСредстваОсновноеСредствоПриИзменении(Элемент)
	
	СтрокаТЧ 		 = Элементы.ОС.ТекущиеДанные;
	ОсновноеСредство = СтрокаТЧ.ОсновноеСредство;
	
	НайденныеСтроки = Объект.ОС.НайтиСтроки(Новый Структура("ОсновноеСредство", ОсновноеСредство)).Количество();
	
	Если НайденныеСтроки > 1 Тогда
		ТекстСообщения = НСтр("ru='Основное средство ""%1"" уже выбрано.'");
		ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, ОсновноеСредство);
		Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", СтрокаТЧ.НомерСтроки, "ОсновноеСредство");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения, , Поле , "Объект");
		СтрокаТЧ.ОсновноеСредство = Неопределено;	
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ОсновноеСредство) Тогда
		СтрокаТЧ.ИнвентарныйНомер 	= "";
		СтрокаТЧ.Штрихкод 			= "";
	Иначе
		СтруктураСведений 			= СведенияОбИнвентарномНомереОС(ОсновноеСредство, Объект.Организация, ТекущаяДата());
		СтрокаТЧ.ИнвентарныйНомер	= СтруктураСведений.ИнвентарныйНомер;
		СтрокаТЧ.Штрихкод 			= ПолучитьШтрихкодОС(ОсновноеСредство);
		СтрокаТЧ.Выбран 			= Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)
	Объект.ОС.Очистить();
КонецПроцедуры

&НаКлиенте
Процедура ДатаСведенийПриИзменении(Элемент)
	ПерезаполнитьДанныеОС();
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	УстановитьСнятьФлажки(Истина);	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	УстановитьСнятьФлажки(Ложь);
КонецПроцедуры

&НаКлиенте
Процедура СформироватьШтрихкода(Команда)
	СформироватьШтрихкодаНаСервере();
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьОсновнымиСредствамиБезШтрихкода(Команда)
	
	Если Объект.ОС.Количество() > 0 Тогда
		ТекстВопроса = НСтр("ru = 'Таблица основных средств будет полностью перезаполнена. Продолжить?'");
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаполнитьОСБезШтрихкода", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	Иначе
	    ЗаполнитьОсновнымиСредствамиБезШтрихкодаНаСервере();
	КонецЕсли;
	
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

&НаСервереБезКонтекста
Функция СведенияОбИнвентарномНомереОС(ОсновноеСредство, Организация, Дата)
	
	Возврат УправлениеВнеоборотнымиАктивамиСервер.СведенияОбИнвентарномНомереОСЗ(ОсновноеСредство, Организация, Дата);

КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьШтрихкодОС(ОсновноеСредство)
	
	Возврат ОбщегоНазначения.ЗначениеРеквизитаОбъекта(ОсновноеСредство, "ШтрихКод");
	
КонецФункции

&НаСервере
Процедура ОбработкаВыбораНаСервере(ВыбранноеЗначение, ИмяТаблицы)
	
	ТаблицаОС = ПолучитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобранныхОСВХранилище);
	
	Для каждого СтрокаОС Из ТаблицаОС Цикл
		
		// Ищем выбранную позицию в таблице подобранной номенклатуры.
		СтруктураОтбора = Новый Структура();
		СтруктураОтбора.Вставить("ОсновноеСредство", СтрокаОС.ОсновноеСредство);
		
		СтрокаТабличнойЧасти = ОбработкаТабличныхЧастейКлиентСервер.НайтиСтрокуТабЧасти(Объект, ИмяТаблицы, СтруктураОтбора);
		
		Если СтрокаТабличнойЧасти = Неопределено Тогда
			СтрокаТабличнойЧасти = Объект[ИмяТаблицы].Добавить();
			ЗаполнитьЗначенияСвойств(СтрокаТабличнойЧасти, СтрокаОС);
			СтрокаТабличнойЧасти.Штрихкод = ПолучитьШтрихкодОС(СтрокаОС.ОсновноеСредство);
		Иначе
			ТекстСообщения = НСтр("ru='Основное средство - %1 уже подобрано!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаОС.ОсновноеСредство);
			Поле = ОбщегоНазначенияКлиентСервер.ПутьКТабличнойЧасти("ОС", СтрокаТабличнойЧасти.НомерСтроки, "ОсновноеСредство");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ,Поле, "Объект");
		КонецЕсли;
	
		СтрокаТабличнойЧасти.Выбран = Истина;
	КонецЦикла;

	УдалитьИзВременногоХранилища(ВыбранноеЗначение.АдресПодобранныхОСВХранилище);
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПроверку()
	
	ЕстьОшибки = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Шаблон) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указан шаблон штрихкода ОС'"),,"Объект.Шаблон");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции

&НаКлиенте
Процедура УстановитьСнятьФлажки(Пометка)
	Для Каждого СтрокаТЧ Из Объект.ОС Цикл
		СтрокаТЧ.Выбран = Пометка;	
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура ПерезаполнитьДанныеОС()
	
	ТекстВопроса = НСтр("ru='Перезаполнить данные основных средств?'");
	Режим 		 = РежимДиалогаВопрос.ДаНет;
	Оповещение 	 = Новый ОписаниеОповещения("ПослеЗакрытияВопроса", ЭтотОбъект);
	ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	
КонецПроцедуры	

&НаКлиенте
Процедура ПослеЗакрытияВопроса(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ПерезаполнитьДанныеОСНаСервере();	
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПерезаполнитьДанныеОСНаСервере()
	УстановитьПривилегированныйРежим(Истина);
	ТаблицаОС = Объект.ОС.Выгрузить();
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	ОсновныеСредства.Ссылка КАК ОсновноеСредство,
	|	ОсновныеСредства.Штрихкод
	|ПОМЕСТИТЬ вт_ОС
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.Ссылка В(&МассивОС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ОС.ОсновноеСредство,
	|	вт_ОС.Штрихкод,
	|	ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ИнвентарныйНомер,
	|	ИСТИНА КАК Выбран
	|ИЗ
	|	вт_ОС КАК вт_ОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	|				&ДатаСведений,
	|				ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							вт_ОС.ОсновноеСредство
	|						ИЗ
	|							вт_ОС КАК вт_ОС)
	|					 "+ ?(ЗначениеЗаполнено(Объект.Организация) ," И Организация = &Организация", "") + ") КАК ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних
	|		ПО вт_ОС.ОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство";
	
	Запрос.УстановитьПараметр("ДатаСведений", КонецДня(Объект.ДатаСведений));
	Запрос.УстановитьПараметр("Организация"	, Объект.Организация);
	Запрос.УстановитьПараметр("МассивОС"	, ТаблицаОС.ВыгрузитьКолонку("ОсновноеСредство"));
	
	РезултатЗапроса = Запрос.Выполнить().Выгрузить();
	
	Объект.ОС.Загрузить(РезултатЗапроса);
	
	УстановитьПривилегированныйРежим(Ложь);
КонецПроцедуры	

&НаСервере
Процедура СформироватьШтрихкодаНаСервере()
	
	ПараметрыОтбора = Новый Структура;
	ПараметрыОтбора.Вставить("Выбран",Истина);
	МассивОС = Объект.ОС.Выгрузить(ПараметрыОтбора).ВыгрузитьКолонку("ОсновноеСредство");
	Если МассивОС.Количество() = 0 Тогда
		ТекстСообщения = НСтр("ru = 'Не выбрано ни одного основного средства'");
		ОбщегоНазначения.СообщитьПользователю(ТекстСообщения);
		Возврат;
	КонецЕсли;	
	
	ПодключаемоеОборудованиеБКВызовСервера.СформироватьШтрихкодаМассиваОС(МассивОС,ФормироватьШрихкодТолькоДляОССПустымШтрихкодом);
	
	ПерезаполнитьДанныеОСНаСервере();
	
КонецПроцедуры	

&НаСервере
Процедура ЗаполнитьОсновнымиСредствамиБезШтрихкодаНаСервере()
	Запрос 			= Новый Запрос;
	Запрос.Текст 	= "ВЫБРАТЬ
	|	ОсновныеСредства.Ссылка КАК ОсновноеСредство
	|ПОМЕСТИТЬ вт_ОС
	|ИЗ
	|	Справочник.ОсновныеСредства КАК ОсновныеСредства
	|ГДЕ
	|	ОсновныеСредства.Штрихкод = &ПустаяСтрока
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МестонахождениеОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство,
	|	ИСТИНА КАК Выбран
	|ИЗ
	|	РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	|			&ДатаСреза,
	|			ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						вт_ОС.ОсновноеСредство
	|					ИЗ
	|						вт_ОС КАК вт_ОС)
	|				"+ ?(ЗначениеЗаполнено(Объект.Организация), " И Организация = &Организация", "") + ") КАК МестонахождениеОСБухгалтерскийУчетСрезПоследних
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.СостоянияОСОрганизаций.СрезПоследних(
	|				&ДатаСреза,
	|				ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							вт_ОС.ОсновноеСредство
	|						ИЗ
	|							вт_ОС КАК вт_ОС)
	|					"+ ?(ЗначениеЗаполнено(Объект.Организация), " И Организация = &Организация", "") + ") КАК СостоянияОСОрганизацийСрезПоследних
	|		ПО МестонахождениеОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство = СостоянияОСОрганизацийСрезПоследних.ОсновноеСредство
	|ГДЕ
	|	СостоянияОСОрганизацийСрезПоследних.Состояние <> ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета)";
	
	Запрос.УстановитьПараметр("ПустаяСтрока","");
	Запрос.УстановитьПараметр("Организация"	,Объект.Организация);
	Запрос.УстановитьПараметр("ДатаСреза"	,Объект.ДатаСведений);
	
	Объект.ОС.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ПерезаполнитьДанныеОСНаСервере();
	
	УстановитьОтображениеПредупрежденияПриРедактировании(ЭтаФорма);
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаполнитьОСБезШтрихкода(Результат, Параметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;		
	КонецЕсли;
	
	Объект.ОС.Очистить();
	
	ЗаполнитьОсновнымиСредствамиБезШтрихкодаНаСервере();
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УстановитьОтображениеПредупрежденияПриРедактировании(Форма)
	
	Если Форма.Объект.ОС.Количество() = 0 Тогда
		ЗначениеОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.НеОтображать;
	Иначе
		ЗначениеОтображениеПредупрежденияПриРедактировании = ОтображениеПредупрежденияПриРедактировании.Отображать;	
	КонецЕсли;	
	
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(
		Форма.Элементы,
		"Организация",
		"ОтображениеПредупрежденияПриРедактировании",
		ЗначениеОтображениеПредупрежденияПриРедактировании);
	
КонецПроцедуры	


#КонецОбласти

#Область Печать

&НаКлиенте
Процедура Печать(Команда)
	
	Если ВыполнитьПроверку() Тогда
		Возврат
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.ОсновныеСредства.ПустаяСсылка"));
	
	ИменаМакетов = "ШтрихкодОС";
	
	ПараметрыПечати = ПолучитьПараметры();
	
	Если ПараметрыПечати = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Нет данных для печати.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе	
		УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
			"Обработка.ПечатьШтрихКодов",
			ИменаМакетов,
			ПараметрКоманды,
			ЭтаФорма,
			ПараметрыПечати);
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция ПолучитьПараметры()
	
	Ошибки = Неопределено;
	
	ОС = Новый ТаблицаЗначений;
	ОС.Колонки.Добавить("ОсновноеСредство"		, Новый ОписаниеТипов("СправочникСсылка.ОсновныеСредства"));
	ОС.Колонки.Добавить("Штрихкод"				, Новый ОписаниеТипов("Строка"));
	ОС.Колонки.Добавить("ИнвентарныйНомер"		, Новый ОписаниеТипов("Строка"));
	
	Для Каждого СтрокаТЧ ИЗ Объект.ОС Цикл
		Если Не СтрокаТЧ.Выбран Тогда 
			Продолжить;
		КонецЕсли;
		
		Если Не ЗначениеЗаполнено(СтрокаТЧ.Штрихкод) Тогда
			ТекстСообщения = НСтр("ru='В строке %1 у основного средства ""%2"" не заполнен штрихкод. Печатная форма не будет сформирована!!!'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаТЧ.НомерСтроки, СтрокаТЧ.ОсновноеСредство);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения); 
			Продолжить;
		КонецЕсли;
		
		НоваяСтрока = ОС.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
	КонецЦикла;
	
	Если ОС.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;	
		
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("ОС"						, ПоместитьВоВременноеХранилище(ОС, УникальныйИдентификатор));
	ПараметрыПечати.Вставить("Организация"				, Объект.Организация);
	ПараметрыПечати.Вставить("ДатаСведений"				, Объект.ДатаСведений);
	ПараметрыПечати.Вставить("СтруктураМакетаШаблона"	, Неопределено);
	ПараметрыПечати.Вставить("ШаблонШтрихкода"			, Объект.Шаблон);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров"	, 1);
	ПараметрыПечати.Вставить("ТипШаблона"				, Перечисления.ТипыШаблоновШтрихкодов.ОсновныеСредства);
	
	Возврат ПараметрыПечати;
	
КонецФункции

#КонецОбласти
	
	




