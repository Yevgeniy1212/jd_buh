
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АдресВХранилище") И
		ЭтоАдресВременногоХранилища(Параметры.АдресВХранилище) Тогда	
		СтруктураДанных = ПолучитьИзВременногоХранилища(Параметры.АдресВХранилище);
		
		ЗаполнитьДанныеНоменклатурыНаСервере(СтруктураДанных.Номенклатура);
		
	КонецЕсли;
	
	Если (ХранилищеНастроекДанныхФорм.ПолучитьСписок("Обработка.ПечатьШтрихкодов.Форма.ФормаПечатиНоменклатуры").Количество() = 0) Тогда
		Объект.Шаблон = Справочники.ШаблоныШтрихкодов.ШаблонЭтикетки_43х25_Номенклатура;
	Иначе
		Объект.Шаблон = ХранилищеНастроекДанныхФорм.Загрузить("Обработка.ПечатьШтрихкодов.Форма.ФормаПечатиНоменклатуры", "Шаблон");
	КонецЕсли;
	
	СформироватьПредварительныйПросмотр();
	
	Количество = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "Запись_ШаблоныШтрихкодов" И Источник = Объект.Шаблон Тогда
		СформироватьПредварительныйПросмотр();
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	СохранитьДанныеНаСервере();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ШаблонПриИзменении(Элемент)
	СформироватьПредварительныйПросмотр();
КонецПроцедуры

#КонецОбласти

#Область КомандыФормы

&НаКлиенте
Процедура Печать(Команда)
	
	Если ВыполнитьПроверку() Тогда
		Возврат
	КонецЕсли;
	
	ПараметрКоманды = Новый Массив;
	ПараметрКоманды.Добавить(ПредопределенноеЗначение("Справочник.Номенклатура.ПустаяСсылка"));
	
	ИменаМакетов = "ШтрихкодНоменклатура";
	
	ПараметрыПечати = ПолучитьПараметры();
	
	Если ПараметрыПечати = Неопределено Тогда
		ТекстСообщения = НСтр("ru = 'Нет данных для печати.'");
		ОбщегоНазначенияКлиент.СообщитьПользователю(ТекстСообщения);
	Иначе
		Если СразуНаПринтер Тогда
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечатиНаПринтер(
				"Обработка.ПечатьШтрихКодов",
				ИменаМакетов,
				ПараметрКоманды,
				ПараметрыПечати);
		Иначе
			УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(
				"Обработка.ПечатьШтрихКодов",
				ИменаМакетов,
				ПараметрКоманды,
				ЭтаФорма,
				ПараметрыПечати);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура РедактироватьШаблон(Команда)
	ПоказатьЗначение(,Объект.Шаблон);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыФункции

#Область Печать

&НаСервере
Функция ПолучитьПараметры()
	
	Ошибки = Неопределено;
	
	Номенклатура = Новый ТаблицаЗначений;
	Номенклатура.Колонки.Добавить("Номенклатура"			, Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Номенклатура.Колонки.Добавить("Штрихкод"				, Новый ОписаниеТипов("Строка"));
	
	Для Каждого СтрокаТЧ ИЗ Объект.Номенклатура Цикл
		
		НоваяСтрока = Номенклатура.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
	КонецЦикла;
	
	Если Номенклатура.Количество() = 0 Тогда
		Возврат Неопределено;
	КонецЕсли;	
		
	ПараметрыПечати = Новый Структура;
	ПараметрыПечати.Вставить("Номенклатура"				, ПоместитьВоВременноеХранилище(Номенклатура, УникальныйИдентификатор));
	ПараметрыПечати.Вставить("СтруктураМакетаШаблона"	, Неопределено);
	ПараметрыПечати.Вставить("ШаблонШтрихкода"			, Объект.Шаблон);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров"	, Количество);
	ПараметрыПечати.Вставить("ТипШаблона"				, Перечисления.ТипыШаблоновШтрихкодов.Номенклатура);
	
	Возврат ПараметрыПечати;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДанныеНоменклатурыНаСервере(ТаблицаНоменклатуры)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	Ном.Ссылка КАК Номенклатура,
		|	ШтрихкодыНоменклатуры.Штрихкод КАК Штрихкод
		|ИЗ
		|	Справочник.Номенклатура КАК Ном
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ШтрихкодыНоменклатуры КАК ШтрихкодыНоменклатуры
		|		ПО (ШтрихкодыНоменклатуры.Номенклатура = Ном.Ссылка)
		|ГДЕ
		|	Ном.Ссылка В(&МассивНоменклатуры)
		|	И НЕ Ном.ЭтоГруппа";
	
	Запрос.УстановитьПараметр("МассивНоменклатуры", ТаблицаНоменклатуры.ВыгрузитьКолонку("Номенклатура"));
	
	Объект.Номенклатура.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецПроцедуры

&НаСервере
Функция ВыполнитьПроверку()
	
	ЕстьОшибки = Ложь;
	
	Если Не ЗначениеЗаполнено(Объект.Шаблон) Тогда
		ОбщегоНазначения.СообщитьПользователю(НСтр("ru = 'Не указан шаблон штрихкода номенклатуры'"),,"Объект.Шаблон");
		ЕстьОшибки = Истина;
	КонецЕсли;
	
	Возврат ЕстьОшибки;
	
КонецФункции

#КонецОбласти

&НаСервере
Процедура СформироватьПредварительныйПросмотр()
	
	ПараметрыПечати = Новый Структура;
	
	Номенклатура = Новый ТаблицаЗначений;
	Номенклатура.Колонки.Добавить("Номенклатура"			, Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));
	Номенклатура.Колонки.Добавить("Штрихкод"				, Новый ОписаниеТипов("Строка"));
	
	Для Каждого Строка Из Объект.Номенклатура Цикл 
		
		НоваяСтрока = Номенклатура.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока,Строка);
		
	КонецЦикла;
	
	ПараметрыПечати.Вставить("Номенклатура"				, ПоместитьВоВременноеХранилище(Номенклатура, УникальныйИдентификатор));
	ПараметрыПечати.Вставить("СтруктураМакетаШаблона"	, Неопределено);
	ПараметрыПечати.Вставить("ШаблонШтрихкода"			, Объект.Шаблон);
	ПараметрыПечати.Вставить("КоличествоЭкземпляров"	, 1);
	ПараметрыПечати.Вставить("ДатаСведений"				, ОбщегоНазначенияБК.ПолучитьРабочуюДату());
	ПараметрыПечати.Вставить("ТипШаблона"				, Объект.Шаблон.ТипШаблона);
	
	ПредварительныйПросмотр = Обработки.ПечатьШтрихкодов.СформироватьПечатнуюФормуШтрихкодаНоменклатуры(ПараметрыПечати, Неопределено);
	
КонецПроцедуры

&НаСервере
Процедура СохранитьДанныеНаСервере()
	
	Если ЗначениеЗаполнено(Объект.Шаблон) Тогда
		ХранилищеНастроекДанныхФорм.Сохранить("Обработка.ПечатьШтрихкодов.Форма.ФормаПечатиНоменклатуры", "Шаблон", Объект.Шаблон);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти
