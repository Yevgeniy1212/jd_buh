
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	ПравоСохраненияДанных = ПравоДоступа("СохранениеДанныхПользователя", Метаданные);
	
	ПоказыватьИнформацию = ?(ПравоСохраненияДанных, ХранилищеОбщихНастроек.Загрузить("ИнформацияОВводеПлатнойПоддержки", "Показывать"), Неопределено);
	ПоказыватьПриНачалеРаботы = (ПоказыватьИнформацию = Неопределено ИЛИ ПоказыватьИнформацию);
	
	Элементы.ПоказыватьПриНачалеРаботы.Видимость = ПравоСохраненияДанных;
	
	ВебСодержимое = Обработки.си_ИнформацияОВводеПлатнойПоддержки.ПолучитьМакет("МакетИнформацияОПлатнойПоддержке").ПолучитьТекст();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриЗакрытии(ЗавершениеРаботы)
	
	Если ЗавершениеРаботы = Истина Тогда
        Возврат;
	ИначеЕсли ПравоСохраненияДанных Тогда
		СохранитьСостояниеФлажка(ПоказыватьПриНачалеРаботы);
	КонецЕсли;

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ВебСодержимоеПриНажатии(Элемент, ДанныеСобытия, СтандартнаяОбработка)
	Если ДанныеСобытия.Свойство("href") И ЗначениеЗаполнено(ДанныеСобытия.href) Тогда
		ИмяОткрываемойСтраницы = СокрЛП(ДанныеСобытия.href);
		Протокол = ВРег(СтрЛевДоСимвола(ИмяОткрываемойСтраницы, ":"));
		Если Протокол <> "HTTP" И Протокол <> "HTTPS" И Протокол <> "E1C" Тогда
			Возврат; // Не ссылка
		Иначе
			#Если ТолстыйКлиентОбычноеПриложение Тогда
				ЗапуститьПриложение(ИмяОткрываемойСтраницы);
			#Иначе
				ПерейтиПоНавигационнойСсылке(ИмяОткрываемойСтраницы);
			#КонецЕсли
		КонецЕсли;
		СтандартнаяОбработка = Ложь;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Процедура СохранитьСостояниеФлажка(ПоказыватьПриНачалеРаботы)
	Если ПравоДоступа("СохранениеДанныхПользователя", Метаданные) Тогда
		ХранилищеОбщихНастроек.Сохранить("ИнформацияОВводеПлатнойПоддержки", "Показывать", ПоказыватьПриНачалеРаботы);
	КонецЕсли;
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция СтрЛевДоСимвола(Строка, Разделитель, Остаток = Неопределено)
	Позиция = Найти(Строка, Разделитель);
	Если Позиция = 0 Тогда
		СтрокаДоТочки = Строка;
		Остаток = "";
	Иначе
		СтрокаДоТочки = Лев(Строка, Позиция - 1);
		Остаток = Сред(Строка, Позиция + СтрДлина(Разделитель));
	КонецЕсли;
	Возврат СтрокаДоТочки;
КонецФункции

#КонецОбласти
