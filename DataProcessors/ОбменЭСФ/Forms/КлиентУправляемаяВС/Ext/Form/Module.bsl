// Текст модуля одинаковый в:
// - Обработка.ОбменЭСФ.МодульОбъекта,
// - Обработка.ОбменЭСФ.Форма.КлиентОбычная,
// - Обработка.ОбменЭСФ.Форма.КлиентУправляемая.
// При изменении в одном месте необходимо внести их в другие места.

#Область Криптография

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Перем ВерсияБЭСФ Экспорт;

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ПрограммныйИнтерфейс

&НаКлиенте
Функция УдалосьПодключитьКриптопровайдерНаКлиенте(Версия = "", РежимТишины = Ложь, ТолькоПодключение = Ложь) Экспорт
	
	Криптопровайдер = КриптографияКлиентСервер.НовыйКриптопровайдер(, РежимТишины, ТолькоПодключение);
		
	Если Криптопровайдер = Неопределено Тогда
		ОбновитьПовторноИспользуемыеЗначения();
		Подключается = Ложь;
		Версия = "";
	Иначе
		Подключается = Истина;
		Версия = Криптопровайдер.Версия;
	КонецЕсли;
		
		
	Возврат Подключается;
	
КонецФункции

&НаКлиенте
Функция УдалосьПодключитьКриптопровайдерНаСервере(Версия = "", РежимТишины = Ложь) Экспорт
	
	Подключается = КриптографияВызовСервера.УдалосьПодключитьКриптопровайдерНаСервере(Версия, РежимТишины);
			       		
	Возврат Подключается;	
	
КонецФункции

&НаКлиенте
Функция КриптопровайдерПодключается(Версия = "", РежимТишины = Ложь, МестоВыполненияОпераций = Неопределено, ТолькоПодключение = Ложь) Экспорт
	// Место выполнения операции
	ВыполнятьКриптографическиеОперацииНаКлиенте = Неопределено;	
	Если ЗначениеЗаполнено(МестоВыполненияОпераций) Тогда
		ВыполнятьКриптографическиеОперацииНаКлиенте = МестоВыполненияОпераций = ПредопределенноеЗначение("Перечисление.МестаВыполненияКриптографическихОпераций.НаКлиенте");
	Иначе
		ВыполнятьКриптографическиеОперацииНаКлиенте = ЭСФВызовСервераПовтИсп.ВыполнятьКриптографическиеОперацииНаКлиенте(); 
	КонецЕсли;	
	
	// Проверка подключения в требуемом контексте.
	Если ВыполнятьКриптографическиеОперацииНаКлиенте Тогда		
		Подключается = УдалосьПодключитьКриптопровайдерНаКлиенте(Версия, РежимТишины, ТолькоПодключение);		
	Иначе		
		Подключается = УдалосьПодключитьКриптопровайдерНаСервере(Версия, РежимТишины);				
	КонецЕсли;
	
	Возврат Подключается;	

КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
// При вызове процедуры из конекста клиента необходимо передавать идентификатор клиентской формы,
// иначе хранилище будет очищено при возврате в контекст клиента
Функция АдресБиблиотекиКриптографии(УникальныйИдентификаторФормыКлиента = Неопределено) Экспорт
	СсылкаНаМодуль = ПоместитьВоВременноеХранилище(Константы.ЭСФВнешнийМодуль.Получить().Получить(), УникальныйИдентификаторФормыКлиента);
	Возврат СсылкаНаМодуль;
КонецФункции

&НаКлиенте
Функция ОшибкаКриптопровайдера(Криптопровайдер, ИнформацияОбОшибке) Экспорт
	
	Ошибка = Новый Структура;
	
	Если Криптопровайдер = Неопределено Тогда
		Ошибка.Вставить("Версия", "");
		Ошибка.Вставить("КодОшибки", "");
		Ошибка.Вставить("ОписаниеОшибки", "");		
	Иначе
		Ошибка.Вставить("Версия", Криптопровайдер.Версия);
		Ошибка.Вставить("КодОшибки", Криптопровайдер.КодОшибки);
		Ошибка.Вставить("ОписаниеОшибки", Криптопровайдер.ОписаниеОшибки);
	КонецЕсли;
	
	Ошибка.Вставить("ИнформацияОбОшибке", ИнформацияОбОшибке);
	
	ТипОшибки = ТипОшибкиКриптопровайдера(Ошибка);
	Ошибка.Вставить("ТипОшибки", ТипОшибки);
	
	ПредставленияОшибки = ПредставленияОшибкиКриптопровайдера(Ошибка);
	Ошибка.Вставить("КраткоеПредставление", ПредставленияОшибки.КраткоеПредставление);
	Ошибка.Вставить("ПодробноеПредставление", ПредставленияОшибки.ПодробноеПредставление);
	
	Возврат Ошибка;
	
КонецФункции

// Возвращает строку, содержащую ЭЦП.
//
// Параметры:
//  СтрокаДляПодписи - Строка - Строка, для которой необходимо получить ЭЦП.
//  Ключ - Строка - Ключевой контейнер PKCS#12 в кодировке Base64.
//  ПарольКлюча - Строка - Пароль от ключевого контейнера PKCS#12.
//
// Возвращаемое значение:
//  Строка - ЭЦП.
//
&НаКлиенте
Функция СоздатьЭЦП(Знач СтрокаДляПодписи, Знач КлючBase64, Знач ПарольКлюча) Экспорт
	
	Попытка
				
		Криптопровайдер = КриптографияКлиентСервер.НовыйКриптопровайдер();		
		Криптопровайдер.ИнициализироватьХранилищеКлюча(КлючBase64, ПарольКлюча, ЭСФКлиентСервер.ИнициализироватьХранилищеКлюча_ТипДанных_СтрокаBase64());	
		ЭЦП = Криптопровайдер.ПолучитьПодпись(СтрокаДляПодписи, ПолучитьПодпись_ТипДанных_UTF8());		
		
		Возврат ЭЦП;	
		
	Исключение
		
		Ошибка = ОшибкаКриптопровайдера(Криптопровайдер, ИнформацияОбОшибке());
		
		ЭСФВызовСервера.СоздатьЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка выполнения'"), "Ошибка", , , Ошибка.ПодробноеПредставление);
		
		ТекстИсключения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр(
		"ru = 'Не удалось подписать электронный счет-фактуру.
		|%1'"),  Ошибка.КраткоеПредставление);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
КонецФункции

// Возвращает тип ключа (ГОСТ или RSA) файла PKCS#12.
//
// Параметры:
//  КлючBase64 - Строка - Ключевой контейнер в Base64, тип ключа которого необходимо определить.
//  ПарольКлюча - Строка - Пароль от ключевого контейнера.
//
// Возвращаемое значение:
//  Строка - Тип ключа. См. КлючGOST(), КлючRSA().
//   Если не удалось определить тип ключа, то возвращается пустая строка.
//
&НаКлиенте
Функция ТипКлюча(Знач КлючBase64, Знач ПарольОтКлюча) Экспорт
	
	СвойстваСертификата = ЭСФКлиентСервер.СвойстваСертификата(КлючBase64, ПарольОтКлюча);	
	ТипКлюча = СвойстваСертификата.ТипКлюча;	
	Возврат ТипКлюча;
	
КонецФункции

// Возвращает сертификат открытого ключа в виде строки Base64.
//
// Параметры:
//  КлючBase64 - Строка - Файл PKCS#12 содержащий сертификат открытого ключа, в кодировке P12.
//  Пароль - Строка - Пароль к файлу PKCS#12 содержащему сертификат открытого ключа.
//
// Возвращаемое значение:
//  Строка - Сертификат открытого ключа в формате Base64.
//
&НаКлиенте
Функция ОткрытыйСертификатBase64(Знач КлючBase64, Знач ПарольКлюча) Экспорт
		
	Попытка
		
		Криптопровайдер = КриптографияКлиентСервер.НовыйКриптопровайдер();
		Криптопровайдер.ИнициализироватьХранилищеКлюча(КлючBase64, ПарольКлюча, ЭСФКлиентСервер.ИнициализироватьХранилищеКлюча_ТипДанных_СтрокаBase64());	
		Сертификат = Криптопровайдер.ПолучитьСертификат(ПолучитьСертификат_ТипВозвращаемыхДанных_СтрокаBase64());
		
	Исключение
		
		Ошибка = ОшибкаКриптопровайдера(Криптопровайдер, ИнформацияОбОшибке());
		
		ЭСФВызовСервера.СоздатьЗаписьЖурналаРегистрации(НСтр("ru = 'Ошибка выполнения'"), "Ошибка", , , Ошибка.ПодробноеПредставление);
		
		ТекстИсключения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр(
		"ru = 'Не удалось выполнить действие.
		|%1'"),Ошибка.КраткоеПредставление);
		
		ВызватьИсключение ТекстИсключения;
		
	КонецПопытки;
	
	СертификатBase64 = "";
	
	Для Счетчик = 1 По СтрЧислоСтрок(Сертификат) Цикл
		ТекущаяСтрока = СтрПолучитьСтроку(Сертификат, Счетчик);
		Если Найти(ТекущаяСтрока, " CERTIFICATE-----") > 0 Тогда
			Продолжить;
		КонецЕсли;	
		СертификатBase64 = СертификатBase64 + СокрЛП(СтрПолучитьСтроку(Сертификат, Счетчик));
	КонецЦикла;
	
	СертификатBase64 = СокрЛП(СертификатBase64);
	
	Возврат СертификатBase64;
			
КонецФункции

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#Область СлужебныеПроцедурыИФункции

#Область ПримитивныеКонстанты

&НаКлиенте
Функция Ошибка_НеверныйПароль() Экспорт
	Возврат "НеверныйПароль";	
КонецФункции

&НаКлиенте
Функция Ошибка_НеверныйФайл() Экспорт
	Возврат "НеверныйФайл";	
КонецФункции

&НаКлиенте
Функция Ошибка_НеизвестнаяОшибка() Экспорт
	Возврат "НеизвестнаяОшибка";	
КонецФункции

&НаКлиенте
Функция ПолучитьСертификат_ТипВозвращаемыхДанных_СтрокаBase64() Экспорт
	Возврат 1;	
КонецФункции

&НаКлиенте
Функция ПолучитьПодпись_ТипДанных_UTF8() Экспорт
	Возврат 1;
КонецФункции

// не имеет директив
Функция ИмяКомпонентыКриптографии() Экспорт
	
	// для обхода ошибки подключения внешней компоненты на серверных базах
	// при параллельных сеансах работы пользователей используем номер сеанса ИБ, начиная с версии 8.3.7
	Попытка
		
		ТекущийСеансИБ = Вычислить("ПолучитьТекущийСеансИнформационнойБазы()");
		
		УникальныйНомерКомпоненты = "" + Формат(ТекущийСеансИБ.НомерСеанса, "ЧРГ=_") + "" + Формат(ТекущийСеансИБ.НачалоСеанса, "ДФ=ддММгггЧЧммсс") + ЭСФВызовСервераПовтИсп.ПолучитьЭСФВерсияВнешнегоМодуля();
		
		Результат = "";
		
		Для НомерСимвола = 1 По СтрДлина(УникальныйНомерКомпоненты) Цикл
			КодСимвола = КодСимвола(УникальныйНомерКомпоненты, НомерСимвола);
			Если КодСимвола > 47 И КодСимвола < 58 Тогда // Число.
				Результат = Результат + Символ(КодСимвола);
			КонецЕсли;
		КонецЦикла;
		
		УникальныйНомерКомпоненты = Результат;
		
	Исключение
		
		УникальныйНомерКомпоненты = "";
		
	КонецПопытки;
	
	Возврат "CryptoAPI" + УникальныйНомерКомпоненты;
	
КонецФункции

// не имеет директив
Функция ИмяОбъектаКриптографии() Экспорт
	Возврат "AddIn." + ИмяКомпонентыКриптографии() + ".Crypto";	
КонецФункции

&НаКлиенте
Функция ПолноеИмяФормыУстановкаКриптографии() Экспорт
	Возврат "Обработка.ОбменЭСФ.Форма.УстановкаКриптографии";	
КонецФункции

&НаКлиенте
Функция КлючGOST() Экспорт 
	
	Возврат "GOST";
	
КонецФункции

&НаКлиенте
Функция КлючRSA() Экспорт
	
	Возврат "RSA";
	
КонецФункции

#КонецОбласти

#Область Сертификат

#КонецОбласти

#Область ОшибкиКриптографии

&НаКлиенте
Функция ТипОшибкиКриптопровайдера(Ошибка)
	
	ТипОшибки = Ошибка_НеизвестнаяОшибка();
	
	Если Найти(Ошибка.ОписаниеОшибки, "0D0680A8") Тогда
		ТипОшибки = Ошибка_НеверныйФайл();		
	ИначеЕсли Найти(Ошибка.ОписаниеОшибки, "23076071") Тогда
		ТипОшибки = Ошибка_НеверныйПароль();	
	Иначе
		ТипОшибки = Ошибка_НеизвестнаяОшибка();	
	КонецЕсли;
	
	Возврат ТипОшибки;
	
КонецФункции

&НаКлиенте
Функция ПредставленияОшибкиКриптопровайдера(Ошибка)
	
	ПредставленияОшибки = Новый Структура;
	
	КраткоеПредставление = КраткоеПредставлениеОшибкиКриптопровайдера(Ошибка);
	КраткоеПредставление = КраткоеПредставление + Символы.ПС + НСтр("ru = 'Подробности ошибки смотрите в журнале регистрации.'");
	ПредставленияОшибки.Вставить("КраткоеПредставление", КраткоеПредставление);
	
	ПодробноеПредставление = ПодробноеПредставлениеОшибкиКриптопровайдера(Ошибка);
	ПредставленияОшибки.Вставить("ПодробноеПредставление", ПодробноеПредставление);
	
	Возврат ПредставленияОшибки;
	
КонецФункции

&НаКлиенте
Функция КраткоеПредставлениеОшибкиКриптопровайдера(Ошибка)
	
	КраткоеПредставление = "";
	
	Если Ошибка.ТипОшибки = Ошибка_НеверныйФайл() Тогда
		КраткоеПредставление = НСтр("ru = 'Возможно выбранный файл не является файлом сертификата ""*.p12"".'");		
	ИначеЕсли Ошибка.ТипОшибки = Ошибка_НеверныйПароль() Тогда
		КраткоеПредставление = НСтр("ru = 'Возможно был введен некорректный пароль для файла сертификата.'");				
	Иначе	
		КраткоеПредставление = КраткоеПредставлениеОшибки(Ошибка.ИнформацияОбОшибке);					
	КонецЕсли;
	
	Возврат КраткоеПредставление;
	
КонецФункции

&НаКлиенте
Функция ПодробноеПредставлениеОшибкиКриптопровайдера(Ошибка)
	
	КраткоеПредставление = КраткоеПредставлениеОшибкиКриптопровайдера(Ошибка);	
		
	Если ПустаяСтрока(Ошибка.Версия) И ПустаяСтрока(Ошибка.КодОшибки) И ПустаяСтрока(Ошибка.ОписаниеОшибки) Тогда
		ИнформацияИзКриптопровайдера = "";
	Иначе
		ИнформацияИзКриптопровайдера = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru = 'Версия криптографии: %1, Код ошибки: %2, Описание ошибки: %3'"),Ошибка.Версия, Ошибка.КодОшибки, Ошибка.ОписаниеОшибки);
	КонецЕсли;	
		
	ПодробноеПредставлениеОшибки = ПодробноеПредставлениеОшибки(Ошибка.ИнформацияОбОшибке);
	
	ПодробноеПредставление = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр(
	"ru = '%1
	|%2
	|%3'"),КраткоеПредставление,ИнформацияИзКриптопровайдера,ПодробноеПредставлениеОшибки);
	
	Возврат ПодробноеПредставление;
	
КонецФункции

#КонецОбласти

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ КОТОРЫЕ НУЖНО ВЫПОЛНЯТЬ ОБЯЗАТЕЛЬНО НА СЕРВЕРЕ

#Область СерверныеПроцедурыИФункции

#КонецОбласти

#КонецОбласти

#Область МодульОбновленияКриптографии

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОНЛАЙН-ОБНОВЛЕНИЯ МОДУЛЯ

// Процедуры c директивой &НаКлиентеНаСервереБезКонтекста в модуле обычной формы
// объявляются БЕЗ ДИРЕКТИВ

#Область ПрограммныйИнтерфейс

&НаКлиенте
Процедура ОбновитьМодульКриптографии(ПараметрыАвторизации, ВыполняемоеОповещение = Неопределено, ТихийРежим = Ложь) Экспорт

	Состояние(НСтр("ru='Подождите, пожалуйста...'"), ,
		НСтр("ru='Производится проверка наличия обновлений модуля криптографии...'"));
		
	ШапкаОшибки = НСтр("ru = 'Ошибка обновления модуля криптографии:'");	
	
	СвойстваМодуляДляТекущегоРелиза = Неопределено;
	КаталогФайловНаСервере = Неопределено;
	
	ПараметрыСервераИнтернета = ПолучитьПараметрыИнтернета(ПараметрыАвторизации);
	
	Если ПроверитьНаличиеОбновлений(ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза, ШапкаОшибки) = Ложь Тогда 
		// обновление не требуется, завершаем процесс
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
		КонецЕсли;
	Иначе
		
		// Добавим к параметрым версию модуля
		ПараметрыСервераИнтернета.ДополнительныеПараметры.Вставить("Версия", СвойстваМодуляДляТекущегоРелиза.Версия);
			
		Если НЕ ТихийРежим Тогда
			
			Состояние(НСтр("ru='Подождите, пожалуйста...'"), , НСтр("ru='Обнаружена новая версия модуля'"), БиблиотекаКартинок.ДлительнаяОперация48);
			ДатаВыпускаМодуля = ЭСФКлиентСервер.ДатаПоСтроке(СвойстваМодуляДляТекущегоРелиза.Дата);
			// запрашиваем подтверждение на закачку нового модуля
			
			Если ЗначениеЗаполнено(ДатаВыпускаМодуля) Тогда				
				ТекстВопроса = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='В Интернете доступно обновление библиотеки криптографии.
				|
				|Новый модуль: %1 от %2
				|
				|Обновить модуль сейчас (рекомендуется)?'"),СвойстваМодуляДляТекущегоРелиза.Версия, Формат(ДатаВыпускаМодуля, "ДЛФ=DD"));
			Иначе				
				ТекстВопроса = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='В Интернете доступно обновление библиотеки криптографии.
				|
				|Новый модуль: %1. 
				|
				|Обновить модуль сейчас (рекомендуется)?'"),СвойстваМодуляДляТекущегоРелиза.Версия);
			КонецЕсли;
						
			// Выполним обработку
			ДополнительныеПараметры = Новый Структура("ВыполняемоеОповещение, ШапкаОшибки, ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза", 
				ВыполняемоеОповещение, ШапкаОшибки, ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза);
			
			ОписаниеОповещения = Новый ОписаниеОповещения("ОбновитьМодульКриптографииЗавершение", ЭтаФорма, ДополнительныеПараметры);
			ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
			
		Иначе
			
			Ответ = КодВозвратаДиалога.Да;
			
			ДополнительныеПараметры = Новый Структура("ВыполняемоеОповещение, ШапкаОшибки, ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза", 
				ВыполняемоеОповещение, ШапкаОшибки, ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза);
			
			ОбновитьМодульКриптографииЗавершение(Ответ, ДополнительныеПараметры);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьМодульКриптографииЗавершение(Ответ, ДополнительныеПараметры) Экспорт
	
	ШапкаОшибки = ДополнительныеПараметры.ШапкаОшибки;
	ПараметрыСервераИнтернета = ДополнительныеПараметры.ПараметрыСервераИнтернета;
	
	//СвойстваМодуляДляТекущегоРелиза - Возвращаемое значение по результатам обновления
	СвойстваМодуляДляТекущегоРелиза = ДополнительныеПараметры.СвойстваМодуляДляТекущегоРелиза;
	ВыполняемоеОповещение = ДополнительныеПараметры.ВыполняемоеОповещение;
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		// закачиваем, кэшируем и включаем признак использования внешнего модуля		
		Состояние(НСтр("ru='Подождите, пожалуйста...'"), , НСтр("ru='Производится получение новой версии модуля...'"), БиблиотекаКартинок.ДлительнаяОперация48);
		Результат = ЗакачатьИВключитьПризнакИспользования(ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза, ШапкаОшибки);
		Если Результат Тогда			
			ОбновитьПовторноИспользуемыеЗначения();
			Оповестить("ОбновленМодульКриптографии");
		КонецЕсли;
		
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Результат);
		КонецЕсли;
	Иначе
		Если ВыполняемоеОповещение <> Неопределено Тогда
			ВыполнитьОбработкуОповещения(ВыполняемоеОповещение, Ложь);
		КонецЕсли;
	КонецЕсли;
		
КонецПроцедуры

//&НаКлиенте
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьПараметрыИнтернета(СтруктураДополнительныхПараметров) Экспорт
	
	ПараметрыСервераИнтернета = Новый Структура("URLСервера,Порт, КаталогФайловНаСервере, ИмяКлючевогоФайлаНаСервере, Логин, Пароль, Прокси, ДополнительныеПараметры");
	
	ПараметрыСервераИнтернета.URLСервера = АдресРесурсаИТС();	
	#Если Не ВебКлиент Тогда
	ПараметрыСервераИнтернета.Прокси = ЭСФКлиентСервер.НовыйПроксиСерверИСЭСФ();
	#КонецЕсли
		
	// аутентификация пользователя	
	ПараметрыСервераИнтернета.ДополнительныеПараметры = СтруктураДополнительныхПараметров;
		
	Возврат ПараметрыСервераИнтернета;	
КонецФункции

#КонецОбласти

#Область СервисыИТС

// Выполняет метод веб-сервиса ИТС.
//
// Параметры:
//  ТекстМетода - Строка - Текст метода, который необходимо выполнить.
//  ИмяВебСервиса - Строка - Имя веб-сервиса, метод которого необходимо вызвать.
//
// Возвращаемое значение:
//  Строка - Результат выполения метода.
//
&НаКлиентеНаСервереБезКонтекста
Функция ВыполнитьМетодВебСервисаИТС(Знач ТекстМетода, Знач ИмяВебСервиса) Экспорт
		
	HTTPСоединение = Новый HTTPСоединение(
			АдресРесурсаИТС(), 
			, 
			, 
			, 
			#Если НЕ ВебКлиент Тогда
			ЭСФКлиентСервер.НовыйПроксиСерверИСЭСФ()
			#КонецЕсли
			, 
			,			
			);
		
	HTTPЗапрос = Новый HTTPЗапрос();
	HTTPЗапрос.АдресРесурса	= ИмяВебСервиса;
	
	Заголовки = Новый Соответствие();
	Заголовки.Вставить("Content-Type", "text/xml;charset=UTF-8");
	
	HTTPЗапрос.Заголовки = Заголовки;
	HTTPЗапрос.УстановитьТелоИзСтроки(ТекстМетода, "UTF-8");
	
	HTTPОтвет = HTTPСоединение.ОтправитьДляОбработки(HTTPЗапрос);	
	
	ТекстОтвета = СокрЛП(HTTPОтвет.ПолучитьТелоКакСтроку());
	
	Возврат ТекстОтвета;
	
КонецФункции

&НаКлиенте
Функция ПолучитьСсылкуНаСкачиваниеБиблиотеки(ДанныеПрофиля, Идентификатор = "") Экспорт
	
	ЗапросXML = ТекстЗапросаEsfLibRequest(ДанныеПрофиля);
	ОтветXML = ВыполнитьМетодВебСервисаИТС(ЗапросXML, ИмяСервисаВыдачиСсылкиПользователю());
		
	СсылкаНаСкачивание = ПолучитьСсылкуНаСкачиваниеБиблиотекиНаСервере(ОтветXML);	
	Возврат СсылкаНаСкачивание;
КонецФункции

&НаСервере
Функция ПолучитьСсылкуНаСкачиваниеБиблиотекиНаСервере(ОтветXML)
	ИнформацияОбОшибке = Неопределено;
	Если НЕ ЭтоОшибкаСервисаИТС(ОтветXML, ИнформацияОбОшибке) Тогда		
		ОтветXDTO = ЭСФСервер.ОбъектXDTOИзСтрокиXML(ОтветXML, ФабрикаXDTO);		
		СсылкаНаСкачивание = ОтветXDTO.reference;	
		Идентификатор = ОтветXDTO.id;			
	Иначе
		ЭСФКлиентСервер.СообщитьПользователю(ИнформацияОбОшибке.ПодробноеПредставление);
		ВызватьИсключение(НСтр("ru='Не удалось получить данные с сервера ИТС!'") + Символы.ПС + ИнформацияОбОшибке.КраткоеПредставление);
	КонецЕсли;
	
	Возврат СсылкаНаСкачивание;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ОтправитьПодтверждениеОЗавершенииСкачивания(Идентификатор)
	СсылкаНаСкачивание = "";
		
	ЗапросXML = ТекстЗапросаEsfDnlAckRequest(Идентификатор);
	
	ОтветXML = ВыполнитьМетодВебСервисаИТС(ЗапросXML,  ИмяСервисаПодтвержденияЗагрузки());		
КонецПроцедуры

///////////////////////////////////////////////////////////
// Имена сервисов

&НаКлиентеНаСервереБезКонтекста
Функция АдресРесурсаИТС()
	Возврат "its.1c.kz";
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСервисаВыдачиСсылкиПользователю()
	Возврат "/download/esf/request";
КонецФункции	

&НаКлиентеНаСервереБезКонтекста
Функция ИмяСервисаПодтвержденияЗагрузки()
	Возврат "/download/esf/acknowledge";
КонецФункции	

// Тексты запросов
&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗапросаEsfLibRequest(ДанныеПрофиля) Экспорт	
	ТекстЗапроса = КриптографияВызовСервера.ПолучитьТекстЗапроса("ЗапросEsfLibRequest");
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[IDConf]", ДанныеПрофиля.ИДКонфигурации);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[Version]", ДанныеПрофиля.Версия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[Isbase]", ?(ДанныеПрофиля.ЭтоБазовая,1,0));
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[RegNumber]", ДанныеПрофиля.РегистрационныйНомер);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[OrgainzationBIN]", ДанныеПрофиля.ОрганизацияБИН);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[OrgainzationName]", ДанныеПрофиля.ОрганизацияНаименование);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[OrgainzationAddr]", ДанныеПрофиля.ОрганизацияАдрес);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[OrgainzationContacts]", ДанныеПрофиля.ОрганизацияТелефон);
	
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[UserIIN]", ДанныеПрофиля.ПользовательИИН);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[UserFisrtName]", ДанныеПрофиля.ПользовательИмя);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[UserSecondName]", ДанныеПрофиля.ПользовательОтчество);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[UserLastName]", ДанныеПрофиля.ПользовательФамилия);
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[UserPosition]", ДанныеПрофиля.ПользовательДолжность);
	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ExtraInfo]", ДанныеПрофиля.ДополнительныеСведения);
	
	Возврат ТекстЗапроса;
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Функция ТекстЗапросаEsfDnlAckRequest(ИдентификаторСессии)Экспорт	
	ТекстЗапроса = КриптографияВызовСервера.ПолучитьТекстЗапроса("ЗапросEsfDnlAckRequest");	
	ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[ID]", ИдентификаторСессии);
	         	
	Возврат ТекстЗапроса;
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТКА ОШИБОК
#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ЭтоОшибкаСервисаИТС(Знач ОтветXML, ИнформацияОбОшибке = Неопределено, IDСессии = "") Экспорт
	
	ЭтоОшибка = Ложь;
	
	ИнформацияОбОшибке = Новый Структура("Код, КраткоеПредставление, ПодробноеПредставление", 0, "", "");
	// Нештатная ситуация, некорректный ответ	
	Если Найти(НРЕГ(ОтветXML), "esf_lib_response") = 0 Тогда
		
		ИнформацияОбОшибке.Код = 100;	
		ИнформацияОбОшибке.КраткоеПредставление = НСтр("ru='Сервер ИТС недоступен или предоставил некорректный ответ.'");
		
		ТекстОшибки = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='Сообщите в службу поддержки нижеприведенную информацию. %1 Ответ сервиса: %2'"),Символы.ПС,СокрЛП(ОтветXML));
		ИнформацияОбОшибке.ПодробноеПредставление = ТекстОшибки;
		
		Возврат Истина;
	КонецЕсли;	
	
	// Нештатная ситуация, корректный ответ	
	ОбъектXDTO = ЭСФСервер.ОбъектXDTOИзСтрокиXML(ОтветXML, ФабрикаXDTO);	
	Попытка
		ИнформацияОбОшибке.Код = Число(СокрЛП(ОбъектXDTO.code));
		ИнформацияОбОшибке.КраткоеПредставление = СокрЛП(ОбъектXDTO.message);		
		IDСессии = ?(ТипЗнч(ОбъектXDTO.id) = Тип("ОбъектXDTO"), "", СокрЛП(ОбъектXDTO.id));// в случае пустого значения, возникает Объект XDTO 			
		
		ТекстОшибки = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru = 'Код ошибки: %1. IDСессии: %2. Ошибка: %3'"),ИнформацияОбОшибке.Код,IDСессии,СокрЛП(ОбъектXDTO.message));			
		ИнформацияОбОшибке.ПодробноеПредставление = ТекстОшибки;
	Исключение
		ИнформацияОбОшибке.Код = "101";	
		ИнформацияОбОшибке.КраткоеПредставление = НСтр("ru='При обработке сведений сервиса ИТС возникла неустановленная ошибка.'");
		
		ТекстОшибки = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru = 'Код ошибки: %1. IDСессии: %2. %3 Ответ сервиса: %4'"),ИнформацияОбОшибке.Код,IDСессии,Символы.ПС,СокрЛП(ОтветXML));			
		ИнформацияОбОшибке.ПодробноеПредставление = ТекстОшибки;
		
		Возврат Истина;
	КонецПопытки;
		
	// штатная ситуация, в выдаче отказано
	Если ИнформацияОбОшибке.Код <> 0 Тогда
		// В случае верных данных сервис возвращает 0 код ошибки.
		// Код, отличный от 0 означает наличие ошибки в регистрационных данных пользователя.
		
		ЭтоОшибка = Истина;				
	КонецЕсли;
	
	Возврат ЭтоОшибка;	
КонецФункции
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

////////////////////////////////////////////////////////////////////////////////////////////
// Функции разбора XML

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция СоздатьДеревоСтруктурыXML() Экспорт
	
	ДеревоСтруктуры = Новый ДеревоЗначений;
	ДеревоСтруктуры.Колонки.Добавить("Имя");
	ДеревоСтруктуры.Колонки.Добавить("Тип");
	ДеревоСтруктуры.Колонки.Добавить("Значение");
	
	Возврат ДеревоСтруктуры;
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ЗагрузитьСтрокуXMLВДеревоЗначений(СтрокаXML, ОписаниеОшибки = Неопределено, ЧтениеXML = Неопределено, Знач ТекУзел = Неопределено) Экспорт
	
	ПерваяИтерация = (ТекУзел = Неопределено);
	Если ПерваяИтерация Тогда
		ТекУзел = СоздатьДеревоСтруктурыXML();
		Попытка
			ЧтениеXML = Новый ЧтениеXML;
			ЧтениеXML.УстановитьСтроку(СтрокаXML);
		Исключение
			ОписаниеОшибки = НСтр("ru='Ошибка разбора XML: '") + ИнформацияОбОшибке().Описание + ".";
			Возврат Неопределено;
		КонецПопытки;
	КонецЕсли;
	
	Попытка
		Пока ЧтениеXML.Прочитать() Цикл
			ТипУзла = ЧтениеXML.ТипУзла;
			Если ТипУзла = ТипУзлаXML.НачалоЭлемента Тогда
				НовУзел = ТекУзел.Строки.Добавить();
				НовУзел.Имя = ЧтениеXML.Имя;
				НовУзел.Тип = "Э";
				НовУзел.Значение = ЧтениеXML.Значение;
				Пока ЧтениеXML.ПрочитатьАтрибут() Цикл
					НовАтрибут = НовУзел.Строки.Добавить();
					НовАтрибут.Имя = ЧтениеXML.Имя;
					НовАтрибут.Тип = "А";
					НовАтрибут.Значение = ЧтениеXML.Значение;
				КонецЦикла;
				ЗагрузитьСтрокуXMLВДеревоЗначений(СтрокаXML, ОписаниеОшибки, ЧтениеXML, НовУзел);
				Если ЗначениеЗаполнено(ОписаниеОшибки) Тогда
					Возврат Неопределено;
				КонецЕсли;
			ИначеЕсли ТипУзла = ТипУзлаXML.КонецЭлемента Тогда
				Прервать;
			ИначеЕсли ТипУзла = ТипУзлаXML.Текст И ТипЗнч(ТекУзел) = Тип("СтрокаДереваЗначений") И ТекУзел.Тип = "Э" Тогда
				ТекУзел.Значение = ЧтениеXML.Значение;
			КонецЕсли;
		КонецЦикла;
	Исключение
		ОписаниеОшибки = НСтр("ru='Ошибка разбора XML: '") + ИнформацияОбОшибке().Описание + ".";
		Возврат Неопределено;
	КонецПопытки;
	
	Если ПерваяИтерация Тогда
		Возврат ТекУзел;
	КонецЕсли;
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ЗагрузитьXMLВДеревоЗначений(ИмяФайлаXML = Неопределено, ТекстXML = Неопределено, ОписаниеОшибки = Неопределено) Экспорт
	
	// считываем текст из файла
	Если НЕ ЗначениеЗаполнено(ТекстXML) Тогда
		ТекстXML = ПрочитатьТекстИзФайла(ИмяФайлаXML, , Истина);
		Если НЕ ЗначениеЗаполнено(ТекстXML) Тогда
			Возврат Неопределено;
		КонецЕсли;
	КонецЕсли;
	
	// восстанавливаем дерево
	Возврат ЗагрузитьСтрокуXMLВДеревоЗначений(ТекстXML, ОписаниеОшибки);
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////
// Функции работы с Интернет

&НаКлиентеНаСервереБезКонтекста
Функция УстановитьСоединениеССерверомИнтернета(URLСервера, ПараметрыСоединения = Неопределено, ОписаниеОшибки = Неопределено) Экспорт
		
	НастройкиПрокси = ЭСФКлиентСервер.НовыйПроксиСерверИСЭСФ();
	
	Попытка
		Соединение = Новый HTTPСоединение(URLСервера, 
					?(ЗначениеЗаполнено(ПараметрыСоединения.Порт), ПараметрыСоединения.Порт, Неопределено),
					?(ЗначениеЗаполнено(ПараметрыСоединения.Логин), ПараметрыСоединения.Логин, Неопределено),
					?(ЗначениеЗаполнено(ПараметрыСоединения.Пароль), ПараметрыСоединения.Пароль, Неопределено),
					НастройкиПрокси);
	Исключение
		ОписаниеОшибки = ИнформацияОбОшибке().Описание;
		Возврат Неопределено;
	КонецПопытки;
	
	Возврат Соединение;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////
// Функции работы с файлами-обновления
&НаКлиентеНаСервереБезКонтекста
Функция ПолучитьИмяФайлаКартыРелизов() Экспорт
	Возврат "/download/esf/releasemap?version=2.0";		
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
// Возвращает структуру: Версия, Имя, Дата См. структуру файла releasemap для текущего релиза конфигурации
Функция ОпределитьСвойстваВнешнегоМодуляДляТекущегоРелиза(ДеревоКартыРелизов)
	
	ИнформацияОПрограмме = ИнформацияОПрограмме();
	
	Для Каждого Стр1 Из ДеревоКартыРелизов.Строки Цикл // уровень узла Файл
		Для Каждого Стр2 Из Стр1.Строки Цикл // уровень узла Платформа
			Если Стр2.Тип = "Э" И ТекущаяПлатформаСоответствуетУзлу(Стр2, ИнформацияОПрограмме.ВерсияПлатформы) Тогда
				Для Каждого Стр3 Из Стр2.Строки Цикл // уровень узла Конфигурация
					Если Стр3.Тип = "Э" И ТекущаяКонфигурацияСоответствуетУзлу(Стр3, ИнформацияОПрограмме) Тогда
						Для Каждого Стр4 Из Стр3.Строки Цикл // уровень узла Релиз
							Если Стр4.Тип = "Э" Тогда
								
								РезультатПоискаАтрибутаСВерсиейРелиза = Стр4.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "version", "А"));
								Если РезультатПоискаАтрибутаСВерсиейРелиза.Количество() = 0 Тогда
									Продолжить;
								КонецЕсли;
								ВерсияРелиза = СокрЛП(РезультатПоискаАтрибутаСВерсиейРелиза[0].Значение);
								
								Если ВерсияРелиза <> ИнформацияОПрограмме.ВерсияКонфигурации И ВерсияРелиза <> ИнформацияОПрограмме.ВерсияКонфигурацииБЭСФ Тогда
									Продолжить;
								КонецЕсли;
								
								Для Каждого Стр5 Из Стр4.Строки Цикл
									Если Стр5.Тип = "Э" И Стр5.Имя = "module" Тогда
										УзелВерсия = Стр5.Строки.Найти("version", "Имя");
										УзелИмя = Стр5.Строки.Найти("name", "Имя");
										УзелДата = Стр5.Строки.Найти("date", "Имя");
										Если УзелВерсия <> Неопределено И УзелИмя <> Неопределено Тогда
											Результат = Новый Структура("Версия, Имя", СокрЛП(УзелВерсия.Значение), СокрЛП(УзелИмя.Значение));
											Если ЗначениеЗаполнено(УзелДата) И ЗначениеЗаполнено(УзелДата.Значение) Тогда
												Результат.Вставить("Дата", УзелДата.Значение);
											КонецЕсли;
											Возврат Результат;
										КонецЕсли;
									КонецЕсли;
								КонецЦикла;
								
							КонецЕсли;
						КонецЦикла;
					КонецЕсли;
				КонецЦикла;
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;
	
	Возврат Неопределено;
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ПрочитатьТекстИзФайла(ИмяФайла, _КодировкаТекста = Неопределено, АвтоопределениеКодировкиXML = Ложь) Экспорт
	
	Если ЭтоАдресВременногоХранилища(ИмяФайла) Тогда
		ИмяФайлаНаДиске = ПолучитьИмяВременногоФайла();
		ПолучитьИзВременногоХранилища(ИмяФайла).Записать(ИмяФайлаНаДиске);
	Иначе
		ИмяФайлаНаДиске = ИмяФайла;
	КонецЕсли;
	
	ОбъектЧтение = Новый ЧтениеТекста(ИмяФайлаНаДиске, _КодировкаТекста);
	ТекстИзФайла = ОбъектЧтение.Прочитать();
	ОбъектЧтение.Закрыть();
	
	Если АвтоопределениеКодировкиXML И ЗначениеЗаполнено(ТекстИзФайла) Тогда
		ПерваяСтрокаФайла = ВРЕГ(Лев(СтрПолучитьСтроку(ТекстИзФайла, 1), 100));
		Если Найти(ПерваяСтрокаФайла, ВРЕГ("encoding=""UTF-8""")) <> 0 Тогда
			ТекстИзФайла = ПрочитатьТекстИзФайла(ИмяФайла, КодировкаТекста.UTF8);
		ИначеЕсли Найти(ПерваяСтрокаФайла, ВРЕГ("encoding=""UTF-16""")) <> 0 Тогда
			ТекстИзФайла = ПрочитатьТекстИзФайла(ИмяФайла, КодировкаТекста.UTF16);
		КонецЕсли;
	КонецЕсли;
	
	Возврат ТекстИзФайла;
	
КонецФункции

&НаКлиенте
// Возвращаемые значения:
// 		Ложь - обновление не требуется
// 		Истина - необходимо обновить внешний модуль
Функция ПроверитьНаличиеОбновлений(ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза, ШапкаОшибки) Экспорт
	#Если НЕ ВебКлиент Тогда
	// пытаемся установить соединение с сервером
	Соединение = УстановитьСоединениеССерверомИнтернета(ПараметрыСервераИнтернета.URLСервера,ПараметрыСервераИнтернета);
	Если Соединение = Неопределено Тогда		
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='%1Ошибка доступа к серверу обновлений модуля документооборота: %2.'"),
																				ШапкаОшибки + Символы.ПС + Символы.ПС,
																				Символы.ПС + ИнформацияОбОшибке().Описание);

		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);		
		Возврат Ложь;
	КонецЕсли;
		
		
	ФайлКартаРелизовАрхив = ПолучитьИмяВременногоФайла("zip");	
	Попытка
		Соединение.Получить(ПолучитьИмяФайлаКартыРелизов(), ФайлКартаРелизовАрхив);
	Исключение
		
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='%1Ошибка доступа к серверу обновлений модуля документооборота: %2.'"),
							ШапкаОшибки + Символы.ПС + Символы.ПС,
							Символы.ПС + ИнформацияОбОшибке().Описание);
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		
		Возврат Ложь;
	КонецПопытки;
	// получаем файл справочника внешних модулей с сервера (карты релизов)	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ФайлКартаРелизовАрхив));
	#Иначе
		АдресВоВременномХранилище = "";
		ПолучитьФайлСРесурсаНаСервере(ПолучитьИмяФайлаКартыРелизов(), АдресВоВременномХранилище);
	#КонецЕсли
	
	СвойстваМодуляДляТекущегоРелиза = Неопределено;			
	
	// РаспаковатьСохранитьСправочникВКэше
	Если НЕ ОпределитьПараметрыМодуляТекущегоРелиза(АдресВоВременномХранилище, ШапкаОшибки, СвойстваМодуляДляТекущегоРелиза) Тогда
		Возврат Ложь;
	КонецЕсли;
	
	// Сравниваем требуемую версию модуля, с версией, загруженной в ИБ	
	ВерсияМодуляВИнтернете 	= СтрЗаменить(СвойстваМодуляДляТекущегоРелиза.Версия, Символы.НПП, "");	
	КонтейнерМетодов = ЭСФКлиентСервер.КонтейнерМетодов();	
	ВерсияМодуляВБазе = СтрЗаменить(КонтейнерМетодов.ВерсияВнешнегоМодуляКриптографии(), Символы.НПП, "");
	
	Если ВерсияМодуляВИнтернете = ВерсияМодуляВБазе Тогда
		// обновление не требуется
		Возврат Ложь;
	КонецЕсли;
	
	Возврат Истина;
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ОпределитьПараметрыМодуляТекущегоРелиза(ФайлКартаРелизовАдрес, ШапкаОшибки, СвойстваМодуляДляТекущегоРелиза) Экспорт
	
	// распаковываем
	ФайлКартыРелизовАрхив = ПолучитьИмяВременногоФайла("zip");
	ФайлКартыРелизовДвоичныеДанные = ПолучитьИзВременногоХранилища(ФайлКартаРелизовАдрес);
	ФайлКартыРелизовДвоичныеДанные.Записать(ФайлКартыРелизовАрхив);
	
	ОписаниеОшибки = "";
	ФайлКартаРелизов = ЭСФСервер.РаспаковатьФайл(ФайлКартыРелизовАрхив, ОписаниеОшибки);
	
	УдалитьФайлы(ФайлКартыРелизовАрхив);
	
	Если НЕ ЗначениеЗаполнено(ФайлКартаРелизов) Тогда
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='%1%2.'"),
									ШапкаОшибки + Символы.ПС + Символы.ПС,
									Символы.ПС + ОписаниеОшибки);
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	// загружаем справочник в дерево
	ТекстФайлаСправочника = "";
	ОписаниеОшибки = "";
	
	ДеревоКартыРелизов = ЗагрузитьXMLВДеревоЗначений(ФайлКартаРелизов, ТекстФайлаСправочника, ОписаниеОшибки);
	
	УдалитьФайлы(ФайлКартаРелизов);
	
	Если НЕ ЗначениеЗаполнено(ДеревоКартыРелизов) Тогда
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='%1%2.'"),
									ШапкаОшибки + Символы.ПС + Символы.ПС,
									Символы.ПС + ОписаниеОшибки);
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	
	Если ДеревоКартыРелизов = Неопределено Тогда		
		Возврат Ложь;                           			
	КонецЕсли;
	
	// определяем последнюю доступную версию модуля документооборота для текущего релиза
	СвойстваМодуляДляТекущегоРелиза = ОпределитьСвойстваВнешнегоМодуляДляТекущегоРелиза(ДеревоКартыРелизов);
	Если НЕ ЗначениеЗаполнено(СвойстваМодуляДляТекущегоРелиза) Тогда
		Возврат Ложь;
	КонецЕсли;
			
	Возврат Истина;
	
КонецФункции //ОпределитьПараметрыМодуляТекущегоРелиза

&НаСервере
Процедура ПолучитьФайлСРесурсаНаСервере(Знач СсылкаНаСкачивание, АдресВоВременномХранилище)
	ИмяФайла = ПолучитьИмяВременногоФайла();	
	
	HTTPСоединение = Новый HTTPСоединение(
			АдресРесурсаИТС(), 
			, 
			, 
			, 
			#Если НЕ ВебКлиент Тогда
			ЭСФКлиентСервер.НовыйПроксиСерверИСЭСФ()
			#КонецЕсли
			,			
			);

	Попытка		
		HTTPСоединение.Получить(СсылкаНаСкачивание, ИмяФайла);		
	Исключение		
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ИнформацияОбОшибке().Описание);		
	КонецПопытки;
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(ИмяФайла));	
КонецПроцедуры

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ПрименитьСвойстваМодуляДокументооборота(ВнешнийМодульАдрес = Неопределено, СвойстваМодуля = Неопределено, ОписаниеОшибки = Неопределено) Экспорт
	
	ВнешнийМодуль = ПолучитьИзВременногоХранилища(ВнешнийМодульАдрес);
	
	// определяем, какие константы будут меняться
	МодифицируемыеКонстанты = Новый Массив;
	
	// подготавливаем двоичные данные внешнего модуля
	Если ВнешнийМодуль <> Неопределено И СвойстваМодуля <> Неопределено Тогда
		
		МодифицируемыеКонстанты.Добавить("ЭСФВерсияВнешнегоМодуля");
		МодифицируемыеКонстанты.Добавить("ЭСФВнешнийМодуль");
		
		ОбъектВнешнийМодуль = Новый ХранилищеЗначения(ВнешнийМодуль);
		ВерсияВнешнегоМодуля = СвойстваМодуля.Версия;					
	КонецЕсли;
		
	СтрМодифицируемыеКонстанты = "";
	Для Счетчик = 0 По МодифицируемыеКонстанты.Количество()-1 Цикл
		Если Счетчик > 0 Тогда
			СтрМодифицируемыеКонстанты = СтрМодифицируемыеКонстанты + ", " ;
		КонецЕсли;		
		СтрМодифицируемыеКонстанты = СтрМодифицируемыеКонстанты + МодифицируемыеКонстанты[Счетчик]; 
	КонецЦикла;
		
	// инициализируем значения констант
	Попытка
		КонстантыНабор = Константы.СоздатьНабор(СтрМодифицируемыеКонстанты);		
		Если ВнешнийМодуль <> Неопределено Тогда
			КонстантыНабор.ЭСФВнешнийМодуль = ОбъектВнешнийМодуль;
			КонстантыНабор.ЭСФВерсияВнешнегоМодуля = ВерсияВнешнегоМодуля;
		КонецЕсли;
		КонстантыНабор.Записать();
		ЗаписьЖурналаРегистрации("Загрузка",,Метаданные.Константы.ЭСФВнешнийМодуль,ВерсияВнешнегоМодуля, НСтр("ru='Загружен новый модуль криптографии'")); 
		Возврат Истина;
	Исключение
		ОписаниеОшибки = ИнформацияОбОшибке().Описание;
		Возврат Ложь;
	КонецПопытки;
	
КонецФункции

&НаКлиенте
Функция ЗакачатьИВключитьПризнакИспользования(ПараметрыСервераИнтернета, СвойстваМодуляДляТекущегоРелиза, ШапкаОшибки)
	ИдентификаторСессии = "";
	СсылкаМодуль = ПолучитьСсылкуНаСкачиваниеБиблиотеки(ПараметрыСервераИнтернета.ДополнительныеПараметры, ИдентификаторСессии);
	#Если НЕ ВебКлиент Тогда
	// пытаемся установить соединение с сервером	
	Соединение = УстановитьСоединениеССерверомИнтернета(ПараметрыСервераИнтернета.URLСервера,ПараметрыСервераИнтернета);
	Если Соединение = Неопределено Тогда
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='%1Ошибка доступа к серверу обновлений модуля документооборота: %2.'"),
																				ШапкаОшибки + Символы.ПС + Символы.ПС,
																				Символы.ПС + ИнформацияОбОшибке().Описание);
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;
	
	АрхивМодуля = ПолучитьИмяВременногоФайла();
	Попытка
		Соединение.Получить(СсылкаМодуль, АрхивМодуля);
		УстановитьСоединение = Ложь;
	Исключение
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='%1%2.'"),
																				ШапкаОшибки + Символы.ПС + Символы.ПС,
																				ИнформацияОбОшибке().Описание);
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецПопытки;
	
	ОписаниеОшибки = "";	
	АдресВоВременномХранилище = ПоместитьВоВременноеХранилище(Новый ДвоичныеДанные(АрхивМодуля)); // переходим на сервер	
	УдалитьФайлы(АрхивМодуля);
	#Иначе
	// Веб-клиент. Получение файла на сервере
	ОписаниеОшибки = "";	
	АдресВоВременномХранилище = "";
	ПолучитьФайлСРесурсаНаСервере(СсылкаМодуль, АдресВоВременномХранилище);	
	#КонецЕсли
	
	РезультатРегистрации = ПрименитьСвойстваМодуляДокументооборота(АдресВоВременномХранилище, СвойстваМодуляДляТекущегоРелиза, ОписаниеОшибки);		
	
	Если ЗначениеЗаполнено(ИдентификаторСессии) Тогда		
		ОтправитьПодтверждениеОЗавершенииСкачивания(ИдентификаторСессии);
	КонецЕсли;
	
	Если НЕ РезультатРегистрации Тогда
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НСтр("ru='%1%2.'"),
																				ШапкаОшибки + Символы.ПС + Символы.ПС,
																				ОписаниеОшибки);
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		Возврат Ложь;
	КонецЕсли;     	
	// сообщаем
	ТекстСообщения = НСтр("ru = 'Новая версия криптографической библиотеки зарегистрирована в информационной базе.'");
	ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
	
	Возврат Истина;	
КонецФункции

////////////////////////////////////////////////////////////////////////////////////////////
// Прочие функции

&НаКлиентеНаСервереБезКонтекста
Функция УсловиеВыполняется(Условие, ИДПеременной, ЗначениеПеременной)
	
	РезультатВычисления = Неопределено;
	Попытка
		РезультатВычисления = Вычислить(СтрЗаменить(Условие, ИДПеременной, ЗначениеПеременной));
	Исключение
		Возврат Ложь;
	КонецПопытки;
	
	Если ТипЗнч(РезультатВычисления) <> Тип("Булево") Тогда
		Возврат Ложь;
	Иначе
		Возврат РезультатВычисления;
	КонецЕсли;
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ИДТекущейКонфигурацииСоответствуетШаблону(ИДТекущейКонфигурации, ИДКонфигурации)
	
	Возврат ВРЕГ(СокрЛП(ИДТекущейКонфигурации)) = ВРЕГ(СокрЛП(ИДКонфигурации));
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ЧастьВерсииСоответствуетЧастиШаблонаВерсии(Знач ЧастьТекущейВерсии, Знач ЧастьШаблона)
	
	ЧастьТекущейВерсии = СокрЛП(ЧастьТекущейВерсии);
	ЧастьШаблона = СокрЛП(ЧастьШаблона);
	
	Если Лев(ЧастьШаблона, 1) <> "[" ИЛИ Прав(ЧастьШаблона, 1) <> "]" Тогда
		Возврат (ЧастьШаблона = ЧастьТекущейВерсии);
	КонецЕсли;
	
	ЧастьШаблона = СокрЛП(Сред(ЧастьШаблона, 2, СтрДлина(ЧастьШаблона) - 2));
	СоставляющиеЧастиШаблона = ЭСФКлиентСервер.РазобратьСтрокуВМассивПоРазделителю(ЧастьШаблона, ",");
	Если СоставляющиеЧастиШаблона.Количество() = 0 Тогда
		Возврат Истина;
	КонецЕсли;
	
	ИДПеременной = "X";
	Для Каждого Составляющее Из СоставляющиеЧастиШаблона Цикл
		Если ЗначениеЗаполнено(Составляющее) Тогда
			Если НЕ УсловиеВыполняется(Составляющее, ИДПеременной, ЧастьТекущейВерсии) Тогда
				Возврат Ложь;
			КонецЕсли;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ТекущаяКонфигурацияСоответствуетУзлу(УзелКонфигурация, ИнформацияОПрограмме)
	
	РезультатПоискаАтрибута = УзелКонфигурация.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "ID", "А"));
	Если РезультатПоискаАтрибута.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		ИДКонфигурацииУзла = СокрЛП(РезультатПоискаАтрибута[0].Значение);
		Если ИДТекущейКонфигурацииСоответствуетШаблону(ИнформацияОПрограмме.ИмяМетаданныхКонфигурации, ИДКонфигурацииУзла)
			ИЛИ ИДТекущейКонфигурацииСоответствуетШаблону(ИнформацияОПрограмме.ИДКонфигурации, ИДКонфигурацииУзла) 
			ИЛИ ИДТекущейКонфигурацииСоответствуетШаблону(ИнформацияОПрограмме.ИДКонфигурацииБЭСФ, ИДКонфигурацииУзла) Тогда
			
			Возврат Истина;
		Иначе
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ВерсияТекущейПлатформыСоответствуетШаблону(ВерсияТекущейПлатформы, ШаблонВерсии)
	
	МассивЧастейШаблона = ЭСФКлиентСервер.РазобратьСтрокуВМассивПоРазделителю(ШаблонВерсии);
	МассивЧастейТекущейВерсии = ЭСФКлиентСервер.РазобратьСтрокуВМассивПоРазделителю(ВерсияТекущейПлатформы);
	
	Для Инд = 0 По МассивЧастейШаблона.Количество() - 1 Цикл
		Если МассивЧастейТекущейВерсии.Количество() <= Инд Тогда
			Прервать;
		КонецЕсли;
		ТекущаяЧастьШаблона = МассивЧастейШаблона[Инд];
		ТекущаяЧастьТекущейВерсии = МассивЧастейТекущейВерсии[Инд];
		Если НЕ ЧастьВерсииСоответствуетЧастиШаблонаВерсии(ТекущаяЧастьТекущейВерсии, ТекущаяЧастьШаблона) Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЦикла;
	
	Возврат Истина;	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ТекущаяПлатформаСоответствуетУзлу(УзелПлатформа, ВерсияТекущейПлатформы)
	
	РезультатПоискаАтрибута = УзелПлатформа.Строки.НайтиСтроки(Новый Структура("Имя, Тип", "version", "А"));
	Если РезультатПоискаАтрибута.Количество() = 0 Тогда
		Возврат Ложь;
	Иначе
		ШаблонВерсии = СокрЛП(РезультатПоискаАтрибута[0].Значение);
		Возврат ВерсияТекущейПлатформыСоответствуетШаблону(ВерсияТекущейПлатформы, ШаблонВерсии);
	КонецЕсли;
	
КонецФункции

#Если НЕ ТолстыйКлиентОбычноеПриложение Тогда
&НаСервере
#КонецЕсли
Функция ИнформацияОПрограмме()
	
	Результат = Новый Структура();
	Результат.Вставить("ВерсияПлатформы",			ЭСФКлиентСервер.ПолучитьВерсиюТекущейПлатформы());
	Результат.Вставить("ИмяМетаданныхКонфигурации",	СокрЛП(Метаданные.Имя));
	Результат.Вставить("ИДКонфигурации",			ЭСФСерверПереопределяемый.ИДКонфигурации());
	Результат.Вставить("ВерсияКонфигурации",		СокрЛП(Метаданные.Версия));
	Результат.Вставить("ИДКонфигурацииБЭСФ",		"БиблиотекаЭлектронныхСчетовФактур");	
	Результат.Вставить("ВерсияКонфигурацииБЭСФ",	СокрЛП(ВерсияБЭСФ));
	
	Возврат Результат;	
КонецФункции

#КонецОбласти

#КонецОбласти

#Область ВСКлиент

&НаКлиенте
Процедура ОтправитьИсходящиеЭДВС(Знач МассивЭД, Знач ДополнительныеПараметры) Экспорт
	Контейнер = ЭСФКлиентСервер.КонтейнерМетодов();	
	Если НЕ Контейнер.КриптопровайдерПодключается() Тогда
		Возврат;
	КонецЕсли;
	
	НовыйМассивЭД = ВСВызовСервера.ПроверитьНаличиеТоваровВиртуальногоСклада(МассивЭД, ДополнительныеПараметры);
	Если НовыйМассивЭД.Количество()=0 Тогда
		Возврат;
	Иначе
		
		Если НЕ ВСВызовСервера.ПроверитьВозможностьОтправкиДокументовЭДВС(НовыйМассивЭД, ДополнительныеПараметры) Тогда
			Возврат;
		КонецЕсли;
		
		ДополнительныеПараметры.Вставить("МассивИсходящихЭД", НовыйМассивЭД);
		ОтправитьИсходящиеЭДПослеВопросаУстановкиДаты = Новый ОписаниеОповещения("ОтправитьИсходящиеЭДПослеВопросаУстановкиДаты", ЭтаФорма, ДополнительныеПараметры);
		ВСКлиент.УстановитьТекущуюДатуДляЭДВСВопрос(НовыйМассивЭД, ЭСФКлиентСервер.ДействиеОтправка(), ОтправитьИсходящиеЭДПослеВопросаУстановкиДаты);
		//ОтправитьИсходящиеЭДПослеВопросаУстановкиДаты(Новый Структура, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьИсходящиеЭДПослеВопросаУстановкиДаты(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		
		//// если действие выполняется при закрытии открытии ИБ, очередь не должна очиститься в случае отмены исправления даты
		//Если НЕ ДополнительныеПараметры.Свойство("НеПерезаполнятьОчередьОтправки") Тогда
		//	ЭСФВызовСервера.ОчиститьОчередьОтправкиЭСФ(ДополнительныеПараметры.МассивИсходящихЭД);
		//КонецЕсли;

		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), ДополнительныеПараметры.МассивИсходящихЭД);
		
		Возврат;

	КонецЕсли;
		
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВСВызовСервера.УстановитьТекущуюДатуДляЭДВС(ДополнительныеПараметры.МассивИсходящихЭД, ДополнительныеПараметры.ТекущаяДата);
		ОтправитьИсходящиеЭДВыборПараметровИСЭСФ(Результат, ДополнительныеПараметры);
	Иначе
		ОтправитьИсходящиеЭДВыборПараметровИСЭСФ(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьИсходящиеЭДВыборПараметровИСЭСФ(Результат, ДополнительныеПараметры) Экспорт
	
	МассивИсходящихЭД = ДополнительныеПараметры.МассивИсходящихЭД;
	
	// Изменили статус документа, нужно перечитать, если отправка происходит из формы
	ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС());
	
	КоллекцияСгруппированныхЭД = ВСВызовСервера.СгруппироватьЭДВСПоСтруктурнымЕдиницам(МассивИсходящихЭД);
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхЭД", КоллекцияСгруппированныхЭД);
	
	МассивИмпортныхЭД = ВСВызовСервера.ПолучитьЭДВСПоИмпорту(МассивИсходящихЭД);
	КоллекцияСгруппированныхЭДИмпорт = ВСВызовСервера.СгруппироватьЭДВСПоСтруктурнымЕдиницам(МассивИмпортныхЭД);
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхЭДИмпорт", КоллекцияСгруппированныхЭДИмпорт);

	ОтправитьИсходящиеЭДЗавершение = Новый ОписаниеОповещения("ОтправитьИсходящиеЭДЗавершение", ЭтаФорма, ДополнительныеПараметры);

	ЭСФКлиент.ОпределитьНеобходимостьВводаДанныхИСЭСФ(КоллекцияСгруппированныхЭД, ОтправитьИсходящиеЭДЗавершение, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
	
КонецПроцедуры

&НаКлиенте
Процедура ОтправитьИсходящиеЭДЗавершение(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт
	
	МассивИсходящихЭД = ДополнительныеПараметры.МассивИсходящихЭД;
	
	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), МассивИсходящихЭД);
		
		Возврат;
		
	КонецЕсли;
	
	ПараметрыВС = ВСВызовСервераПовтИсп.ПолучитьПараметрыВС();
	ВерсияВС = ПараметрыВС.ВерсияВС;

	ЗапускатьФоновоеЗадание = ДополнительныеПараметры.ЗапускатьФоновоеЗадание;
	КоллекцияСгруппированныхЭД = ДополнительныеПараметры.КоллекцияСгруппированныхЭД;
	
	КоллекцияПодписейЭД = Новый Соответствие;
	КоллекцияАдресКоллекцииUttnXML = Новый Соответствие;
	КоллекцияСоответствиеЭД = Новый Соответствие;
	
	//+
	//Если ВСВызовСервера.ВедетсяУчетПоИдентификаторамТоваровВС() Тогда
	//	//Запрос ИД товаров для импортных документов
	//	КоллекцияСгруппированныхЭДИмпорт = ДополнительныеПараметры.КоллекцияСгруппированныхЭДИмпорт;
	//	
	//	// присвоение ИД импортным документам выполняется не в фоне, т.к. к моменту отправки ИД должны быть уже присвоены
	//	// а фоновое задание в силу своей параллельности может привести к тому, что когда будет выполняться фоновое задание на отправку (вызов ниже)
	//	// ФЗ по получению ИД еще не будет завершено.
	//	ВСВызовСервера.ПолучитьИдентификаторыТоваровУТТНизВС(КоллекцияСгруппированныхЭДИмпорт, ДанныеПрофилейИСЭСФ);
	//КонецЕсли;	
	//-
	
	Попытка
	
		Если ЭСФВызовСервераПовтИсп.ВыполнятьКриптографическиеОперацииНаКлиенте() Тогда
			
			НастройкиПодписиКомпоненты = ЭСФВызовСервера.ПолучитьПараметрыИспользованияКомпоненты();
			ИспользоватьПодписьНовойКомпоненты = НастройкиПодписиКомпоненты.ИспользоватьПодписьНовойКомпоненты;
			ИспользоватьВнешнююКриптографиюДляКомпоненты = НастройкиПодписиКомпоненты.ИспользоватьВнешнююКриптографиюДляКомпоненты;
			
			Для Каждого СгруппированныеЭД Из КоллекцияСгруппированныхЭД Цикл
				
				СтруктурнаяЕдиница = СгруппированныеЭД.Ключ;
				МассивЭД = СгруппированныеЭД.Значение;
				
				ДанныеКлючаЭЦП = ДанныеПрофилейИСЭСФ.Получить(СтруктурнаяЕдиница);
				
				ДанныеПрофиляИСЭСФ = ЭСФВызовСервера.ДанныеПрофиляИСЭСФ(ДанныеКлючаЭЦП.ПрофильИСЭСФ);
				
				Если НЕ ЗначениеЗаполнено(ДанныеПрофиляИСЭСФ) Тогда
					Возврат;	
				КонецЕсли;
				
				Если ИспользоватьПодписьНовойКомпоненты И НЕ ИспользоватьВнешнююКриптографиюДляКомпоненты Тогда
					ТипПодписиЭСФ = ПредопределенноеЗначение("Перечисление.ТипыПодписиЭСФ.ПустаяСсылка");
				Иначе
					ТипПодписиЭСФ = ЭСФКлиентСервер.ТипПодписиЭСФ(ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ);
				КонецЕсли;
				
				АдресКоллекцииUttnXML = Неопределено;
				КоллекцияSignedContentXML = Неопределено;
				ВСВызовСервера.СоздатьИсходящиеUForm(МассивЭД, Истина, ТипПодписиЭСФ, АдресКоллекцииUttnXML, КоллекцияSignedContentXML, ВерсияВС);
				
				Если ИспользоватьПодписьНовойКомпоненты Тогда
					КоллекцияПодписейЭД2 = ЭСФКлиент.НоваяКоллекцияПодписей(КоллекцияSignedContentXML, ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ);
					КоллекцияПодписейЭД.Вставить(СтруктурнаяЕдиница, КоллекцияПодписейЭД2);
					
					Если КоллекцияПодписейЭД2.Количество() = 0 Тогда
						ТекстОшибки = НСтр("ru='Не удалось выполнить подпись документов.'");
						ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
						ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), МассивИсходящихЭД);
						Возврат;
					КонецЕсли;

					Если ДанныеПрофиляИСЭСФ.Свойство("ОткрытыйСертификатBase64") Тогда
						ДанныеКлючаЭЦП.Вставить("ОткрытыйСертификатBase64", ДанныеПрофиляИСЭСФ.ОткрытыйСертификатBase64);
					КонецЕсли;
					Если ДанныеПрофиляИСЭСФ.Свойство("ТипКлюча") Тогда
						ТипПодписиЭСФ = ЭСФКлиентСервер.ТипПодписиЭСФ(ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ);
						ДанныеКлючаЭЦП.Вставить("ТипПодписи", ТипПодписиЭСФ);
					КонецЕсли;
				Иначе
					КоллекцияПодписейЭД.Вставить(СтруктурнаяЕдиница, ВСКлиентСервер.НоваяКоллекцияПодписейЭД(КоллекцияSignedContentXML, ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ));
				КонецЕсли;
				
				КоллекцияСоответствиеЭД.Вставить(СтруктурнаяЕдиница, ПолучитьИзВременногоХранилища(АдресКоллекцииUttnXML));
				
				КоллекцияАдресКоллекцииUttnXML.Вставить(СтруктурнаяЕдиница, АдресКоллекцииUttnXML);
				
			КонецЦикла;
			
		КонецЕсли;
		
	Исключение
		
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), МассивИсходящихЭД);
		
		ВызватьИсключение ОписаниеОшибки();
		
	КонецПопытки;
	// До перехода на сервер нужно получить ИД сессии на клиенте! При этом учеть получение для каждого профиля
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	// Создать и отправить коллекцию Invoice.
	Если ЭСФВызовСервераПовтИсп.ВыполнятьКриптографическиеОперацииНаКлиенте() Тогда
				
		// Если отправляем из формы списка, делаем фоновую отправку
		Если ЗапускатьФоновоеЗадание Тогда
			
			КлючФоновогоЗадания = Новый УникальныйИдентификатор;
			
			ПараметрыЗадания = Новый Структура("ВерсияВС, КоллекцияСоответствиеЭД, КоллекцияПодписейЭД, ДанныеПрофилейИСЭСФ", ВерсияВС, КоллекцияСоответствиеЭД, КоллекцияПодписейЭД, ДанныеПрофилейИСЭСФ);
			ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне();
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Отправка ЭДВС'");
			ПараметрыВыполнения.КлючФоновогоЗадания = КлючФоновогоЗадания;
			
			Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ОтправитьИсходящиеUTTNВФоне", ПараметрыЗадания, ПараметрыВыполнения);
			
		Иначе
			
			Попытка
				
				Результат = ВСВызовСервера.ОтправитьИсходящиеUTTN(
					ВерсияВС,
					КоллекцияАдресКоллекцииUttnXML, 
					КоллекцияПодписейЭД, 
					ДанныеПрофилейИСЭСФ
					);
					
			Исключение
				
				// Обновим данные открытых форм, т.к. до вызова исключения часть ЭДВС может быть отправлена и обновлена в ИБ
				ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), МассивИсходящихЭД);
				
				ВызватьИсключение ОписаниеОшибки();
				
			КонецПопытки;
			
		КонецЕсли;
		
		Для Каждого СтрокаАдресКоллекцииUttnXML Из КоллекцияАдресКоллекцииUttnXML Цикл
			
			АдресКоллекцииUttnXML = СтрокаАдресКоллекцииUttnXML.Значение;
			
			// Принудительное удаление, иначе значение не удалится.
			УдалитьИзВременногоХранилища(АдресКоллекцииUttnXML);
			
		КонецЦикла;

	Иначе 
		
		Результат = ВСВызовСервера.СоздатьИОтправитьКоллекциюUForm(КоллекцияСгруппированныхЭД, ДанныеПрофилейИСЭСФ, ДополнительныеПараметры, ВерсияВС);
		
	КонецЕсли;

	// Если отправляем из формы списка, делаем фоновую отправку
	Если ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Отправка документов в ИС ЭСФ'"));
		КонецЕсли;
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), МассивИсходящихЭД);
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ВыгрузитьИсходящиеЭДВС(Знач МассивИсходящихЭДВС) Экспорт
		
	Если МассивИсходящихЭДВС.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ПроверкиВозможностиВыгрузкиФайла() Тогда
		Возврат;
	КонецЕсли;
	
	ВыгрузитьИсходящиеЭДВСПослеВопросаУстановкиДаты = Новый ОписаниеОповещения("ВыгрузитьИсходящиеЭДВСПослеВопросаУстановкиДаты", ЭтаФорма, Новый Структура("МассивИсходящихЭДВС", МассивИсходящихЭДВС));
	ВСКлиент.УстановитьТекущуюДатуДляЭДВСВопрос(МассивИсходящихЭДВС, ЭСФКлиентСервер.ДействиеВыгрузка(), ВыгрузитьИсходящиеЭДВСПослеВопросаУстановкиДаты);
	
КонецПроцедуры

&НаСервере
Функция ПроверкиВозможностиВыгрузкиФайла()
	
	Возврат ЭСФСерверПереопределяемый.ПроверкаПоддержкиМеханизмаВАвтономномРабочемМесте();
		
КонецФункции

&НаКлиенте
Процедура ВыгрузитьИсходящиеЭДВСПослеВопросаУстановкиДаты(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда
		Возврат;	
	КонецЕсли;
		
	Если Результат = КодВозвратаДиалога.Да Тогда
		ВСВызовСервера.УстановитьТекущуюДатуДляЭДВС(ДополнительныеПараметры.МассивИсходящихЭДВС, ДополнительныеПараметры.ТекущаяДата);
	КонецЕсли;
	
	XML = ВСВызовСервера.СоздатьXMLДляИмпортаВВС(ДополнительныеПараметры.МассивИсходящихЭДВС);
	
	Если НЕ ПустаяСтрока(XML) Тогда
		ВСКлиент.СохранитьXMLДляИмпортаВИСЭСФ(XML);
	КонецЕсли;
	
	// Изменили статус документа, нужно перечитать, если отправка происходит из формы
	ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС());

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусыЭДВСПоИдентификаторам(Знач МассивЭДВС, Знач ЗапускатьФоновоеЗадание) Экспорт
		
	КоллекцияСгруппированныхЭДВС = ВСВызовСервера.СгруппироватьЭДВСПоСтруктурнымЕдиницам(МассивЭДВС);		
	
	МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхЭДВС);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхЭДВС", КоллекцияСгруппированныхЭДВС);
	ДополнительныеПараметры.Вставить("ЗапускатьФоновоеЗадание", ЗапускатьФоновоеЗадание);
	
	ОбновитьСтатусыЭДВСПоИдентификаторамВыборДанныхПрофилей = Новый ОписаниеОповещения("ОбновитьСтатусыЭДВСПоИдентификаторамВыборДанныхПрофилей", ЭтаФорма, ДополнительныеПараметры);
	
	Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
		ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ТребуетсяВыборСертификатаВхода", КоллекцияСгруппированныхЭДВС, Истина);
		ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьСтатусыЭДВСПоИдентификаторамВыборДанныхПрофилей, ПараметрыФормы);
		
	Иначе
		
		Если МассивПрофилейИСЭСФ = Неопределено Тогда
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхЭДВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьСтатусыЭДВСПоИдентификаторамВыборДанныхПрофилей, ПараметрыФормы);	
		Иначе
			ОбновитьСтатусыЭДВСПоИдентификаторамВыборДанныхПрофилей(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусыЭДВСПоИдентификаторамВыборДанныхПрофилей(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт

	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		
		
 		ПараметрыЗадания = Новый Структура("КоллекцияСгруппированныхЭДВС, ДанныеПрофилейИСЭСФ", ДополнительныеПараметры.КоллекцияСгруппированныхЭДВС, ДанныеПрофилейИСЭСФ);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне(); //ЭтаФорма
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление статусов ЭСФ'");
		
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ОбновитьСтатусыЭДВСПоИдентификаторамВФоне", ПараметрыЗадания, ПараметрыВыполнения);
		
	Иначе
	
		ВСВызовСервера.ОбновитьСтатусыЭДВСПоИдентификаторам(ДополнительныеПараметры.КоллекцияСгруппированныхЭДВС, ДанныеПрофилейИСЭСФ);
			
	КонецЕсли;

	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Обновление документов из ИС ВС'"));
		КонецЕсли;
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат); //, ДополнительныеПараметры.КлючФормы
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), ДополнительныеПараметры.КоллекцияСгруппированныхЭДВС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОшибкиЭДВСПоИдентификаторам(Знач МассивЭДВС, Знач ЗапускатьФоновоеЗадание, Знач ОбновитьДокументыВС) Экспорт
		
	КоллекцияСгруппированныхЭДВС = ВСВызовСервера.СгруппироватьЭДВСПоСтруктурнымЕдиницам(МассивЭДВС);		
	
	МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхЭДВС);

	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхЭДВС", КоллекцияСгруппированныхЭДВС);
	ДополнительныеПараметры.Вставить("ЗапускатьФоновоеЗадание", ЗапускатьФоновоеЗадание);
	ДополнительныеПараметры.Вставить("ОбновитьДокументыВС", ОбновитьДокументыВС);
	ДополнительныеПараметры.Вставить("МассивЭДВС", МассивЭДВС);
	
	ПолучитьОшибкиЭДВСПоИдентификаторамВыборДанныхПрофилей = Новый ОписаниеОповещения("ПолучитьОшибкиЭДВСПоИдентификаторамВыборДанныхПрофилей", ЭтаФорма, ДополнительныеПараметры);
	
	Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
		ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ТребуетсяВыборСертификатаВхода", КоллекцияСгруппированныхЭДВС, Истина);
		ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ПолучитьОшибкиЭДВСПоИдентификаторамВыборДанныхПрофилей, ПараметрыФормы);
		
	Иначе
		
		Если МассивПрофилейИСЭСФ = Неопределено Тогда
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхЭДВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ПолучитьОшибкиЭДВСПоИдентификаторамВыборДанныхПрофилей, ПараметрыФормы);	
		Иначе
			ПолучитьОшибкиЭДВСПоИдентификаторамВыборДанныхПрофилей(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьОшибкиЭДВСПоИдентификаторамВыборДанныхПрофилей(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт

	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		
		
 		ПараметрыЗадания = Новый Структура("КоллекцияСгруппированныхЭДВС, ДанныеПрофилейИСЭСФ", ДополнительныеПараметры.КоллекцияСгруппированныхЭДВС, ДанныеПрофилейИСЭСФ);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне(); //ЭтаФорма
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Получение ошибок документов ВС'");
		
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ПолучитьОшибкиЭДВСПоИдентификаторамВФоне", ПараметрыЗадания, ПараметрыВыполнения);
		
	Иначе
	
		ВСВызовСервера.ПолучитьОшибкиЭДВСПоИдентификаторам(ДополнительныеПараметры.КоллекцияСгруппированныхЭДВС, ДанныеПрофилейИСЭСФ, ДополнительныеПараметры.ОбновитьДокументыВС);
			
	КонецЕсли;

	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Получение ошибок документов из ИС ВС'"));
		КонецЕсли;
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат); //, ДополнительныеПараметры.КлючФормы
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), ДополнительныеПараметры.КоллекцияСгруппированныхЭДВС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДокументыУТТНИзВС(Знач МассивЭДВС, Знач ДополнительныеПараметры) Экспорт
	
	КоллекцияСгруппированныхЭДВС = ВСВызовСервера.СгруппироватьЭДВСПоСтруктурнымЕдиницам(МассивЭДВС);		
	
	МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхЭДВС);
	
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхУТТН", КоллекцияСгруппированныхЭДВС);

	ОбновитьДокументыВыборДанныхПрофилейИСЭСФ = Новый ОписаниеОповещения("ОбновитьДокументыВыборДанныхПрофилейВС", ЭтаФорма, ДополнительныеПараметры);

	Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
		ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ТребуетсяВыборСертификатаВхода", КоллекцияСгруппированныхЭДВС, Истина);
		ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьДокументыВыборДанныхПрофилейИСЭСФ, ПараметрыФормы);
		
	Иначе
		
		Если МассивПрофилейИСЭСФ = Неопределено Тогда
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхЭДВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьДокументыВыборДанныхПрофилейИСЭСФ, ПараметрыФормы);	
		Иначе
			ОбновитьДокументыВыборДанныхПрофилейВС(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьДокументыВыборДанныхПрофилейВС(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт

	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	// До перехода на сервер нужно получить ИД сессии на клиенте! При этом учеть получение для каждого профиля
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		
		ПараметрыЗадания = Новый Структура("КоллекцияСгруппированныхУТТН, ДанныеПрофилейИСЭСФ", ДополнительныеПараметры.КоллекцияСгруппированныхУТТН, ДанныеПрофилейИСЭСФ);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне();
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление статусов ЭДВС'");
		
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ОбновитьДокументыУТТНИзВСВФоне", ПараметрыЗадания, ПараметрыВыполнения);
		
	Иначе
							
		ВСВызовСервера.ОбновитьДокументыУТТНИзВС(ДополнительныеПараметры.КоллекцияСгруппированныхУТТН, ДанныеПрофилейИСЭСФ);
			
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Обновление документов из ИС ЭСФ'"));
		КонецЕсли;
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), ДополнительныеПараметры.КоллекцияСгруппированныхУТТН);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторыТоваровУТТНИзВС(Знач МассивЭДВС, Знач ДополнительныеПараметры) Экспорт
	
	КоллекцияСгруппированныхЭДВС = ВСВызовСервера.СгруппироватьЭДВСПоСтруктурнымЕдиницам(МассивЭДВС, Истина);
	
	МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхЭДВС);
	
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхДокументов", КоллекцияСгруппированныхЭДВС);
	ДополнительныеПараметры.Вставить("ТипДокумента", "ЭДВС");
	
	ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ = Новый ОписаниеОповещения("ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ", ЭтаФорма, ДополнительныеПараметры);

	Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
		ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ТребуетсяВыборСертификатаВхода", КоллекцияСгруппированныхЭДВС, Истина);
		ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ, ПараметрыФормы);
		
	Иначе
		
		Если МассивПрофилейИСЭСФ = Неопределено Тогда
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхЭДВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ, ПараметрыФормы);
		Иначе
			ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторыТоваровЭСФИзВС(Знач МассивЭСФ, Знач ДополнительныеПараметры) Экспорт
	
	КоллекцияСгруппированныхЭСФ = ЭСФВызовСервера.СгруппироватьЭСФПоСтруктурнымЕдиницам(МассивЭСФ);		
	
	МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхЭСФ);
	
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхДокументов", КоллекцияСгруппированныхЭСФ);
	ДополнительныеПараметры.Вставить("ТипДокумента", "ЭСФ");
	
	ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ = Новый ОписаниеОповещения("ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ", ЭтаФорма, ДополнительныеПараметры);
	
	Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
		ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ТребуетсяВыборСертификатаВхода", КоллекцияСгруппированныхЭСФ, Истина);
		ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ, ПараметрыФормы);
		
	Иначе
		
		Если МассивПрофилейИСЭСФ = Неопределено Тогда
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхЭСФ, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ, ПараметрыФормы);	
		Иначе
			ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПолучитьИдентификаторыТоваровПослеВыбораПрофилейИСЭСФ(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт

	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;

	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		
		Если ДополнительныеПараметры.Свойство("ПолучитьВсеИдентификаторы") Тогда
			ПолучитьВсеИдентификаторы = ДополнительныеПараметры.ПолучитьВсеИдентификаторы;
		Иначе
			ПолучитьВсеИдентификаторы = Ложь;
		КонецЕсли;
		
		ПараметрыЗадания = Новый Структура("КоллекцияСгруппированныхДокументов, ДанныеПрофилейИСЭСФ, ПолучитьВсеИдентификаторы", ДополнительныеПараметры.КоллекцияСгруппированныхДокументов, ДанныеПрофилейИСЭСФ, ПолучитьВсеИдентификаторы);

		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне();
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление идентификаторов товаров.'");

		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ПолучитьИдентификаторыТоваровУТТНизВСВФоне", ПараметрыЗадания, ПараметрыВыполнения);

	Иначе

		ВСВызовСервера.ПолучитьИдентификаторыТоваровУТТНизВС(ДополнительныеПараметры.КоллекцияСгруппированныхДокументов, ДанныеПрофилейИСЭСФ, ДополнительныеПараметры);

	КонецЕсли;

	Если ДополнительныеПараметры.Свойство("ТипДокумента") И ДополнительныеПараметры.ТипДокумента = "ЭДВС" Тогда
		ТипДокумента = ДополнительныеПараметры.ТипДокумента;
	ИначеЕсли ДополнительныеПараметры.Свойство("ТипДокумента") И ДополнительныеПараметры.ТипДокумента = "ЭСФ" Тогда
		ТипДокумента = ДополнительныеПараметры.ТипДокумента;
	ИначеЕсли ДополнительныеПараметры.Свойство("ТипДокумента") И ДополнительныеПараметры.ТипДокумента = "СНТ" Тогда
		ТипДокумента = ДополнительныеПараметры.ТипДокумента;
	Иначе
		ТипДокумента = "";
	КонецЕсли;

	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Обновление идентификаторов товаров из ИС ЭСФ'"));
		КонецЕсли;

		Если ТипДокумента = "ЭДВС" Тогда
			ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
		ИначеЕсли ТипДокумента = "ЭСФ" Тогда
			ЭСФКлиент.ОповеститьФормы("ЭСФ_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
		ИначеЕсли ТипДокумента = "СНТ" Тогда
			ЭСФКлиент.ОповеститьФормы("СНТ_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
		КонецЕсли;
	Иначе
		Если ТипДокумента = "ЭДВС" Тогда
			ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), ДополнительныеПараметры.КоллекцияСгруппированныхДокументов);
		ИначеЕсли ТипДокумента = "ЭСФ" Тогда
			ЭСФКлиент.ОповеститьФормы(ЭСФКлиентСервер.ИмяСобытияЗаписьЭСФ(), ДополнительныеПараметры.КоллекцияСгруппированныхДокументов);
		ИначеЕсли ТипДокумента = "СНТ" Тогда
			ЭСФКлиент.ОповеститьФормы(ВСКлиентСерверПереопределяемый.ИмяСобытияЗаписьСНТ(), ДополнительныеПараметры.КоллекцияСгруппированныхДокументов);
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОткрытьФормуИзменениеСтатусовЭДВС(Знач Действие, Знач МассивЭД, Знач ЗапускатьФоновоеЗадание) Экспорт
	
	ПараметрыФормы = ВСКлиентСервер.ПустыеПараметрыФормыИзменениеСтатусовЭДВС();
	ПараметрыФормы.Действие = Действие;
	ПараметрыФормы.МассивЭД = МассивЭД;
	ПараметрыФормы.ЗапускатьФоновоеЗадание = ЗапускатьФоновоеЗадание;
	
	ОткрытьФорму("Обработка.ОбменЭСФ.Форма.ИзменениеСтатусовЭДВС", ПараметрыФормы);
	
КонецПроцедуры

&НаКлиенте
Функция ИзменитьСтатусыЭДВС(Знач Действие, Знач КоллецияДляИзмененияСтатусов, Знач ДанныеПрофиляИСЭСФ, Знач ДанныеКлючаЭЦП, Знач УникальныйИдентификаторФормыВладельца, Знач ЗапускатьФоновоеЗадание) Экспорт	
	
	КоллекцияПодписейЭД = Новый Соответствие;
	
	Если ЭСФВызовСервераПовтИсп.ВыполнятьКриптографическиеОперацииНаКлиенте() Тогда
		
		НастройкиПодписиКомпоненты = ЭСФВызовСервера.ПолучитьПараметрыИспользованияКомпоненты();
		ИспользоватьПодписьНовойКомпоненты = НастройкиПодписиКомпоненты.ИспользоватьПодписьНовойКомпоненты;
		ИспользоватьВнешнююКриптографиюДляКомпоненты = НастройкиПодписиКомпоненты.ИспользоватьВнешнююКриптографиюДляКомпоненты;
		
		Если ИспользоватьПодписьНовойКомпоненты И НЕ ИспользоватьВнешнююКриптографиюДляКомпоненты Тогда
			ДанныеКлючаЭЦП.Вставить("ОткрытыйСертификатBase64", "");
		КонецЕсли;
		
		Результат = ВСВызовСервера.СоздатьЗапросНаИзменениеСтатусовВС(Действие, КоллецияДляИзмененияСтатусов, ДанныеКлючаЭЦП.ОткрытыйСертификатBase64);
		
		Если ИспользоватьПодписьНовойКомпоненты Тогда
			
			СтрокаЗапросаВМассив = Новый Массив;
			СтрокаЗапросаВМассив.Добавить(Результат.СтрокаДляПодписи);
			
			Если ИспользоватьВнешнююКриптографиюДляКомпоненты Тогда
				КоллекцияПодписейЭД = ЭСФКлиентСервер.НоваяКоллекцияПодписейЭСФ(СтрокаЗапросаВМассив, ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ);
			Иначе
				КоллекцияПодписейЭД = ЭСФКлиент.НоваяКоллекцияПодписей(СтрокаЗапросаВМассив, ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ);
			КонецЕсли;
			
			Если КоллекцияПодписейЭД.Количество() = 0 Тогда
				ТекстОшибки = НСтр("ru='Не удалось выполнить подпись документов.'");
				ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстОшибки);
				ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьЭДВС(), КоллецияДляИзмененияСтатусов);
				Возврат Неопределено;
			КонецЕсли;

			Если ДанныеПрофиляИСЭСФ.Свойство("ОткрытыйСертификатBase64") Тогда
				Сертификат = ДанныеПрофиляИСЭСФ.ОткрытыйСертификатBase64;
				ДанныеКлючаЭЦП.Вставить("ОткрытыйСертификатBase64", Сертификат);
			КонецЕсли;
			
			ПодписьЗапроса = КоллекцияПодписейЭД.Получить(Результат.СтрокаДляПодписи);
			
		Иначе
			
			ПодписьЗапроса = СоздатьЭЦП(
			Результат.СтрокаДляПодписи, 
			ДанныеКлючаЭЦП.КлючBase64,
			ДанныеКлючаЭЦП.Пароль);
			
		КонецЕсли;
		
		ТекстЗапроса = Результат.ТекстЗапроса; 
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "[signature]", ПодписьЗапроса);
		
		Если ИспользоватьПодписьНовойКомпоненты И НЕ ИспользоватьВнешнююКриптографиюДляКомпоненты Тогда
			// Добавляем сертификат
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "<x509Certificate></x509Certificate>", "<x509Certificate>"+Сертификат+"</x509Certificate>");
		КонецЕсли;
		
		Если ЗапускатьФоновоеЗадание Тогда
			
			ПараметрыЗадания = Новый Структура("Действие, ТекстЗапроса, ДанныеПрофиляИСЭСФ", Действие, ТекстЗапроса, ДанныеПрофиляИСЭСФ);
			
			ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне(УникальныйИдентификаторФормыВладельца);
			НаименованиеЗадания = НСтр("ru = '%1 документов ВС'");
			НаименованиеЗадания = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НаименованиеЗадания, Строка(Действие));
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
			
			Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ВыполнитьЗапросНаИзменениеСтатусовВФоне", ПараметрыЗадания, ПараметрыВыполнения);
			
		Иначе
			
			Результат = ВСВызовСервера.ВыполнитьЗапросНаИзменениеСтатусовВС(Действие, ТекстЗапроса, ДанныеПрофиляИСЭСФ);
			
		КонецЕсли;
		
	Иначе 
		
		Если ЗапускатьФоновоеЗадание Тогда
			
			ПараметрыЗадания = Новый Структура("Действие, КоллецияДляИзмененияСтатусов, ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ", Действие, КоллецияДляИзмененияСтатусов, ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ);
			ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне(УникальныйИдентификаторФормыВладельца);
			
			НаименованиеЗадания = НСтр("ru = '%1 документов ВС'");
			НаименованиеЗадания = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(НаименованиеЗадания, Строка(Действие));
			ПараметрыВыполнения.НаименованиеФоновогоЗадания = НаименованиеЗадания;
			
			Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ИзменитьСтатусыЭДВСВФоне", ПараметрыЗадания, ПараметрыВыполнения);
			
		Иначе
			
			Результат = ВСВызовСервера.ИзменитьСтатусыЭДВС(Действие, КоллецияДляИзмененияСтатусов, ДанныеКлючаЭЦП, ДанныеПрофиляИСЭСФ);
			
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат Результат;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьСтатусВСПоИдентификатору(Знач ВС, Знач ЗапускатьФоновоеЗадание) Экспорт
		
	КоллекцияСгруппированныхВС = ВСВызовСервера.СгруппироватьВСПоСтруктурнымЕдиницам(ВС);
	
	Если КоллекцияСгруппированныхВС.Количество() = 0 Тогда
		//Возврат;
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(НСтр("ru='Невозможно закрыть/восстановить склад. Склад не создан в ИС ЭСФ!'"));
		
	Иначе
		МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхВС);
		
		СтруктураВС = Новый Структура;
		
		Для Каждого СгруппированныеВС Из  КоллекцияСгруппированныхВС Цикл
			СтруктурнаяЕдиница = СгруппированныеВС.Ключ;
		КонецЦикла;
		СтруктураВС.Вставить("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
		СтруктураВС.Вставить("ВС", ВС);
		
		ДополнительныеПараметры = Новый Структура;
		ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхВС", КоллекцияСгруппированныхВС);
		
		ДополнительныеПараметры.Вставить("СтруктураВС", СтруктураВС);
		ДополнительныеПараметры.Вставить("ЗапускатьФоновоеЗадание", ЗапускатьФоновоеЗадание);
		
		
		Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
			
			ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей = Новый ОписаниеОповещения("ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей", ЭтаФорма, ДополнительныеПараметры);
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ПараметрыФормы.Вставить("ТребуетсяВыборСертификатаВхода", Истина);
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей, ПараметрыФормы);
			
		Иначе
			Если МассивПрофилейИСЭСФ = Неопределено Тогда
				ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей = Новый ОписаниеОповещения("ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей", ЭтаФорма, ДополнительныеПараметры);
				ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
				
				ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей, ПараметрыФормы);
			Иначе
				ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
			КонецЕсли;
		КонецЕсли;
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ОбновитьСтатусВСПоИдентификаторуВыборДанныхПрофилей(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт

	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//до перехода на сервер нужно получить ИД сессии на клиенте!
	//при этом учеть получение для каждого профиля
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		
 		ПараметрыЗадания = Новый Структура("СтруктураВС, ДанныеПрофилейИСЭСФ", ДополнительныеПараметры.СтруктураВС, ДанныеПрофилейИСЭСФ);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне(); //ЭтаФорма
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление статусов ВС'");
		
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ОбновитьСтатусВСПоИдентификатору", ПараметрыЗадания, ПараметрыВыполнения);
		
	Иначе
	
		ВСВызовСервера.ОбновитьСтатусВСПоИдентификатору(ДополнительныеПараметры.СтруктураВС, ДанныеПрофилейИСЭСФ);
			
	КонецЕсли;

	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Обновление документов из ИС ВС'"));
		КонецЕсли;
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат); //, ДополнительныеПараметры.КлючФормы
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьВС(), ДополнительныеПараметры.КоллекцияСгруппированныхВС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция ОбновитьВиртуальныеСкладыИзВС(Знач МассивСкладов, Знач ДополнительныеПараметры) Экспорт
	
	КоллекцияСгруппированныхСкладов = ВСВызовСервера.СгруппироватьСкладыПоСтруктурнымЕдиницам(МассивСкладов);		
	
	МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхСкладов);
	
	ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхСкладов", КоллекцияСгруппированныхСкладов);
	
	Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
		ОбновитьСкладыВыборДанныхПрофилейИСЭСФ = Новый ОписаниеОповещения("ОбновитьВиртуальныеСкладыВыборДанныхПрофилейВС", ЭтаФорма, ДополнительныеПараметры);
		ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхСкладов, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
		ПараметрыФормы.Вставить("ТребуетсяВыборСертификатаВхода", Истина);
		ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьСкладыВыборДанныхПрофилейИСЭСФ, ПараметрыФормы);
	Иначе
		Если МассивПрофилейИСЭСФ = Неопределено Тогда
			ОбновитьСкладыВыборДанныхПрофилейИСЭСФ = Новый ОписаниеОповещения("ОбновитьВиртуальныеСкладыВыборДанныхПрофилейВС", ЭтаФорма, ДополнительныеПараметры);
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхСкладов, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ОбновитьСкладыВыборДанныхПрофилейИСЭСФ, ПараметрыФормы);	
		Иначе
			ОбновитьВиртуальныеСкладыВыборДанныхПрофилейВС(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
		КонецЕсли;
	КонецЕсли;
	
КонецФункции

&НаКлиенте
Процедура ОбновитьВиртуальныеСкладыВыборДанныхПрофилейВС(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт

	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//до перехода на сервер нужно получить ИД сессии на клиенте!
	//при этом учеть получение для каждого профиля
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		
		ПараметрыЗадания = Новый Структура("КоллекцияСгруппированныхСкладов, ДанныеПрофилейИСЭСФ", ДополнительныеПараметры.КоллекцияСгруппированныхСкладов, ДанныеПрофилейИСЭСФ);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне();
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Обновление складов ВС'");
		
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ОбновитьСкладыИзВСВФоне", ПараметрыЗадания, ПараметрыВыполнения);
		
	Иначе
							
		ВСВызовСервера.ОбновитьСкладыИзВС(ДополнительныеПараметры.КоллекцияСгруппированныхСкладов, ДанныеПрофилейИСЭСФ);
			
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Обновление виртуальных складов'"));
		КонецЕсли;
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьВС(), ДополнительныеПараметры.КоллекцияСгруппированныхСкладов);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура СоздатьВиртуальныеСклады(Знач МассивВС, Знач ДополнительныеПараметры) Экспорт
	
	ВСВызовСервера.ПроверитьВозможностьСозданияСкладовПоставитьВОчередь(МассивВС, ДополнительныеПараметры);
	
	Если МассивВС.Количество() = 0 Тогда
		Возврат;
	Иначе
		
		КоллекцияСгруппированныхВС = ВСВызовСервера.СгруппироватьВСПоСтруктурнымЕдиницам(МассивВС);		
		
		МассивПрофилейИСЭСФ = ЭСФКлиент.ПолучитьНастройкиПараметровИСЭСФ(КоллекцияСгруппированныхВС);
		
		ДополнительныеПараметры.Вставить("КоллекцияСгруппированныхВС", КоллекцияСгруппированныхВС);
		
		Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
			СоздатьВСВыборДанныхПрофилейИСЭСФ = Новый ОписаниеОповещения("СоздатьВСВыборДанныхПрофилейВС", ЭтаФорма, ДополнительныеПараметры);
			ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
			ПараметрыФормы.Вставить("ТребуетсяВыборСертификатаВхода", Истина);
			
			ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(СоздатьВСВыборДанныхПрофилейИСЭСФ, ПараметрыФормы);	
			
		Иначе
			Если МассивПрофилейИСЭСФ = Неопределено Тогда
				
				СоздатьВСВыборДанныхПрофилейИСЭСФ = Новый ОписаниеОповещения("СоздатьВСВыборДанныхПрофилейВС", ЭтаФорма, ДополнительныеПараметры);
				ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц, ВызвавшийМодуль", КоллекцияСгруппированныхВС, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
				
				ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(СоздатьВСВыборДанныхПрофилейИСЭСФ, ПараметрыФормы);	
				
			Иначе
				СоздатьВСВыборДанныхПрофилейВС(МассивПрофилейИСЭСФ, ДополнительныеПараметры);
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СоздатьВСВыборДанныхПрофилейВС(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт

	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	//до перехода на сервер нужно получить ИД сессии на клиенте!
	//при этом учеть получение для каждого профиля
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		
		ПараметрыЗадания = Новый Структура("КоллекцияСгруппированныхВС, ДанныеПрофилейИСЭСФ", ДополнительныеПараметры.КоллекцияСгруппированныхВС, ДанныеПрофилейИСЭСФ);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне();
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Создание виртуальных складов'");
		
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.СоздатьВСВФоне", ПараметрыЗадания, ПараметрыВыполнения);
		
	Иначе
							
		ВСВызовСервера.СоздатьВС(ДополнительныеПараметры.КоллекцияСгруппированныхВС, ДанныеПрофилейИСЭСФ);
			
	КонецЕсли;
	
	Если ДополнительныеПараметры.ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Создание виртуальных складов'"));
		КонецЕсли;
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияЗаписьВС(), ДополнительныеПараметры.КоллекцияСгруппированныхВС);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
// Открывает сессии с подписью для переданных данных с профилей ИСЭСФ.
//
// Параметры:
//  ДанныеПрофилейИСЭСФ - Массив или Соответствие, или Ссылка  на справочник ПрофилиИСЭСФ,
//   по которым(му) происходит открытие сессий.
//  ПараметрыОткрытияСессии - Структура - параметры для открытия сессии с подписью,
//   функция для получения параметров - ЭСФКлиентСервер.ПолучитьПараметрыОткрытияСессииСПодписьюПоУмолчанию().
//
// Возвращаемое значение:
//  <Булево> - Результат работы функции.
//
Функция ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ,ПараметрыОткрытияСессии = Неопределено) Экспорт
	
	Если ПараметрыОткрытияСессии = Неопределено Тогда
		ПараметрыОткрытияСессии = ЭСФКлиентСервер.ПолучитьПараметрыОткрытияСессииСПодписьюПоУмолчанию();
		ПараметрыОткрытияСессии.ИмяМодуля = "VS";
	КонецЕсли;
	
	Возврат ЭСФКлиент.ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ,ПараметрыОткрытияСессии);
	
КонецФункции

#КонецОбласти

#Область ОткрытиеФорм

&НаКлиенте
Процедура ПриОткрытииФормы(Форма, Отказ) Экспорт
		
КонецПроцедуры

#КонецОбласти

#Область ОбновлениеНоменклатурыГСВС

&НаКлиенте
Процедура ЗагрузитьНоменклатуруГСВС(ПараметрыВызова) Экспорт
	ЗагрузитьСправочникГСВСЗавершение = Новый ОписаниеОповещения("ЗагрузитьСправочникГСВСЗавершение", ЭтотОбъект, ПараметрыВызова);	
	//ПараметрыФормы = Новый Структура("СписокСтруктурныхЕдиниц", ДоступныеДляСинхронизацииОрганизацииНаСервере(), Истина);
	ПараметрыФормы = Новый Структура("ТребуетсяВыборПрофиляИСЭСФ, ВызвавшийМодуль", Истина, ПредопределенноеЗначение("Перечисление.МодулиЭСФ.ВС"));
	
	Если ЭСФВызовСервера.ИспользоватьОткрытиеСессииСПодписью() И ЭСФВызовСервера.ИспользоватьВнешнююКриптографиюДляКомпоненты() Тогда
		ПараметрыФормы.Вставить("ТребуетсяВыборСертификатаВхода", Истина);
	КонецЕсли;
	
	ЭСФКлиент.ОткрытьФормуВводаДанныхИСЭСФ(ЗагрузитьСправочникГСВСЗавершение, ПараметрыФормы);		
КонецПроцедуры

&НаКлиенте
Процедура ЗагрузитьСправочникГСВСЗавершение(ДанныеПрофилейИСЭСФ, ДополнительныеПараметры) Экспорт
	
	Если ДанныеПрофилейИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПрофиляИСЭСФ = Неопределено;
	
	Для Каждого Элемент Из ДанныеПрофилейИСЭСФ Цикл
		ПараметрыПрофиляИСЭСФ = Элемент.Значение;
	КонецЦикла;
	
	Если ПараметрыПрофиляИСЭСФ = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЗапускатьФоновоеЗадание = ЭСФКлиентСерверПереопределяемый.ИспользоватьФоновуюОтправкуЭСФ();
	ПараметрыПроцедуры = Новый Структура;
	ПараметрыПроцедуры.Вставить("ИспользоватьФоновуюОтправкуЭСФ", ЗапускатьФоновоеЗадание);	
	ПараметрыПроцедуры.Вставить("ПараметрыПрофиляИСЭСФ", ПараметрыПрофиляИСЭСФ);
	ПараметрыПроцедуры.Вставить("ИдентификаторСессии", Неопределено);
	
	Если ДополнительныеПараметры.Свойство("Дозагрузка") Тогда
		ПараметрыПроцедуры.Вставить("Дозагрузка", ДополнительныеПараметры.Дозагрузка);	
	КонецЕсли;
	
	// До перехода на сервер нужно получить ИД сессии на клиенте! При этом учеть получение для каждого профиля
	Если НЕ ОткрытьСессиюСПодписьюДляПрофилей(ДанныеПрофилейИСЭСФ) Тогда
		Возврат;
	КонецЕсли;
	
	Если ЗапускатьФоновоеЗадание Тогда
		ЭСФКлиентСервер.СообщитьПользователю(НСтр("ru='Загрузка справочника ГСВС будет выполняться в фоновом режиме. Вы можете продолжить работу ...'"));
		КлючФоновогоЗадания = Новый УникальныйИдентификатор;
		
		ПараметрыПроцедуры.Вставить("КлючФоновогоЗадания", КлючФоновогоЗадания);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне();
		ПараметрыВыполнения.КлючФоновогоЗадания = КлючФоновогоЗадания;
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка номенклатуры ГСВС'");
		
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ВСВызовСервера.ЗагрузитьСправочникГСВСВФоне", ПараметрыПроцедуры, ПараметрыВыполнения);
	Иначе
		Результат = ВСВызовСервера.ЗагрузитьСправочникГСВС(ПараметрыПроцедуры);
	КонецЕсли;
		
	Если ЗапускатьФоновоеЗадание Тогда
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Загрузка справочника Номенклатура ГСВС с сервера ВС ИС ЭСФ.'"));
		КонецЕсли;
		
		ЭСФКлиент.ОповеститьФормы("ВС_ПроверятьОповещенияФоновогоЗадания", Результат, ДополнительныеПараметры.КлючФормы);
	Иначе
		ЭСФКлиент.ОповеститьФормы(ВСКлиентСервер.ИмяСобытияСинхронизацияГСВС(), Результат,ДополнительныеПараметры.КлючФормы);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДоступныеДляСинхронизацииОрганизацииНаСервере()
	СписокПрофилей = Новый Соответствие;
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	Организации.Ссылка
	                      |ИЗ
	                      |	Справочник.Организации КАК Организации
	                      |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Справочник.ПрофилиИСЭСФ КАК ПрофилиИСЭСФ
	                      |		ПО Организации.Ссылка = ПрофилиИСЭСФ.СтруктурнаяЕдиница
	                      |ГДЕ
	                      |	НЕ ПрофилиИСЭСФ.Ссылка ЕСТЬ NULL 
	                      |	И НЕ ПрофилиИСЭСФ.ПометкаУдаления");
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	Пока Выборка.Следующий() Цикл		
		СписокПрофилей.Вставить(Выборка.Ссылка);		
	КонецЦикла;
	
	Возврат СписокПрофилей;	
КонецФункции

&НаКлиенте
Процедура ЗагрузитьНоменклатуруГСВСИзФайла(ПараметрыВызова) Экспорт
	ДиалогОткрытияФайла = Новый ДиалогВыбораФайла(РежимДиалогаВыбораФайла.Открытие);
	ДиалогОткрытияФайла.ПолноеИмяФайла = "";
	ДиалогОткрытияФайла.Фильтр = НСтр("ru = 'Все файлы'; en = 'All files'") + " (*.*)|*.*";
	ДиалогОткрытияФайла.МножественныйВыбор = Ложь;
	ДиалогОткрытияФайла.Заголовок = НСтр("ru='Выберите файл ГСВС'");
	Если ДиалогОткрытияФайла.Выбрать() Тогда
		
		Попытка
			ЧтениеТекста = Новый ЧтениеТекста;
			ЧтениеТекста.Открыть(ДиалогОткрытияФайла.ПолноеИмяФайла);
			СтрокаСДанными = ЧтениеТекста.Прочитать();
			ЧтениеТекста.Закрыть();
		Исключение
			Возврат
		КонецПопытки;
		
		КлючФоновогоЗадания = Новый УникальныйИдентификатор;		
			
		ПараметрыПроцедуры = Новый Структура;
		ПараметрыПроцедуры.Вставить("СтрокаСДанными", СтрокаСДанными);
		ПараметрыПроцедуры.Вставить("КлючФоновогоЗадания", КлючФоновогоЗадания);
		
		ПараметрыВыполнения = ЭСФКлиентСерверПереопределяемый.ПараметрыВыполненияВФоне();
		ПараметрыВыполнения.КлючФоновогоЗадания = КлючФоновогоЗадания;
		ПараметрыВыполнения.НаименованиеФоновогоЗадания = НСтр("ru = 'Загрузка номенклатуры ГСВС'");
				    
		Результат = ЭСФВызовСервера.ВыполнитьВФоне("ЭСФСерверПереопределяемый.ЗагрузитьСправочникГСВСИзФайлаВФоне", ПараметрыПроцедуры, ПараметрыВыполнения);						
		
		Если ТипЗнч(Результат) = Тип("Структура") Тогда
			Результат.Вставить("ТекстСообщения", НСтр("ru = 'Загрузка справочника ""Номенклатура ГСВС"" из файла.'"));
		КонецЕсли;
		
		ЭСФКлиент.ОповеститьФормы("ЗагрузкаГСВС_ПроверятьОповещенияФоновогоЗадания", Результат, ПараметрыВызова.КлючФормы);

	КонецЕсли;	
КонецПроцедуры


#КонецОбласти
