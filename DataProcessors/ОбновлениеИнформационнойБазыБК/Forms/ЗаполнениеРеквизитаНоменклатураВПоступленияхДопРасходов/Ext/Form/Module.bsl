
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	КоличествоОбъектовВТранзакции = 100;
	Режим   = 1;
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РежимПриИзменении(Элемент)
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ВыбратьПериод(Команда)
	
	ПараметрыВыбора = Новый Структура("НачалоПериода,КонецПериода", НачалоПериода, КонецПериода);
	ОписаниеОповещения = Новый ОписаниеОповещения("ВыбратьПериодЗавершение", ЭтотОбъект);
	ОткрытьФорму("ОбщаяФорма.ВыборСтандартногоПериода", ПараметрыВыбора, Элементы.ВыбратьПериод, , , , ОписаниеОповещения);

КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьОбработку(Команда) 
	
	Если Режим = 0 И НЕ ЗначениеЗаполнено(Номенклатура) Тогда
		ОбщегоНазначенияКлиент.СообщитьПользователю(
			НСтр("ru = 'Не указана номенклатура для заполнения'"),
			,
			"Номенклатура",
			);
		Возврат;
	КонецЕсли;
	
	// Запуск и отображение выполнения обработчика обновления выполняется в форме ЗапускОбработчиковОбновления.
	
	ПараметрыОбработки = Новый Структура;
	ПараметрыОбработки.Вставить("Режим",         Режим);
	ПараметрыОбработки.Вставить("Номенклатура",  ?(Режим = 0, Номенклатура, Неопределено));
	ПараметрыОбработки.Вставить("НачалоПериода", НачалоПериода);
	ПараметрыОбработки.Вставить("КонецПериода",  КонецПериода);
	ПараметрыОбработки.Вставить("КоличествоОбъектовВТранзакции", КоличествоОбъектовВТранзакции);
	
	ЭтаФорма.Закрыть(ПараметрыОбработки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	ОбщегоНазначенияКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "Номенклатура", "Доступность", Форма.Режим = 0);
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПериодЗавершение(РезультатВыбора, ДопПараметры) Экспорт
	
	Если РезультатВыбора = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РезультатВыбора, "НачалоПериода,КонецПериода");
	
КонецПроцедуры
