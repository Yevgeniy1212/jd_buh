
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Если Параметры.Свойство("ПараметрыОтбораСписка") Тогда
		ПараметрыОтбораСписка = Параметры.ПараметрыОтбораСписка;
		Если ПараметрыОтбораСписка.Свойство("ПомощникВводаНачальныхОстатков") Тогда
			ОткрытИзПомощника = Истина;
		КонецЕсли; 
	КонецЕсли; 
	
	Если ОткрытИзПомощника Тогда
		
		Если ПараметрыОтбораСписка.Свойство("Организация") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ЭтаФорма.Список, "Организация", ПараметрыОтбораСписка.Организация, ВидСравненияКомпоновкиДанных.Равно,, Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		КонецЕсли; 

		Если ПараметрыОтбораСписка.Свойство("Дата") Тогда
			ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
			ЭтаФорма.Список, "Дата", ПараметрыОтбораСписка.Дата, ВидСравненияКомпоновкиДанных.МеньшеИлиРавно,, Истина, 
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		КонецЕсли;

	Иначе
		ОбщегоНазначенияБКВызовСервера.УстановитьОтборПоОсновнойОрганизации(ЭтотОбъект);
	КонецЕсли; 

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ


// СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов
&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);
	
КонецПроцедуры
// Конец СтандартныеПодсистемы.ГрупповоеИзменениеОбъектов

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ СПИСОК

&НаСервере
Процедура СписокПередЗагрузкойПользовательскихНастроекНаСервере(Элемент, Настройки)
	
	Если ОткрытИзПомощника Тогда
		
		ОтборОрганизация = Неопределено;
		ОтборДата        = Неопределено;
		
		Отборы = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.КомпоновщикНастроек.Настройки.Отбор, "Организация");
		
		Если Отборы.Количество() > 0 Тогда
			ОтборОрганизация = Отборы[0];
		КонецЕсли;
		
		Отборы = ОбщегоНазначенияКлиентСервер.НайтиЭлементыИГруппыОтбора(Список.КомпоновщикНастроек.Настройки.Отбор, "Дата");
		
		Если Отборы.Количество() > 0 Тогда
			ОтборДата = Отборы[0];
		КонецЕсли;
		
		Если ОтборОрганизация = Неопределено И ОтборДата = Неопределено Тогда
			Возврат;
		КонецЕсли;
		
		Для Каждого ЭлементНастроек Из Настройки.Элементы Цикл
			Если ТипЗнч(ЭлементНастроек) = Тип("ЭлементОтбораКомпоновкиДанных") Тогда
				Если ЭлементНастроек.ИдентификаторПользовательскойНастройки = "Организация" Тогда
					ЭлементНастроек.Использование  = ОтборОрганизация.Использование;
					ЭлементНастроек.ВидСравнения   = ОтборОрганизация.ВидСравнения;
					ЭлементНастроек.ПравоеЗначение = ОтборОрганизация.ПравоеЗначение;
				ИначеЕсли ЭлементНастроек.ИдентификаторПользовательскойНастройки = "Дата" И ОтборДата <> Неопределено Тогда
					ЭлементНастроек.Использование  = ОтборДата.Использование;
					ЭлементНастроек.ВидСравнения   = ОтборДата.ВидСравнения;
					ЭлементНастроек.ПравоеЗначение = ОтборДата.ПравоеЗначение;
				КонецЕсли;
			КонецЕсли;
		КонецЦикла;
			
	Иначе
		ОбщегоНазначенияБККлиентСервер.ВосстановитьОтборСписка(Список, Настройки, "Организация");
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦ ФОРМЫ

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Функция ПечатьОпись(ПараметрыПечати) Экспорт
	
	СтрукутраПолей = Неопределено;
	
	Если ТипЗнч(ПараметрыПечати) = Тип("Структура") Тогда
		Если ПараметрыПечати.Свойство("Форма") Тогда
			СтрукутраПолей = ПолучитьСоставКолонокДляПечати();
		КонецЕсли; 
	КонецЕсли;	
	
	ПараметрыПечати.ДополнительныеПараметры.Вставить("СтрукутраПолей", СтрукутраПолей);
	
	УправлениеПечатьюКлиент.ВыполнитьКомандуПечати(ПараметрыПечати.МенеджерПечати, ПараметрыПечати.Идентификатор, 
		ПараметрыПечати.ОбъектыПечати, ПараметрыПечати.Форма, ПараметрыПечати.ДополнительныеПараметры);
		
КонецФункции

&НаКлиенте
Функция ПолучитьСоставКолонокДляПечати()
	
	СтруктРекв = Новый Структура;
	
	ФормаДокумента = ПолучитьФорму("Документ.ВводНачальныхОстатковОСЗабаланс.Форма.ФормаДокумента");
	ЭлементыФормыДокумента = ФормаДокумента.Элементы;
		
	Для Каждого ТекСтрока Из ЭлементыФормыДокумента Цикл
		
		Если Лев(ТекСтрока.Имя, 2) <> "ОС" Тогда
			Продолжить;
		КонецЕсли; 
		
		Если ТипЗнч(ТекСтрока) <> Тип("ПолеФормы") Тогда
		    Продолжить;		
		КонецЕсли;
		
		Если Не ТекСтрока.Видимость Тогда
			Продолжить;
		КонецЕсли; 
		
		ИмяКолонки = Прав(ТекСтрока.Имя, СтрДлина(ТекСтрока.Имя) - 2);
		
		Если Не (ИмяКолонки = "НомерСтроки" ИЛИ ИмяКолонки = "ОсновноеСредствоКод" ИЛИ ИмяКолонки = "ЛиквидационнаяСтоимостьБУ") Тогда
			СтруктРекв.Вставить(ИмяКолонки, ИмяКолонки);
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат СтруктРекв;
		
КонецФункции

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

