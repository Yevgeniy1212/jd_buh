#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СписокДокументов = Параметры.СписокДокументов;
	
	КоличествоДокументов = СписокДокументов.Количество();
	Если КоличествоДокументов = 0 Тогда
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	КоллекцияСтатусов = РегистрыСведений.СтатусыДокументов.ПолучитьСтатусыДокументов(СписокДокументов.ВыгрузитьЗначения());
	
	СтатусыДокумента = КоллекцияСтатусов[СписокДокументов[0].Значение];
	СтатусОплаты   = СтатусыДокумента.Статус;
	СтатусПоступления = СтатусыДокумента.ДополнительныйСтатус;
	
	// Проверим совпадают ли статусы всех выбранных документов
	Для Индекс = 1 По КоллекцияСтатусов.Количество() - 1 Цикл
		СтатусыДокумента = КоллекцияСтатусов[СписокДокументов[Индекс].Значение];
		Если ЗначениеЗаполнено(СтатусОплаты) И СтатусОплаты <> СтатусыДокумента.Статус Тогда
			СтатусОплаты = Перечисления.СтатусОплатыСчета.ПустаяСсылка();
		КонецЕсли;
		Если ЗначениеЗаполнено(СтатусПоступления) И СтатусПоступления <> СтатусыДокумента.ДополнительныйСтатус Тогда
			СтатусПоступления = Перечисления.СтатусыОтгрузки.ПустаяСсылка();
		КонецЕсли;
	КонецЦикла;
	
	УправлениеФормой(ЭтотОбъект);
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	
	Если Модифицированность И Не ЗавершениеРаботы Тогда
		Отказ = Истина;
		ТекстВопроса = НСтр("ru = 'Изменения не записаны. Сохранить изменения?'");
		Оповещение = Новый ОписаниеОповещения("ВопросПередЗакрытиемЗавершение", ЭтотОбъект);
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВопросПередЗакрытиемЗавершение(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		Если СохранитьИзменения() Тогда
			Закрыть();
		КонецЕсли;
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		Модифицированность = Ложь;
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОК(Команда)
	
	Если СохранитьИзменения() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;
	
	СписокДокументов = Форма.СписокДокументов;
	
	КоличествоДокументов = СписокДокументов.Количество();
	
	Если КоличествоДокументов = 1 Тогда
		Форма.Заголовок = Строка(СписокДокументов[0].Значение);
	Иначе
		Форма.Заголовок = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru = 'Изменить статусы документов (%1)'"), СписокДокументов.Количество());
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Функция СохранитьИзменения()
	
	Если Не ПроверитьЗаполнение() Тогда
		Возврат Ложь;
	КонецЕсли;
	
	Модифицированность = Ложь;
	
	Если УстановитьСтатусДокументовНаСервере(СписокДокументов.ВыгрузитьЗначения(), СтатусОплаты, СтатусПоступления) Тогда
		
		ОповеститьОбИзменении(Тип("ДокументСсылка.СчетНаОплатуПоставщика"));
		Если Не ВладелецФормы.ОтображатьСтатусыДокументов Тогда
						ОповеститьОбИзмененииСтатусовДокументов(
				ВладелецФормы, СписокДокументов, СтатусОплаты, СтатусПоступления);
		КонецЕсли;
			
	КонецЕсли;
	
	Возврат Истина;
	
КонецФункции 

&НаКлиенте
// Выводит оповещение о результате установки статуса документов.
//
// Параметры:
//  Форма        - ФормаКлиентскогоПриложения - содержит реквизит Список.
//  СписокДокументов - СписокЗначений - ссылки на документы, которые изменили свой статус.
//  СтатусДокумента - ПеречислениеСсылка - основной статус, установленный документам.
//  ДополнительныйСтатус - ПеречислениеСсылка - дополнительный статус, установленный документам.
//
Процедура ОповеститьОбИзмененииСтатусовДокументов(Форма, СписокДокументов, СтатусДокумента, ДополнительныйСтатус = Неопределено) Экспорт
	
	Оповестить("ИзмененСтатусДокументов", СписокДокументов.ВыгрузитьЗначения()); // Оповещение открытых форм документов
	
	КоличествоДокументов = СписокДокументов.Количество();
	
	ПредставлениеСтатуса = Строка(СтатусДокумента);

	Если ЗначениеЗаполнено(ДополнительныйСтатус) Тогда
		ПредставлениеСтатуса = ПредставлениеСтатуса + ", " + ДополнительныйСтатус;
	КонецЕсли;
	
	Если КоличествоДокументов = 1 Тогда
		
		ПоказатьОповещениеПользователя(ПредставлениеСтатуса,
			ПолучитьНавигационнуюСсылку(СписокДокументов[0].Значение),
			Строка(СписокДокументов[0].Значение),,
			СтатусОповещенияПользователя.Информация,
			"ОповеститьОбИзмененииСтатусовДокументов");
			
	Иначе
		
		ПредставлениеОписания = СтроковыеФункцииКлиентСервер.СтрокаСЧисломДляЛюбогоЯзыка(
			Нстр("ru = ';%1 документ;;%1 документа;%1 документов;%1 документа'"), КоличествоДокументов);
		
		ПоказатьОповещениеПользователя(ПредставлениеСтатуса,
			,
			ПредставлениеОписания,,
			СтатусОповещенияПользователя.Информация,
			"ОповеститьОбИзмененииСтатусовДокументов");
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция УстановитьСтатусДокументовНаСервере(Знач МассивДокументов, Знач СтатусОплаты, Знач СтатусОтгрузки)
	
	СтатусыДокумента = РегистрыСведений.СтатусыДокументов.НовыеСтатусыДокумента();
	СтатусыДокумента.Статус               = СтатусОплаты;
	СтатусыДокумента.ДополнительныйСтатус = СтатусОтгрузки;
	
	СтатусИзменен = РегистрыСведений.СтатусыДокументов.УстановитьСтатусыДокументов(
		МассивДокументов, СтатусыДокумента);
	
	Возврат СтатусИзменен;
	
КонецФункции

#КонецОбласти