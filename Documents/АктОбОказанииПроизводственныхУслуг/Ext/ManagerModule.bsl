#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Заполняет счета учета номенклатуры в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект) Экспорт

	ДанныеОбъекта = Новый Структура("Дата, Организация, ПодразделениеОрганизации, УчитыватьКПН, УчитыватьНДС");
	
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);

	СоответствиеСчетовУчета = ПроцедурыБухгалтерскогоУчета.ПолучитьСчетаУчетаСпискаНоменклатуры(
			ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(Объект.Услуги, "Номенклатура", Истина), ДанныеОбъекта.Дата);
			
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл
		СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти.Номенклатура);
		ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, СчетаУчета);
	КонецЦикла;
	
КонецПроцедуры

// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - структура сведений о номенклатуре, либо стуктура счетов учета
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, СведенияОНоменклатуре) Экспорт

	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОНоменклатуре.Свойство("СчетаУчета") Тогда
		// СведенияОНоменклатуре - структура сведений номенклатуры
		СчетаУчета = СведенияОНоменклатуре.СчетаУчета;
	ИначеЕсли СведенияОНоменклатуре.Свойство("СчетУчетаБУ") Тогда
		// СведенияОНоменклатуре - структура счетов учета номенклатуры
		СчетаУчета = СведенияОНоменклатуре;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетДоходовОтРеализацииБУ) Тогда
		Если СтрокаТабличнойЧасти.СчетДоходовБУ <> СчетаУчета.СчетДоходовОтРеализацииБУ Тогда
			Для Счетчик = 1 По 3 Цикл
				СтрокаТабличнойЧасти["СубконтоДоходовБУ" + Счетчик] = Неопределено;
			КонецЦикла;
		КонецЕсли;
		СтрокаТабличнойЧасти.СчетДоходовБУ = СчетаУчета.СчетДоходовОтРеализацииБУ;
		ПроцедурыБухгалтерскогоУчета.ЗаполнитьАналитикуСчетаТабличнойЧасти(СтрокаТабличнойЧасти, СчетаУчета, "СубконтоДоходовБУ", "СчетДоходовБУ", "СубконтоДоходовБУ");
		
		РаботаСДиалогами.ЗаполнитьАналитикуПодразделениеСчетовДоходовЗатрат(СтрокаТабличнойЧасти, СтрокаТабличнойЧасти.СчетДоходовБУ, "СубконтоДоходовБУ", ДанныеОбъекта.ПодразделениеОрганизации);
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетУчетаНДСПоРеализации) Тогда 
		СтрокаТабличнойЧасти.СчетУчетаНДСПоРеализации = СчетаУчета.СчетУчетаНДСПоРеализации;
	КонецЕсли;
	Если ЗначениеЗаполнено(СчетаУчета.СчетДоходовОтРеализацииНУ) Тогда
		Если СтрокаТабличнойЧасти.СчетДоходовНУ <> СчетаУчета.СчетДоходовОтРеализацииНУ Тогда
			Для Счетчик = 1 По 3 Цикл
				СтрокаТабличнойЧасти["СубконтоДоходовНУ" + Счетчик] = Неопределено;
			КонецЦикла;
		КонецЕсли;
		СтрокаТабличнойЧасти.СчетДоходовНУ = СчетаУчета.СчетДоходовОтРеализацииНУ;
		ПроцедурыБухгалтерскогоУчета.ЗаполнитьАналитикуСчетаТабличнойЧасти(СтрокаТабличнойЧасти, СчетаУчета, "СубконтоДоходовНУ", "СчетДоходовНУ", "СубконтоДоходовНУ");
		
		РаботаСДиалогами.ЗаполнитьАналитикуПодразделениеСчетовДоходовЗатрат(СтрокаТабличнойЧасти, СтрокаТабличнойЧасти.СчетДоходовНУ, "СубконтоДоходовНУ", ДанныеОбъекта.ПодразделениеОрганизации);
		
	КонецЕсли;
	
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетСебестоимостиПриРеализацииБУ) Тогда
		Если СтрокаТабличнойЧасти.СчетСписанияСебестоимостиБУ <> СчетаУчета.СчетСебестоимостиПриРеализацииБУ Тогда
			Для Счетчик = 1 По 3 Цикл
				СтрокаТабличнойЧасти["СубконтоСписанияСебестоимостиБУ" + Счетчик] = Неопределено;
			КонецЦикла;
		КонецЕсли;
		СтрокаТабличнойЧасти.СчетСписанияСебестоимостиБУ = СчетаУчета.СчетСебестоимостиПриРеализацииБУ;
		ПроцедурыБухгалтерскогоУчета.ЗаполнитьАналитикуСчетаТабличнойЧасти(СтрокаТабличнойЧасти, СчетаУчета, "СубконтоСебестоимостиБУ", "СчетСписанияСебестоимостиБУ", "СубконтоСписанияСебестоимостиБУ");
		
		РаботаСДиалогами.ЗаполнитьАналитикуПодразделениеСчетовДоходовЗатрат(СтрокаТабличнойЧасти, СтрокаТабличнойЧасти.СчетСписанияСебестоимостиБУ, "СубконтоСписанияСебестоимостиБУ", ДанныеОбъекта.ПодразделениеОрганизации);
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетСебестоимостиПриРеализацииНУ) Тогда
		Если СтрокаТабличнойЧасти.СчетСписанияСебестоимостиНУ <> СчетаУчета.СчетСебестоимостиПриРеализацииБУ Тогда
			Для Счетчик = 1 По 3 Цикл
				СтрокаТабличнойЧасти["СубконтоСписанияСебестоимостиНУ" + Счетчик] = Неопределено;
			КонецЦикла;
		КонецЕсли;
		СтрокаТабличнойЧасти.СчетСписанияСебестоимостиНУ = СчетаУчета.СчетСебестоимостиПриРеализацииНУ;
		ПроцедурыБухгалтерскогоУчета.ЗаполнитьАналитикуСчетаТабличнойЧасти(СтрокаТабличнойЧасти, СчетаУчета, "СубконтоСебестоимостиНУ", "СчетСписанияСебестоимостиНУ", "СубконтоСписанияСебестоимостиНУ");
		
		РаботаСДиалогами.ЗаполнитьАналитикуПодразделениеСчетовДоходовЗатрат(СтрокаТабличнойЧасти, СтрокаТабличнойЧасти.СчетСписанияСебестоимостиНУ, "СубконтоСписанияСебестоимостиНУ", ДанныеОбъекта.ПодразделениеОрганизации);
	КонецЕсли;
	
	
КонецПроцедуры

// Производит заполнение и установку реквизитов налогового учета и НДС в табличной части
//
Процедура ЗаполнитьРеквизитыНалоговогоУчета(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти = "Услуги") Экспорт
	
	МетаданныеДокумента = ДанныеОбъекта.Ссылка.Метаданные();
	ОбработкаТабличныхЧастей.ЗаполнитьНДСВидРеализацииТабЧасти(СтрокаТабличнойЧасти, ИмяТабличнойЧасти, МетаданныеДокумента, Пользователи.ТекущийПользователь());
	                  
КонецПроцедуры

Процедура ЗаполнитьСчетаРасчетовСКонтрагентом(Объект)   Экспорт
	
	ДанныеОбъекта = Новый Структура("Организация, Контрагент, ДоговорКонтрагента");
	
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	СчетаУчета = УправлениеВзаиморасчетамиСервер.ПолучитьСчетаРасчетовСКонтрагентом(ДанныеОбъекта.Организация, ДанныеОбъекта.Контрагент, ДанныеОбъекта.ДоговорКонтрагента);
	
	Объект.СчетУчетаРасчетовСКонтрагентом = СчетаУчета.СчетРасчетовПокупателя;
	Объект.СчетУчетаРасчетовПоАвансам     = СчетаУчета.СчетАвансовПокупателя;

КонецПроцедуры

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти


Функция ТекстЗапросаДанныеДляОбновленияЦенДокументов() Экспорт
	
	ТекстЗапроса = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ
	|	АктОбОказанииПроизводственныхУслугУслуги.Номенклатура КАК Номенклатура,
	|	АктОбОказанииПроизводственныхУслугУслуги.Цена КАК Цена,
	|	&Валюта КАК Валюта,
	|	&СпособЗаполненияЦены,
	|	&ЦенаВключаетНДС
	|ПОМЕСТИТЬ ТаблицаНоменклатуры
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг.Услуги КАК АктОбОказанииПроизводственныхУслугУслуги
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслугУслуги.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Номенклатура,
	|	Цена,
	|	Валюта";
	
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
	
КонецФункции

// См. ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами.
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;

	Возврат БлокируемыеРеквизиты;
	
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Заполнение

Процедура СкопироватьУслуги(Объект, Основание)

	Если НЕ ЗначениеЗаполнено(Основание) Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Основание);
	Запрос.Текст = "ВЫБРАТЬ
	               |	СчетНаОплатуПокупателюУслуги.НомерСтроки КАК НомерСтроки,
	               |	СчетНаОплатуПокупателюУслуги.Содержание,
	               |	СчетНаОплатуПокупателюУслуги.Количество,
	               |	СчетНаОплатуПокупателюУслуги.Цена,
	               |	СчетНаОплатуПокупателюУслуги.Сумма,
	               |	СчетНаОплатуПокупателюУслуги.СтавкаНДС,
	               |	СчетНаОплатуПокупателюУслуги.СуммаНДС,
	               |	СчетНаОплатуПокупателюУслуги.Номенклатура,
	               |	СчетНаОплатуПокупателюУслуги.Номенклатура.БазоваяЕдиницаИзмерения КАК ЕдиницаИзмерения,
	               |	1 КАК Коэффициент
	               |ИЗ
	               |	Документ.СчетНаОплатуПокупателю.Услуги КАК СчетНаОплатуПокупателюУслуги
	               |ГДЕ
	               |	СчетНаОплатуПокупателюУслуги.Ссылка = &Ссылка
	               |
	               |УПОРЯДОЧИТЬ ПО
	               |	НомерСтроки";
	
	Объект.Услуги.Загрузить(Запрос.Выполнить().Выгрузить());
	
	ДанныеОбъекта = Новый Структура("Ссылка, Дата, Организация, Склад, ВидОперации, УчитыватьКПН, УчитыватьНДС,СуммаВключаетНДС,
									|ВалютаДокумента, КурсВзаиморасчетов, КратностьВзаиморасчетов, НДСВключенВСтоимость");
									
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект);

	ФлагиНалоговОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "УчитыватьНДС, СуммаВключаетНДС");

	ПересчитыватьНДС = ФлагиНалоговОснования.УчитыватьНДС <> ДанныеОбъекта.УчитыватьНДС
						ИЛИ ФлагиНалоговОснования.СуммаВключаетНДС <> ДанныеОбъекта.СуммаВключаетНДС;
	
	Для Каждого СтрокаТЧ Из Объект.Услуги Цикл
		ЗаполнитьРеквизитыНалоговогоУчета(Объект, СтрокаТЧ);
		
		Если ПересчитыватьНДС Тогда
			СтрокаТЧ.Цена = ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьЦенуПриИзмененииФлаговНалогов(СтрокаТЧ.Цена, 
																	Неопределено,
																	ФлагиНалоговОснования.СуммаВключаетНДС,
																	ДанныеОбъекта.УчитыватьНДС,
																	ДанныеОбъекта.СуммаВключаетНДС, 
																	УчетНДСиАкцизаВызовСервераПовтИсп.ПолучитьСтавкуНДС(СтрокаТЧ.СтавкаНДС));
																	
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуТабЧасти(СтрокаТЧ);
			ОбработкаТабличныхЧастейКлиентСервер.РассчитатьСуммуНДСТабЧасти(СтрокаТЧ, ДанныеОбъекта.СуммаВключаетНДС);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

Процедура ЗаполнитьДокументПоСчетуНаОплатуПокупателю(Объект, Основание) Экспорт
	
	ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(Объект, Основание);
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "СтруктурнаяЕдиница, АдресДоставки");
	                                                                         		
	Объект.БанковскийСчетОрганизации   = РеквизитыОснования.СтруктурнаяЕдиница;
	Объект.АдресДоставки			   = РеквизитыОснования.АдресДоставки;
	Объект.ДокументОснование		   = Основание;
	
	Объект.СпособВыпискиАктовВыполненныхРабот = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание.ДоговорКонтрагента, "СпособВыпискиАктовВыполненныхРабот");
	
	Если НЕ ЗначениеЗаполнено(Объект.СпособВыпискиАктовВыполненныхРабот) Тогда 
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ГосударственноеУчреждение") Тогда
			Объект.СпособВыпискиАктовВыполненныхРабот = ПредопределенноеЗначение("Перечисление.СпособыВыпискиАктовВыполненныхРабот.НаПорталеГосЗакупа");	
		Иначе
			Объект.СпособВыпискиАктовВыполненныхРабот = ПредопределенноеЗначение("Перечисление.СпособыВыпискиАктовВыполненныхРабот.ВБумажномВиде");		
		КонецЕсли; 			
	КонецЕсли;
	           
	ТабличнаяЧасть = Объект.Услуги;
	
	Если ТабличнаяЧасть.Количество() > 0 Тогда
		ТабличнаяЧасть.Очистить();
	КонецЕсли;
	
	СкопироватьУслуги(Объект, Основание);
		
КонецПроцедуры

Процедура ЗаполнитьДокументПоСчетФактураВыданный(Объект, Основание) Экспорт
	
	РеквизитыОснования = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Основание, "Контрагент, Организация, СчетОрганизации, ПунктНазначения,  ПодтвержденДокументамиОтгрузки");
	Если РеквизитыОснования = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	Если РеквизитыОснования.ПодтвержденДокументамиОтгрузки Тогда
		ВызватьИсключение(НСтр("ru='Данные счет-фактуры уже подтверждены документами отгрузки. Ввод на основании не возможен.'"));
	КонецЕсли;  	
	
	Объект.Дата = Основание.ДатаСовершенияОборотаПоРеализации;
	
	ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(Объект, Основание);
	
	Объект.ДокументОснование = Основание;
	
	Объект.Грузополучатель           = РеквизитыОснования.Контрагент;
	Объект.БанковскийСчетОрганизации = РеквизитыОснования.СчетОрганизации;
	Объект.АдресДоставки			 = РеквизитыОснования.ПунктНазначения;
	
	Объект.СпособВыпискиАктовВыполненныхРабот = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Основание.ДоговорКонтрагента, "СпособВыпискиАктовВыполненныхРабот");
	
	Если НЕ ЗначениеЗаполнено(Объект.СпособВыпискиАктовВыполненныхРабот) Тогда 
		Если ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Объект.Контрагент, "ГосударственноеУчреждение") Тогда
			Объект.СпособВыпискиАктовВыполненныхРабот = ПредопределенноеЗначение("Перечисление.СпособыВыпискиАктовВыполненныхРабот.НаПорталеГосЗакупа");	
		Иначе
			Объект.СпособВыпискиАктовВыполненныхРабот = ПредопределенноеЗначение("Перечисление.СпособыВыпискиАктовВыполненныхРабот.ВБумажномВиде");		
		КонецЕсли; 			
	КонецЕсли;
	
	ДанныеОбъекта = Новый Структура(
	"Дата, Организация, СтруктурноеПодразделение, ПодразделениеОрганизации,
	|УчитыватьКПН, ВидУчетаНУ, Ссылка, ТипЦен");
	
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	ТипЦенПлановойСебестоимости   = Константы.ТипЦенПлановойСебестоимостиНоменклатуры.Получить();				  
				                          			
	ДанныеОбъекта.Вставить("ТипЦенПлановойСебестоимости", ТипЦенПлановойСебестоимости);
	ДанныеОбъекта.Вставить("ЗаполнятьСпецификацию", Истина);
	
	УчетНДСИАкциза.ЗаполнитьТабличныеЧастиИзДокументаОснования(Объект, Основание);
	
	МассивНоменклатуры = ОбщегоНазначения.ВыгрузитьКолонку(Объект.Услуги, "Номенклатура", Истина);
	
	СоответствиеСчетовУчета = ПроцедурыБухгалтерскогоУчета.ПолучитьСчетаУчетаСпискаНоменклатуры(
			ДанныеОбъекта.Организация, МассивНоменклатуры, ДанныеОбъекта.Дата);
		
	СоответствиеСведенийОНоменклатуре = ОбработкаТабличныхЧастей.ПолучитьСведенияОСпискеНоменклатуры(
				МассивНоменклатуры, ДанныеОбъекта);
		
	Для Каждого СтрокаТабличнойЧасти Из Объект.Услуги Цикл
		
			СведенияОНоменклатуре = СоответствиеСведенийОНоменклатуре.Получить(СтрокаТабличнойЧасти.Номенклатура);
		
			Если СведенияОНоменклатуре <> Неопределено Тогда
				
				СтрокаТабличнойЧасти.Спецификация      = СведенияОНоменклатуре.Спецификация;
				СтрокаТабличнойЧасти.ПлановаяСтоимость = СведенияОНоменклатуре.ПлановаяСтоимость;
				
				ОбработкаТабличныхЧастейКлиентСервер.ПересчитатьПлановуюСумму(СтрокаТабличнойЧасти);
				
			КонецЕсли;
			
			ЗаполнитьРеквизитыНалоговогоУчета(ДанныеОбъекта, СтрокаТабличнойЧасти, "Услуги");
			ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти.Номенклатура));
			          			
	КонецЦикла;       	  
	
		
КонецПроцедуры

Процедура ЗаполнитьДокументПоАктОбОказанииПроизводственныхУслуг(Объект, Основание) Экспорт
	
	ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(Объект, Основание);
	
	ЗаполнитьЗначенияСвойств(Объект, Основание,
		"СчетЗатратБУ,
		|СчетЗатратНУ,
		|СпособВыпискиАктовВыполненныхРабот,
		|БанковскийСчетОрганизации,
		|АдресДоставки,
		|Грузополучатель,
		|Сделка,
		|СуммаДокумента,
		|СчетУчетаРасчетовПоАвансам,
		|СчетУчетаРасчетовСКонтрагентом,
		|ДатаНачалаОтчетногоПериода,
		|ДатаОкончанияОтчетногоПериода,
		|НомерДокументаГЗ,
		|ДатаДокументаГЗ,
		//|ДатаПодписанияГЗ,
		|ПереченьДокументации");
	
	Объект.ДокументОснование = Основание;
	
	Для Каждого ТабличнаяЧасть Из Основание.Метаданные().ТабличныеЧасти Цикл
		Объект[ТабличнаяЧасть.Имя].Очистить();
		Для Каждого СтрокаТабличнойЧасти Из Основание[ТабличнаяЧасть.Имя] Цикл
			ЗаполнитьЗначенияСвойств(Объект[ТабличнаяЧасть.Имя].Добавить(), СтрокаТабличнойЧасти);
		КонецЦикла;
	КонецЦикла;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт 
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",	Константы.ВалютаРегламентированногоУчета.Получить());
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Если НЕ УчетнаяПолитикаСервер.Существует(Реквизиты.Организация, Реквизиты.Период, "БУ", Истина, ДокументСсылка) 
		ИЛИ НЕ УчетнаяПолитикаСервер.Существует(Реквизиты.Организация, Реквизиты.Период, "НУ", Истина, ДокументСсылка) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если НЕ УправлениеВзаиморасчетамиСервер.ВозможноПроведениеВ_БУ_НУ(Реквизиты, Отказ) Тогда 
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Реквизиты.Вставить("РасчетыВВалюте", Реквизиты.ВалютаВзаиморасчетов <> Реквизиты.ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("РасчетыВВалюте",  Реквизиты.РасчетыВВалюте);
	
	Если Реквизиты.РасчетыВВалюте Тогда
		ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты);
	Иначе
		Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
		Если НЕ ПустаяСтрока(Запрос.Текст) Тогда 
			Результат    = Запрос.ВыполнитьПакет();
		КонецЕсли;
	КонецЕсли;
	
	ОрганизацияПлательщикНалогаНаПрибыль 			= ПроцедурыНалоговогоУчета.ПолучитьПризнакПлательщикаНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль 	= ОрганизацияПлательщикНалогаНаПрибыль И ПроцедурыНалоговогоУчета.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	НеобходимостьОтраженияВНУ 						= ОрганизацияПлательщикНалогаНаПрибыль И Реквизиты.УчитыватьКПН И (ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль ИЛИ Реквизиты.ВидУчетаНУ = Справочники.ВидыУчетаНУ.НУ);
	ВедениеУчетаВременныхРазницБалансовымМетодом = УчетнаяПолитикаСервер.ВедетсяУчетВременныхРазницБалансовымМетодом(Реквизиты.Организация, Реквизиты.Период);

	Реквизиты.Вставить("ОрганизацияПлательщикНалогаНаПрибыль", 			 ОрганизацияПлательщикНалогаНаПрибыль);
	Реквизиты.Вставить("ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль", ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль);
	Реквизиты.Вставить("НеобходимостьОтраженияВНУ", 					 НеобходимостьОтраженияВНУ);
	Реквизиты.Вставить("ВедениеУчетаВременныхРазницБалансовымМетодом", 	 ВедениеУчетаВременныхРазницБалансовымМетодом);

	
	Налогоплательщик = Реквизиты.Организация;
	Если ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями") Тогда
		Налогоплательщик = ПроцедурыНалоговогоУчетаВызовСервераПовтИсп.ПолучитьНалогоплательщикаСтруктурнойЕдиницы(Реквизиты.СтруктурноеПодразделение,
							Реквизиты.Организация,
							Перечисления.РазделыНалоговогоУчета.НДС);
	КонецЕсли;	
	
	Реквизиты.Вставить("Налогоплательщик", Налогоплательщик);
	
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(НеобходимостьОтраженияВНУ, "НеобходимостьОтраженияВНУ");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(Налогоплательщик		 , "Налогоплательщик");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ВедениеУчетаВременныхРазницБалансовымМетодом , "ВедениеУчетаВременныхРазницБалансовымМетодом");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль , "ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль");

	
	НомераТаблиц = Новый Структура;
	Запрос.Текст =  ТекстЗапросаРеализацияУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаПлановаяСтоимостьУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты) 
					+ ТекстЗапросаУчастникиСовместнойДеятельности(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаВыпускУслугБУ(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаВыпускУслугНУ(НомераТаблиц, ПараметрыПроведения, Реквизиты);
					
	КодОсновногоЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();	
	Запрос.УстановитьПараметр("СинонимУслуги",	           НСтр("ru = 'Услуги'"));
	Запрос.УстановитьПараметр("СодержаниеВыделенНДС",      НСтр("ru = 'НДС'", КодОсновногоЯзыка));
	Запрос.УстановитьПараметр("СодержаниеОтложенНДС",      НСтр("ru = 'НДС (отложен)'", КодОсновногоЯзыка));
	Запрос.УстановитьПараметр("СодержаниеУслуги",     	   НСтр("ru = 'Затраты от реализации произв. услуг в план.ценах'", КодОсновногоЯзыка));
	Запрос.УстановитьПараметр("НеобходимостьОтраженияВНУ", НеобходимостьОтраженияВНУ);
	Запрос.УстановитьПараметр("СчетУчетаРасчетовСКонтрагентомНУ", ПроцедурыНалоговогоУчетаВызовСервераПовтИсп.ПолучитьСчетРасчетовСКонтрагентомНУ(Реквизиты.СчетУчетаРасчетовСКонтрагентом));
	Запрос.УстановитьПараметр("ВедениеУчетаВременныхРазницБалансовымМетодом", ВедениеУчетаВременныхРазницБалансовымМетодом);
    Запрос.УстановитьПараметр("СчетУчетаНДСОтложенный",                 ПланыСчетов.Типовой.НалогНаДобавленнуюСтоимостьОтложенный);

	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда 
		
		Результат = Запрос.ВыполнитьПакет();
		
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
		
	КонецЕсли;
			
	Возврат ПараметрыПроведения;
	
КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСоставДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МАКСИМУМ(СоставДокумента.ЕстьУслуги) КАК ЕстьУслуги,
	|	МАКСИМУМ(СоставДокумента.ЕстьУчастникиСовместнойДеятельности) КАК ЕстьУчастникиСовместнойДеятельности
	|ПОМЕСТИТЬ СоставДокумента
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ИСТИНА КАК ЕстьУслуги,
	|		ЛОЖЬ КАК ЕстьУчастникиСовместнойДеятельности
	|	ИЗ
	|		Документ.АктОбОказанииПроизводственныхУслуг.Услуги КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		Документ.АктОбОказанииПроизводственныхУслуг.УчастникиСовместнойДеятельности КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка) КАК СоставДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	ВЫБОР
	|		КОГДА Реквизиты.СпособВыпискиАктовВыполненныхРабот = ЗНАЧЕНИЕ(Перечисление.СпособыВыпискиАктовВыполненныхРабот.ВБумажномВиде) 
	|			ТОГДА Реквизиты.Дата
	|		ИНАЧЕ Реквизиты.ДатаподписанияГЗ
	|	КОНЕЦ КАК Дата,
	|	Реквизиты.СпособВыпискиАктовВыполненныхРабот КАК СпособВыпискиАктовВыполненныхРабот,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Организация КАК Налогоплательщик,
	|	Реквизиты.Организация КАК НалогоплательщикАкциза,
	|	Реквизиты.СтруктурноеПодразделение,
	|	ВЫБОР
	|		КОГДА Организации.ГоловнаяОрганизация ЕСТЬ NULL 
	|				ИЛИ Организации.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Реквизиты.Организация
	|		ИНАЧЕ Организации.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Реквизиты.УчитыватьКПН,
	|	Реквизиты.ВидУчетаНУ,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.Сделка,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ДоговорСовместнойДеятельности, НЕОПРЕДЕЛЕНО) КАК ПризнакДоговорСовместнойДеятельности,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.УчастникСРП, НЕОПРЕДЕЛЕНО) КАК ПризнакУчастникСРП,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ВедениеВзаиморасчетов, НЕОПРЕДЕЛЕНО) КАК ВедениеВзаиморасчетов,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ВидДоговора, НЕОПРЕДЕЛЕНО) КАК ВидДоговора,
	|	ЕСТЬNULL(ДоговорыКонтрагентов.ВалютаВзаиморасчетов, НЕОПРЕДЕЛЕНО) КАК ВалютаВзаиморасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.УчитыватьНДС,
	|	Реквизиты.СуммаВключаетНДС,
	|	Реквизиты.КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента,
	|	&ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	ЛОЖЬ КАК НеобходимостьОтраженияВНУ,
	|	ЛОЖЬ КАК ВедениеУчетаВременныхРазницБалансовымМетодом,
	|	ЛОЖЬ КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
	|	Реквизиты.СчетЗатратБУ,
	|	Реквизиты.СчетЗатратНУ,
	|	Реквизиты.НоменклатурнаяГруппа,
	|	Реквизиты.ПодразделениеОрганизации,
	|	Реквизиты.ОтложитьНачислениеНДС
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК Организации
	|		ПО Реквизиты.Организация = Организации.Ссылка
	|		ЛЕВОЕ СОЕДИНЕНИЕ Справочник.ДоговорыКонтрагентов КАК ДоговорыКонтрагентов
	|		ПО Реквизиты.ДоговорКонтрагента = ДоговорыКонтрагентов.Ссылка
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.СпособВыпискиАктовВыполненныхРабот КАК СпособВыпискиАктовВыполненныхРабот,
	|	Реквизиты.Организация,
	|	Реквизиты.Налогоплательщик,
	|	Реквизиты.НалогоплательщикАкциза,
	|	Реквизиты.СтруктурноеПодразделение,
	|	Реквизиты.СтруктурноеПодразделение КАК КорСтруктурноеПодразделение,
	|	Реквизиты.ГоловнаяОрганизация,
	|	Реквизиты.УчитыватьКПН,
	|	Реквизиты.ВидУчетаНУ,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ПризнакДоговорСовместнойДеятельности,
	|	Реквизиты.ПризнакУчастникСРП,
	|	Реквизиты.ВедениеВзаиморасчетов,
	|	Реквизиты.ВидДоговора,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.УчитыватьНДС,
	|	Реквизиты.СуммаВключаетНДС,
	|	Реквизиты.КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	Реквизиты.НеобходимостьОтраженияВНУ КАК НеобходимостьОтраженияВНУ,
	|	Реквизиты.ВедениеУчетаВременныхРазницБалансовымМетодом КАК ВедениеУчетаВременныхРазницБалансовымМетодом,
	|	Реквизиты.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
	|	ЕСТЬNULL(СоставДокумента.ЕстьУслуги, ЛОЖЬ) КАК ЕстьУслуги,
	|	ЕСТЬNULL(СоставДокумента.ЕстьУчастникиСовместнойДеятельности, ЛОЖЬ) КАК ЕстьУчастникиСовместнойДеятельности,
	|	Реквизиты.СчетЗатратБУ,
	|	Реквизиты.СчетЗатратНУ,
	|	Реквизиты.НоменклатурнаяГруппа,
	|	Реквизиты.ПодразделениеОрганизации,
	|	Реквизиты.ОтложитьНачислениеНДС КАК ОтложитьНачислениеНДС
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоставДокумента КАК СоставДокумента
	|		ПО (ИСТИНА)";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты)

	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьУслуги Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаУслуги", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаУслуги.Ссылка,
		|	ТаблицаУслуги.НомерСтроки,
		|	ТаблицаУслуги.Номенклатура,
		|	ТаблицаУслуги.Количество КАК Количество,
		|	ТаблицаУслуги.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма,
		|	ТаблицаУслуги.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаУслуги.СуммаНДС,
		|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаУслуги.СтавкаНДС,
		|	ТаблицаУслуги.СчетУчетаНДСПоРеализации КАК СчетУчетаНДС,
		|	ТаблицаУслуги.НДСВидОперацииРеализации,
		|	ТаблицаУслуги.СчетДоходовБУ,
		|	ТаблицаУслуги.СубконтоДоходовБУ1,
		|	ТаблицаУслуги.СубконтоДоходовБУ2,
		|	ТаблицаУслуги.СубконтоДоходовБУ3,
		|	ТаблицаУслуги.СчетДоходовНУ,
		|	ТаблицаУслуги.СубконтоДоходовНУ1,
		|	ТаблицаУслуги.СубконтоДоходовНУ2,
		|	ТаблицаУслуги.СубконтоДоходовНУ3,
		|	ТаблицаУслуги.СчетСписанияСебестоимостиБУ КАК СчетБУ,
		|	ТаблицаУслуги.СубконтоСписанияСебестоимостиБУ1 КАК СубконтоБУ1,
		|	ТаблицаУслуги.СубконтоСписанияСебестоимостиБУ2 КАК СубконтоБУ2,
		|	ТаблицаУслуги.СубконтоСписанияСебестоимостиБУ3 КАК СубконтоБУ3,
		|	ТаблицаУслуги.СчетСписанияСебестоимостиНУ КАК СчетНУ,
		|	ТаблицаУслуги.СубконтоСписанияСебестоимостиНУ1 КАК СубконтоНУ1,
		|	ТаблицаУслуги.СубконтоСписанияСебестоимостиНУ2 КАК СубконтоНУ2,
		|	ТаблицаУслуги.СубконтоСписанияСебестоимостиНУ3 КАК СубконтоНУ3,
		|	ТаблицаУслуги.СуммаПлановая  КАК СуммаПлановая
		|ПОМЕСТИТЬ ТаблицаУслуги
		|ИЗ
		|	Документ.АктОбОказанииПроизводственныхУслуг.Услуги КАК ТаблицаУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаУслуги.Ссылка = Реквизиты.Ссылка
		|ГДЕ
		|	ТаблицаУслуги.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	
	Если Реквизиты.ЕстьУчастникиСовместнойДеятельности Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаУчастникиСовместнойДеятельности", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаУчастникиСовместнойДеятельности.НомерСтроки,
		|	ТаблицаУчастникиСовместнойДеятельности.УчастникСовместнойДеятельности,
		|	ТаблицаУчастникиСовместнойДеятельности.ДоляУчастия
		|ПОМЕСТИТЬ ТаблицаУчастникиСовместнойДеятельности
		|ИЗ
		|	Документ.АктОбОказанииПроизводственныхУслуг.УчастникиСовместнойДеятельности КАК ТаблицаУчастникиСовместнойДеятельности
		|ГДЕ
		|	ТаблицаУчастникиСовместнойДеятельности.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

Процедура ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты)
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты);
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат    = Запрос.ВыполнитьПакет();
	КонецЕсли;
	
	ТекстЗапроса = "";
	Если Реквизиты.ЕстьУслуги Тогда
		СуммыТаблицыУслуги = Результат[НомераТаблиц["СуммыТаблицыУслуги"]].Выгрузить();
		УправлениеВзаиморасчетамиСервер.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыУслуги, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыУслуги", СуммыТаблицыУслуги);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеУслуги(НомераТаблиц, Реквизиты);
	КонецЕсли;
	
	Запрос.Текст = ТекстЗапроса;
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат    = Запрос.ВыполнитьПакет();
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты)

	ТекстЗапроса = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
	
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаУслуги", "ПОМЕСТИТЬ ВременнаяТаблицаУслуги");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаУслуги.Ссылка = &Ссылка", "ТаблицаУслуги.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУслуги.НомерСтроки,
		|	ВременнаяТаблицаУслуги.СтавкаНДС,
		|	ВременнаяТаблицаУслуги.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.Сумма,
		|	ВременнаяТаблицаУслуги.СуммаНДС,
		|	ВременнаяТаблицаУслуги.СуммаПлановая
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыВВалютеУслуги(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("ТаблицаУслуги", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыУслуги.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.Сумма,
	|	СуммыТаблицыУслуги.СуммаНДС
	|ПОМЕСТИТЬ СуммыТаблицыУслуги
	|ИЗ
	|	&СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаУслуги.Ссылка,
	|	ВременнаяТаблицаУслуги.НомерСтроки,
	|	ВременнаяТаблицаУслуги.Номенклатура,
	|	ВременнаяТаблицаУслуги.Количество,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.Сумма,
	|	СуммыТаблицыУслуги.СуммаНДС,
	|	ВременнаяТаблицаУслуги.СтавкаНДС,
	|	ВременнаяТаблицаУслуги.СчетУчетаНДС,
	|	ВременнаяТаблицаУслуги.НДСВидОперацииРеализации,
	|	ВременнаяТаблицаУслуги.СчетДоходовБУ,
	|	ВременнаяТаблицаУслуги.СубконтоДоходовБУ1,
	|	ВременнаяТаблицаУслуги.СубконтоДоходовБУ2,
	|	ВременнаяТаблицаУслуги.СубконтоДоходовБУ3,
	|	ВременнаяТаблицаУслуги.СчетДоходовНУ,
	|	ВременнаяТаблицаУслуги.СубконтоДоходовНУ1,
	|	ВременнаяТаблицаУслуги.СубконтоДоходовНУ2,
	|	ВременнаяТаблицаУслуги.СубконтоДоходовНУ3,
	|	ВременнаяТаблицаУслуги.СчетБУ,
	|	ВременнаяТаблицаУслуги.СубконтоБУ1,
	|	ВременнаяТаблицаУслуги.СубконтоБУ2,
	|	ВременнаяТаблицаУслуги.СубконтоБУ3,
	|	ВременнаяТаблицаУслуги.СчетНУ,
	|	ВременнаяТаблицаУслуги.СубконтоНУ1,
	|	ВременнаяТаблицаУслуги.СубконтоНУ2,
	|	ВременнаяТаблицаУслуги.СубконтоНУ3,
	|	ВременнаяТаблицаУслуги.СуммаПлановая
	|ПОМЕСТИТЬ ТаблицаУслуги
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|		ПО ВременнаяТаблицаУслуги.НомерСтроки = СуммыТаблицыУслуги.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРеализацияУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	ТекстЗапроса = "";
	Если  НЕ Реквизиты.ЕстьУслуги Тогда 
		ПараметрыПроведения.Вставить("РеализацияТаблицаДокумента", Неопределено);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРеализация", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("РеализацияТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	NULL КАК ИмяСписка,
	|	NULL КАК НомерСтроки,
	|	NULL КАК Номенклатура,
	|	NULL КАК Количество,
	|	NULL КАК Стоимость,
	|	NULL КАК Сумма,
	|	NULL КАК СуммаНДС,
	|	NULL КАК СуммаВзаиморасчетов,
	|	NULL КАК СуммаНДСВзаиморасчетов,
	|	NULL КАК СчетДоходовБУ,
	|	NULL КАК СубконтоДоходовБУ1,
	|	NULL КАК СубконтоДоходовБУ2,
	|	NULL КАК СубконтоДоходовБУ3,
	|	NULL КАК СчетДоходовНУ,
	|	NULL КАК СубконтоДоходовНУ1,
	|	NULL КАК СубконтоДоходовНУ2,
	|	NULL КАК СубконтоДоходовНУ3,
	|	NULL КАК ЭтоУслуга,
	|	NULL КАК СчетУчета,
	|	NULL КАК СуммаАкциза,
	|	NULL КАК СуммаАкцизаВзаиморасчетов,
	|	NULL КАК Склад,
	|	NULL КАК Партия
	|ПОМЕСТИТЬ ВременнаяТаблицаРеализация
	|ГДЕ
	|	ЛОЖЬ";
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ
		|	""Услуги"" КАК ИмяСписка,
		|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
		|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
		|	ТаблицаУслуги.Количество КАК Количество,
		|	0 КАК Стоимость,
		|	ТаблицаУслуги.Сумма КАК Сумма,
		|	ТаблицаУслуги.СуммаНДС КАК СуммаНДС,
		|	ТаблицаУслуги.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
		|	ТаблицаУслуги.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаУслуги.СчетДоходовБУ КАК СчетДоходовБУ,
		|	ТаблицаУслуги.СубконтоДоходовБУ1 КАК СубконтоДоходовБУ1,
		|	ТаблицаУслуги.СубконтоДоходовБУ2 КАК СубконтоДоходовБУ2,
		|	ТаблицаУслуги.СубконтоДоходовБУ3 КАК СубконтоДоходовБУ3,
		|	ТаблицаУслуги.СчетДоходовНУ КАК СчетДоходовНУ,
		|	ТаблицаУслуги.СубконтоДоходовНУ1 КАК СубконтоДоходовНУ1,
		|	ТаблицаУслуги.СубконтоДоходовНУ2 КАК СубконтоДоходовНУ2,
		|	ТаблицаУслуги.СубконтоДоходовНУ3 КАК СубконтоДоходовНУ3,
		|	Истина КАК ЭтоУслуга,
		|	НЕОПРЕДЕЛЕНО КАК СчетУчета,
		|	НЕОПРЕДЕЛЕНО КАК Склад,
		|	НЕОПРЕДЕЛЕНО КАК Партия,
		|	0 КАК СуммаАкциза,
		|	0 КАК СуммаАкцизаВзаиморасчетов
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета() + 
	"ВЫБРАТЬ
	|	ВременнаяТаблицаРеализация.ИмяСписка КАК ИмяСписка,
	|	ВременнаяТаблицаРеализация.НомерСтроки КАК НомерСтроки,
	|	ВременнаяТаблицаРеализация.Номенклатура КАК Номенклатура,
	|	ВременнаяТаблицаРеализация.Количество КАК Количество,
	|	ВременнаяТаблицаРеализация.Стоимость КАК Стоимость,
	|	ВременнаяТаблицаРеализация.Сумма КАК Сумма,
	|	ВременнаяТаблицаРеализация.СуммаНДС КАК СуммаНДС,
	|	ВременнаяТаблицаРеализация.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов,
	|	ВременнаяТаблицаРеализация.СуммаНДСВзаиморасчетов КАК СуммаНДСВзаиморасчетов,
	|	ВременнаяТаблицаРеализация.СчетДоходовБУ КАК СчетДоходовБУ,
	|	ВременнаяТаблицаРеализация.СубконтоДоходовБУ1 КАК СубконтоДоходовБУ1,
	|	ВременнаяТаблицаРеализация.СубконтоДоходовБУ2 КАК СубконтоДоходовБУ2,
	|	ВременнаяТаблицаРеализация.СубконтоДоходовБУ3 КАК СубконтоДоходовБУ3,
	|	ВременнаяТаблицаРеализация.СчетДоходовНУ КАК СчетДоходовНУ,
	|	ВременнаяТаблицаРеализация.СубконтоДоходовНУ1 КАК СубконтоДоходовНУ1,
	|	ВременнаяТаблицаРеализация.СубконтоДоходовНУ2 КАК СубконтоДоходовНУ2,
	|	ВременнаяТаблицаРеализация.СубконтоДоходовНУ3 КАК СубконтоДоходовНУ3,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	Реквизиты.ВалютаДокумента КАК ВалютаВзаиморасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК КорСчет,
	|	Реквизиты.Контрагент КАК КорСубконто1,
	|	Реквизиты.ДоговорКонтрагента КАК КорСубконто2,
	|	Реквизиты.Ссылка КАК КорСубконто3,
	|	Реквизиты.СчетЗатратБУ КАК СчетЗатратБУ,
	|	Реквизиты.СчетЗатратНУ КАК СчетЗатратНУ,
	|	Реквизиты.ПодразделениеОрганизации КАК СубконтоЗатратБУ1,
	|	Реквизиты.НоменклатурнаяГруппа КАК СубконтоЗатратБУ2,
	|	Реквизиты.ПодразделениеОрганизации КАК СубконтоЗатратНУ1,
	|	Реквизиты.НоменклатурнаяГруппа КАК СубконтоЗатратНУ2,
	|	ВременнаяТаблицаРеализация.ЭтоУслуга КАК ЭтоУслуга,
	|	ВременнаяТаблицаРеализация.СчетУчета КАК СчетУчета,
	|	ВременнаяТаблицаРеализация.Склад КАК Склад,
	|	ВременнаяТаблицаРеализация.Партия КАК Партия,
	|	ВременнаяТаблицаРеализация.СуммаАкциза КАК СуммаАкциза,
	|	ВременнаяТаблицаРеализация.СуммаАкцизаВзаиморасчетов КАК СуммаАкцизаВзаиморасчетов
	|ИЗ
	|	ВременнаяТаблицаРеализация КАК ВременнаяТаблицаРеализация
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ИмяСписка,
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСуммВзаиморасчетов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовРеквизиты",        НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ТаблицаСуммВзаиморасчетов.Сумма) КАК СуммаРегл
	|ПОМЕСТИТЬ ТаблицаСуммВзаиморасчетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК СуммаВзаиморасчетов,
	|		0 КАК Сумма
	|	ГДЕ
	|		ЛОЖЬ";
	
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = ТекстЗапроса + "
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		ТаблицаУслуги.СуммаВзаиморасчетов + ТаблицаУслуги.СуммаНДСВзаиморасчетов,
		|		ТаблицаУслуги.Сумма + ТаблицаУслуги.СуммаНДС
		|	ИЗ
		|		ТаблицаУслуги КАК ТаблицаУслуги";
	КонецЕсли;
	ТекстЗапроса = ТекстЗапроса + ") КАК ТаблицаСуммВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ВидУчетаНУ КАК ВидУчетаНУ,
	|	Реквизиты.КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов,
	|	""Выбытие"" КАК НаправлениеДвижения,
	|	&НеобходимостьОтраженияВНУ КАК НеобходимостьОтраженияВНУ,
	|	ЛОЖЬ КАК ЭтоВозврат
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетов,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора,
	|	Реквизиты.ВедениеВзаиморасчетов,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	&РасчетыВВалюте КАК РасчетыВВалюте,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВедениеВзаиморасчетов = ЗНАЧЕНИЕ(Перечисление.ВедениеВзаиморасчетовПоДоговорам.ПоРасчетнымДокументам)
	|			ТОГДА Реквизиты.Сделка
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК Сделка,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов,
	|	ТаблицаСуммВзаиморасчетов.СуммаРегл
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСуммВзаиморасчетов КАК ТаблицаСуммВзаиморасчетов
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов ЕСТЬ НЕ NULL "
	+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса
	
КонецФункции

Функция ТекстЗапросаПлановаяСтоимостьУслуг(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	НомераТаблиц.Вставить("ПлановаяСтоимостьУслугРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ПлановаяСтоимостьУслугТаблица"  , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	&НеобходимостьОтраженияВНУ КАК НеобходимостьОтраженияВНУ,
	|	&ВедениеУчетаВременныхРазницБалансовымМетодом КАК ВедениеУчетаВременныхРазницБалансовымМетодом,
	|	Реквизиты.ВидУчетаНУ КАК ВидУчетаНУ
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
	|	Реквизиты.СчетЗатратБУ КАК СчетЗатратБУ,
	|	Реквизиты.ПодразделениеОрганизации КАК СубконтоЗатратБУ1,
	|	Реквизиты.НоменклатурнаяГруппа КАК СубконтоЗатратБУ2,
	|	Реквизиты.СчетЗатратНУ КАК СчетЗатратНУ,
	|	Реквизиты.ПодразделениеОрганизации КАК СубконтоЗатратНУ1,
	|	Реквизиты.НоменклатурнаяГруппа КАК СубконтоЗатратНУ2,
	|	ТаблицаУслуги.СчетБУ КАК СчетБУ,
	|	ТаблицаУслуги.СубконтоБУ1 КАК СубконтоБУ1,
	|	ТаблицаУслуги.СубконтоБУ2 КАК СубконтоБУ2,
	|	ТаблицаУслуги.СубконтоБУ3 КАК СубконтоБУ3,
	|	ТаблицаУслуги.СчетНУ КАК СчетНУ,
	|	ТаблицаУслуги.СубконтоНУ1 КАК СубконтоНУ1,
	|	ТаблицаУслуги.СубконтоНУ2 КАК СубконтоНУ2,
	|	ТаблицаУслуги.СубконтоНУ3 КАК СубконтоНУ3,
	|	ТаблицаУслуги.Количество КАК Количество,
	|	ТаблицаУслуги.СуммаПлановая КАК СуммаПлановая,
	|	&СодержаниеУслуги КАК Содержание,
	|	ЛОЖЬ КАК ВедетсяПартионныйУчет
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	ТекстЗапроса = "";
	Если НЕ Реквизиты.УчитыватьНДС  Тогда 
		ПараметрыПроведения.Вставить("ТаблицаНДС", Неопределено);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда 
		ТекстЗапроса = ТекстЗапроса + "
		|ВЫБРАТЬ
		|	""УслугиНДС"" КАК ИмяСписка,
		|	&СинонимУслуги КАК СинонимСписка,
		|	ТаблицаУслуги.НомерСтроки,
		|	ТаблицаУслуги.Номенклатура КАК ТМЗ,
		|	ТаблицаУслуги.Сумма КАК СуммаБезНДС,
		|	ТаблицаУслуги.Сумма КАК ОборотПоРеализации,
		|	ТаблицаУслуги.СуммаНДС КАК СуммаНДС,
		|	ТаблицаУслуги.СуммаНДСВзаиморасчетов КАК СуммаНДСВал,
		|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаУслуги.НДСВидОперацииРеализации КАК ВидОперацииРеализации,
		|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК Счет,
		|	Реквизиты.Контрагент КАК Субконто1,
		|	Реквизиты.ДоговорКонтрагента КАК Субконто2,
		|	Реквизиты.Ссылка КАК Субконто3,
		|	ТаблицаУслуги.СчетУчетаНДС КАК КорСчет,
		|	ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость) КАК КорСубконто1,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог) КАК КорСубконто2,
		|	НЕОПРЕДЕЛЕНО КАК КорСубконто3,
		|	&СодержаниеВыделенНДС КАК Содержание,
		|   Значение(Перечисление.ВидыНДС.НДС) КАК ВидНалогаНДС,
		|	ЛОЖЬ КАК ЭтоАкциз
		|ИЗ
		|	ТаблицаУслуги КАК ТаблицаУслуги
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)";
		
		Если Реквизиты.ОтложитьНачислениеНДС Тогда
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаУслуги.СчетУчетаНДС КАК КорСчет,", "&СчетУчетаНДСОтложенный КАК КорСчет,");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость) КАК КорСубконто1,", "Реквизиты.Контрагент КАК КорСубконто1,");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог) КАК КорСубконто2,", "Реквизиты.Ссылка КАК КорСубконто2,");
			ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "&СодержаниеВыделенНДС КАК Содержание,", "&СодержаниеОтложенНДС КАК Содержание,");
		КонецЕсли;
		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда 
		НомераТаблиц.Вставить("ТаблицаНДС",	НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса + "
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки" + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	Иначе 
		ПараметрыПроведения.Вставить("ТаблицаНДС", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаУчастникиСовместнойДеятельности(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьУчастникиСовместнойДеятельности Тогда 
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаУчастникиСовместнойДеятельности.НомерСтроки,
		|	ТаблицаУчастникиСовместнойДеятельности.УчастникСовместнойДеятельности КАК УчастникСовместнойДеятельности,
		|	ТаблицаУчастникиСовместнойДеятельности.ДоляУчастия КАК ДоляУчастия
		|ИЗ
		|	ТаблицаУчастникиСовместнойДеятельности КАК ТаблицаУчастникиСовместнойДеятельности
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки" + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		НомераТаблиц.Вставить("ТаблицаУчастникиСовместнойДеятельности",	НомераТаблиц.Количество());
	Иначе
		ПараметрыПроведения.Вставить("ТаблицаУчастникиСовместнойДеятельности", Неопределено);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаВыпускУслугБУ(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	НомераТаблиц.Вставить("ВыпускУслугТаблицаБУ"  , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУслуги.Номенклатура КАК Продукция,
	|	Реквизиты.СчетЗатратБУ КАК СчетЗатрат,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ТаблицаУслуги.СчетБУ КАК СчетСписания,
	|	ТаблицаУслуги.СубконтоБУ1 КАК СубконтоСписания1,
	|	ТаблицаУслуги.СубконтоБУ2 КАК СубконтоСписания2,
	|	ТаблицаУслуги.СубконтоБУ3 КАК СубконтоСписания3,
	|	ТаблицаУслуги.Количество КАК Количество,
	|	ТаблицаУслуги.СуммаПлановая КАК ПлановаяСтоимость
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаВыпускУслугНУ(НомераТаблиц, ПараметрыПроведения, Реквизиты)

	НомераТаблиц.Вставить("ВыпускУслугТаблицаНУ"  , НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
	|	ТаблицаУслуги.Номенклатура КАК Продукция,
	|	Реквизиты.СчетЗатратНУ КАК СчетЗатрат,
	|	Реквизиты.ВидУчетаНУ КАК ВидУчета,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|	ТаблицаУслуги.СчетНУ КАК СчетСписания,
	|	Реквизиты.ПодразделениеОрганизации КАК Подразделение,
	|	ТаблицаУслуги.СубконтоНУ1 КАК СубконтоСписания1,
	|	ТаблицаУслуги.СубконтоНУ2 КАК СубконтоСписания2,
	|	ТаблицаУслуги.СубконтоНУ3 КАК СубконтоСписания3,
	|	ТаблицаУслуги.Количество КАК Количество,
	|	ТаблицаУслуги.СуммаПлановая КАК ПлановаяСтоимость
	|ИЗ
	|	ТаблицаУслуги КАК ТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Процедура ДобавитьКолонкуСодержание(ТаблицаЗначений, ТекстСодержания = "") Экспорт
	
	Если ТаблицаЗначений = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если ТаблицаЗначений.Колонки.Найти("Содержание") = Неопределено Тогда
		ТаблицаЗначений.Колонки.Добавить("Содержание", ОбщегоНазначенияБККлиентСервер.ПолучитьОписаниеТиповСтроки(50));
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ТекстСодержания) Тогда 
		ТекстСодержания = НСтр("ru='Выручка от реализации'", ОбщегоНазначения.КодОсновногоЯзыка());
	КонецЕсли;
	
	Для каждого СтрокаТаблицы Из ТаблицаЗначений Цикл
		
		СтрокаТаблицы.Содержание = ТекстСодержания;
		
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Акт об оказании услуг
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьАктаОбОказанииУслуг";
	КомандаПечати.Представление = НСтр("ru = 'Акт об оказании услуг'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.Порядок = 50;
	
	// Р-1 (акт выполненных работ)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьР1";
	КомандаПечати.Представление = НСтр("ru = 'Р-1 (акт выполненных работ)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.Порядок = 51;
	
	// Р1 с колонками НДС
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьР1_НДС";
	КомандаПечати.Представление = НСтр("ru = 'Р-1 (акт выполненных работ, с НДС)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.Порядок = 52;
	
	// Счет-фактура
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактураВыданный";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБККлиент.ВыполнитьКомандуПечатиСчетаФактуры";
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.Порядок = 54;
	
	// Счет-фактура в валюте регл. учета
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "СчетФактураВыданныйВВалютеРеглУчета";
	КомандаПечати.Представление = НСтр("ru = 'Счет-фактура (в валюте регл. учета)'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБККлиент.ВыполнитьКомандуПечатиСчетаФактуры";
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.Порядок = 55;
	КомандаПечати.ФункциональныеОпции = "ИспользоватьВалютныйУчет";
	
	Если ПравоДоступа("Использование", Метаданные.Обработки.ПечатьРеглСуммДокументовВВалюте) Тогда
		// Справка-расчет "Регл. суммы документа в валюте"
		КомандаПечати = КомандыПечати.Добавить();
		КомандаПечати.МенеджерПечати = "Обработка.ПечатьРеглСуммДокументовВВалюте";
		КомандаПечати.Идентификатор = "РеглСуммыДокументаВВалюте";
		КомандаПечати.Представление = НСтр("ru = 'Справка-расчет ""Регл. суммы документа в валюте""'");
		КомандаПечати.Обработчик    = "УправлениеПечатьюБККлиент.ВыполнитьКомандуПечати";
		КомандаПечати.ФункциональныеОпции = "ИспользоватьВалютныйУчет";
		КомандаПечати.ДополнительныеПараметры.Вставить("НеВыводитьВКомплекте",Истина);
		КомандаПечати.Порядок = 56;
	КонецЕсли;
	
	// Комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьАктаОбОказанииУслуг,ПечатьР1,ПечатьР1_НДС,СчетФактураВыданный,СчетФактураВыданныйВВалютеРеглУчета";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.ФиксированныйКомплект = Истина;
	КомандаПечати.ПереопределитьПользовательскиеНастройкиКоличества = Истина;
	КомандаПечати.Порядок = 75;
	
	// Комплект документов (на принтер)
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьАктаОбОказанииУслуг,ПечатьР1,ПечатьР1_НДС,СчетФактураВыданный,СчетФактураВыданныйВВалютеРеглУчета";
	КомандаПечати.Представление = НСтр("ru = 'Комплект документов (на принтер)'");
	КомандаПечати.Картинка = БиблиотекаКартинок.ПечатьСразу;
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.ФиксированныйКомплект = Истина;
	КомандаПечати.ПереопределитьПользовательскиеНастройкиКоличества = Истина;
	КомандаПечати.СразуНаПринтер = Истина;
	КомандаПечати.Порядок = 76;
	
	// Настраиваемый комплект документов
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "ПечатьАктаОбОказанииУслуг,ПечатьР1,ПечатьР1_НДС,СчетФактураВыданный,СчетФактураВыданныйВВалютеРеглУчета";
	КомандаПечати.Представление = НСтр("ru = 'Настраиваемый комплект документов'");
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.ЗаголовокФормы = НСтр("ru = 'Настраиваемый комплект'");
	КомандаПечати.ДополнитьКомплектВнешнимиПечатнымиФормами = Истина;
	КомандаПечати.Порядок = 77;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьАктаОбОказанииУслуг") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПечатьАктаОбОказанииУслуг",
			НСтр("ru = 'Акт об оказании услуг'"),
			ПечатьАктаОбОказанииУслуг(МассивОбъектов, ОбъектыПечати, ПараметрыВывода.КодЯзыка),
			,
			"Документ.АктОбОказанииПроизводственныхУслуг.ПФ_MXL_Акт");
		
		ОписаниеПечатнойФормы = КоллекцияПечатныхФорм.Найти(ВРег("ПечатьАктаОбОказанииУслуг"), "ИмяВРЕГ");
		Если ОписаниеПечатнойФормы <> Неопределено Тогда
			ОписаниеПечатнойФормы.ДоступенВыводНаДругихЯзыках = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// Печать формы Р-1
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьР1") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПечатьР1",
			НСтр("ru = 'Р-1 (акт выполненных работ)'"),
			ПечатьР1(МассивОбъектов, ОбъектыПечати),
			,
			"ОбщийМакет.ПФ_MXL_Р1");
	КонецЕсли;
	
	// Печать формы Р-1 с колонками НДС
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "ПечатьР1_НДС") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"ПечатьР1_НДС",
			НСтр("ru = 'Р-1 (акт выполненных работ, с НДС)'"),
			ПечатьР1(МассивОбъектов, ОбъектыПечати, Истина),
			,
			"ОбщийМакет.ПФ_MXL_Р1");
	КонецЕсли;
	
	// Печать счета-фактуры
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактураВыданный") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СчетФактураВыданный",
			НСтр("ru = 'Счет-фактура'"),
			ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати, Ложь, ПараметрыВывода.КодЯзыка),
			,
			"Документ.СчетФактураВыданный.ПФ_MXL_СчетФактура");
		
		ОписаниеПечатнойФормы = КоллекцияПечатныхФорм.Найти(ВРег("СчетФактураВыданный"), "ИмяВРЕГ");
		Если ОписаниеПечатнойФормы <> Неопределено Тогда
			ОписаниеПечатнойФормы.ДоступенВыводНаДругихЯзыках = Истина;
		КонецЕсли;
		
	КонецЕсли;
	
	// Печать счета-фактуры
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "СчетФактураВыданныйВВалютеРеглУчета") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"СчетФактураВыданныйВВалютеРеглУчета",
			НСтр("ru = 'Счет-фактура (в валюте регл. учета)'"),
			ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати, Истина, ПараметрыВывода.КодЯзыка),
			,
			"Документ.СчетФактураВыданный.ПФ_MXL_СчетФактура");
		
		ОписаниеПечатнойФормы = КоллекцияПечатныхФорм.Найти(ВРег("СчетФактураВыданныйВВалютеРеглУчета"), "ИмяВРЕГ");
		Если ОписаниеПечатнойФормы <> Неопределено Тогда
			ОписаниеПечатнойФормы.ДоступенВыводНаДругихЯзыках = Истина;
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Подготовка табличных печатных документов.

Функция ПечатьАктаОбОказанииУслуг(МассивОбъектов, ОбъектыПечати, КодЯзыка = Неопределено) Экспорт

	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов" , МассивОбъектов);
	Запрос.УстановитьПараметр("парамУволен", Перечисления.ПричиныИзмененияСостояния.Увольнение);

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка,
	|	АктОбОказанииПроизводственныхУслуг.Номер,
	|	АктОбОказанииПроизводственныхУслуг.Дата,
	|	АктОбОказанииПроизводственныхУслуг.ДоговорКонтрагента КАК Основание,
	|	АктОбОказанииПроизводственныхУслуг.Контрагент,
	|	АктОбОказанииПроизводственныхУслуг.Организация,
	|	АктОбОказанииПроизводственныхУслуг.СтруктурноеПодразделение,
	|	АктОбОказанииПроизводственныхУслуг.СуммаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.ВалютаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.УчитыватьНДС,
	|	АктОбОказанииПроизводственныхУслуг.СуммаВключаетНДС,
	|	АктОбОказанииПроизводственныхУслуг.Ответственный.ФизЛицо КАК ФИООтветственного,
	|	АктОбОказанииПроизводственныхУслуг.БанковскийСчетОрганизации
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг КАК АктОбОказанииПроизводственныхУслуг
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(РаботникиОрганизации.Период) КАК Период,
	|	РаботникиОрганизации.Организация КАК Организация,
	|	РаботникиОрганизации.Сотрудник.Физлицо КАК ФизЛицо,
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата КАК Дата
	|ПОМЕСТИТЬ ВТ_ПериодыРаботников
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокумента КАК ДанныеДокумента
	|		ПО РаботникиОрганизации.Организация = ДанныеДокумента.Организация
	|			И РаботникиОрганизации.Период <= ДанныеДокумента.Дата
	|			И (ДанныеДокумента.ФИООтветственного = РаботникиОрганизации.Сотрудник.Физлицо)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.Сотрудник.Физлицо,
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРаботников.Ссылка КАК Ссылка,
	|	ПериодыРаботников.Организация КАК Организация,
	|	ПериодыРаботников.ФизЛицо,
	|	РаботникиОрганизации.Должность КАК ДолжностьОтветственного
	|ПОМЕСТИТЬ ВТ_ПериодыДолжности
	|ИЗ
	|	ВТ_ПериодыРаботников КАК ПериодыРаботников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО ПериодыРаботников.Период = РаботникиОрганизации.Период
	|			И ПериодыРаботников.Организация = РаботникиОрганизации.Организация
	|			И ПериодыРаботников.ФизЛицо = РаботникиОрганизации.Сотрудник.Физлицо
	|           И  РаботникиОрганизации.ПричинаИзмененияСостояния <> &парамУволен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ 
	|	АктОбОказанииПроизводственныхУслуг.Ссылка,
	|	АктОбОказанииПроизводственныхУслуг.Номер,
	|	АктОбОказанииПроизводственныхУслуг.Дата,
	|	АктОбОказанииПроизводственныхУслуг.Основание,
	|	АктОбОказанииПроизводственныхУслуг.Контрагент,
	|	АктОбОказанииПроизводственныхУслуг.Организация,
	|	АктОбОказанииПроизводственныхУслуг.СтруктурноеПодразделение,
	|	АктОбОказанииПроизводственныхУслуг.СуммаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.ВалютаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.УчитыватьНДС,
	|	АктОбОказанииПроизводственныхУслуг.СуммаВключаетНДС,
	|	АктОбОказанииПроизводственныхУслуг.ФИООтветственного,
	|	ВТ_ПериодыДолжности.ДолжностьОтветственного КАК ДолжностьОтветственного,
	|	АктОбОказанииПроизводственныхУслуг.БанковскийСчетОрганизации
	|ИЗ
	|	ДанныеДокумента КАК АктОбОказанииПроизводственныхУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыДолжности КАК ВТ_ПериодыДолжности
	|		ПО АктОбОказанииПроизводственныхУслуг.Ссылка = ВТ_ПериодыДолжности.Ссылка";	

	ДанныеДокументов = Запрос.Выполнить().Выбрать();

	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АктОбОказанииПроизводственныхУслуг.Номенклатура КАК Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ АктОбОказанииПроизводственныхУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ВЫРАЗИТЬ(АктОбОказанииПроизводственныхУслуг.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		ИНАЧЕ АктОбОказанииПроизводственныхУслуг.Номенклатура.Наименование
	|	КОНЕЦ КАК Товар,
	|	АктОбОказанииПроизводственныхУслуг.Количество,
	|	АктОбОказанииПроизводственныхУслуг.ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|	АктОбОказанииПроизводственныхУслуг.Цена,
	|	АктОбОказанииПроизводственныхУслуг.Сумма КАК Сумма,
	|	АктОбОказанииПроизводственныхУслуг.СтавкаНДС,
	|	АктОбОказанииПроизводственныхУслуг.СуммаНДС КАК СуммаНДС,
	|	АктОбОказанииПроизводственныхУслуг.Ссылка КАК Ссылка
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг.Услуги КАК АктОбОказанииПроизводственныхУслуг
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка В(&МассивОбъектов)
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаНДС)
	|ПО
	|	Ссылка";
	
	ВыборкаУслугиИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкам);
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.КлючПараметровПечати = "АктОбОказанииПроизводственныхУслуг_Акт";

	Макет       = УправлениеПечатью.МакетПечатнойФормы("Документ.АктОбОказанииПроизводственныхУслуг.ПФ_MXL_Акт", КодЯзыка);

	ОбластьЗаголовок 		= Макет.ПолучитьОбласть("Заголовок");
	ОбластьПоставщик 		= Макет.ПолучитьОбласть("Поставщик");
	ОбластьПокупатель 		= Макет.ПолучитьОбласть("Покупатель");
	ОбластьОснование		= Макет.ПолучитьОбласть("Основание");
	ОбластьШапкаТаблицы 	= Макет.ПолучитьОбласть("ШапкаТаблицы");
	ОбластьСтрокаТаблицы 	= Макет.ПолучитьОбласть("Строка");
	ОбластьИтого 			= Макет.ПолучитьОбласть("Итого");
	ОбластьИтогоНДС 		= Макет.ПолучитьОбласть("ИтогоНДС");
	ОбластьСуммаПрописью 	= Макет.ПолучитьОбласть("СуммаПрописью");
	ОбластьПодписи 			= Макет.ПолучитьОбласть("Подписи");
	
	Пока ДанныеДокументов.Следующий() Цикл 
		
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;

		СтруктурнаяЕдиницаОрганизация = ОбщегоНазначенияБК.ПолучитьСтруктурнуюЕдиницу(ДанныеДокументов.Организация, ДанныеДокументов.СтруктурноеПодразделение);
		
		ОбластьЗаголовок.Параметры.ТекстЗаголовка = РаботаСДиалогами.СформироватьЗаголовокДокумента(ДанныеДокументов.Ссылка, НСтр("ru = 'Акт'", КодЯзыка));
		ТабДокумент.Вывести(ОбластьЗаголовок);
		
		Если НЕ ЗначениеЗаполнено(ДанныеДокументов.БанковскийСчетОрганизации) Тогда
			СведенияОбОрганизации = ОбщегоНазначенияБКВызовСервера.СведенияОЮрФизЛице(СтруктурнаяЕдиницаОрганизация, ДанныеДокументов.Дата);
		Иначе
			СведенияОбОрганизации = ОбщегоНазначенияБКВызовСервера.СведенияОЮрФизЛице(СтруктурнаяЕдиницаОрганизация, ДанныеДокументов.Дата, , ДанныеДокументов.БанковскийСчетОрганизации);
		КонецЕсли;	
		
		ПредставлениеПоставщика = ОбщегоНазначенияБКВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,");
		ОбластьПоставщик.Параметры.ПредставлениеПоставщика = СокрЛП(ПредставлениеПоставщика);
		ОбластьПоставщик.Параметры.Поставщик = СтруктурнаяЕдиницаОрганизация;
		ТабДокумент.Вывести(ОбластьПоставщик);

		СведенияОПокупателе = ОбщегоНазначенияБКВызовСервера.СведенияОЮрФизЛице(ДанныеДокументов.Контрагент, ДанныеДокументов.Дата);
		ПредставлениеПолучателя = ОбщегоНазначенияБКВызовСервера.ОписаниеОрганизации(СведенияОПокупателе, "ПолноеНаименование,");
		ОбластьПокупатель.Параметры.ПредставлениеПолучателя = СокрЛП(ПредставлениеПолучателя);
		ОбластьПокупатель.Параметры.Получатель = ДанныеДокументов.Контрагент;
		ТабДокумент.Вывести(ОбластьПокупатель);
		
		Если ЗначениеЗаполнено(ДанныеДокументов.Основание) Тогда 
			ОбластьОснование.Параметры.Заполнить(ДанныеДокументов);
			ТабДокумент.Вывести(ОбластьОснование);
		КонецЕсли;	
		
		ТабДокумент.Вывести(ОбластьШапкаТаблицы);
		НомерСтроки = 0;

		ВыборкаУслугиИтоги.Сбросить();
		
		Если ВыборкаУслугиИтоги.НайтиСледующий(ДанныеДокументов.Ссылка) Тогда
			ВыборкаСтрокУслуги = ВыборкаУслугиИтоги.Выбрать();
		Иначе
			ВыборкаСтрокУслуги = Неопределено;
		КонецЕсли;
		
		Сумма    = 0;
		СуммаНДС = 0;
		
		Если ВыборкаСтрокУслуги <> Неопределено Тогда 
			
			Пока ВыборкаСтрокУслуги.Следующий() Цикл

				НомерСтроки = НомерСтроки + 1;
				
				ОбластьСтрокаТаблицы.Параметры.Заполнить(ВыборкаСтрокУслуги);
				ОбластьСтрокаТаблицы.Параметры.НомерСтроки = НомерСтроки;
		        ОбластьСтрокаТаблицы.Параметры.Товар = СокрЛП(ВыборкаСтрокУслуги.Товар);
				
				ТабДокумент.Вывести(ОбластьСтрокаТаблицы);

			КонецЦикла;
			
			Сумма    = ВыборкаУслугиИтоги.Сумма;
			СуммаНДС = ВыборкаУслугиИтоги.СуммаНДС;
			
		КонецЕсли;

		ОбластьИтого.Параметры.Всего = ОбщегоНазначенияБКВызовСервера.ФорматСумм(Сумма);
		ТабДокумент.Вывести(ОбластьИтого);

		Если ДанныеДокументов.УчитыватьНДС Тогда

			ОбластьИтогоНДС.Параметры.ВсегоНДС = ОбщегоНазначенияБКВызовСервера.ФорматСумм(СуммаНДС);
			ОбластьИтогоНДС.Параметры.НДС      = ?(ДанныеДокументов.СуммаВключаетНДС, НСтр("ru='В том числе НДС'", КодЯзыка), НСтр("ru='Сумма НДС'", КодЯзыка));
			ТабДокумент.Вывести(ОбластьИтогоНДС);

		КонецЕсли;

		СуммаКПрописи = Сумма + ?(ДанныеДокументов.СуммаВключаетНДС, 0, СуммаНДС);
		ИтоговаяСтрока= НСтр("ru='Всего оказано услуг %1, на сумму %2'", КодЯзыка);
		ОбластьСуммаПрописью.Параметры.ИтоговаяСтрока = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ИтоговаяСтрока,
																НомерСтроки,
		                                       					ОбщегоНазначенияБКВызовСервера.ФорматСумм(СуммаКПрописи, ДанныеДокументов.ВалютаДокумента));
		
		ОбластьСуммаПрописью.Параметры.СуммаПрописью  = ОбщегоНазначенияБКВызовСервера.СформироватьСуммуПрописью(СуммаКПрописи, ДанныеДокументов.ВалютаДокумента, КодЯзыка);
		ТабДокумент.Вывести(ОбластьСуммаПрописью);

		ОбластьПодписи.Параметры.Организация              = СведенияОбОрганизации.ПолноеНаименование;
		ОбластьПодписи.Параметры.РНН_БИНОрганизации       = ОбщегоНазначенияБК.ПолучитьРегистрационныйНомерОрганизацииКонтрагентаВПечатнуюФорму(СведенияОбОрганизации, ДанныеДокументов.Дата);
		ОбластьПодписи.Параметры.АдресОрганизации         = СведенияОбОрганизации.ЮридическийАдрес;
		ОбластьПодписи.Параметры.РасчетныйСчетОрганизации = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 в %2, %3'", КодЯзыка),
																	СведенияОбОрганизации.НомерСчета, 
																	СведенияОбОрганизации.Банк, 
																	СведенияОбОрганизации.АдресБанка);
														 

	   	ОбластьПодписи.Параметры.БИКОрганизации           = СведенияОбОрганизации.БИК;													 
		Если ТипЗнч(ДанныеДокументов.ФИООтветственного) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ДанныеОтветственного	= ПроцедурыУправленияПерсоналомВызовСервера.ДанныеФизЛица(ДанныеДокументов.Организация, ДанныеДокументов.ФИООтветственного, ДанныеДокументов.Дата); 
			ОтветственныйДляПечати  = ДанныеОтветственного.Представление;
			ДолжностьОтветственного = ДанныеОтветственного.Должность;
		Иначе
			ОтветственныйДляПечати = ДанныеДокументов.ФИООтветственного;
		КонецЕсли;
		ОбластьПодписи.Параметры.ДолжностьОтветственного  = ДолжностьОтветственного;
		ОбластьПодписи.Параметры.ФИООтветственного        = ОтветственныйДляПечати;
		
		ОбластьПодписи.Параметры.Контрагент               = СведенияОПокупателе.ПолноеНаименование;
		ОбластьПодписи.Параметры.РНН_БИНКонтрагента       = ОбщегоНазначенияБК.ПолучитьРегистрационныйНомерОрганизацииКонтрагентаВПечатнуюФорму(СведенияОПокупателе, ДанныеДокументов.Дата);
		ОбластьПодписи.Параметры.АдресКонтрагента         = СведенияОПокупателе.ЮридическийАдрес;
		ОбластьПодписи.Параметры.РасчетныйСчетКонтрагента = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='%1 в %2, %3'", КодЯзыка),
																	СведенияОПокупателе.НомерСчета, 
																	СведенияОПокупателе.Банк, 
																	СведенияОПокупателе.АдресБанка);
														 
	   	ОбластьПодписи.Параметры.БИККонтрагента           = СведенияОПокупателе.БИК;	
		
		ТабДокумент.Вывести(ОбластьПодписи);

		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);
		
		//KaspiQR
		МакетНастроенДляВыводаKaspiQR = Истина;
		Попытка
			РисунокКолонтитул = ТабДокумент.Рисунки.КолонтитулKaspi;
			РисунокКод = ТабДокумент.Рисунки.QRKaspi;
		Исключение
			МакетНастроенДляВыводаKaspiQR = Ложь;
		КонецПопытки;
		Если МакетНастроенДляВыводаKaspiQR Тогда
			ОбщиеДанныеНастроек = QRКод_ГенерацияКода.СохраненныеРеквизитыКода(ДанныеДокументов.Организация, ДанныеДокументов.СтруктурноеПодразделение);
			Если ОбщиеДанныеНастроек.Количество() > 0 Тогда
				ДанныеКомпании = ОбщиеДанныеНастроек[0];
				Если ДанныеКомпании.Свойство("ВыводитьQRКод") И ДанныеКомпании.ВыводитьQRКод = Истина Тогда
					
					НомерДокументаДляПечати = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеДокументов.Номер, ДанныеДокументов.Ссылка);
					
					ОтветСервиса = QRКод_ГенерацияКода.РезультатЗапросаСервиса(СведенияОбОрганизации.БИН_ИИН,
																			КодироватьСтроку(НомерДокументаДляПечати, СпособКодированияСтроки.КодировкаURL),
																			СуммаКПрописи,
																			ДанныеКомпании);
					
					Если ТипЗнч(ОтветСервиса) = Тип("ДвоичныеДанные") Тогда
						// сдвиг текста заголовка
						ТабДокумент.Область(2, 2, 2, 34).Разъединить();
						ТабДокумент.Область(2, 2, 2, 29).Объединить();
						ТабДокумент.Область(2,31, 2, 34).Обвести();
						// сдвиг предлставления поставщика
						ТабДокумент.Область(4, 6, 4, 34).Разъединить();
						ТабДокумент.Область(4, 6, 4, 29).Объединить();
						// сдвиг представления получателя
						ТабДокумент.Область(6, 6, 6, 34).Разъединить();
						ТабДокумент.Область(6, 6, 6, 29).Объединить();
						// вывод и выравнивание картинок
						РисунокКолонтитул.Картинка = БиблиотекаКартинок.QRКод_Колонтитул;
						РисунокКолонтитул.Линия    = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
						РисунокКолонтитул.Высота   = РисунокКолонтитул.Ширина/4;
						РисунокКод.Картинка = Новый Картинка(ОтветСервиса);
						РисунокКод.Линия    = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
						РисунокКод.Верх = РисунокКолонтитул.Верх + РисунокКолонтитул.Высота + 1;
						РисунокКод.Лево = РисунокКолонтитул.Лево;
						РисунокКод.Ширина = РисунокКолонтитул.Ширина;
						РисунокКод.Высота = РисунокКолонтитул.Ширина;
					Иначе
						ТабДокумент.Рисунки.Удалить(РисунокКолонтитул);
						ТабДокумент.Рисунки.Удалить(РисунокКод);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОтветСервиса);
					КонецЕсли;
				Иначе
					ТабДокумент.Рисунки.Удалить(РисунокКолонтитул);
					ТабДокумент.Рисунки.Удалить(РисунокКод);
				КонецЕсли;
			Иначе
				ТабДокумент.Рисунки.Удалить(РисунокКолонтитул);
				ТабДокумент.Рисунки.Удалить(РисунокКод);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТабДокумент; 	
	
КонецФункции

Функция ПечатьР1(МассивОбъектов, ОбъектыПечати, КолонкиНДС = Ложь) Экспорт
	
	ДанныеДляПечати = ПолучитьДанныеДляПечатиР1(МассивОбъектов);
	
	ДанныеДокументов = ДанныеДляПечати.ДанныеДокументов;
	ВыборкаСтрокУслугиИтоги = ДанныеДляПечати.ВыборкаСтрокУслугиИтоги;
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_Р1"); 
	
	КодЯзыка = Метаданные.Языки.Русский.КодЯзыка;
	Макет.КодЯзыка =  КодЯзыка;

	// Получаем области макета для вывода в табличный документ
	Шапка            = Макет.ПолучитьОбласть("Шапка");
	Запасы 			 = Макет.ПолучитьОбласть("Запасы");
	Подвал 			 = Макет.ПолучитьОбласть("Подвал");	
	
	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.КлючПараметровПечати = "АктОбОказанииПроизводственныхУслуг_Р1";
	
	Пока ДанныеДокументов.Следующий() Цикл 
		
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		НомерСтрокиНачало = ТабДокумент.ВысотаТаблицы + 1;
		
		УчитыватьНДС_     = ДанныеДокументов.УчитыватьНДС;
		СуммаВключаетНДС_ = ДанныеДокументов.СуммаВключаетНДС;
		
		Если КолонкиНДС И УчитыватьНДС_ И (НЕ СуммаВключаетНДС_) Тогда 
			ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицыСНДС");
			СтрокаТаблицы    = Макет.ПолучитьОбласть("СтрокаТаблицыСНДС");
			Итого 			 = Макет.ПолучитьОбласть("ИтогоСНДС");
		ИначеЕсли КолонкиНДС И УчитыватьНДС_ И СуммаВключаетНДС_ Тогда 
			ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицыВклНДС");
			СтрокаТаблицы    = Макет.ПолучитьОбласть("СтрокаТаблицыВклНДС");
			Итого 			 = Макет.ПолучитьОбласть("ИтогоВклНДС");
		Иначе 
			ЗаголовокТаблицы = Макет.ПолучитьОбласть("ЗаголовокТаблицы");
			СтрокаТаблицы    = Макет.ПолучитьОбласть("СтрокаТаблицы");
			Итого 			 = Макет.ПолучитьОбласть("Итого");
		КонецЕсли;
		
		СтруктурнаяЕдиницаОрганизация = ОбщегоНазначенияБК.ПолучитьСтруктурнуюЕдиницу(ДанныеДокументов.Организация, ДанныеДокументов.СтруктурноеПодразделение);

		СведенияОбОрганизации = ОбщегоНазначенияБКВызовСервера.СведенияОЮрФизЛице(СтруктурнаяЕдиницаОрганизация, ДанныеДокументов.Дата);
		
		Руководители 		  = ОбщегоНазначенияБКВызовСервера.ОтветственныеЛицаОрганизаций(СтруктурнаяЕдиницаОрганизация, ДанныеДокументов.Дата);
		
		Шапка.Параметры.Заполнить(ДанныеДокументов);
		
		// Организация-исполнитель
		ПредставлениеИсполнителя 				 = ОбщегоНазначенияБКВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "ПолноеНаименование,ЮридическийАдрес,Телефоны,", , ДанныеДокументов.Дата, "ru");
		Шапка.Параметры.ПредставлениеИсполнителя = ПредставлениеИсполнителя;
		Шапка.Параметры.ОрганизацияРНН_БИН		 = ОбщегоНазначенияБКВызовСервера.ОписаниеОрганизации(СведенияОбОрганизации, "БИН_ИИН,", Ложь, ДанныеДокументов.Дата, "ru");
		
		// Организация-сдатчик
		СведенияОбОрганизацииЗаказчике 		   = ОбщегоНазначенияБКВызовСервера.СведенияОЮрФизЛице(ДанныеДокументов.Контрагент, ДанныеДокументов.Дата);
		ПредставлениеЗаказчика				   = ОбщегоНазначенияБКВызовСервера.ОписаниеОрганизации(СведенияОбОрганизацииЗаказчике, "ПолноеНаименование,ЮридическийАдрес,Телефоны,", , ДанныеДокументов.Дата, "ru");
		Шапка.Параметры.ПредставлениеЗаказчика = ПредставлениеЗаказчика;
		Шапка.Параметры.КонтрагентРНН_БИН	   = ОбщегоНазначенияБКВызовСервера.ОписаниеОрганизации(СведенияОбОрганизацииЗаказчике, "БИН_ИИН,", Ложь, ДанныеДокументов.Дата, "ru");
		
		//Номер, Дата
		Шапка.Параметры.ДатаДокумента  = Формат(ДанныеДокументов.Дата,"Л=" + КодЯзыка + "; ДЛФ=Д");
		Шапка.Параметры.НомерДокумента = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеДокументов.Номер, ДанныеДокументов.Ссылка); 
		
		//   Договор 
		Шапка.Параметры.ДокументОснование = ДанныеДокументов.Основание;
		
		ТабДокумент.Вывести(Шапка);
		
		//Заголовок таблицы
		Если КолонкиНДС И УчитыватьНДС_ Тогда 
			ЗаголовокТаблицы.Параметры.Валюта = ДанныеДокументов.ВалютаДокумента;
		КонецЕсли;
		ТабДокумент.Вывести(ЗаголовокТаблицы);
		
		ИтогоКоличество = 0;
		ИтогоСумма	    = 0;
		ИтогоСуммаНДС   = 0;
		ИтогоСуммаСНДС	= 0;
		
		НомерСтроки = 1;
		
		ПрибавлятьНДС = УчитыватьНДС_ И (НЕ СуммаВключаетНДС_);
		
		ВыборкаСтрокУслугиИтоги.Сбросить();
		
		Если ВыборкаСтрокУслугиИтоги.НайтиСледующий(ДанныеДокументов.Ссылка) Тогда
			ВыборкаСтрокУслуги = ВыборкаСтрокУслугиИтоги.Выбрать();
		Иначе
			ВыборкаСтрокУслуги = Неопределено;
		КонецЕсли;
		
		Если ВыборкаСтрокУслуги <> Неопределено Тогда 
			Пока ВыборкаСтрокУслуги.Следующий() Цикл

				Количество = ВыборкаСтрокУслуги.Количество;
				
				Если Не ОбщегоНазначения.ПроверитьВыводТабличногоДокумента(ТабДокумент, СтрокаТаблицы) Тогда
					ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
					ТабДокумент.Вывести(ЗаголовокТаблицы);
				КонецЕсли;
				
				СтрокаТаблицы.Параметры.Заполнить(ВыборкаСтрокУслуги);
				
				Если КолонкиНДС Тогда
					
					Если УчитыватьНДС_ Тогда 
						СтрокаТаблицы.Параметры.СуммаНДС = ВыборкаСтрокУслуги.СуммаНДС;
						ИтогоСуммаНДС = ИтогоСуммаНДС + ВыборкаСтрокУслуги.СуммаНДС;
					КонецЕсли;
					
					Если УчитыватьНДС_ И (НЕ СуммаВключаетНДС_) Тогда 
						СуммаСНДС = ?(ПрибавлятьНДС, ВыборкаСтрокУслуги.Сумма + ВыборкаСтрокУслуги.СуммаНДС, ВыборкаСтрокУслуги.Сумма);
						СтрокаТаблицы.Параметры.СуммаСНДС = СуммаСНДС;
						ИтогоСуммаСНДС = ИтогоСуммаСНДС + СуммаСНДС;
					КонецЕсли;	
					
					ИтогоСумма = ИтогоСумма + ВыборкаСтрокУслуги.Сумма;
					
				Иначе
					
					Если УчитыватьНДС_ И НЕ СуммаВключаетНДС_ Тогда
						СтрокаТаблицы.Параметры.Цена = Окр((ВыборкаСтрокУслуги.Сумма + ВыборкаСтрокУслуги.СуммаНДС) / ВыборкаСтрокУслуги.Количество, 2) ;
						СтрокаТаблицы.Параметры.Сумма = ВыборкаСтрокУслуги.Сумма + ВыборкаСтрокУслуги.СуммаНДС;
						ИтогоСумма = ИтогоСумма + ВыборкаСтрокУслуги.Сумма + ВыборкаСтрокУслуги.СуммаНДС;
					Иначе 
						СтрокаТаблицы.Параметры.Сумма = ВыборкаСтрокУслуги.Сумма;
						ИтогоСумма = ИтогоСумма + ВыборкаСтрокУслуги.Сумма;
					КонецЕсли;
					
				КонецЕсли;
					
				СтрокаТаблицы.Параметры.НомерПП 					 = НомерСтроки;
				СтрокаТаблицы.Параметры.Наименование				 = СокрЛП(ВыборкаСтрокУслуги.Содержание);
				СтрокаТаблицы.Параметры.ЕдиницаИзмеренияНаименование = ВыборкаСтрокУслуги.ЕдиницаИзмерения;
				
				ИтогоКоличество = ИтогоКоличество + Количество;
				
				ТабДокумент.Вывести(СтрокаТаблицы);

				НомерСтроки = НомерСтроки + 1;
			КонецЦикла;
		КонецЕсли;
		
		Если КолонкиНДС И УчитыватьНДС_ И (НЕ СуммаВключаетНДС_) Тогда 
			Итого.Параметры.ИтогоСуммаСНДС  = ИтогоСуммаСНДС;
		КонецЕсли;	
			
		Если КолонкиНДС И УчитыватьНДС_ Тогда 
			Итого.Параметры.ИтогоСуммаНДС   = ИтогоСуммаНДС;
		КонецЕсли;
		
		Итого.Параметры.ИтогоСумма 		= ИтогоСумма;
		Итого.Параметры.ИтогоКоличество = ИтогоКоличество;
		
	    ТабДокумент.Вывести(Итого);
		
		// Запасы
		Запасы.Параметры.Заполнить(ДанныеДокументов);
		ТабДокумент.Вывести(Запасы);
		
		//Подвал
		Если ТипЗнч(ДанныеДокументов.ФИООтветственного) = Тип("СправочникСсылка.ФизическиеЛица") Тогда
			ДанныеОтветственного	= ПроцедурыУправленияПерсоналомВызовСервера.ДанныеФизЛица(ДанныеДокументов.Организация, ДанныеДокументов.ФИООтветственного, ДанныеДокументов.Дата); 
			ОтветственныйДляПечати  = ДанныеОтветственного.Представление;
			ДолжностьОтветственного = ДанныеОтветственного.Должность;
		Иначе
			ОтветственныйДляПечати = ДанныеДокументов.ФИООтветственного;
		КонецЕсли;
		Подвал.Параметры.ДолжностьОтветственного = ДолжностьОтветственного;
		Подвал.Параметры.ФИООтветственного       = ОтветственныйДляПечати;
		
		Подвал.Параметры.ДатаПринятия            = Формат(ДанныеДокументов.ДатаПодписанияГЗ, "Л=" + КодЯзыка + "; ДЛФ=Д");
				
		ТабДокумент.Вывести(Подвал);
		
		УправлениеПечатью.ЗадатьОбластьПечатиДокумента(ТабДокумент, НомерСтрокиНачало, ОбъектыПечати, ДанныеДокументов.Ссылка);
	
		//KaspiQR
		МакетНастроенДляВыводаKaspiQR = Истина;
		Попытка
			РисунокКолонтитул = ТабДокумент.Рисунки.КолонтитулKaspi;
			РисунокКод = ТабДокумент.Рисунки.QRKaspi;
		Исключение
			МакетНастроенДляВыводаKaspiQR = Ложь;
		КонецПопытки;
		Если МакетНастроенДляВыводаKaspiQR Тогда
			ОбщиеДанныеНастроек = QRКод_ГенерацияКода.СохраненныеРеквизитыКода(ДанныеДокументов.Организация, ДанныеДокументов.СтруктурноеПодразделение);
			Если ОбщиеДанныеНастроек.Количество() > 0 Тогда
				ДанныеКомпании = ОбщиеДанныеНастроек[0];
				Если ДанныеКомпании.Свойство("ВыводитьQRКод") И ДанныеКомпании.ВыводитьQRКод = Истина Тогда
					
					НомерДокументаДляПечати = ПрефиксацияОбъектовКлиентСервер.ПолучитьНомерНаПечать(ДанныеДокументов.Номер, ДанныеДокументов.Ссылка);
					
					ОтветСервиса = QRКод_ГенерацияКода.РезультатЗапросаСервиса(СведенияОбОрганизации.БИН_ИИН,
																			КодироватьСтроку(НомерДокументаДляПечати, СпособКодированияСтроки.КодировкаURL),
																			?(ПрибавлятьНДС, ИтогоСумма + ИтогоСуммаНДС, ИтогоСумма),
																			ДанныеКомпании);
					
					Если ТипЗнч(ОтветСервиса) = Тип("ДвоичныеДанные") Тогда
						// сдвиг текста заголовка
						Для НомерСтрокиТабличногоДокумента = 1 По 4 Цикл
							ТабДокумент.Область(НомерСтрокиТабличногоДокумента, 40, НомерСтрокиТабличногоДокумента, 49).Разъединить();
							ТабДокумент.Область(НомерСтрокиТабличногоДокумента, 35).Текст = ТабДокумент.Область(НомерСтрокиТабличногоДокумента, 40).Текст;
							ТабДокумент.Область(НомерСтрокиТабличногоДокумента, 35, НомерСтрокиТабличногоДокумента, 44).Объединить();
						КонецЦикла;
						ТабДокумент.Область(6, 44).Текст = ТабДокумент.Область(6, 49).Текст;
						ТабДокумент.Область(1, 45, 7, 49).Очистить(Истина, Истина, Истина);
						// вывод и выравнивание картинок
						РисунокКолонтитул.Картинка = БиблиотекаКартинок.QRКод_Колонтитул;
						РисунокКолонтитул.Линия    = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
						РисунокКолонтитул.Высота   = РисунокКолонтитул.Ширина/4;
						РисунокКод.Картинка = Новый Картинка(ОтветСервиса);
						РисунокКод.Линия    = Новый Линия(ТипЛинииРисункаТабличногоДокумента.НетЛинии);
						РисунокКод.Верх = РисунокКолонтитул.Верх + РисунокКолонтитул.Высота + 1;
						РисунокКод.Лево = РисунокКолонтитул.Лево;
						РисунокКод.Ширина = РисунокКолонтитул.Ширина;
						РисунокКод.Высота = РисунокКолонтитул.Ширина;
					Иначе
						ТабДокумент.Рисунки.Удалить(РисунокКолонтитул);
						ТабДокумент.Рисунки.Удалить(РисунокКод);
						ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ОтветСервиса);
					КонецЕсли;
				Иначе
					ТабДокумент.Рисунки.Удалить(РисунокКолонтитул);
					ТабДокумент.Рисунки.Удалить(РисунокКод);
				КонецЕсли;
			Иначе
				ТабДокумент.Рисунки.Удалить(РисунокКолонтитул);
				ТабДокумент.Рисунки.Удалить(РисунокКод);
			КонецЕсли;
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат ТабДокумент;
	
КонецФункции

Функция ПолучитьДанныеДляПечатиР1(МассивОбъектов)
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	Запрос.УстановитьПараметр("ПустаяДата", Дата(1, 1, 1));
	Запрос.УстановитьПараметр("парамУволен", Перечисления.ПричиныИзмененияСостояния.Увольнение);
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка,
	|	АктОбОказанииПроизводственныхУслуг.Номер,
	|	АктОбОказанииПроизводственныхУслуг.Дата,
	|	ВЫБОР
	|		КОГДА АктОбОказанииПроизводственныхУслуг.ОтложитьНачислениеНДС
	|			ТОГДА ЕСТЬNULL(СведенияПоАктамВыполненныхРабот.ДатаПодписания, &ПустаяДата)
	|		ИНАЧЕ АктОбОказанииПроизводственныхУслуг.ДатаПодписанияГЗ
	|	КОНЕЦ КАК ДатаПодписанияГЗ,	
	|	АктОбОказанииПроизводственныхУслуг.СпособВыпискиАктовВыполненныхРабот,
	|	АктОбОказанииПроизводственныхУслуг.ДоговорКонтрагента КАК Основание,
	|	АктОбОказанииПроизводственныхУслуг.Контрагент,
	|	АктОбОказанииПроизводственныхУслуг.Организация,
	|	АктОбОказанииПроизводственныхУслуг.СтруктурноеПодразделение,
	|	АктОбОказанииПроизводственныхУслуг.СуммаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.ВалютаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.УчитыватьНДС,
	|	АктОбОказанииПроизводственныхУслуг.СуммаВключаетНДС,
	|	АктОбОказанииПроизводственныхУслуг.Ответственный.ФизЛицо КАК ФИООтветственного,
	|	АктОбОказанииПроизводственныхУслуг.ДоговорКонтрагента.НомерДоговора КАК НомерДоговора,
	|	АктОбОказанииПроизводственныхУслуг.ДоговорКонтрагента.ДатаДоговора КАК ДатаДоговора,
	|	АктОбОказанииПроизводственныхУслуг.ДатаНачалаОтчетногоПериода КАК ДатаНачалаОтчетногоПериода,
	|	АктОбОказанииПроизводственныхУслуг.ДатаОкончанияОтчетногоПериода КАК ДатаОкончанияОтчетногоПериода,
	|	АктОбОказанииПроизводственныхУслуг.ПереченьДокументации КАК ПереченьДокументации
	|ПОМЕСТИТЬ ДанныеДокумента
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг КАК АктОбОказанииПроизводственныхУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СведенияПоАктамВыполненныхРабот КАК СведенияПоАктамВыполненныхРабот
	|		ПО АктОбОказанииПроизводственныхУслуг.Организация = СведенияПоАктамВыполненныхРабот.Организация
	|		И АктОбОказанииПроизводственныхУслуг.Ссылка = СведенияПоАктамВыполненныхРабот.ДокументРеализацииУслуг
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка В(&МассивОбъектов)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(РаботникиОрганизации.Период) КАК Период,
	|	РаботникиОрганизации.Организация КАК Организация,
	|	РаботникиОрганизации.Сотрудник.Физлицо КАК ФизЛицо,
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата КАК Дата,
	|	ДанныеДокумента.ДатаПодписанияГЗ КАК ДатаПодписанияГЗ,
	|	ДанныеДокумента.СпособВыпискиАктовВыполненныхРабот КАК СпособВыпискиАктовВыполненныхРабот
	|ПОМЕСТИТЬ ВТ_ПериодыРаботников
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ДанныеДокумента КАК ДанныеДокумента
	|		ПО РаботникиОрганизации.Организация = ДанныеДокумента.Организация
	|			И РаботникиОрганизации.Период <= ДанныеДокумента.Дата
	|			И (ДанныеДокумента.ФИООтветственного = РаботникиОрганизации.Сотрудник.Физлицо)
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.Организация,
	|	РаботникиОрганизации.Сотрудник.Физлицо,
	|	ДанныеДокумента.Ссылка,
	|	ДанныеДокумента.Дата,
	|	ДанныеДокумента.ДатаПодписанияГЗ,
	|	ДанныеДокумента.СпособВыпискиАктовВыполненныхРабот
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПериодыРаботников.Ссылка КАК Ссылка,
	|	ПериодыРаботников.Организация КАК Организация,
	|	ПериодыРаботников.ФизЛицо,
	|	РаботникиОрганизации.Должность КАК ДолжностьОтветственного
	|ПОМЕСТИТЬ ВТ_ПериодыДолжности
	|ИЗ
	|	ВТ_ПериодыРаботников КАК ПериодыРаботников
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрСведений.РаботникиОрганизаций КАК РаботникиОрганизации
	|		ПО ПериодыРаботников.Период = РаботникиОрганизации.Период
	|			И ПериодыРаботников.Организация = РаботникиОрганизации.Организация
	|			И ПериодыРаботников.ФизЛицо = РаботникиОрганизации.Сотрудник.Физлицо
	|           И  РаботникиОрганизации.ПричинаИзмененияСостояния <> &парамУволен
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка,
	|	АктОбОказанииПроизводственныхУслуг.Номер,
	|	АктОбОказанииПроизводственныхУслуг.Дата,
	|	АктОбОказанииПроизводственныхУслуг.ДатаПодписанияГЗ,
	|	АктОбОказанииПроизводственныхУслуг.СпособВыпискиАктовВыполненныхРабот,
	|	АктОбОказанииПроизводственныхУслуг.Основание,
	|	АктОбОказанииПроизводственныхУслуг.Контрагент,
	|	АктОбОказанииПроизводственныхУслуг.Организация,
	|	АктОбОказанииПроизводственныхУслуг.СтруктурноеПодразделение,
	|	АктОбОказанииПроизводственныхУслуг.СуммаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.ВалютаДокумента,
	|	АктОбОказанииПроизводственныхУслуг.УчитыватьНДС,
	|	АктОбОказанииПроизводственныхУслуг.СуммаВключаетНДС,
	|	АктОбОказанииПроизводственныхУслуг.ФИООтветственного,
	|	АктОбОказанииПроизводственныхУслуг.НомерДоговора КАК НомерДоговора,
	|	АктОбОказанииПроизводственныхУслуг.ДатаДоговора КАК ДатаДоговора,
	|	АктОбОказанииПроизводственныхУслуг.ДатаНачалаОтчетногоПериода КАК ДатаНачалаОтчетногоПериода,
	|	АктОбОказанииПроизводственныхУслуг.ДатаОкончанияОтчетногоПериода КАК ДатаОкончанияОтчетногоПериода,
	|	АктОбОказанииПроизводственныхУслуг.ПереченьДокументации,
	|	ВТ_ПериодыДолжности.ДолжностьОтветственного КАК ДолжностьОтветственного
	|ИЗ
	|	ДанныеДокумента КАК АктОбОказанииПроизводственныхУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_ПериодыДолжности КАК ВТ_ПериодыДолжности
	|		ПО АктОбОказанииПроизводственныхУслуг.Ссылка = ВТ_ПериодыДолжности.Ссылка";	
	
	ДанныеДокументов = Запрос.Выполнить().Выбрать();
	
	Запрос.Текст =
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка КАК Ссылка,
	|	МИНИМУМ(АктОбОказанииПроизводственныхУслуг.НомерСтроки) КАК НомерСтроки,
	|	АктОбОказанииПроизводственныхУслуг.Номенклатура,
	|	ВЫБОР
	|		КОГДА НЕ АктОбОказанииПроизводственныхУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ВЫРАЗИТЬ(АктОбОказанииПроизводственныхУслуг.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		ИНАЧЕ АктОбОказанииПроизводственныхУслуг.Номенклатура.Наименование
	|	КОНЕЦ КАК Содержание,
	|	АктОбОказанииПроизводственныхУслуг.ЕдиницаИзмерения,
	|	АктОбОказанииПроизводственныхУслуг.Номенклатура.Код КАК УслугаКод,
	|	СУММА(АктОбОказанииПроизводственныхУслуг.Количество) КАК Количество,
	|	АктОбОказанииПроизводственныхУслуг.Цена,
	|	СУММА(АктОбОказанииПроизводственныхУслуг.Сумма) КАК Сумма,
	|	СУММА(АктОбОказанииПроизводственныхУслуг.СуммаНДС) КАК СуммаНДС
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг.Услуги КАК АктОбОказанииПроизводственныхУслуг
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслуг.Ссылка В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	АктОбОказанииПроизводственныхУслуг.Номенклатура,
	|	АктОбОказанииПроизводственныхУслуг.ЕдиницаИзмерения,
	|	АктОбОказанииПроизводственныхУслуг.Номенклатура.Код,
	|	АктОбОказанииПроизводственныхУслуг.Цена,
	|	АктОбОказанииПроизводственныхУслуг.Ссылка,
	|	ВЫБОР
	|		КОГДА НЕ АктОбОказанииПроизводственныхУслуг.Номенклатура.НаименованиеПолное ПОДОБНО """"
	|			ТОГДА ВЫРАЗИТЬ(АктОбОказанииПроизводственныхУслуг.Номенклатура.НаименованиеПолное КАК СТРОКА(1000))
	|		ИНАЧЕ АктОбОказанииПроизводственныхУслуг.Номенклатура.Наименование
	|	КОНЕЦ
	|
	|УПОРЯДОЧИТЬ ПО
	|	НомерСтроки
	|ИТОГИ
	|	СУММА(Сумма),
	|	СУММА(СуммаНДС)
	|ПО
	|	Ссылка";
	
	ВыборкаСтрокУслугиИтоги = Запрос.Выполнить().Выбрать(ОбходРезультатаЗапроса.ПоГруппировкамСИерархией);
	
	РезультатыЗапросов = Новый Структура;
	РезультатыЗапросов.Вставить("ДанныеДокументов", ДанныеДокументов);
	РезультатыЗапросов.Вставить("ВыборкаСтрокУслугиИтоги", ВыборкаСтрокУслугиИтоги);
	
	Возврат РезультатыЗапросов;
	
КонецФункции

Функция ПечатьСчетаФактуры(МассивОбъектов, ОбъектыПечати, ВВалютеРеглУчета = Ложь, КодЯзыка = Неопределено) Экспорт
	
	УстановитьПривилегированныйРежим(Истина);
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	МАКСИМУМ(ТЧ_Документов.Ссылка) КАК СчетФактура,
	|	ТЧ_Документов.ДокументОснование
	|ИЗ
	|	Документ.СчетФактураВыданный.ДокументыОснования КАК ТЧ_Документов
	|ГДЕ
	|	ТЧ_Документов.ДокументОснование В(&МассивОбъектов)
	|
	|СГРУППИРОВАТЬ ПО
	|	ТЧ_Документов.ДокументОснование";
	
	Запрос.УстановитьПараметр("МассивОбъектов", МассивОбъектов);
	
	МассивСчетовФактур = Запрос.Выполнить().Выгрузить().ВыгрузитьКолонку("СчетФактура");
	
	Возврат ?(ВВалютеРеглУчета, 
				Документы.СчетФактураВыданный.ПечатьСчетФактураВВалютеРеглУчета(МассивСчетовФактур, ОбъектыПечати, КодЯзыка), 
				Документы.СчетФактураВыданный.ПечатьСчетФактура(МассивСчетовФактур, ОбъектыПечати, Ложь, КодЯзыка));
	
КонецФункции

Функция ПодготовитьТекстЗапросаДляПечатиСправкиРасчетаРеглСуммыДокументовВВалюте(НомераТаблиц) Экспорт
	
	НомераТаблиц.Вставить("ВТ_ТаблицаПоШапкеДокумента",                                НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаРеквизитов",                                         НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВТ_ПоДокументамЗачетнныхАвансов",                           НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПредоплат",                                          НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ПоДокументамЗачетнныхАвансов",               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаСумм",                                               НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Уничтожение_ВТ_ТаблицаПоШапкеДокумента",                    НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ДанныеОбрабатываемогоДокумента.Ссылка КАК Ссылка,
	|	ДанныеОбрабатываемогоДокумента.Дата КАК Дата,
	|	ДанныеОбрабатываемогоДокумента.Проведен КАК Проведен,
	|	ДанныеОбрабатываемогоДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ДанныеОбрабатываемогоДокумента.Организация КАК Организация,
	|	ДанныеОбрабатываемогоДокумента.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	ДанныеОбрабатываемогоДокумента.Контрагент КАК Контрагент,
	|	ДанныеОбрабатываемогоДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовСКонтрагентом КАК СчетУчетаРасчетовСКонтрагентом,
	|	ДанныеОбрабатываемогоДокумента.СчетУчетаРасчетовПоАвансам КАК СчетУчетаРасчетовПоАвансам,
	|	ДанныеОбрабатываемогоДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ВЫБОР
	|		КОГДА ДанныеОбрабатываемогоДокумента.КратностьВзаиморасчетов = 0
	|			ТОГДА 1
	|		ИНАЧЕ ДанныеОбрабатываемогоДокумента.КратностьВзаиморасчетов
	|	КОНЕЦ КАК КратностьВзаиморасчетов,
	|	"""" КАК НомерВходящегоДокумента,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВходящегоДокумента,
	|	ДанныеОбрабатываемогоДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ДанныеОбрабатываемогоДокумента.УчитыватьНДС КАК УчитыватьНДС,
	|	ВЫБОР
	|		КОГДА ДанныеОбрабатываемогоДокумента.УчитыватьНДС И ДанныеОбрабатываемогоДокумента.СуммаВключаетНДС
	|			ТОГДА 1
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК УчНДС
	|ПОМЕСТИТЬ ВТ_ТаблицаПоШапкеДокумента
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг КАК ДанныеОбрабатываемогоДокумента
	|ГДЕ
	|	ДанныеОбрабатываемогоДокумента.Ссылка В(&МассивОбъектов)
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаПоШапкеДокумента.Ссылка КАК Ссылка,
	|	ВТ_ТаблицаПоШапкеДокумента.Дата КАК Дата,
	|	ВТ_ТаблицаПоШапкеДокумента.Проведен КАК Проведен,
	|	ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента КАК ВалютаДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаПоШапкеДокумента.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	ВТ_ТаблицаПоШапкеДокумента.Контрагент КАК Контрагент,
	|	ВТ_ТаблицаПоШапкеДокумента.ДоговорКонтрагента КАК ДоговорКонтрагента,
	|	ВТ_ТаблицаПоШапкеДокумента.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов,
	|	ВТ_ТаблицаПоШапкеДокумента.НомерВходящегоДокумента КАК НомерВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.ДатаВходящегоДокумента КАК ДатаВходящегоДокумента,
	|	ВТ_ТаблицаПоШапкеДокумента.СуммаВключаетНДС КАК СуммаВключаетНДС,
	|	ВТ_ТаблицаПоШапкеДокумента.УчитыватьНДС КАК УчитыватьНДС,
	|	0 КАК ВсегоВал,
	|	0 КАК НДСВал
	|ИЗ
	|	ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Типовой.Регистратор КАК Ссылка,
	|	Типовой.Сумма КАК СуммаПредоплатыРегл,
	|	Типовой.ВалютнаяСуммаДт КАК СуммаПредоплатыВал,
	|	ТиповойСубконто.Значение КАК Документ,
	|	ПРЕДСТАВЛЕНИЕ(ТиповойСубконто.Значение) КАК ДокументПредоплатыПредставление,
	|	ВТ_ТаблицаПоШапкеДокумента.Организация КАК Организация,
	|	ВТ_ТаблицаПоШапкеДокумента.СтруктурноеПодразделение КАК СтруктурноеПодразделение
	|ПОМЕСТИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|ИЗ
	|	РегистрБухгалтерии.Типовой КАК Типовой
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО Типовой.Регистратор = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|			И Типовой.СчетДт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовПоАвансам
	|			И Типовой.СчетКт = ВТ_ТаблицаПоШапкеДокумента.СчетУчетаРасчетовСКонтрагентом
	|			И (ВТ_ТаблицаПоШапкеДокумента.ВалютаДокумента <> &ВалютаРегламентированногоУчета)
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Субконто КАК ТиповойСубконто
	|		ПО Типовой.Регистратор = ТиповойСубконто.Регистратор
	|			И Типовой.НомерСтроки = ТиповойСубконто.НомерСтроки
	|			И (ТиповойСубконто.ВидДвижения = ЗНАЧЕНИЕ(ВидДвиженияБухгалтерии.Дебет))
	|			И (ТиповойСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.ДокументыРасчетовСКонтрагентами))
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Документ,
	|	Организация
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка КАК Ссылка,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыРегл КАК СуммаПредоплатыРегл,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.СуммаПредоплатыВал КАК СуммаПредоплатыВал,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Документ КАК Документ,
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.ДокументПредоплатыПредставление КАК ДокументПредоплатыПредставление,
	|	"""" КАК НомерВходящегоДокумента,
	|	ДАТАВРЕМЯ(1, 1, 1) КАК ДатаВходящегоДокумента
	|ИЗ
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента КАК ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента.Ссылка
	|ИТОГИ
	|	СУММА(СуммаПредоплатыРегл),
	|	СУММА(СуммаПредоплатыВал)
	|ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ПоДокументамЗачетнныхАвансовДоПолученияНомераИДатыВходящегоДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВТ_ТаблицаПоШапкеДокумента.Ссылка КАК Ссылка,
	|	ОбрабатываемаяТаблица.НомерСтроки КАК НомерСтроки,
	|	ОбрабатываемаяТаблица.Номенклатура КАК Товар,
	|	ОбрабатываемаяТаблица.Номенклатура.Наименование КАК ТоварНаименование,
	|	ОбрабатываемаяТаблица.Сумма КАК ВсегоВал,
	|	ОбрабатываемаяТаблица.СуммаНДС КАК НДСВал,
	|	ОбрабатываемаяТаблица.СтавкаНДС КАК СтавкаНДС,
	|	ВЫРАЗИТЬ(ОбрабатываемаяТаблица.Сумма*ВТ_ТаблицаПоШапкеДокумента.КурсВзаиморасчетов/ВТ_ТаблицаПоШапкеДокумента.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2)) КАК ВсегоРегл,
	|	ВЫРАЗИТЬ(ОбрабатываемаяТаблица.СуммаНДС*ВТ_ТаблицаПоШапкеДокумента.КурсВзаиморасчетов/ВТ_ТаблицаПоШапкеДокумента.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2)) КАК НДСРегл,
	|	ВЫРАЗИТЬ((ОбрабатываемаяТаблица.Сумма-(ОбрабатываемаяТаблица.СуммаНДС*ВТ_ТаблицаПоШапкеДокумента.УчНДС))*ВТ_ТаблицаПоШапкеДокумента.КурсВзаиморасчетов/ВТ_ТаблицаПоШапкеДокумента.КратностьВзаиморасчетов КАК ЧИСЛО(15, 2)) КАК НалоговаяБазаНДСРегл
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг.Услуги КАК ОбрабатываемаяТаблица
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ТаблицаПоШапкеДокумента КАК ВТ_ТаблицаПоШапкеДокумента
	|		ПО ОбрабатываемаяТаблица.Ссылка = ВТ_ТаблицаПоШапкеДокумента.Ссылка
	|ГДЕ
	|	ОбрабатываемаяТаблица.Сумма <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	Ссылка,
	|	НомерСтроки
	|
	|ИТОГИ
	|	СУММА(ВсегоВал),
	|	СУММА(НДСВал),
	|	СУММА(ВсегоРегл),
	|	СУММА(НДСРегл),
	|	СУММА(НалоговаяБазаНДСРегл)
	|ПО
	|	Ссылка
	|;
	|////////////////////////////////////////////////////////////////////////////////
	|УНИЧТОЖИТЬ ВТ_ТаблицаПоШапкеДокумента";
	
	ВестиРасчетыПоДокументам = ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.НаСчетеВедетсяУчетПоДокументамРасчетов(ПланыСчетов.Типовой.КраткосрочнаяДебиторскаяЗадолженностьПокупателейИЗаказчиков);
	Если НЕ ВестиРасчетыПоДокументам Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "И (ТиповойСубконто.Вид = ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.ДокументыРасчетовСКонтрагентами))", "И Ложь");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТиповойСубконто.Значение КАК Документ", """<" + НСтр("ru = 'Документ расчетов отсутствует'") + ">"" КАК Документ");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПРЕДСТАВЛЕНИЕ(ТиповойСубконто.Значение) КАК ДокументПредоплатыПредставление", """<" + НСтр("ru = 'Документ расчетов отсутствует'") + ">"" КАК ДокументПредоплатыПредставление");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ВНУТРЕННЕЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Субконто", "ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Субконто");
	КонецЕсли;

	Возврат ТекстЗапроса;
	
КонецФункции	

////////////////////////////////////////////////////////////////////////////////
// Обработчики обновления ИБ

Процедура ЗаполнитьДатуПодписанияГЗПриОбновлении(Параметры) Экспорт
	
	Запрос = Новый Запрос;
	
	Запрос.Текст = 
	"ВЫБРАТЬ ПЕРВЫЕ 1000
	|	АктОбОказанииПроизводственныхУслуг.Ссылка КАК Ссылка,
	|	АктОбОказанииПроизводственныхУслуг.Дата КАК Дата,
	|	АктОбОказанииПроизводственныхУслуг.ДатаПодписанияГЗ КАК ДатаПодписанияГЗ,
	|	АктОбОказанииПроизводственныхУслуг.Представление КАК Представление,
	|	МАКСИМУМ(ВЫБОР
	|			КОГДА ЕСТЬNULL(ЭлектронныйАктВыполненныхРабот.Ссылка, ЗНАЧЕНИЕ(Документ.ЭлектронныйАктВыполненныхРабот.ПустаяСсылка)) <> ЗНАЧЕНИЕ(Документ.ЭлектронныйАктВыполненныхРабот.ПустаяСсылка)
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЛОЖЬ
	|		КОНЕЦ) КАК ЕстьАВР
	|ИЗ
	|	Документ.АктОбОказанииПроизводственныхУслуг КАК АктОбОказанииПроизводственныхУслуг
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.ЭлектронныйАктВыполненныхРабот КАК ЭлектронныйАктВыполненныхРабот
	|		ПО АктОбОказанииПроизводственныхУслуг.Ссылка = ЭлектронныйАктВыполненныхРабот.ДокументОснование
	|ГДЕ
	|	АктОбОказанииПроизводственныхУслуг.СпособВыпискиАктовВыполненныхРабот = &парамПустойСпособ
	|
	|СГРУППИРОВАТЬ ПО
	|	АктОбОказанииПроизводственныхУслуг.Ссылка,
	|	АктОбОказанииПроизводственныхУслуг.Дата,
	|	АктОбОказанииПроизводственныхУслуг.Представление,
	|	АктОбОказанииПроизводственныхУслуг.ДатаПодписанияГЗ";
	
	Запрос.УстановитьПараметр("парамПустойСпособ", 	Перечисления.СпособыВыпискиАктовВыполненныхРабот.ПустаяСсылка());
	
	Результат = Запрос.Выполнить();
	Если Результат.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Выборка = Результат.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		НачатьТранзакцию();
		ДокументОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Если ДокументОбъект = Неопределено Тогда
			ОтменитьТранзакцию();
			Продолжить;
		КонецЕсли;
		
		Блокировка = Новый БлокировкаДанных;
		ЭлементБлокировки = Блокировка.Добавить("Документ.АктОбОказанииПроизводственныхУслуг");
		ЭлементБлокировки.УстановитьЗначение("Ссылка", Выборка.Ссылка);
		
		Попытка
			Блокировка.Заблокировать();
			Если ДокументОбъект.ДатаПодписанияГЗ = Дата (1,1,1) Тогда
				ДокументОбъект.ДатаПодписанияГЗ = Выборка.Дата;
				ДокументОбъект.СпособВыпискиАктовВыполненныхРабот = ?(Выборка.ЕстьАВР, Перечисления.СпособыВыпискиАктовВыполненныхРабот.НаПорталеИСЭСФ, Перечисления.СпособыВыпискиАктовВыполненныхРабот.ВБумажномВиде) ;
			Иначе
				
				Если НачалоДня(Выборка.ДатаПодписанияГЗ) = НачалоДня(Выборка.Дата) Тогда
					ДокументОбъект.СпособВыпискиАктовВыполненныхРабот = Перечисления.СпособыВыпискиАктовВыполненныхРабот.НаПорталеГосЗакупа;
				Иначе
					ДокументОбъект.СпособВыпискиАктовВыполненныхРабот = Перечисления.СпособыВыпискиАктовВыполненныхРабот.ВБумажномВиде;
					
					ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
					НСтр("ru = 'В документе ""%1"" заполнена дата подписания ГЗ, но так как она отличается от даты документа
					| признак ""Способ выставления актов выполненных работ"" установлен в значение ""В бумажном виде"".'", ОбщегоНазначения.КодОсновногоЯзыка()),
					Выборка.Представление);
					ЗаписьЖурналаРегистрации(
						НСтр("ru = 'Обновление ИБ. Документ ""Акт об оказании производственных услуг"". Заполнение даты подписания ГЗ'", ОбщегоНазначения.КодОсновногоЯзыка()),
						УровеньЖурналаРегистрации.Информация, , ,
						ТекстСообщения);
					
				КонецЕсли;
			КонецЕсли;
			
			ДокументОбъект.ОбменДанными.Загрузка = Истина;
			ДокументОбъект.Записать();
			ЗафиксироватьТранзакцию();
		Исключение
			ОтменитьТранзакцию();
			ТекстСообщения = НСтр("ru='Операция не выполнена'", ОбщегоНазначения.КодОсновногоЯзыка());
			ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
		КонецПопытки;
		
	КонецЦикла;
	
КонецПроцедуры

#КонецЕсли