#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьТабличнуюДляСпискаОС(Объект, ПоддержкаРаботыСоСтруктурнымиПодразделениями) Экспорт 
		
	СписокОС = Объект.ОС.Выгрузить(,"ОсновноеСредство");

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("СписокОС"	, СписокОС);
	Запрос.УстановитьПараметр("Организация"	, Объект.Организация);
	
	Если ПоддержкаРаботыСоСтруктурнымиПодразделениями Тогда
		Запрос.УстановитьПараметр("СтруктурноеПодразделение", Объект.СтруктурноеПодразделение);
		УсловиеСтруктурноеПодразделение = " И СтруктурноеПодразделение = &СтруктурноеПодразделение ";
	Иначе
		УсловиеСтруктурноеПодразделение = "";
	КонецЕсли;
	
	Запрос.УстановитьПараметр("Период"		, Объект.Дата);
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования КАК СрокПолезногоИспользованияБУ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРабот КАК ОбъемПродукцииРаботБУ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации КАК СрокИспользованияДляВычисленияАмортизацииБУ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СтоимостьДляВычисленияАмортизации КАК СтоимостьДляВычисленияАмортизацииБУ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимостьБУ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации КАК ОбъемПродукцииРаботДляВычисленияАмортизацииБУ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.КоэффициентАмортизации КАК КоэффициентАмортизацииБУ,
		|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.КоэффициентУскорения КАК КоэффициентУскоренияБУ,
		|	ВЫБОР КОГДА НЕ((ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство) ЕСТЬ NULL ) ТОГДА ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство  ИНАЧЕ NULL КОНЕЦ КАК ОсновноеСредство
		|ИЗ
		|	РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(&Период, Организация = &Организация " + УсловиеСтруктурноеПодразделение + "И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних";
	ТЗ = Запрос.Выполнить().Выгрузить();

	Для каждого СтрокаТЧ Из Объект.ОС Цикл

		СтрокаТЗ = ТЗ.Найти(СтрокаТЧ.ОсновноеСредство,"ОсновноеСредство");

		Если СтрокаТЗ = Неопределено Тогда
			СтрокаТЧ.СрокПолезногоИспользованияБУ                  = 0;
			СтрокаТЧ.СрокИспользованияДляВычисленияАмортизацииБУ   = 0;
			СтрокаТЧ.ОбъемПродукцииРаботБУ                         = 0;
			СтрокаТЧ.ОбъемПродукцииРаботДляВычисленияАмортизацииБУ = 0;
			СтрокаТЧ.СтоимостьДляВычисленияАмортизацииБУ           = 0;
			СтрокаТЧ.КоэффициентАмортизацииБУ                      = 0;
			СтрокаТЧ.КоэффициентУскоренияБУ                        = 0;
			СтрокаТЧ.ЛиквидационнаяСтоимостьБУ					   = 0;

		Иначе
			СтрокаТЧ.СрокПолезногоИспользованияБУ                  = СтрокаТЗ.СрокПолезногоИспользованияБУ;
			СтрокаТЧ.СрокИспользованияДляВычисленияАмортизацииБУ   = СтрокаТЗ.СрокИспользованияДляВычисленияАмортизацииБУ;
			СтрокаТЧ.ОбъемПродукцииРаботБУ                         = СтрокаТЗ.ОбъемПродукцииРаботБУ;
			СтрокаТЧ.ОбъемПродукцииРаботДляВычисленияАмортизацииБУ = СтрокаТЗ.ОбъемПродукцииРаботДляВычисленияАмортизацииБУ;
			СтрокаТЧ.СтоимостьДляВычисленияАмортизацииБУ           = СтрокаТЗ.СтоимостьДляВычисленияАмортизацииБУ;
			СтрокаТЧ.КоэффициентАмортизацииБУ                      = СтрокаТЗ.КоэффициентАмортизацииБУ;
			СтрокаТЧ.КоэффициентУскоренияБУ                        = СтрокаТЗ.КоэффициентУскоренияБУ;
            СтрокаТЧ.ЛиквидационнаяСтоимостьБУ					   = СтрокаТЗ.ЛиквидационнаяСтоимостьБУ;
			
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Запрос.УстановитьПараметр("СинонимОС",  НСтр("ru = 'ОС'"));

	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)
					+ ТекстЗапросаИзменениеПараметровНачисленияАмортизацииОС(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты"				 , НомераТаблиц.Количество());
	
	ТекстЗапроса = "ВЫБРАТЬ
	|	""ИзменениеПараметровНачисленияАмортизацииОС"" КАК ВидДокумента,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.СобытиеОС КАК СобытиеОС
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ИзменениеПараметровНачисленияАмортизацииОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.ВидДокумента,
	|	Реквизиты.Регистратор,
	|	Реквизиты.Период,
	|	Реквизиты.Организация,
	|	Реквизиты.СтруктурноеПодразделение,
	|	Реквизиты.Номер,
	|	Реквизиты.СобытиеОС
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаОС", НомераТаблиц.Количество());
 	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Ссылка,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
	|	ТаблицаОС.СрокИспользованияДляВычисленияАмортизацииБУ КАК СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.ОбъемПродукцииРаботБУ КАК ОбъемПродукцииРабот,
	|	ТаблицаОС.ОбъемПродукцииРаботДляВычисленияАмортизацииБУ КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаОС.СтоимостьДляВычисленияАмортизацииБУ КАК СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.КоэффициентАмортизацииБУ КАК КоэффициентАмортизации,
	|	ТаблицаОС.КоэффициентУскоренияБУ КАК КоэффициентУскорения,
	|	ТаблицаОС.ЛиквидационнаяСтоимостьБУ КАК ЛиквидационнаяСтоимость
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	Документ.ИзменениеПараметровНачисленияАмортизацииОС.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаИзменениеПараметровНачисленияАмортизацииОС(НомераТаблиц)
	
	НомераТаблиц.Вставить("ИзменениеПараметровНачисленияАмортизацииОСРеквизитыТаблицаОС", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Номер КАК Номер,
	|	Реквизиты.Регистратор КАК Регистратор,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.ВидДокумента КАК НазваниеДокумента,
	|	Реквизиты.СобытиеОС,
	|	0 КАК СуммаЗатратБУ,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство,
	|	ТаблицаОС.СрокПолезногоИспользования,
	|	ТаблицаОС.СрокИспользованияДляВычисленияАмортизации,
	|	ТаблицаОС.ОбъемПродукцииРабот,
	|	ТаблицаОС.ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ТаблицаОС.СтоимостьДляВычисленияАмортизации,
	|	ТаблицаОС.КоэффициентАмортизации,
	|	ТаблицаОС.КоэффициентУскорения,
	|	ТаблицаОС.ЛиквидационнаяСтоимость
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
	|		ПО (ИСТИНА)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

#КонецЕсли
