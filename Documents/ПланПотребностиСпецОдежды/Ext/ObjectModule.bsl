
Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Дата", КонецДня(?(ЭтоНовый(), ТекущаяДата(), Дата)));
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Должность", Должность);
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	МИНИМУМ(тз.НомерСтроки) КАК НомерСтроки,
	|	тз.ТРУ,
	|	СУММА(тз.Количество) КАК Количество,
	|	СУММА(тз.СрокСлужбы) КАК СрокСлужбы,
	|	МАКСИМУМ(тз.ВДоке) КАК ВДоке
	|ИЗ
	|	(ВЫБРАТЬ
	|		ПланПотребностиСпецОдеждыСписок.НомерСтроки КАК НомерСтроки,
	|		ПланПотребностиСпецОдеждыСписок.ТРУ КАК ТРУ,
	|		ПланПотребностиСпецОдеждыСписок.Количество КАК Количество,
	|		ПланПотребностиСпецОдеждыСписок.СрокСлужбы КАК СрокСлужбы,
	|		ИСТИНА КАК ВДоке
	|	ИЗ
	|		Документ.ПланПотребностиСпецОдежды.Список КАК ПланПотребностиСпецОдеждыСписок
	|	ГДЕ
	|		ПланПотребностиСпецОдеждыСписок.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		NULL,
	|		ПотребностьСпецОдеждыСрезПоследних.ТРУ,
	|		NULL,
	|		NULL,
	|		ЛОЖЬ
	|	ИЗ
	|		РегистрСведений.ПотребностьСпецОдежды.СрезПоследних(
	|				&Дата,
	|				Регистратор <> &Ссылка
	|					И Должность = &Должность
	|					И Подразделение = &Подразделение) КАК ПотребностьСпецОдеждыСрезПоследних
	|	ГДЕ
	|		ПотребностьСпецОдеждыСрезПоследних.Количество <> 0) КАК тз
	|
	|СГРУППИРОВАТЬ ПО
	|	тз.ТРУ";
	Выб = Запрос.Выполнить().Выбрать();
	
	Пока Выб.Следующий() Цикл
		Если Не Выб.ВДоке Тогда
			Движение = Движения.ПотребностьСпецОдежды.Добавить();
			Движение.Период = Дата;
			Движение.Подразделение = Подразделение;
			Движение.Должность = Должность;
			Движение.ТРУ = Выб.ТРУ;
			Движение.Количество = 0;
			Движение.СрокСлужбы = 0;
			Движение.Исключено = Истина;
			Сообщить("ТРУ """+Выб.ТРУ+""" исключена из потребности", СтатусСообщения.Информация);
		ИначеЕсли Выб.ТРУ.Пустая() Тогда
			Отказ = Истина;
			Сообщить("В строке "+Выб.НомерСтроки+" не указана ТРУ", СтатусСообщения.Внимание);
		ИначеЕсли Выб.Количество=0 Тогда
			Отказ = Истина;
			Сообщить("Для ТРУ "+Выб.ТРУ+" не указано количество", СтатусСообщения.Внимание);
		ИначеЕсли Выб.СрокСлужбы=0 Тогда
			Отказ = Истина;
			Сообщить("Для ТРУ "+Выб.ТРУ+" не указан срок службы", СтатусСообщения.Внимание);
		ИначеЕсли Не Отказ Тогда
			Движение = Движения.ПотребностьСпецОдежды.Добавить();
			Движение.Период = Дата;
			Движение.Подразделение = Подразделение;
			Движение.Должность = Должность;
			Движение.ТРУ = Выб.ТРУ;
			Движение.Количество = Выб.Количество;
			Движение.СрокСлужбы = Выб.СрокСлужбы;
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры
