#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.си_ВозвратМатериаловИзЭксплуатации.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	
	ТаблицаМатериалыКВозврату = си_УчетСпецодеждыСервер.ПодготовитьТаблицуПередачиВЭксплуатацию(ПараметрыПроведения.ТаблицаМатериалы,ПараметрыПроведения,Движения,Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// в эксплуатации
	си_УчетСпецодеждыУправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ТаблицаМатериалыКВозврату, Движения, Отказ,"Материалы","си_УчетСпецодеждыСервер","ЗарегистрироватьДвиженияМатериалыВЭксплуатации");
	
	// б/у
	си_УчетСпецодеждыУправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ТаблицаМатериалыКВозврату, Движения, Отказ,"Материалы","си_УчетСпецодеждыСервер","ЗарегистрироватьДвиженияМатериалыБывшиеВЭксплуатации");
	
	// по регистрам бухгалтерии
	си_УчетСпецодеждыУправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ТаблицаМатериалыКВозврату, Движения, Отказ,"Материалы","си_УчетСпецодеждыСервер","ЗарегистрироватьДвиженияВозвратМатериаловПоБухУчету");
	
	// возврат на ордерный склад
	си_УчетСпецодеждыУправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаМатериалы, Движения, Отказ,"Материалы","си_УчетСпецодеждыСервер","ЗарегистрироватьДвиженияПриходРасходПоОрдерномуСкладу");
	
	// Отражение ПР в НУ 
	общ_ПереопределяемыеПроцедурыБКСервер.ОтразитьПостоянныеРазницыВНУ(ЭтотОбъект,ПараметрыПроведения.СписаниеТоваровРеквизиты, Движения, Отказ);

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	НеобходимаПострочнаяПроверка = Истина;
	ПараметрыПострочнойПроверки = Новый Структура;
	МассивНепроверяемыхРеквизитов = Новый Массив();
	Если НЕ УчитыватьКПН Тогда
		МассивНепроверяемыхРеквизитов.Добавить("Материалы.СчетПередачиНУ");
		МассивНепроверяемыхРеквизитов.Добавить("ВидУчетаНУ");
	КонецЕсли;
	си_ОбщегоНазначения.ИсключитьПроверку(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	Если НеобходимаПострочнаяПроверка Тогда 
		Если Материалы.Количество() > 0 Тогда
			ПроверитьЗаполнениеТабличнойЧастиМатериалыПострочно(Материалы, "Материалы", Отказ, ПараметрыПострочнойПроверки);
		КонецЕсли;
	КонецЕсли;
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
		
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
		
	Если ДанныеЗаполнения <> Неопределено И ТипДанныхЗаполнения <> Тип("Структура") 
		И Метаданные().ВводитсяНаОсновании.Содержит(ДанныеЗаполнения.Метаданные()) Тогда
		Документы.си_ВозвратМатериаловИзЭксплуатации.ЗаполнитьПоДокументуОснованию(ЭтотОбъект, ДанныеЗаполнения);
		Возврат;
	КонецЕсли;
	
	общ_ПереопределяемыеПроцедурыБКСервер.ЗаполнитьШапкуДокумента(ЭтотОбъект, СтандартнаяОбработка);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Процедура ПроверитьЗаполнениеТабличнойЧастиМатериалыПострочно(ПроверяемаяТабличнаячасть, ИмяТабличнойЧасти, Отказ, ПараметрыПроверки = Неопределено)
	
	ВыборкаПоСтрокамДокумента = Документы.си_ПередачаМатериаловВЭксплуатацию.СформироватьЗапросПоМатериалыПроверка(ПроверяемаяТабличнаячасть.Выгрузить()).Выбрать();
	
	Пока ВыборкаПоСтрокамДокумента.Следующий() Цикл
		Если ВыборкаПоСтрокамДокумента.ЯвляетсяСпецодеждойИнвентарем = Ложь Тогда
				ТекстСообщения = си_УчетСпецодеждыСервер.ПолучитьТекстСообщения("Колонка", "Корректность", НСтр("ru = 'Номенклатура'"),
						ВыборкаПоСтрокамДокумента.НомерСтроки, ИмяТабличнойЧасти,"выбранная номенклатурная позиция не является спецодеждой/инвентарем");
				Поле = ИмяТабличнойЧасти + "[" + Формат(ВыборкаПоСтрокамДокумента.НомерСтроки - 1, "ЧН=0; ЧГ=") + "].Номенклатура";
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли