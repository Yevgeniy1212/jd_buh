
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьТаблицуСчетовНаСервере(Параметры.МассивВыбранныхСчетов, Параметры.МассивДоступныхСчетов);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ТаблицаСчетов

&НаКлиенте
Процедура ТаблицаСчетовПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура КомандаОК(Команда)
	
	МассивВыбранныхСчетов       = Новый Массив;
	МассивСсылокВыбранныхСчетов = Новый Массив;
	
	Для Каждого СтрокаТабличнойЧасти Из ТаблицаСчетов Цикл
		Если СтрокаТабличнойЧасти.Пометка Тогда 
			МассивВыбранныхСчетов.Добавить(СтрокаТабличнойЧасти.Код);
			МассивСсылокВыбранныхСчетов.Добавить(СтрокаТабличнойЧасти.Ссылка);
		КонецЕсли;
	КонецЦикла;
	
	ПараметрыЗакрытия = Новый Структура("МассивВыбранныхСчетов, МассивСсылокВыбранныхСчетов", МассивВыбранныхСчетов, МассивСсылокВыбранныхСчетов);
	Закрыть(ПараметрыЗакрытия);
	
КонецПроцедуры

&НаКлиенте
Процедура КомандаОтмена(Команда)
	
	Закрыть(Неопределено);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьОтметкуСтрокТаблицыЗначений(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьОтметкуСтрокТаблицыЗначений(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура ИнвертироватьФлажки(Команда)
	
	Для Каждого СтрокаТабличнойЧасти Из ТаблицаСчетов Цикл
		СтрокаТабличнойЧасти.Пометка = НЕ СтрокаТабличнойЧасти.Пометка;
	КонецЦикла;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура УстановитьОтметкуСтрокТаблицыЗначений(Значение)

	Для Каждого СтрокаТабличнойЧасти Из ТаблицаСчетов Цикл
		СтрокаТабличнойЧасти.Пометка = Значение;
	КонецЦикла;

КонецПроцедуры

&НаСервере
Функция ЗаполнитьТаблицуСчетовНаСервере(МассивВыбранныхСчетов, МассивДоступныхСчетов)
	
	Запрос = Новый Запрос;	
	
	Запрос.УстановитьПараметр("МассивДоступныхСчетов", МассивДоступныхСчетов);
	Запрос.УстановитьПараметр("МассивВыбранныхСчетов", МассивВыбранныхСчетов);		
	
	Если МассивВыбранныхСчетов.Количество() <> 0 Тогда
		ТекстИспользованияСчета = "
		|	ВЫБОР
		|		КОГДА Типовой.Код В (&МассивВыбранныхСчетов)
		|			ТОГДА ИСТИНА
		|		ИНАЧЕ ЛОЖЬ
		|	КОНЕЦ КАК Пометка ";
	Иначе
		ТекстИспользованияСчета = "
		|	Ложь Как Пометка ";
	КонецЕсли;
              
	Запрос.Текст = "ВЫБРАТЬ
	|	Типовой.Ссылка КАК Ссылка,
	|	Типовой.Наименование КАК Представление,
	|	Типовой.Код КАК Код,
	| " + ТекстИспользованияСчета + "
	|ИЗ
	|	ПланСчетов.Типовой КАК Типовой
	|ГДЕ
	|	(НЕ Типовой.ЗапретитьИспользоватьВПроводках)
	|	И (НЕ Типовой.Забалансовый)
	|	И (Типовой.Ссылка В ИЕРАРХИИ(&МассивДоступныхСчетов))
	|
	|УПОРЯДОЧИТЬ ПО
	|	Код";
	
	ТаблицаСчетов.Загрузить(Запрос.Выполнить().Выгрузить());
	
КонецФункции
