#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область УправлениеДоступом

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	ЭТДСервер.ЗаполнитьНаборыЗначенийДоступа(ЭтотОбъект, Таблица);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	ДатаДокумента = ТекущаяДатаСеанса();
	
	ЭтотОбъект.Дата = ДатаДокумента;
	
	// формируем ЭТД с учетом данных заполненных на рабочем месте ЭТД
	Если ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		
	// формируем ЭТД по данным, загруженным из ЕСУТД
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("СтрокаТаблицыЗначений") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
		ЭтотОбъект.УстановитьНовыйНомер();
		
		СтрокаУсловияТруда = ЭтотОбъект.УсловияТруда.Добавить();
		СтрокаУсловияТруда.УсловиеТруда = ДанныеЗаполнения.УсловиеТруда;
		
		Возврат;
		
	// формируем дополнительное соглашение
	ИначеЕсли ТипЗнч(ДанныеЗаполнения) = Тип("ДокументСсылка.ЭТД") Тогда
		
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения,, "Номер,Идентификатор,ЗагруженИзЕСУТД,Автор,ДокументОснование,Статус,ЯвляетсяЭТД,НаличиеЗаявленияРаботника,СогласиеРаботника,СогласиеРаботодателя");
		
		ЗаполняемыеРеквизиты = Новый Структура;
		ЗаполняемыеРеквизиты.Вставить("ЭтоДополнительноеСоглашение", Истина);
		ЗаполняемыеРеквизиты.Вставить("Статус", Перечисления.СтатусыЭТД.Сформирован);
		ЗаполняемыеРеквизиты.Вставить("ИдентификаторОсновногоЭТД", ДанныеЗаполнения.Идентификатор);
		ЗаполняемыеРеквизиты.Вставить("НомерДоговора", ДанныеЗаполнения.НомерДоговора + "/" + ЭтотОбъект.Номер);
		ЗаполняемыеРеквизиты.Вставить("ДатаНачала", ДатаДокумента);
		ЗаполняемыеРеквизиты.Вставить("ДатаЗаключения", ДатаДокумента);
		ЗаполняемыеРеквизиты.Вставить("ДатаОкончанияДоговора", ДанныеЗаполнения.ДатаОкончания);
		ЗаполняемыеРеквизиты.Вставить("ОсновнойЭТД", ДанныеЗаполнения);
		ИнформацияОКодеДолжности = ЭТДСервер.ПолучитьИнформациюОКодеДолжности(ДатаДокумента, ДанныеЗаполнения.Должность, ДанныеЗаполнения.ДолжностьПоШтатномуРасписанию, ДанныеЗаполнения.Организация);
		ЗаполняемыеРеквизиты.Вставить("КодДолжности", ИнформацияОКодеДолжности.КодДолжности);
		ЗаполняемыеРеквизиты.Вставить("НаименованиеДолжности", ИнформацияОКодеДолжности.НаименованиеДолжности);
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ЗаполняемыеРеквизиты);
		
		ЭтотОбъект.ОбщиеНавыки.Загрузить(ДанныеЗаполнения.ОбщиеНавыки.Выгрузить());
		ЭтотОбъект.СпециальныеНавыки.Загрузить(ДанныеЗаполнения.СпециальныеНавыки.Выгрузить());
		
		Для Каждого УсловиеТрудаОснования Из ДанныеЗаполнения.УсловияТруда Цикл
			НовоеУсловиеТруда = ЭтотОбъект.УсловияТруда.Добавить();
			НовоеУсловиеТруда.УсловиеТруда = УсловиеТрудаОснования.УсловиеТруда;
		КонецЦикла;
		
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ДатаНачала) Тогда
		Если ЗначениеЗаполнено(ЭтотОбъект.ДатаЗаключения) Тогда
			ЭтотОбъект.ДатаНачала = ЭтотОбъект.ДатаЗаключения;
		Иначе
			ЭтотОбъект.ДатаНачала = ДатаДокумента;
		КонецЕсли;
	КонецЕсли;
	
	// данные организации
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.Организация) Тогда
		ЭтотОбъект.Организация = ЭТДСервер.ОрганизацияПоУмолчанию();
	КонецЕсли;
	
	Если ЗначениеЗаполнено(ЭтотОбъект.Организация) Тогда
		
		Если НЕ ЗначениеЗаполнено(ЭтотОбъект.БИНОрганизации) Тогда
			ЭтотОбъект.БИНОрганизации = ЭТДСервер.БИНОрганизации(ЭтотОбъект.Организация);	
		КонецЕсли;
		
		ПериодДанных = ?(ЗначениеЗаполнено(ЭтотОбъект.ДатаНачала), ЭтотОбъект.ДатаНачала, ДатаДокумента);
		ДанныеДляАвтозаполненияЭТД = ЭТДСервер.ПолучитьДанныеДляАвтозаполненияЭТД(ЭтотОбъект.Организация, ПериодДанных);
		
		Для Каждого СтрокаАвтозаполнения Из ДанныеДляАвтозаполненияЭТД Цикл
			
			Если НЕ ЗначениеЗаполнено(ЭтотОбъект[СтрокаАвтозаполнения.Ключ]) Тогда
				ЭтотОбъект[СтрокаАвтозаполнения.Ключ] = СтрокаАвтозаполнения.Значение;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	// данные сотрудника
	ДополнительныеДанныеСотрудника = ЭТДСервер.ПолучитьДанныеСотрудника(ЭтотОбъект.ДатаНачала, ЭтотОбъект.Сотрудник);
	
	Если ДополнительныеДанныеСотрудника.Количество() > 0 Тогда
		
		Для Каждого СтрокаДанныхСотрудника Из ДополнительныеДанныеСотрудника Цикл
			
			ЗаполняемыйРеквизит = ЭтотОбъект[СтрокаДанныхСотрудника.Ключ];
			
			Если ТипЗнч(ЗаполняемыйРеквизит) = Тип("Булево") ИЛИ НЕ ЗначениеЗаполнено(ЗаполняемыйРеквизит) Тогда
				
				Если ТипЗнч(СтрокаДанныхСотрудника.Значение) = Тип("ТаблицаЗначений") Тогда
					
					ЭтотОбъект[СтрокаДанныхСотрудника.Ключ].Загрузить(СтрокаДанныхСотрудника.Значение);
					
				Иначе
					
					ЭтотОбъект[СтрокаДанныхСотрудника.Ключ] = СтрокаДанныхСотрудника.Значение;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЕсли;
	
	Если ЭтотОбъект.УсловияТруда.Количество() = 0 Тогда
		УсловияТрудаСтрока = ЭТДСервер.ПолучитьУсловияТрудаПоУмолчанию();
		
		ЭТДКлиентСервер.ЗаполнитьУсловияТруда(УсловияТруда, УсловияТрудаСтрока);
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ДатаЗаключения) Тогда
		Если ЗначениеЗаполнено(ЭтотОбъект.ДатаНачала) Тогда
			ЭтотОбъект.ДатаЗаключения = ЭтотОбъект.ДатаНачала;
		Иначе
			ЭтотОбъект.ДатаЗаключения = ДатаДокумента;
		КонецЕсли;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ЯвляетсяЭТД) И НЕ ЭтотОбъект.ЭтоДополнительноеСоглашение Тогда
		ЭтотОбъект.ЯвляетсяЭТД = "false";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.ПричинаРасторженияЦифровизация) Тогда
		ЭтотОбъект.ПричинаРасторженияЦифровизация = "false";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.НаличиеЗаявленияРаботника) Тогда
		ЭтотОбъект.НаличиеЗаявленияРаботника = "0";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.СогласиеРаботника) Тогда
		ЭтотОбъект.СогласиеРаботника = "0";
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ЭтотОбъект.СогласиеРаботодателя) Тогда
		ЭтотОбъект.СогласиеРаботодателя = "0";
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если Статус.Пустая() Тогда
		Статус = Перечисления.СтатусыЭТД.Сформирован;
	КонецЕсли;
	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	СписокРеквизитов = Новый Массив();
	СписокРеквизитов.Добавить("Автор");
	СписокРеквизитов.Добавить("ДатаРасторжения");
	СписокРеквизитов.Добавить("Идентификатор");
	СписокРеквизитов.Добавить("ОснованиеУвольнения");
	СписокРеквизитов.Добавить("Статус");
	СписокРеквизитов.Добавить("ОтношениеКВоинскойСлужбе");
	СписокРеквизитов.Добавить("КодПричиныРасторжения");
	СписокРеквизитов.Добавить("НомерДоговора");
	СписокРеквизитов.Добавить("ДокументОснование");
	СписокРеквизитов.Добавить("ЗагруженИзЕСУТД");
	СписокРеквизитов.Добавить("КодСтраныМестаРаботы");
	СписокРеквизитов.Добавить("ОснованиеПривлеченияИностранца");
	СписокРеквизитов.Добавить("НомерРазрешенияИностранца");
	СписокРеквизитов.Добавить("ДатаРазрешенияИностранца");
	СписокРеквизитов.Добавить("ОбразованиеИностранца");
	СписокРеквизитов.Добавить("ДатаНачалаРазрешенияИностранца");
	СписокРеквизитов.Добавить("ДатаОкончанияРазрешенияИностранца");
	СписокРеквизитов.Добавить("ИныеИзмененияУсловийТруда");
	СписокРеквизитов.Добавить("ЯвляетсяЭТД");
	СписокРеквизитов.Добавить("ДолжностьНАОМСУ");
	СписокРеквизитов.Добавить("НаличиеЗаявленияРаботника");
	СписокРеквизитов.Добавить("СогласиеРаботника");
	СписокРеквизитов.Добавить("СогласиеРаботодателя");
	СписокРеквизитов.Добавить("НомерТелефонаРаботника");
	СписокРеквизитов.Добавить("НомерТелефонаМенеджера");
	СписокРеквизитов.Добавить("НаименованиеБанка");
	СписокРеквизитов.Добавить("БИКБанка");
	СписокРеквизитов.Добавить("БанковскийСчетРаботника");
	СписокРеквизитов.Добавить("НаименованиеОбразованияИностранца");
	СписокРеквизитов.Добавить("НаименованиеОснованияПривлеченияИностранца");
	СписокРеквизитов.Добавить("ПричинаРасторженияЦифровизация");
	СписокРеквизитов.Добавить("КодРегионаРабочегоМеста"); // убрать после вывода в интерфейс
	
	Для Каждого Реквизит Из СписокРеквизитов Цикл
		Если НЕ ОбщегоНазначенияКлиентСервер.ЕстьРеквизитИлиСвойствоОбъекта(ЭтотОбъект, Реквизит) Тогда
			Продолжить;
		КонецЕсли;
		
		Если Реквизит = "ЯвляетсяЭТД" Тогда
			Если НЕ ЭтотОбъект.ЭтоДополнительноеСоглашение Тогда
				ЭтотОбъект[Реквизит] = "false";
			Иначе
				ЭтотОбъект[Реквизит] = "";
			КонецЕсли;
		ИначеЕсли Реквизит = "ПричинаРасторженияЦифровизация" Тогда
			Если НЕ ЭтотОбъект.ЭтоДополнительноеСоглашение Тогда
				ЭтотОбъект[Реквизит] = "false";
			Иначе
				ЭтотОбъект[Реквизит] = "";
			КонецЕсли;
		ИначеЕсли Реквизит = "НаличиеЗаявленияРаботника" Тогда
			ЭтотОбъект[Реквизит] = "0";
		ИначеЕсли Реквизит = "СогласиеРаботника" Тогда
			ЭтотОбъект[Реквизит] = "0";
		ИначеЕсли Реквизит = "СогласиеРаботодателя" Тогда
			ЭтотОбъект[Реквизит] = "0";
		Иначе
			ЭтотОбъект[Реквизит] = Неопределено;
		КонецЕсли;
	КонецЦикла;
	
	МетаданныеОбъекта = ЭтотОбъект.Ссылка.Метаданные();
	
	Для Каждого ТабличнаяЧасть Из МетаданныеОбъекта.ТабличныеЧасти Цикл
		ИмяТабличнойЧасти = ТабличнаяЧасть.Имя;
		Если ИмяТабличнойЧасти <> "УсловияТруда" Тогда
			ЭтотОбъект[ИмяТабличнойЧасти].Очистить();
		КонецЕсли;
	КонецЦикла;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли