
Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Месяц", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Участок", Участок);
	Запрос.УстановитьПараметр("ВидСкважины", ВидСкважины);   
	//++ Кибернетика Беляев Е.И. 12.07.2024
	Запрос.УстановитьПараметр("ВидБурения", киб_ВидБурения);
	//-- Кибернетика Беляев Е.И. 12.07.2024
	Запрос.УстановитьПараметр("пустаяНоменклатура", Справочники.Номенклатура.ПустаяСсылка());
	Если Дата < Дата('20170601') Тогда
		Запрос.Текст =
		"ВЫБРАТЬ
		|	&Месяц КАК Период,
		|	ВложенныйЗапрос.Участок КАК Участок,
		|	ВложенныйЗапрос.ВидСкважины КАК ВидСкважины,
		|	ВложенныйЗапрос.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ЗНАЧЕНИЕ(Перечисление.КТ_ВидыТМЦ_bug.Нормируемые) КАК ВидОперацииТМЦ,
		|	ЗНАЧЕНИЕ(Перечисление.КТ_ВариантПланФакт.Факт) КАК ВидРасхода,
		|	МАКСИМУМ(ВложенныйЗапрос.ВидНормы) КАК ВидНормы,
		|	МАКСИМУМ(ВложенныйЗапрос.Норма) КАК Норма,
		|	ВложенныйЗапрос.ВидыБурения КАК ВидыБурения
		|ИЗ
		|	(ВЫБРАТЬ
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка.Участок КАК Участок,
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка.ВидСкважины КАК ВидСкважины,
		|		КТ_НормыРасходаТМЦСписокНорм.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|		КТ_НормыРасходаТМЦСписокНорм.ВидНормы КАК ВидНормы,
		|		КТ_НормыРасходаТМЦСписокНорм.Норма КАК Норма,
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка.киб_ВидБурения КАК ВидыБурения
		|	ИЗ
		|		Документ.КТ_НормыРасходаТМЦ.СписокНорм КАК КТ_НормыРасходаТМЦСписокНорм
		|	ГДЕ
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КТ_НормыРасходаТМЦСрезПоследних.Участок,
		|		КТ_НормыРасходаТМЦСрезПоследних.ВидСкважины,
		|		КТ_НормыРасходаТМЦСрезПоследних.НоменклатурнаяГруппа,
		|		NULL,
		|		0,
		|		КТ_НормыРасходаТМЦСрезПоследних.ВидыБурения
		|	ИЗ
		|		РегистрСведений.КТ_НормыРасходаТМЦ.СрезПоследних(
		|				&Месяц,
		|				Регистратор <> &Ссылка
		|					И ВидСкважины = &ВидСкважины
		|					И ВидыБурения = &ВидБурения
		|					И Участок = &Участок) КАК КТ_НормыРасходаТМЦСрезПоследних) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Участок,
		|	ВложенныйЗапрос.ВидСкважины,
		|	ВложенныйЗапрос.НоменклатурнаяГруппа,
		|	ВложенныйЗапрос.ВидыБурения";
	иначе	
		Запрос.Текст =
		"ВЫБРАТЬ
		|	&Месяц КАК Период,
		|	ВложенныйЗапрос.Участок КАК Участок,
		|	ВложенныйЗапрос.ВидСкважины КАК ВидСкважины,
		|	ВложенныйЗапрос.Номенклатура.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
		|	ЗНАЧЕНИЕ(Перечисление.КТ_ВидыТМЦ_bug.Нормируемые) КАК ВидОперацииТМЦ,
		|	ЗНАЧЕНИЕ(Перечисление.КТ_ВариантПланФакт.Факт) КАК ВидРасхода,
		|	МАКСИМУМ(ВложенныйЗапрос.ВидНормы) КАК ВидНормы,
		|	МАКСИМУМ(ВложенныйЗапрос.Норма) КАК Норма,
		|	ВложенныйЗапрос.Номенклатура КАК Номенклатура,
		|	ВложенныйЗапрос.ВидыБурения КАК ВидыБурения
		|ИЗ
		|	(ВЫБРАТЬ
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка.Участок КАК Участок,
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка.ВидСкважины КАК ВидСкважины,
		|		КТ_НормыРасходаТМЦСписокНорм.ВидНормы КАК ВидНормы,
		|		КТ_НормыРасходаТМЦСписокНорм.Норма КАК Норма,
		|		КТ_НормыРасходаТМЦСписокНорм.Номенклатура КАК Номенклатура,
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка.киб_ВидБурения КАК ВидыБурения
		|	ИЗ
		|		Документ.КТ_НормыРасходаТМЦ.СписокНорм КАК КТ_НормыРасходаТМЦСписокНорм
		|	ГДЕ
		|		КТ_НормыРасходаТМЦСписокНорм.Ссылка = &Ссылка
		|	
		|	ОБЪЕДИНИТЬ ВСЕ
		|	
		|	ВЫБРАТЬ
		|		КТ_НормыРасходаТМЦСрезПоследних.Участок,
		|		КТ_НормыРасходаТМЦСрезПоследних.ВидСкважины,
		|		NULL,
		|		0,
		|		КТ_НормыРасходаТМЦСрезПоследних.Номенклатура,
		|		КТ_НормыРасходаТМЦСрезПоследних.ВидыБурения
		|	ИЗ
		|		РегистрСведений.КТ_НормыРасходаТМЦ.СрезПоследних(
		|				&Месяц,
		|				Регистратор <> &Ссылка
		|					И ВидСкважины = &ВидСкважины
		|					И ВидыБурения = &ВидБурения
		|					И Участок = &Участок) КАК КТ_НормыРасходаТМЦСрезПоследних
		|	ГДЕ
		|		КТ_НормыРасходаТМЦСрезПоследних.Номенклатура <> &пустаяНоменклатура) КАК ВложенныйЗапрос
		|
		|СГРУППИРОВАТЬ ПО
		|	ВложенныйЗапрос.Участок,
		|	ВложенныйЗапрос.ВидСкважины,
		|	ВложенныйЗапрос.Номенклатура,
		|	ВложенныйЗапрос.Номенклатура.НоменклатурнаяГруппа,
		|	ВложенныйЗапрос.ВидыБурения";
	КонецЕсли;
	
	Движения.КТ_НормыРасходаТМЦ.Записывать = Истина;
	Движения.КТ_НормыРасходаТМЦ.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда 
		УстановитьНовыйНомер();
	КонецЕсли;
КонецПроцедуры


Процедура ПриКопировании(ОбъектКопирования)
	Автор = Справочники.Пользователи.ПустаяСсылка();
КонецПроцедуры

