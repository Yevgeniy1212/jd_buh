#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры


Функция КонфликтующиеРегистраторы(Регистратор,Месяц, Сотрудник) Экспорт
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ПрименениеВычетовИПН.Регистратор,
		|	ПРЕДСТАВЛЕНИЕССЫЛКИ(ПрименениеВычетовИПН.Регистратор) КАК ПредставлениеРегистратора
		|ИЗ
		|	РегистрСведений.ПрименениеВычетовИПН КАК ПрименениеВычетовИПН
		|ГДЕ
		|	ПрименениеВычетовИПН.Период = &Период
		|	И ПрименениеВычетовИПН.ФизическоеЛицо = &ФизическоеЛицо
		|	И ПрименениеВычетовИПН.Регистратор <> &Регистратор";
	
	Запрос.УстановитьПараметр("ФизическоеЛицо"	, Сотрудник);
	Запрос.УстановитьПараметр("Период"			, Месяц);
	Запрос.УстановитьПараметр("Регистратор"		, Регистратор);
	
	Возврат Запрос.Выполнить();
	
КонецФункции

#Область Проведение

//Подготовка параметров
Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("вт_Реквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПрекращениеПредоставленияВычетовИПН.Дата КАК Дата,
	|	ПрекращениеПредоставленияВычетовИПН.Организация КАК Организация,
	|	ПрекращениеПредоставленияВычетовИПН.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	ПрекращениеПредоставленияВычетовИПН.Организация КАК Налогоплательщик,
	|	ПрекращениеПредоставленияВычетовИПН.Ссылка КАК Ссылка,
	|	ПрекращениеПредоставленияВычетовИПН.Сотрудник КАК ФизическоеЛицо,
	|	ПрекращениеПредоставленияВычетовИПН.Месяц КАК Месяц
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.ПрекращениеПредоставленияВычетовИПН КАК ПрекращениеПредоставленияВычетовИПН
	|ГДЕ
	|	ПрекращениеПредоставленияВычетовИПН.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.Налогоплательщик КАК Налогоплательщик,
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.ФизическоеЛицо КАК ФизическоеЛицо,
	|	Реквизиты.ФизическоеЛицо КАК ФизЛицо,
	|	Реквизиты.Месяц КАК Месяц,
	|	Реквизиты.Месяц КАК Период
	|ИЗ
	|	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса; 
	
КонецФункции

Функция ТекстЗапросаТаблицаПрименениеВычетовИПН(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("Вычеты", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВычетыФизическихЛицИПНСрезПоследних.ВидВычета КАК ВидВычетаИПН,
	|	ВычетыФизическихЛицИПНСрезПоследних.Основание,
	|	ВычетыФизическихЛицИПНСрезПоследних.ДействуетПо,
	|	ВычетыФизическихЛицИПНСрезПоследних.Период
	|ИЗ
	|	РегистрСведений.ВычетыФизическихЛицИПН.СрезПоследних(
	|			&Месяц,
	|			ФизическоеЛицо = &ФизическоеЛицо
	|				И Регистратор <> &Ссылка) КАК ВычетыФизическихЛицИПНСрезПоследних
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВычетыФизическихЛицИПНСрезПоследних.ДействуетПо = &ПустаяДата
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВычетыФизическихЛицИПНСрезПоследних.ДействуетПо >= &Месяц
	|		КОНЕЦ
	|	И ВычетыФизическихЛицИПНСрезПоследних.ПредоставлятьВычет = ИСТИНА
	|	И ВычетыФизическихЛицИПНСрезПоследних.Регистратор <> &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДействуетПо,
	|	ВидВычетаИПН";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаСтандартныеВычетыИПН(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("ТаблицаСтандартныеВычетыИПН", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ВычетыФизическихЛицИПНСрезПоследних.ВидВычета КАК ВидВычетаИПН,
	|   &ФизическоеЛицо КАК ФизЛицо,
	|   &Месяц КАК Период,
	|	ВычетыФизическихЛицИПНСрезПоследних.Основание,
	|	ВычетыФизическихЛицИПНСрезПоследних.ДействуетПо,
	|	ВычетыФизическихЛицИПНСрезПоследних.Период КАК ПериодРегистра
	|ИЗ
	|	РегистрСведений.ВычетыФизическихЛицИПН.СрезПоследних(
	|			&Месяц,
	|			ФизическоеЛицо = &ФизическоеЛицо
	|				И Регистратор <> &Ссылка) КАК ВычетыФизическихЛицИПНСрезПоследних
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ВычетыФизическихЛицИПНСрезПоследних.ДействуетПо = &ПустаяДата
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ВычетыФизическихЛицИПНСрезПоследних.ДействуетПо >= &Месяц
	|		КОНЕЦ
	|	И ВычетыФизическихЛицИПНСрезПоследних.ПредоставлятьВычет = ИСТИНА
	|	И ВычетыФизическихЛицИПНСрезПоследних.Регистратор <> &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДействуетПо,
	|	ВидВычетаИПН";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаПрочиеВычетыИПН(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("ТаблицаПрочиеВычетыИПН", НомераТаблиц.Количество());

	ТекстЗапроса = "ВЫБРАТЬ
	|	ПрочиеВычетыФизическихЛицИПНСрезПоследних.ВидВычета КАК ВидВычетаИПН,
	|   &ФизическоеЛицо КАК ФизЛицо,
	|   &Месяц КАК Период,
	|	ПрочиеВычетыФизическихЛицИПНСрезПоследних.ДействуетПо КАК ДействуетПо,
	|	ПрочиеВычетыФизическихЛицИПНСрезПоследних.Размер,
	|	ПрочиеВычетыФизическихЛицИПНСрезПоследних.Основание КАК ДокументПодтверждающийПравоНаВычет,
	|	ПрочиеВычетыФизическихЛицИПНСрезПоследних.ДействуетС
	|ИЗ
	|	РегистрСведений.ПрочиеВычетыФизическихЛицИПН.СрезПоследних(
	|			&Месяц,
	|			Регистратор <> &Ссылка
	|				И ФизическоеЛицо = &ФизическоеЛицо) КАК ПрочиеВычетыФизическихЛицИПНСрезПоследних
	|ГДЕ
	|	ВЫБОР
	|			КОГДА ПрочиеВычетыФизическихЛицИПНСрезПоследних.ДействуетПо = &ПустаяДата
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ПрочиеВычетыФизическихЛицИПНСрезПоследних.ДействуетПо >= &Месяц
	|		КОНЕЦ
	|	И ПрочиеВычетыФизическихЛицИПНСрезПоследних.Размер <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ПрочиеВычетыФизическихЛицИПНСрезПоследних.ВидВычета,
	|	ДействуетПо";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	
	Запрос.УстановитьПараметр("Ссылка"							, ДокументСсылка);
	Запрос.УстановитьПараметр("Месяц"							, ДокументСсылка.Месяц);
	Запрос.УстановитьПараметр("ПустаяДата"						, Дата(1,1,1));
	Запрос.УстановитьПараметр("ФизическоеЛицо"					, ДокументСсылка.Сотрудник);
	Запрос.УстановитьПараметр("ДатаОграниченияПолученияДанных"	, '20180101');
	
	Запрос.МенеджерВременныхТаблиц 	= Новый МенеджерВременныхТаблиц;
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	
	Результат 			= Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты 	= Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Если ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями") Тогда
		Налогоплательщик = ПроцедурыНалоговогоУчетаВызовСервераПовтИсп.ПолучитьНалогоплательщикаСтруктурнойЕдиницы(
		?(ЗначениеЗаполнено(Реквизиты.СтруктурноеПодразделение), Реквизиты.СтруктурноеПодразделение, Реквизиты.Организация),
		Реквизиты.Организация,
		Перечисления.РазделыНалоговогоУчета.НалогиСЗаработнойПлаты);
		ТаблицаРеквизиты.ЗаполнитьЗначения(Налогоплательщик, "Налогоплательщик")
	КонецЕсли;
	
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаСтандартныеВычетыИПН(НомераТаблиц, Реквизиты)
	+ТекстЗапросаТаблицаПрочиеВычетыИПН(НомераТаблиц, Реквизиты);
	
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда 
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	//ТаблицаПрименениеВычетовИПН = Новый ТаблицаЗначений;
	//ТаблицаПрименениеВычетовИПН.Колонки.Добавить("Организация");
	//ТаблицаПрименениеВычетовИПН.Колонки.Добавить("Период");
	//ТаблицаПрименениеВычетовИПН.Колонки.Добавить("ФизЛицо");
	//Строка = ТаблицаПрименениеВычетовИПН.Добавить();
	//Строка.Организация = Реквизиты.Организация;
	//Строка.Период = Реквизиты.Месяц;
	//Строка.ФизЛицо = Реквизиты.ФизическоеЛицо;
	
	ПараметрыПроведения.Вставить("ТаблицаПрименениеВычетовИПН", ТаблицаРеквизиты);
	
	Возврат ПараметрыПроведения;

КонецФункции 

#КонецОбласти

#КонецОбласти

#КонецЕсли
	