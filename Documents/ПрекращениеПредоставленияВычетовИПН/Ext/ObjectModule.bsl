
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ДанныеЗаполнения) И ТипЗнч(ДанныеЗаполнения) = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Организация") Тогда
			ЭтотОбъект.Организация = ДанныеЗаполнения.Организация;	
		КонецЕсли;	
		
		Если ДанныеЗаполнения.Свойство("Месяц") Тогда
			ЭтотОбъект.Месяц = ДанныеЗаполнения.Месяц;
		Иначе
			ЭтотОбъект.Месяц = НачалоМесяца(ОбщегоНазначения.ТекущаяДатаПользователя()); 
		КонецЕсли;	
		
		Если ДанныеЗаполнения.Свойство("Сотрудник") Тогда
			ЭтотОбъект.Сотрудник = ДанныеЗаполнения.Сотрудник;	
		КонецЕсли;	
	Иначе
		ЭтотОбъект.Месяц = НачалоМесяца(ОбщегоНазначения.ТекущаяДатаПользователя()); 	
	КонецЕсли;	
	
	Если ЭтотОбъект.Месяц < Дата(2018,1,1) Тогда
		ЭтотОбъект.Месяц = НачалоМесяца(Дата(2018,1,1));
	КонецЕсли;
	
	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект);	
	
КонецПроцедуры
	
#КонецОбласти

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	ПараметрыПроведения = Документы.ПрекращениеПредоставленияВычетовИПН.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	РасчетЗарплатыСервер.СформироватьДвиженияПрекращениеПредоставленияВычетовИПН(ПараметрыПроведения.ТаблицаПрименениеВычетовИПН, ПараметрыПроведения.ТаблицаСтандартныеВычетыИПН, ПараметрыПроведения.ТаблицаПрочиеВычетыИПН, Движения, Отказ);
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	// Проверка на дубли движений
	
	УстановитьПривилегированныйРежим(Истина);
	
	РезультатЗапроса = Документы.ПрекращениеПредоставленияВычетовИПН.КонфликтующиеРегистраторы(Ссылка, Месяц, Сотрудник);
	
	Если НЕ РезультатЗапроса.Пустой() Тогда
		
		Выборка = РезультатЗапроса.Выбрать();
		Пока Выборка.Следующий() Цикл
			ТекстСообщения = НСтр("ru = 'Не удалось провести документ. Требуемые изменения вычетов необходимо провести в документе %1..'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Регистратор);
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Выборка.Регистратор, , , Отказ);
		КонецЦикла;
		
	КонецЕсли;
	
	УстановитьПривилегированныйРежим(Ложь);

КонецПроцедуры

#КонецЕсли

