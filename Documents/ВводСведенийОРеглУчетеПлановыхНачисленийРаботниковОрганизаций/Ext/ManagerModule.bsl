#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ПЕЧАТИ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Запрос.УстановитьПараметр("Организация", 			   Реквизиты.Организация);
	Запрос.УстановитьПараметр("ОбособленноеПодразделение", Реквизиты.ОбособленноеПодразделение);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение",  Реквизиты.СтруктурноеПодразделение);

	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаОсновныеНачисления(НомераТаблиц);
	
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда 
		
		Результат = Запрос.ВыполнитьПакет();
		
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
		
	КонецЕсли;
	
	Возврат ПараметрыПроведения;

КонецФункции 

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	Реквизиты.Ссылка,
	|	Реквизиты.Дата,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетнаяПолитикаПоПерсоналуОрганизаций.ВестиУчетПоГоловнойОрганизации, ИСТИНА)
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|						ТОГДА Реквизиты.Организация
	|					ИНАЧЕ Реквизиты.Организация.ГоловнаяОрганизация
	|				КОНЕЦ
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК Организация,
	|	Реквизиты.Организация КАК ОбособленноеПодразделение,
	|	Реквизиты.СтруктурноеПодразделение
	|ИЗ
	|	Документ.ВводСведенийОРеглУчетеПлановыхНачисленийРаботниковОрганизаций КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаПоПерсоналуОрганизаций КАК УчетнаяПолитикаПоПерсоналуОрганизаций
	|		ПО Реквизиты.Организация = УчетнаяПолитикаПоПерсоналуОрганизаций.Организация
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса 
	
КонецФункции

Функция ТекстЗапросаТаблицаОсновныеНачисления(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаОсновныеНачисления", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	&Организация,
	|	&ОбособленноеПодразделение,
	|	&СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	ТаблицаОсновныеНачисления.ДатаНачала КАК Период,
	|	ТаблицаОсновныеНачисления.Сотрудник,
	|	ТаблицаОсновныеНачисления.ВидРасчета,
	|	ТаблицаОсновныеНачисления.СпособОтраженияВБухучете
	|ИЗ
	|	Документ.ВводСведенийОРеглУчетеПлановыхНачисленийРаботниковОрганизаций.ОсновныеНачисления КАК ТаблицаОсновныеНачисления
	|ГДЕ
	|	ТаблицаОсновныеНачисления.Ссылка = &Ссылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОсновныеНачисления.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Заполнение на основании

Процедура ЗаполнитьПоПриемНаРаботуВОрганизацию(Объект, Основание) Экспорт

	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(Объект, Основание);
	
	ЗапросДляЗаполнения	= Новый Запрос;
	ЗапросДляЗаполнения.УстановитьПараметр("ДокументСсылка", Основание);
	ЗапросДляЗаполнения.Текст	=	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	НачисленияПриПриеме.ФизЛицо,
	|	НачисленияПриПриеме.Сотрудник,
	|	НачисленияПриПриеме.ВидРасчета,
	|	ПринятыеРаботники.ДатаПриема КАК ДатаНачала
	|ИЗ
	|	Документ.ПриемНаРаботуВОрганизацию.ОсновныеНачисления КАК НачисленияПриПриеме
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.ПриемНаРаботуВОрганизацию.РаботникиОрганизации КАК ПринятыеРаботники
	|		ПО НачисленияПриПриеме.Ссылка = ПринятыеРаботники.Ссылка 
	|			И НачисленияПриПриеме.Сотрудник = ПринятыеРаботники.Сотрудник
	|
	|ГДЕ
	|	НачисленияПриПриеме.Ссылка = &ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	НачисленияПриПриеме.НомерСтроки";

	РезультатЗапроса = ЗапросДляЗаполнения.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Объект.ОсновныеНачисления.Добавить();
		
		СтрокаТабличнойЧасти.ФизЛицо    = Выборка.ФизЛицо;
		СтрокаТабличнойЧасти.Сотрудник  = Выборка.Сотрудник;
		СтрокаТабличнойЧасти.ВидРасчета = Выборка.ВидРасчета;
		СтрокаТабличнойЧасти.ДатаНачала = Выборка.ДатаНачала;
		
	КонецЦикла;
	
	Объект.ДокументОснование = Основание;
	
КонецПроцедуры

Процедура ЗаполнитьПоКадровоеПеремещениеОрганизаций(Объект, Основание) Экспорт
	
	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(Объект, Основание);
	
	ЗапросДляЗаполнения	= Новый	Запрос;
	ЗапросДляЗаполнения.УстановитьПараметр("ДокументСсылка", Основание);
	ЗапросДляЗаполнения.Текст	=	
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Начисления.ФизЛицо,
	|	Начисления.Сотрудник,
	|	Начисления.ВидРасчета,
	|	ПринятыеРаботники.ДатаНачала КАК ДатаНачала
	|ИЗ
	|	Документ.КадровоеПеремещениеОрганизаций.ОсновныеНачисления КАК Начисления
	|
	|	ЛЕВОЕ СОЕДИНЕНИЕ Документ.КадровоеПеремещениеОрганизаций.РаботникиОрганизации КАК ПринятыеРаботники
	|		ПО Начисления.Ссылка = ПринятыеРаботники.Ссылка 
	|			И Начисления.Сотрудник = ПринятыеРаботники.Сотрудник 
	|
	|ГДЕ
	|	Начисления.Ссылка = &ДокументСсылка
	|
	|УПОРЯДОЧИТЬ ПО
	|	Начисления.НомерСтроки";

	РезультатЗапроса = ЗапросДляЗаполнения.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Объект.ОсновныеНачисления.Добавить();
		
		СтрокаТабличнойЧасти.ФизЛицо    = Выборка.ФизЛицо;
		СтрокаТабличнойЧасти.Сотрудник  = Выборка.Сотрудник;
		СтрокаТабличнойЧасти.ВидРасчета = Выборка.ВидРасчета;
		СтрокаТабличнойЧасти.ДатаНачала = Выборка.ДатаНачала;
		
	КонецЦикла;
	
	Объект.ДокументОснование = Основание;
	
КонецПроцедуры

Процедура ЗаполнитьПоОтражениеЗарплатыВРеглУчете(Объект, Основание) Экспорт
	
	// Заполним реквизиты из стандартного набора по документу основанию.
	ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(Объект, Основание);
	
	ЗапросДляЗаполнения = Новый Запрос;
	ЗапросДляЗаполнения.УстановитьПараметр("ДокументСсылка", 	  Основание);
	ЗапросДляЗаполнения.УстановитьПараметр("ПериодРегистрации",   Основание.ПериодРегистрации);
	ЗапросДляЗаполнения.УстановитьПараметр("Организация", 		  Основание.Организация);
	ЗапросДляЗаполнения.УстановитьПараметр("ГоловнаяОрганизация", ОбщегоНазначенияБКВызовСервера.ГоловнаяОрганизацияДляУчетаЗарплаты(Основание.Организация));
	
	ЗапросДляЗаполнения.Текст = 
	"ВЫБРАТЬ
	|	НачислениеЗарплаты.ВидРасчета,
	|	МАКСИМУМ(НачислениеЗарплаты.Сотрудник) КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_НачислениеЗарплаты
	|ИЗ
	|	Документ.НачислениеЗарплатыРаботникамОрганизаций.Начисления КАК НачислениеЗарплаты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.РеглУчетПлановыхНачисленийРаботниковОрганизаций.СрезПоследних(&ПериодРегистрации, Организация = &ГоловнаяОрганизация) КАК РеглУчетПлановыхНачисленийРаботниковОрганизаций
	|		ПО НачислениеЗарплаты.Сотрудник = РеглУчетПлановыхНачисленийРаботниковОрганизаций.Сотрудник
	|			И НачислениеЗарплаты.ВидРасчета = РеглУчетПлановыхНачисленийРаботниковОрганизаций.ВидРасчета
	|ГДЕ
	|	НачислениеЗарплаты.Ссылка.ПериодРегистрации = &ПериодРегистрации
	|	И НачислениеЗарплаты.Ссылка.Организация = &Организация
	|	И НачислениеЗарплаты.Ссылка.Проведен
	|	И РеглУчетПлановыхНачисленийРаботниковОрганизаций.Сотрудник ЕСТЬ NULL 
	|
	|СГРУППИРОВАТЬ ПО
	|	НачислениеЗарплаты.Физлицо,
	|	НачислениеЗарплаты.ВидРасчета
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	МАКСИМУМ(РаботникиОрганизации.Сотрудник) КАК Сотрудник
	|ПОМЕСТИТЬ ВТ_РаботникиОрганизации
	|ИЗ
	|	РегистрСведений.РаботникиОрганизаций.СрезПоследних(
	|			КОНЕЦПЕРИОДА(&ПериодРегистрации, МЕСЯЦ),
	|			Организация = &ГоловнаяОрганизация
	|				И Сотрудник.ВидЗанятости <> ЗНАЧЕНИЕ(Перечисление.ВидыЗанятостиВОрганизации.ВнутреннееСовместительство)
	|				И ПричинаИзмененияСостояния = ЗНАЧЕНИЕ(Перечисление.ПричиныИзмененияСостояния.ПриемНаРаботу)) КАК РаботникиОрганизации
	|
	|СГРУППИРОВАТЬ ПО
	|	РаботникиОрганизации.Сотрудник
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ОтражениеВУчете.ФизЛицо,
	|	ЕСТЬNULL(НачислениеЗарплаты.Сотрудник, РаботникиОрганизации.Сотрудник) КАК Сотрудник,
	|	ОтражениеВУчете.ВидРасчета,
	|	&ПериодРегистрации КАК ДатаНачала
	|ИЗ
	|	Документ.ОтражениеЗарплатыВРеглУчете.ОтражениеВУчете КАК ОтражениеВУчете
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_НачислениеЗарплаты КАК НачислениеЗарплаты
	|		ПО ОтражениеВУчете.ФизЛицо = НачислениеЗарплаты.Сотрудник.Физлицо
	|			И ОтражениеВУчете.ВидРасчета = НачислениеЗарплаты.ВидРасчета
	|		ЛЕВОЕ СОЕДИНЕНИЕ ВТ_РаботникиОрганизации КАК РаботникиОрганизации
	|		ПО ОтражениеВУчете.ФизЛицо = РаботникиОрганизации.Сотрудник.Физлицо
	|ГДЕ
	|	ОтражениеВУчете.Ссылка = &ДокументСсылка
	|	И ОтражениеВУчете.ВидРасчета ССЫЛКА ПланВидовРасчета.ОсновныеНачисленияОрганизаций
	|	И (ОтражениеВУчете.СчетДт = ЗНАЧЕНИЕ(ПланСчетов.Типовой.ПустаяСсылка)
	|			ИЛИ ОтражениеВУчете.СчетКт = ЗНАЧЕНИЕ(ПланСчетов.Типовой.ПустаяСсылка))";
	
	РезультатЗапроса = ЗапросДляЗаполнения.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтрокаТабличнойЧасти = Объект.ОсновныеНачисления.Добавить();
		
		СтрокаТабличнойЧасти.ФизЛицо    = Выборка.ФизЛицо;
		СтрокаТабличнойЧасти.Сотрудник  = Выборка.Сотрудник;
		СтрокаТабличнойЧасти.ВидРасчета = Выборка.ВидРасчета;
		СтрокаТабличнойЧасти.ДатаНачала = Выборка.ДатаНачала;
		
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецЕсли