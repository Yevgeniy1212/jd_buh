#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаПроведения(Отказ, Режим)
	
	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыПроведения = Документы.си_ВводНачальныхОстатковМатериаловВЭксплуатации.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	си_УчетСпецодеждыУправлениеПроведениемДокументовСервер.СформироватьДвиженияПоРегистрам(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаМатериалы, Движения, Отказ,"Материалы","си_УчетСпецодеждыСервер","ЗарегистрироватьДвиженияВводНачальныхОстатков",,);
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	Отказ = си_УчетСпецодеждыСервер.ПроверкаЗаполненияСтруктурнойЕдиницы(Организация,СтруктурноеПодразделение);
	си_УчетСпецодеждыСервер.ПроверкаЗаполненияСкладаВТабличнойЧасти(Материалы, Отказ);
	МассивНепроверяемыхРеквизитов = Новый Массив();
	Если УчитыватьКПН Тогда
		Если ЗначениеЗаполнено(ВидУчетаНУ) Тогда
			МассивНепроверяемыхРеквизитов.Добавить("ВидУчетаНУ");
		Иначе
			ОбщегоНазначения.СообщитьПользователю("Заполните Вид учета НУ");
			Отказ = Истина;	
		КонецЕсли;
	КонецЕсли;
	
	си_ОбщегоНазначения.ИсключитьПроверку(ПроверяемыеРеквизиты,МассивНепроверяемыхРеквизитов);
	
	ЕстьРеквизитСпецодежда = Метаданные.Справочники.Номенклатура.Реквизиты.Найти("си_ЯвляетсяСпецодеждойИнвентарем");
	
	Для Каждого СтрокаТЧ Из Материалы Цикл
		ИндексСтроки = Формат(СтрокаТЧ.НомерСтроки-1,"ЧГ=");
		Если Не СтрокаТЧ.Состояние = Перечисления.си_СостоянияМатериалов.НаходящиесяНаСкладеНовая Тогда
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ДатаПередачиВЭксплуатацию) Тогда 
				ОбщегоНазначения.СообщитьПользователю("В строке №" + СтрокаТЧ.НомерСтроки + " не заполнена дата передачи в эксплуатацию",ЭтотОбъект,"Материалы["+ИндексСтроки+"].ДатаПередачиВЭксплуатацию");
				Отказ = Истина;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаТЧ.ДокументПередачи) Тогда 
				ОбщегоНазначения.СообщитьПользователю("В строке №" + СтрокаТЧ.НомерСтроки + " не заполнен документ передачи",ЭтотОбъект,"Материалы["+ИндексСтроки+"].ДокументПередачи");
				Отказ = Истина;
			КонецЕсли;
			Если Не ЗначениеЗаполнено(СтрокаТЧ.НазначениеИспользования) Тогда 
				ОбщегоНазначения.СообщитьПользователю("В строке №" + СтрокаТЧ.НомерСтроки + " не заполнено назначение использования",ЭтотОбъект,"Материалы["+ИндексСтроки+"].НазначениеИспользования");
				Отказ = Истина;
			ИначеЕсли СтрокаТЧ.НазначениеИспользования.СпособПогашенияСтоимости=Перечисления.си_СпособыПогашенияСтоимости.ПриПередачеВЭксплуатацию И СтрокаТЧ.ОстаточнаяСтоимость>0 Тогда 
				ОбщегоНазначения.СообщитьПользователю("В строке №" + СтрокаТЧ.НомерСтроки + " указана остаточная стоимость, что не предусмотрено параметрами назначения использования",ЭтотОбъект,"Материалы["+ИндексСтроки+"].ОстаточнаяСтоимость");
				Отказ = Истина;
			КонецЕсли;
			Если СтрокаТЧ.Состояние = Перечисления.си_СостоянияМатериалов.НаходящиесяВЭксплуатации Тогда
				Если Не ЗначениеЗаполнено(СтрокаТЧ.ПодразделениеОрганизации) Тогда 
					ОбщегоНазначения.СообщитьПользователю("В строке №" + СтрокаТЧ.НомерСтроки + " не заполнено подразделение организации",ЭтотОбъект,"Материалы["+ИндексСтроки+"].ПодразделениеОрганизации");
					Отказ = Истина;
				КонецЕсли;
			КонецЕсли;
		КонецЕсли;
		
		Если Не ЕстьРеквизитСпецодежда = Неопределено Тогда
			Если Не СтрокаТЧ.Номенклатура.си_ЯвляетсяСпецодеждойИнвентарем Тогда
				ОбщегоНазначения.СообщитьПользователю("В строке №" + СтрокаТЧ.НомерСтроки + " указана номенклатурная позиция, у которой не установлен признак принадлежности к спецодежде или инвентарю",ЭтотОбъект,"Материалы["+ИндексСтроки+"].Номенклатура");
				Отказ = Истина;
			КонецЕсли;
		Иначе
			ОбщегоНазначения.СообщитьПользователю("В справочнике номенклатура отстутствует необходимый реквизит ""Является спецодеждой или инвентарем"". Проверьте корректность встраивания дополнения",ЭтотОбъект,"Материалы["+ИндексСтроки+"].Номенклатура");
			Отказ = Истина;
			Прервать;
		КонецЕсли;
	КонецЦикла;
	
	ПроверкаНаСтоимостьПриВыбранномСпособеПогашения(Отказ);
КонецПроцедуры

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	общ_ПереопределяемыеПроцедурыБКСервер.ЗаполнитьШапкуДокумента(ЭтотОбъект, СтандартнаяОбработка);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Проверяет не установлена ли стоимость для позиции если выбран способ погашения стоимости "При передачи в эксплуатацию"
//
Процедура ПроверкаНаСтоимостьПриВыбранномСпособеПогашения(Отказ)
	Для Каждого СтрокаТЧ Из Материалы Цикл
		Если СтрокаТЧ.НазначениеИспользования.СпособПогашенияСтоимости = Перечисления.си_СпособыПогашенияСтоимости.ПриПередачеВЭксплуатацию 
			И НЕ СтрокаТЧ.ОстаточнаяСтоимость = 0 Тогда
			Отказ = Истина;
			Сообщить("Строка № "+СтрокаТЧ.НомерСтроки+": Остаточная стоимость не равна нулю, спосособ погашения "+СтрокаТЧ.НазначениеИспользования.СпособПогашенияСтоимости + ", назначение использования "+СтрокаТЧ.НазначениеИспользования);
			
		КонецЕсли;
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли