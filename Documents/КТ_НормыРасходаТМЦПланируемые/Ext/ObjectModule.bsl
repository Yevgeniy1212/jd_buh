
Процедура ОбработкаПроведения(Отказ, Режим)
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	Запрос.УстановитьПараметр("Месяц", НачалоМесяца(Дата));
	Запрос.УстановитьПараметр("Участок", Участок);
	Запрос.УстановитьПараметр("ВидСкважины", ВидСкважины);
	Запрос.УстановитьПараметр("ВидОперацииТМЦ", ВидОперации);
	Запрос.Текст =
	"ВЫБРАТЬ
	|	&Месяц КАК Период,
	|	ВложенныйЗапрос.Участок,
	|	ВложенныйЗапрос.ВидСкважины,
	|	ВложенныйЗапрос.НоменклатурнаяГруппа,
	|	ВложенныйЗапрос.ВидОперацииТМЦ,
	|	ЗНАЧЕНИЕ(Перечисление.КТ_ВариантПланФакт.План) КАК ВидРасхода,
	|	МАКСИМУМ(ВложенныйЗапрос.ВидНормы) КАК ВидНормы,
	|	МАКСИМУМ(ВложенныйЗапрос.Норма) КАК Норма
	|ИЗ
	|	(ВЫБРАТЬ
	|		КТ_НормыРасходаТМЦПланируемыеСписокНорм.Ссылка.Участок КАК Участок,
	|		КТ_НормыРасходаТМЦПланируемыеСписокНорм.Ссылка.ВидСкважины КАК ВидСкважины,
	|		КТ_НормыРасходаТМЦПланируемыеСписокНорм.НоменклатурнаяГруппа КАК НоменклатурнаяГруппа,
	|		КТ_НормыРасходаТМЦПланируемыеСписокНорм.ВидНормы КАК ВидНормы,
	|		КТ_НормыРасходаТМЦПланируемыеСписокНорм.Норма КАК Норма,
	|		КТ_НормыРасходаТМЦПланируемыеСписокНорм.Ссылка.ВидОперации КАК ВидОперацииТМЦ
	|	ИЗ
	|		Документ.КТ_НормыРасходаТМЦПланируемые.СписокНорм КАК КТ_НормыРасходаТМЦПланируемыеСписокНорм
	|	ГДЕ
	|		КТ_НормыРасходаТМЦПланируемыеСписокНорм.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		КТ_НормыРасходаТМЦСрезПоследних.Участок,
	|		КТ_НормыРасходаТМЦСрезПоследних.ВидСкважины,
	|		КТ_НормыРасходаТМЦСрезПоследних.НоменклатурнаяГруппа,
	|		NULL,
	|		0,
	|		КТ_НормыРасходаТМЦСрезПоследних.ВидОперацииТМЦ
	|	ИЗ
	|		РегистрСведений.КТ_НормыРасходаТМЦ.СрезПоследних(
	|				&Месяц,
	|				Регистратор <> &Ссылка
	|					И ВидСкважины = &ВидСкважины
	|					И Участок = &Участок
	|					И ВидОперацииТМЦ = &ВидОперацииТМЦ) КАК КТ_НормыРасходаТМЦСрезПоследних) КАК ВложенныйЗапрос
	|
	|СГРУППИРОВАТЬ ПО
	|	ВложенныйЗапрос.Участок,
	|	ВложенныйЗапрос.ВидСкважины,
	|	ВложенныйЗапрос.НоменклатурнаяГруппа,
	|	ВложенныйЗапрос.ВидОперацииТМЦ";
	
	Движения.КТ_НормыРасходаТМЦ.Записывать = Истина;
	Движения.КТ_НормыРасходаТМЦ.Загрузить(Запрос.Выполнить().Выгрузить());
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ЭтоНовый() Тогда 
		УстановитьНовыйНомер();
	КонецЕсли;
КонецПроцедуры
