#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АВРСерверПереопределяемый.АВРФормаСпискаИВыбораПриСозданииНаСервере(ЭтаФорма);
	
	Если ЭСФКлиентСерверПереопределяемый.ИспользуютсяСтруктурныеПодразделения() Тогда
		Список.ТекстЗапроса = СтрЗаменить(
			Список.ТекстЗапроса,
			""""" КАК СтруктурноеПодразделение,",
			"ЭлектронныйАктВыполненныхРабот.СтруктурноеПодразделение,");
	Иначе
		Элементы.СписокСтруктурноеПодразделение.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеАВР") Тогда
		ОтказПриОткрытии = Истина;
	КонецЕсли;
	
	Если ЭтаФорма.Параметры.Свойство("ЗначенияЗаполнения") Тогда
		Если ЭтаФорма.Параметры.ЗначенияЗаполнения.Свойство("Организация") Тогда
			Организация = ЭтаФорма.Параметры.ЗначенияЗаполнения.Организация;
		КонецЕсли;
	КонецЕсли;
	
	ОграничитьВыборЭАВРВерсия2();
	
	УправлениеФормой();
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Контейнер = АВРКлиентСервер.КонтейнерМетодов();
	Контейнер.ПриОткрытииФормы(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтказПриОткрытии Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(, АВРКлиентСервер.ТекстСообщенияНеУстановленаИспользоватьАВР());
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Организация) Тогда
		УстановитьБыстрыйОтбор("Организация", Организация);
	КонецЕсли;

	Если ЗначениеЗаполнено(Состояние) Тогда
		УстановитьБыстрыйОтбор("Состояние", Состояние);
	КонецЕсли;
	
	КлючУникальности = Новый УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = АВРКлиентСервер.ИмяСобытияЗаписьАВР() Тогда
		Элементы.Список.Обновить();
		УправлениеФормой();
	ИначеЕсли ИмяСобытия = "АВР_ПроверятьОповещенияФоновогоЗадания"
		И ЭтаФорма.КлючУникальности = Источник Тогда
		
		АВРКлиент.ОбработкаОповещенияАВР_ПроверятьОповещенияФоновогоЗадания(ЭтаФорма, Параметр);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)	
	УстановитьБыстрыйОтбор("Организация", Организация);
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)
	УстановитьБыстрыйОтбор("Состояние", Состояние);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБыстрыйОтбор(ВидЭлемента, ЗначениеЭлемента)
	
	ЭСФКлиентСерверПереопределяемый.ИзменитьЭлементОтбораСписка(Список, ВидЭлемента, ЗначениеЭлемента, ЗначениеЗаполнено(ЗначениеЭлемента));
	
КонецПроцедуры

&НаСервере
Процедура ОграничитьВыборЭАВРВерсия2()
	
	СвязанныйАВР = Ложь;
	
	Если Параметры.Свойство("СвязанныйАВР", СвязанныйАВР) И СвязанныйАВР Тогда
		
		ДатаВерсия2 = АВРКлиентСервер.ДатаВводаЭАВРВерсия2();
		
		ЭСФКлиентСерверПереопределяемый.ИзменитьЭлементОтбораСписка(
			Список,
			"Дата",
			ДатаВерсия2,
			ЗначениеЗаполнено(ДатаВерсия2),
			ВидСравненияКомпоновкиДанных.БольшеИлиРавно);
		
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
	ПодключаемыеКомандыКлиент.ВыполнитьКоманду(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаСервере
Процедура Подключаемый_ВыполнитьКомандуНаСервере(Контекст, Результат)
	ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, Контекст, Элементы.Список, Результат);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
	ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти