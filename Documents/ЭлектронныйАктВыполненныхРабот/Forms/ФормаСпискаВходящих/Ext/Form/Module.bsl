
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	АВРСерверПереопределяемый.АВРФормаСпискаИВыбораПриСозданииНаСервере(ЭтаФорма);
	
	Если ЭСФКлиентСерверПереопределяемый.ИспользуютсяСтруктурныеПодразделения() Тогда
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, """"" КАК СтруктурноеПодразделение,", "ЭлектронныйАктВыполненныхРабот.СтруктурноеПодразделение,");
	Иначе
		Элементы.СписокСтруктурноеПодразделение.Видимость = Ложь;
	КонецЕсли;
	
	Если НЕ ПолучитьФункциональнуюОпцию("ИспользоватьЭлектронныеАВР") Тогда
		ОтказПриОткрытии = Истина;
	КонецЕсли;
	
	УправлениеФормой();
	
	ПараметрыЭСФ = ЭСФСервер.ПолучитьПараметрыЭСФ();
	Если не ПараметрыЭСФ.ИспользоватьОткрытиеСессииСПодписью Тогда
		Элементы.СписокДанныеАктивностиТикетов.Видимость = Ложь;
	КонецЕсли;
	
	ЭСФСервер.ПроверитьИспользованиеСервернойКриптографии(ЭтаФорма);

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Контейнер = АВРКлиентСервер.КонтейнерМетодов();	
	Контейнер.ПриОткрытииФормы(ЭтаФорма, Отказ);
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	Если ОтказПриОткрытии Тогда
		Отказ = Истина;
		ПоказатьПредупреждение(, АВРКлиентСервер.ТекстСообщенияНеУстановленаИспользоватьАВР());
	КонецЕсли;
	
	КлючУникальности = Новый УникальныйИдентификатор;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = АВРКлиентСервер.ИмяСобытияЗаписьАВР() Тогда
		Элементы.Список.Обновить();
		УправлениеФормой();
	ИначеЕсли ИмяСобытия = "АВР_ПроверятьОповещенияФоновогоЗадания"
		И ЭтаФорма.КлючУникальности = Источник Тогда
		
		АВРКлиент.ОбработкаОповещенияАВР_ПроверятьОповещенияФоновогоЗадания(ЭтаФорма, Параметр);		
	ИначеЕсли ИмяСобытия = ЭСФКлиентСервер.ИмяСобытияЗаписьУведомленийИСЭСФ() Тогда
		
		Элементы.ОткрытьОповещенияЭСФ.Заголовок = ЭСФВызовСервера.ПолучитьТекстГиперссылкиОповещений();
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиенте
Процедура ОрганизацияПриИзменении(Элемент)	
	УстановитьБыстрыйОтбор("Организация", Организация);
КонецПроцедуры

&НаКлиенте
Процедура СостояниеПриИзменении(Элемент)
	УстановитьБыстрыйОтбор("Состояние", Состояние);
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	УстановитьВидимостьКнопокИзмененияСтатуса();
КонецПроцедуры

&НаКлиенте
Процедура ОткрытьОповещенияИСЭСФ(Команда)
	ЭСФКлиент.ОткрытьФормуОповещенийИСЭСФ(,);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьОповещенияИСЭСФ(Команда)
	Элементы.ОткрытьОповещенияЭСФ.Заголовок = ЭСФВызовСервера.ПолучитьТекстГиперссылкиОповещений();
КонецПроцедуры

&НаКлиенте
Процедура ДанныеАктивностиТикетов(Команда)
	ОткрытьФорму("Обработка.ОбменЭСФ.Форма.РаботаСТикетами");
КонецПроцедуры

&НаКлиенте
Процедура КомментарийМестоУстановкиБиблиотекиНаСервереОбработкаНавигационнойСсылки(Элемент, НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка)
	
	ЭСФКлиент.ОбработкаНавигационнойСсылкиМестоУстановкиБиблиотеки(НавигационнаяСсылкаФорматированнойСтроки, СтандартнаяОбработка, ЭтотОбъект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервере
Процедура УправлениеФормой()
	УстановитьВидимостьКнопокИзмененияСтатуса();
	Элементы.ОткрытьОповещенияЭСФ.Заголовок = ЭСФВызовСервера.ПолучитьТекстГиперссылкиОповещений();
КонецПроцедуры

&НаСервере
Процедура УстановитьВидимостьКнопокИзмененияСтатуса()
	Если Элементы.Список.ВыделенныеСтроки.Количество() > 0 Тогда
		ПервыйВыделенныйАВР = Элементы.Список.ВыделенныеСтроки[0];
		СответвствиеСтатусов = АВРСервер.РазрешенныеДействияПоСтатусамАВР(ПервыйВыделенныйАВР.Направление);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокПодтвердить"			, "Видимость", СответвствиеСтатусов[АВРКлиентСервер.ДействиеПодтверждение()][ПервыйВыделенныйАВР.Статус]);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокОтклонить"				, "Видимость", СответвствиеСтатусов[АВРКлиентСервер.ДействиеОтклонение()][ПервыйВыделенныйАВР.Статус]);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокРасторгнуть"			, "Видимость", СответвствиеСтатусов[АВРКлиентСервер.ДействиеРасторжение()][ПервыйВыделенныйАВР.Статус]);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокОтклонитьРасторжение"	, "Видимость", СответвствиеСтатусов[АВРКлиентСервер.ДействиеОтклонениеРасторжения()][ПервыйВыделенныйАВР.Статус]);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокПодтвердитьРасторжение", "Видимость", СответвствиеСтатусов[АВРКлиентСервер.ДействиеПодтверждениеРасторжения()][ПервыйВыделенныйАВР.Статус]);
	Иначе
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокПодтвердить"			, "Видимость", Ложь);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокОтклонить"				, "Видимость", Ложь);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокРасторгнуть"			, "Видимость", Ложь);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокОтклонитьРасторжение"	, "Видимость", Ложь);
		АВРКлиентСервер.УстановитьСвойствоЭлементаФормы(Элементы, "СписокПодтвердитьРасторжение", "Видимость", Ложь);	
	КонецЕсли;	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьБыстрыйОтбор(ВидЭлемента, ЗначениеЭлемента)
	
	ЭСФКлиентСерверПереопределяемый.ИзменитьЭлементОтбораСписка(Список, ВидЭлемента, ЗначениеЭлемента, ЗначениеЗаполнено(ЗначениеЭлемента));
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
    ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
    ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
    ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
    ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

#КонецОбласти