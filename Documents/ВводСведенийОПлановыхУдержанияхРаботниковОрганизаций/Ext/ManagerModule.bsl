#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["ТаблицаРеквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("ТаблицаРеквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаУдержания(НомераТаблиц);
	Запрос.УстановитьПараметр("Организация", 			   Реквизиты.ГоловнаяОрганизация);
	Запрос.УстановитьПараметр("ОбособленноеПодразделение", Реквизиты.Организация);

	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда 
		
		Результат = Запрос.ВыполнитьПакет();
		
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
		
	КонецЕсли;
			
	Возврат ПараметрыПроведения;
	
КонецФункции 

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаРеквизиты", НомераТаблиц.Количество());

	ТекстЗапроса = "ВЫБРАТЬ
	|	Реквизиты.Дата,
	|	Реквизиты.Организация,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(УчетнаяПолитикаПоПерсоналуОрганизаций.ВестиУчетПоГоловнойОрганизации, ИСТИНА)
	|			ТОГДА ВЫБОР
	|					КОГДА Реквизиты.Организация.ГоловнаяОрганизация <> ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|						ТОГДА Реквизиты.Организация.ГоловнаяОрганизация
	|					ИНАЧЕ Реквизиты.Организация
	|				КОНЕЦ
	|		ИНАЧЕ Реквизиты.Организация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Реквизиты.Ссылка
	|ИЗ
	|	Документ.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.УчетнаяПолитикаПоПерсоналуОрганизаций КАК УчетнаяПолитикаПоПерсоналуОрганизаций
	|		ПО Реквизиты.Организация = УчетнаяПолитикаПоПерсоналуОрганизаций.Организация
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаТаблицаУдержания(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаУдержания", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаУдержания", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТЧТаблицаУдержания.ДатаДействия,
	|	ТЧТаблицаУдержания.ФизЛицо,
	|	ТЧТаблицаУдержания.ВидРасчета,
	|	ТЧТаблицаУдержания.ДокументОснование,
	|	ТЧТаблицаУдержания.Действие,
	|	ТЧТаблицаУдержания.Размер,
	|	ТЧТаблицаУдержания.СпособОтраженияВБухучете,
	|	ТЧТаблицаУдержания.ДатаДействияКонец
	|ПОМЕСТИТЬ ВТ_ТаблицаУдержания
	|ИЗ
	|	Документ.ВводСведенийОПлановыхУдержанияхРаботниковОрганизаций.Удержания КАК ТЧТаблицаУдержания
	|ГДЕ
	|	ТЧТаблицаУдержания.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	&Организация КАК Организация,
	|	&ОбособленноеПодразделение КАК ОбособленноеПодразделение,
	|	ТаблицаУдержания.ФизЛицо,
	|	ТаблицаУдержания.ДатаДействия КАК Период,
	|	ТаблицаУдержания.ВидРасчета,
	|	ТаблицаУдержания.ДокументОснование,
	|	ТаблицаУдержания.Действие,
	|	ВЫБОР
	|		КОГДА ТаблицаУдержания.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|			ТОГДА ТаблицаУдержания.Размер
	|		ИНАЧЕ 0 
	|	КОНЕЦ КАК Размер,
	|	ВЫБОР
	|		КОГДА ТаблицаУдержания.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|			ТОГДА ТаблицаУдержания.СпособОтраженияВБухучете
	|		ИНАЧЕ ЗНАЧЕНИЕ(Справочник.СпособыОтраженияЗарплатыВРеглУчете.ПустаяСсылка)
	|	КОНЕЦ КАК СпособОтраженияВБухучете,
	|	ТаблицаУдержания.ДатаДействияКонец КАК ПериодЗавершения,
	|	ВЫБОР
	|		КОГДА ТаблицаУдержания.ДатаДействияКонец <> ДАТАВРЕМЯ(1, 1, 1)
	|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.Прекратить)
	|		ИНАЧЕ НЕОПРЕДЕЛЕНО
	|	КОНЕЦ КАК ДействиеЗавершения
	|ИЗ
	|	ВТ_ТаблицаУдержания КАК ТаблицаУдержания
	|ГДЕ
	|	ТаблицаУдержания.Действие <> ЗНАЧЕНИЕ(Перечисление.ВидыДействияСНачислением.НеИзменять)";
	
	Возврат ТекстЗапроса;

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
КонецПроцедуры

#КонецЕсли
