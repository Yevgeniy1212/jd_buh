////////////////////////////////////////////////////////////////////////////////
// Описание формы
// Параметры фомры:
//  Ошибки - Булево - Истина - будут показаны ошибки, Ложь - комментарии.
//  СсылкаЗакрытиеМесяца - ДокументСсылка.ЗакрытиеМесяца - Ссылка на документ
//   ЗакрытиеМесяца для которого необходимо показать ошибки или комментарии.
//  ВидРегламентнойОперации - ПеречислениеСсылка.ВидыРегламентныхОпераций -
//   Вид операции, для которого будут показаны ошибки или комментарии.
//   Если не задан, то ошибки и комментарии будут показаны в целом для документа.
//  
////////////////////////////////////////////////////////////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	//////////////////////////////////////////////////////////////////////////////
	// Заполнить реквизит ЗакрытиеМесяца.
	ЗакрытиеМесяца = Параметры.СсылкаЗакрытиеМесяца;
	
	
	
	//////////////////////////////////////////////////////////////////////////////
	// Заполнить таблицу.	
	
	ПараметрыПоиска = Новый Структура;
	ПараметрыПоиска.Вставить("Комментарий", НЕ Параметры.Ошибки);
	Если ЗначениеЗаполнено(Параметры.ВидРегламентнойОперации) Тогда
		ПараметрыПоиска.Вставить("ВидРегламентнойОперации", Параметры.ВидРегламентнойОперации);
	КонецЕсли;
	МассивСтрок = Параметры.СсылкаЗакрытиеМесяца.Ошибки.НайтиСтроки(ПараметрыПоиска);	
	
	НомерСтроки = 1;
	Для Каждого СтрокаТаблицы Из МассивСтрок Цикл
		НоваяСтрока = Ошибки.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
		НоваяСтрока.НомерСтроки = НомерСтроки;
		НомерСтроки = НомерСтроки + 1;
	КонецЦикла;
	
	
	
	//////////////////////////////////////////////////////////////////////////////
	// Установить заголовок формы.
	Если Параметры.Ошибки Тогда
		Заголовок = НСтр("ru = 'Ошибки документа ""Закрытие месяца""'");	
	Иначе
		Заголовок = НСтр("ru = 'Комментарии документа ""Закрытие месяца""'");	
	КонецЕсли;
	
	
	
	//////////////////////////////////////////////////////////////////////////////	
	// Установить заголовок таблицы.
	Если Параметры.Ошибки Тогда
		Элементы.ЗаголовокТаблицы.Заголовок = НСтр("ru = 'Устраните указанные ошибки и повторите выполнение операции:'");
	Иначе
		Элементы.ЗаголовокТаблицы.Заголовок = НСтр("ru = 'Список сопроводительных сообщений:'");
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытиеМесяцаНажатие(Элемент, СтандартнаяОбработка)
	ПоказатьЗначение(,ЗакрытиеМесяца);
КонецПроцедуры
