#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	ПредставлениеПериода = ПредставлениеПериода(ДатаНачала, КонецДня(ДатаОкончания), "Л=" + ОбщегоНазначения.КодОсновногоЯзыка() + "; ФП = Истина");
	
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	УстановитьНомерДокументаОтчета();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПолучитьПрефиксДокументаОтчета() Экспорт
	
	Префикс = ?(НЕ ЗначениеЗаполнено(ЭтотОбъект.Организация.Префикс), "00", ЭтотОбъект.Организация.Префикс);
	
	Если ПолучитьФункциональнуюОпцию("ИспользоватьСинхронизациюДанных") Тогда
		ПрефиксУзла = ОбменДаннымиСервер.ПрефиксИнформационнойБазы();
		Префикс = ПрефиксУзла + Префикс;
	КонецЕсли;
				
	ЭлементСправочника = Справочники.РегламентированныеОтчеты.НайтиПоРеквизиту("ИсточникОтчета", СокрЛП(ЭтотОбъект.ИсточникОтчета));
	Если ЭлементСправочника <> Неопределено Тогда
		Префикс = Префикс + ?(НЕ ЗначениеЗаполнено(ЭлементСправочника.ПрефиксОтчета), "00", ЭлементСправочника.ПрефиксОтчета);
	КонецЕсли;
	
	Возврат Префикс;
	
КонецФункции

Процедура УстановитьНомерДокументаОтчета() Экспорт

	// Если  нет реквизита "Организация" или для нее не задан префикс, то 
	// используем пустой префикс,
	// иначе используем префикс организации.
	
	ЭтотОбъект.УстановитьНовыйНомер(ПолучитьПрефиксДокументаОтчета());
	
КонецПроцедуры



Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда 
		Если ДанныеЗаполнения.Свойство("Автор") Тогда
			ДанныеЗаполнения.Удалить("Автор");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;

КонецПроцедуры

#КонецЕсли



