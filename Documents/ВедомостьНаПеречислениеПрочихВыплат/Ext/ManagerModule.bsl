#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти   

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ПодготовитьДанныеДляЗаполнения(Параметры, АдресХранилища) Экспорт
	
	ДанныеРеглУчетаТекст = "";

	УсловиеСтруктурноеПодразделение = " И СтруктурнаяЕдиница = &СтруктурнаяЕдиница ";
	
	ДанныеРеглУчетаТекст = ДанныеРеглУчетаТекст + 
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВзаиморасчетыПоПрочимВыплатамОстатки.Физлицо КАК ФизЛицо,
	|   НАЧАЛОПЕРИОДА(ВзаиморасчетыПоПрочимВыплатамОстатки.ПериодВзаиморасчетов, МЕСЯЦ) КАК ПериодВзаиморасчетов,
	|	СУММА(ВзаиморасчетыПоПрочимВыплатамОстатки.СуммаВзаиморасчетовОстаток) КАК Сумма
	|ИЗ
	|	РегистрНакопления.ВзаиморасчетыПоПрочимВыплатам.Остатки(
	|				&парамГраницаОстатков,
	|				Организация = &парамОрганизация" + УсловиеСтруктурноеПодразделение + "
	|				И ВидПрочихВыплат = &парамВидПрочихВыплат
	|				И " + ?(Параметры.ПоОстаткам, "
	|					ПериодВзаиморасчетов <= КОНЕЦПЕРИОДА(&парамМесяц, МЕСЯЦ)", "
	|					ПериодВзаиморасчетов МЕЖДУ НАЧАЛОПЕРИОДА(&парамМесяц, МЕСЯЦ) И КОНЕЦПЕРИОДА(&парамМесяц, МЕСЯЦ)") + ") КАК ВзаиморасчетыПоПрочимВыплатамОстатки
	|СГРУППИРОВАТЬ ПО
	|	ВзаиморасчетыПоПрочимВыплатамОстатки.Физлицо,
	|	НАЧАЛОПЕРИОДА(ВзаиморасчетыПоПрочимВыплатамОстатки.ПериодВзаиморасчетов, МЕСЯЦ)
	|УПОРЯДОЧИТЬ ПО
	|	ВзаиморасчетыПоПрочимВыплатамОстатки.Физлицо.Наименование,
	|	НАЧАЛОПЕРИОДА(ВзаиморасчетыПоПрочимВыплатамОстатки.ПериодВзаиморасчетов, МЕСЯЦ)
	|";
	
	Запрос = Новый Запрос(ДанныеРеглУчетаТекст);
	
	Для Каждого Параметр Из Параметры Цикл
		Запрос.УстановитьПараметр(Параметр.Ключ, Параметр.Значение);		
	КонецЦикла;
	
	Если ЗначениеЗаполнено(Параметры.парамСтруктурноеПодразделение) Тогда
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Параметры.парамСтруктурноеПодразделение);
	Иначе
		Запрос.УстановитьПараметр("СтруктурнаяЕдиница", Параметры.парамОрганизация);
	КонецЕсли;
			
	Выборка = Запрос.Выполнить().Выбрать();
	Пока Выборка.Следующий() Цикл
		Если ЗначениеЗаполнено(Выборка.Сумма) И Выборка.Сумма > 0 Тогда
			НоваяСтрока = Параметры.ТаблицаПрочиеВыплаты.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, Выборка);
		КонецЕсли;
	КонецЦикла;
	
	СтруктураДанных = Новый Структура();
	СтруктураДанных.Вставить("ТаблицаПрочиеВыплаты", Параметры.ТаблицаПрочиеВыплаты); 
	 
	ПоместитьВоВременноеХранилище(СтруктураДанных, АдресХранилища); 
	
КонецПроцедуры 

Процедура НайтиДокументПлатежногоПорученияИсходящегоИЗаполнитьСсылку(ВедомостьСсылка, ЭлементСсылкаНаПлатежноеПоручение, СписокПлатежныхПорученийИсходящих) Экспорт
	
	СписокПлатежныхПорученийИсходящих.Очистить();
	
	Запрос = Новый Запрос;
	Запрос.УстановитьПараметр("ВедомостьСсылка", ВедомостьСсылка);
	Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ПлатежноеПоручениеИсходящее.Ссылка КАК Ссылка,
	|	ПлатежноеПоручениеИсходящее.ВидОперации КАК ВидОперации,
	|	ПлатежноеПоручениеИсходящее.Проведен КАК Проведен
	|ПОМЕСТИТЬ ВТ_ППИ
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее КАК ПлатежноеПоручениеИсходящее
	|ГДЕ
	|	ПлатежноеПоручениеИсходящее.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийППИсходящее.ПеречислениеПрочихВыплат)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВТ_ППИ.Ссылка КАК ПлатежноеПоручение,
	|	ВТ_ППИ.Проведен КАК Проведен,
	|	ПлатежноеПоручениеИсходящееПеречислениеПрочихВыплат.Документ КАК Ведомость
	|ПОМЕСТИТЬ ВТ_ППИ_Ведомости
	|ИЗ
	|	Документ.ПлатежноеПоручениеИсходящее.ПеречислениеПрочихВыплат КАК ПлатежноеПоручениеИсходящееПеречислениеПрочихВыплат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ППИ КАК ВТ_ППИ
	|		ПО ПлатежноеПоручениеИсходящееПеречислениеПрочихВыплат.Ссылка = ВТ_ППИ.Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВедомостьНаПеречислениеПрочихВыплат.Ссылка КАК Ведомость,
	|	ВТ_ППИ_Ведомости.ПлатежноеПоручение КАК ПлатежноеПоручение,
	|	ПРЕДСТАВЛЕНИЕ(ВТ_ППИ_Ведомости.ПлатежноеПоручение) КАК ПлатежноеПоручениеПредставление,
	|	ВТ_ППИ_Ведомости.Проведен КАК Проведен
	|ИЗ
	|	Документ.ВедомостьНаПеречислениеПрочихВыплат КАК ВедомостьНаПеречислениеПрочихВыплат
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ВТ_ППИ_Ведомости КАК ВТ_ППИ_Ведомости
	|		ПО ВедомостьНаПеречислениеПрочихВыплат.Ссылка = ВТ_ППИ_Ведомости.Ведомость
	|ГДЕ
	|	ВедомостьНаПеречислениеПрочихВыплат.Ссылка = &ВедомостьСсылка";
					   
	РезультатЗапроса = Запрос.Выполнить().Выбрать(); 
	
	Пока РезультатЗапроса.Следующий() Цикл  	
		СтрокаСпискаППИ = СписокПлатежныхПорученийИсходящих.Добавить();
		СтрокаСпискаППИ.Значение = РезультатЗапроса.ПлатежноеПоручение; 
		СтрокаСпискаППИ.Представление = РезультатЗапроса.ПлатежноеПоручениеПредставление;		
	КонецЦикла;  
	
	Если СписокПлатежныхПорученийИсходящих.Количество() = 0 Тогда
		ЭлементСсылкаНаПлатежноеПоручение.Заголовок = НСтр("ru = 'Создать документ'");
		Возврат;
	ИначеЕсли СписокПлатежныхПорученийИсходящих.Количество() > 1 Тогда 
		НадписьКоличествоПлатежныхПорученийИсходящих = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Всего документов платежных поручений (исходящих): (%1)'"),
																												СписокПлатежныхПорученийИсходящих.Количество());
		
		ЭлементСсылкаНаПлатежноеПоручение.Заголовок = НадписьКоличествоПлатежныхПорученийИсходящих;	
	Иначе
		ЭлементСсылкаНаПлатежноеПоручение.Заголовок = СписокПлатежныхПорученийИсходящих[0].Представление;
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецЕсли