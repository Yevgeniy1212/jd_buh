#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Заполняет счета учета номенклатуры в табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти) Экспорт

	ТабличнаяЧасть = Объект[ИмяТабличнойЧасти];
	
	ДанныеОбъекта = Новый Структура("Дата, Организация");
	
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
	
	СоответствиеСчетовУчета = ПроцедурыБухгалтерскогоУчета.ПолучитьСчетаУчетаСпискаНоменклатуры(
		ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, "Номенклатура", Истина), ДанныеОбъекта.Дата);
	
	Для каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
		СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти.Номенклатура);
		ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СчетаУчета);
	КонецЦикла;

КонецПроцедуры

// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - структура сведений о номенклатуре, либо стуктура счетов учета
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти, СведенияОНоменклатуре, ВключаяЗаполненные = Истина) Экспорт

/// Контротек, Талпин Нурлан, СпецОдежда
	СтрокаТабличнойЧасти.СчетУчетаБУ      = ПланыСчетов.Типовой.СпецОдежда;
	СтрокаТабличнойЧасти.НовыйСчетУчетаБУ = ПланыСчетов.Типовой.СпецОдежда;
	
КонецПроцедуры

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// ПОДГОТОВКА ПАРАМЕТРОВ ПРОВЕДЕНИЯ ДОКУМЕНТА

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт

	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",	Константы.ВалютаРегламентированногоУчета.Получить());
	Запрос.УстановитьПараметр("СодержаниеСписаныТМЗ", НСтр("ru='Перемещена спецодежда'"));
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Если НЕ УчетнаяПолитикаСервер.Существует(Реквизиты.Организация, Реквизиты.Период, "БУ", Истина, ДокументСсылка) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат ПараметрыПроведения;
	КонецЕсли;

	ОрганизацияПлательщикНалогаНаПрибыль 		   = Ложь;
	ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль = Ложь;
	НеобходимостьОтраженияВНУ 					   = Ложь;
	
	Запрос.УстановитьПараметр("СинонимТовары"				  , НСтр("ru = 'Товары'"));
	Запрос.УстановитьПараметр("СинонимТоварыНДС"			  , НСтр("ru = 'Товары НДС'"));
	Запрос.УстановитьПараметр("НеобходимостьОтраженияВНУ"	  , НеобходимостьОтраженияВНУ);
	Запрос.УстановитьПараметр("ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль", ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль);
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(Реквизиты, НомераТаблиц)
					+ ТекстЗапросаСписаниеТоваров(Реквизиты, НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	УправлениеВзаиморасчетамиСервер.ПодготовкаТаблицыЗначенийДляЦелейПриобретенияИРеализации(ПараметрыПроведения.СписаниеТоваровТаблицаТовары, ПараметрыПроведения.СписаниеТоваровРеквизиты, Истина);
	
	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Реквизиты.Ссылка КАК Ссылка,
	               |	Реквизиты.Дата КАК Дата,
	               |	Реквизиты.Организация КАК Организация,
	               |	Реквизиты.СтруктурноеПодразделениеОтправитель КАК СтруктурноеПодразделениеОтправитель,
	               |	Реквизиты.СтруктурноеПодразделениеПолучатель КАК СтруктурноеПодразделениеПолучатель,
	               |	Реквизиты.СкладОтправитель КАК СкладОтправитель,
	               |	Реквизиты.СкладПолучатель КАК СкладПолучатель,
	               |	Реквизиты.МОЛОтправитель КАК МОЛОтправитель,
	               |	Реквизиты.МОЛПолучатель КАК МОЛПолучатель,
	               |	Реквизиты.СкладПолучатель КАК СкладПолучатель1,
	               |	&СодержаниеСписаныТМЗ КАК Содержание,
	               |	&ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	               |	Реквизиты.МОЛОтправитель КАК МОЛОтправитель1,
	               |	Реквизиты.МОЛПолучатель КАК МОЛПолучатель1
	               |ПОМЕСТИТЬ Реквизиты
	               |ИЗ
	               |	Документ.ВедомостьВыдачиБУСпецодежды КАК Реквизиты
	               |ГДЕ
	               |	Реквизиты.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	Реквизиты.Ссылка КАК Регистратор,
	               |	Реквизиты.Дата КАК Период,
	               |	Реквизиты.Организация КАК Организация,
	               |	Реквизиты.СтруктурноеПодразделениеОтправитель КАК СтруктурноеПодразделение,
	               |	Реквизиты.СтруктурноеПодразделениеПолучатель КАК КорСтруктурноеПодразделение,
	               |	Реквизиты.СкладОтправитель КАК СкладОтправитель,
	               |	Реквизиты.СкладПолучатель КАК СкладПолучатель,
	               |	Реквизиты.МОЛОтправитель КАК МОЛОтправитель,
	               |	Реквизиты.МОЛПолучатель КАК МОЛПолучатель,
	               |	Реквизиты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	               |	Реквизиты.Содержание КАК Содержание
	               |ИЗ
	               |	Реквизиты КАК Реквизиты";	
		
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(Реквизиты, НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаТовары", НомераТаблиц.Количество());

	ТекстЗапроса =
		"ВЫБРАТЬ
		|	ТаблицаТовары.Ссылка,
		|	ТаблицаТовары.НомерСтроки,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Количество,
		|	ТаблицаТовары.СчетУчетаБУ,
		|	ТаблицаТовары.НовыйСчетУчетаБУ,
		|	ТаблицаТовары.Коэффициент,
		|	ТаблицаТовары.КлючСвязи
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	Документ.ВедомостьВыдачиБУСпецодежды.Товары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаСписаниеТоваров(Реквизиты, НомераТаблиц)
	
	ТекстЗапроса = "";
	НомераТаблиц.Вставить("СписаниеТоваровРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("СписаниеТоваровТаблицаТовары", НомераТаблиц.Количество());
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Период,
		|	""Списание"" КАК ТипСписания,
		|	Реквизиты.Ссылка КАК ДокументРеализации,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.СтруктурноеПодразделениеОтправитель КАК СтруктурноеПодразделение,
		|	Реквизиты.СтруктурноеПодразделениеПолучатель КАК КорСтруктурноеПодразделение,
		|	НЕОПРЕДЕЛЕНО КАК Контрагент,
		|	НЕОПРЕДЕЛЕНО КАК ВалютаВзаиморасчетов,
		|	Реквизиты.Содержание КАК Содержание,
		|	Реквизиты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
		|	ЛОЖЬ КАК НеобходимостьОтраженияВНУ,
		|	ЛОЖЬ КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
		|	ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПустаяСсылка) КАК ВидУчетаНУ,
		|	Реквизиты.МОЛОтправитель КАК МОЛОтправитель,
		|	Реквизиты.МОЛПолучатель КАК МОЛПолучатель
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	""Товары"" КАК ИмяСписка,
		|	&СинонимТовары КАК СинонимСписка,
		|	Реквизиты.Дата КАК Период,
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.СчетУчетаБУ КАК СчетУчетаБУ,
		|	ЗНАЧЕНИЕ(ПланСчетов.Налоговый.ПустаяСсылка) КАК СчетУчетаНУ,
		|	ТаблицаТовары.Номенклатура КАК Номенклатура,
		|	Реквизиты.СкладОтправитель КАК Склад,
		|	Реквизиты.МОЛОтправитель КАК МОЛ,
		|	Реквизиты.СкладПолучатель КАК КорСклад,
		|	ВЫРАЗИТЬ(ТаблицаТовары.Количество * ТаблицаТовары.Коэффициент КАК ЧИСЛО(19, 3)) КАК Количество,
		|	Реквизиты.СтруктурноеПодразделениеОтправитель КАК СтруктурноеПодразделение,
		|	Реквизиты.СтруктурноеПодразделениеПолучатель КАК КорСтруктурноеПодразделение,
		|	0 КАК Себестоимость,
		|	0 КАК Сумма,
		|	0 КАК СуммаВал,
		|	НЕОПРЕДЕЛЕНО КАК ДокументОприходования,
		|	ТаблицаТовары.НовыйСчетУчетаБУ КАК КорСчетСписанияБУ,
		|	ЗНАЧЕНИЕ(ПланСчетов.Налоговый.ПустаяСсылка) КАК КорСчетСписанияНУ,
		|	ТаблицаТовары.Номенклатура КАК КорСубконтоБУ1,
		|	Реквизиты.СкладПолучатель КАК КорСубконтоБУ2,
		|	Реквизиты.МОЛПолучатель КАК КорСубконтоБУ3,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.Номенклатура) КАК ВидКорСубконтоБУ1,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.Склады) КАК ВидКорСубконтоБУ2,
		|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконтоБУ3,
		|	ТаблицаТовары.Номенклатура КАК КорСубконтоНУ1,
		|	Реквизиты.СкладПолучатель КАК КорСубконтоНУ2,
		|	Реквизиты.МОЛПолучатель КАК КорСубконтоНУ3,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.Номенклатура) КАК ВидКорСубконтоНУ1,
		|	ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.Склады) КАК ВидКорСубконтоНУ2,
		|	НЕОПРЕДЕЛЕНО КАК ВидКорСубконтоНУ3,
		|	ЛОЖЬ КАК НеобходимостьОтраженияВНУ,
		|	ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.ПустаяСсылка) КАК ВидУчетаНУ,
		|	ЛОЖЬ КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
		|	Реквизиты.МОЛОтправитель КАК МОЛОтправитель,
		|	Реквизиты.МОЛПолучатель КАК МОЛПолучатель
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаТовары КАК ТаблицаТовары
		|		ПО Реквизиты.Ссылка = ТаблицаТовары.Ссылка
		|ГДЕ
		|	Реквизиты.СкладПолучатель <> Реквизиты.СкладОтправитель
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаТовары.НомерСтроки"; 
		
	Возврат ТекстЗапроса;
	
КонецФункции


////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
	// Накладная на перемещение товаров
	КомандаПечати = КомандыПечати.Добавить();
	КомандаПечати.Идентификатор = "Ведомость";
	КомандаПечати.Представление = НСтр("ru = 'Ведомость'");
	КомандаПечати.Обработчик    = "УправлениеПечатьюБККлиент.ВыполнитьКомандуПечати";
	КомандаПечати.ПроверкаПроведенияПередПечатью = НЕ ПользователиБКВызовСервераПовтИсп.РазрешитьПечатьНепроведенныхДокументов();
	КомандаПечати.Порядок = 50;
	
КонецПроцедуры

// Формирует печатные формы.
//
// Параметры:
//  МассивОбъектов  - Массив    - ссылки на объекты, которые нужно распечатать;
//  ПараметрыПечати - Структура - дополнительные настройки печати;
//  КоллекцияПечатныхФорм - ТаблицаЗначений - сформированные табличные документы (выходной параметр)
//  ОбъектыПечати         - СписокЗначений  - значение - ссылка на объект;
//                                            представление - имя области в которой был выведен объект (выходной параметр);
//  ПараметрыВывода       - Структура       - дополнительные параметры сформированных табличных документов (выходной параметр).
//
Процедура Печать(МассивОбъектов, ПараметрыПечати, КоллекцияПечатныхФорм, ОбъектыПечати, ПараметрыВывода) Экспорт
	
	// Печать накладной на перемещение
	Если УправлениеПечатью.НужноПечататьМакет(КоллекцияПечатныхФорм, "Ведомость") Тогда
		УправлениеПечатью.ВывестиТабличныйДокументВКоллекцию(
			КоллекцияПечатныхФорм,
			"Ведомость",
			НСтр("ru = 'Ведомость'"),
			ПечатьВедомости(МассивОбъектов, ОбъектыПечати),
			,
			"Документ.ВедомостьВыдачиБУСпецодежды.Ведомость");
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Подготовка табличных печатных документов.

// Функция формирует табличный документ с печатной формой ведомости,
//
// Возвращаемое значение:
//  Табличный документ - печатная форма ведомости
//
Функция ПечатьВедомости(МассивОбъектов, ОбъектыПечати) Экспорт

	УстановитьПривилегированныйРежим(Истина);

	ТабДокумент = Новый ТабличныйДокумент;
	ТабДокумент.ИмяПараметровПечати = "ПАРАМЕТРЫ_ПЕЧАТИ_Ведомость";
	
	Для Каждого Элемент Из МассивОбъектов Цикл
		Документ = Элемент;
		
		Если ТабДокумент.ВысотаТаблицы > 0 Тогда
			ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
		КонецЕсли;
		
		Макет   = УправлениеПечатью.МакетПечатнойФормы("Документ.ВедомостьВыдачиСпецодежды.Ведомость");
		Шапка   = Макет.ПолучитьОбласть("Шапка");
		СтрокаТ = Макет.ПолучитьОбласть("Строка");
		Подвал  = Макет.ПолучитьОбласть("Подвал");
		
		ДанныеМОЛПолучатель  = ПроцедурыУправленияПерсоналомВызовСервера.ДанныеФизЛица(Документ.Организация, Документ.МОЛПолучатель, Документ.Дата);
		ДанныеМОЛОтправитель = ПроцедурыУправленияПерсоналомВызовСервера.ДанныеФизЛица(Документ.Организация, Документ.МОЛОтправитель, Документ.Дата);
	
		Шапка.Параметры.Организация 	= Документ.Организация.НаименованиеПолное;
		Шапка.Параметры.МОЛ 			= ДанныеМОЛПолучатель.Представление;
		Шапка.Параметры.Получатель		= Документ.СкладПолучатель;
		Шапка.Параметры.МОЛОтправитель 	= ДанныеМОЛОтправитель.Представление;
		Шапка.Параметры.ПолучательОтправитель = Документ.СкладОтправитель;
		Шапка.Параметры.Дата 			= формат(Документ.Дата,"ДФ=""дд.ММ.гггг""");
		Шапка.параметры.подразделение 	= Документ.Подразделение;
		Шапка.параметры.Номер 			= Документ.Номер;
		
		Если ЗначениеЗаполнено(Документ.МОЛПолучатель) тогда
			ДанныеФизЛица = ПроцедурыУправленияПерсоналомВызовСервера.ДанныеФизЛица(Документ.Организация, Документ.МОЛПолучатель, Документ.Дата);
			Шапка.Параметры.должностьМол = ДанныеФизЛица.Должность;
		Конецесли;
		
		Шапка.Параметры.Должность = Документ.ДолжностьДляПодписи;
		ТабДокумент.Вывести(Шапка);
		
		НомерСтроки = 0;
		Для Каждого ТекСтрока Из Документ.Товары Цикл 
			НомерСтроки = НомерСтроки + 1;
			СтрокаТ.Параметры.Заполнить(ТекСтрока);
			СтрокаТ.Параметры.НомНомер = ТекСтрока.Номенклатура.Код;
			СтрокаТ.Параметры.ДатаПоступления = формат(Документ.Дата, "ДФ=""дд.ММ.гггг""");
			СтрокаТ.Параметры.ДатаОкончания = формат(ТекСтрока.ДатаОкончания, "ДФ=""дд.ММ.гггг""");
			СтрокаТ.Параметры.нпп = НомерСтроки;
			ТабДокумент.Вывести(СтрокаТ);
		КонецЦикла;
		
		Если ЗначениеЗаполнено(Документ.Ответственный) Тогда 
			ДанныеФизЛица = ПроцедурыУправленияПерсоналомВызовСервера.ДанныеФизЛица(Документ.Организация, Документ.Ответственный.ФизЛицо, Документ.Дата);
			Подвал.Параметры.должность 		= ДанныеФизЛица.Должность;
			Подвал.Параметры.МолОтправитель = ДанныеФизЛица.Представление;
		Иначе 
			Подвал.Параметры.должность = ДанныеМОЛОтправитель.Должность;
			Подвал.Параметры.МолОтправитель = ДанныеМОЛОтправитель.Представление;
		Конецесли;
		ТабДокумент.Вывести(Подвал);
		ТабДокумент.ВывестиГоризонтальныйРазделительСтраниц();
	КонецЦикла;	
		
	Возврат ТабДокумент;
	

КонецФункции

#КонецЕсли