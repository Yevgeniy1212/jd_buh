#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область СчетаУчета

Процедура УстановитьПравилаЗаполненияСчетовУчета(Правила) Экспорт
	
	// Шапка
	
	// расчеты с покупателем
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовСКонтрагентом", "РасчетыСПокупателем");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетРасчетов");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтражатьВБухУчете");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ОтложитьНачислениеНДС");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "", "СчетУчетаРасчетовПоАвансам", "АвансыПокупателя");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ТребуетсяУчетАвансов");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтражатьВБухУчете");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеЗапрещено(Правила, "ОтложитьНачислениеНДС");
	
	// Табличная часть Товары
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СчетУчетаНДСПоРеализации", "РеализацияНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтражатьВБухУчете");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "НДСВидОперацииРеализации", "НДСВидОперацииРеализации");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СтавкаНДС", "СтавкаНДС");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Товары", "СтавкаНДС", "СтавкаНДС_Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	
	// Табличная часть Услуги
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "СчетУчетаНДСПоРеализации", "РеализацияНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СтавкаНДС", "СтавкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтражатьВБухУчете");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "Услуги", "НДСВидОперацииРеализации", "НДСВидОперацииРеализации");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "Номенклатура", "Номенклатура");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СтавкаНДС", "СтавкаНДС");
	
	// Табличная часть ОсновныеСредства
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОС", "СчетУчетаНДСПоРеализации", "РеализацияНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОсновноеСредство", "ОсновноеСредство");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СтавкаНДС", "СтавкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтражатьВБухУчете");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "ОС", "НДСВидОперацииРеализации", "НДСВидОперацииРеализации");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "ОсновноеСредство", "ОсновноеСредство");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СтавкаНДС", "СтавкаНДС");
	
	// Табличная часть НематериальныеАктивы
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "НМА", "СчетУчетаНДСПоРеализации", "РеализацияНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "НематериальныйАктив", "НематериальныйАктив");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СтавкаНДС", "СтавкаНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоУсловиеРазрешено(Правила, "ОтражатьВБухУчете");
	
	СчетаУчетаВДокументах.ДобавитьПравилоЗаполнения(Правила, "НМА", "НДСВидОперацииРеализации", "НДСВидОперацииРеализации");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "НематериальныйАктив", "НематериальныйАктив");
	СчетаУчетаВДокументах.ДобавитьВПравилоИсточникДанныхРеквизитОбъекта(Правила, "СтавкаНДС", "СтавкаНДС");
	
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Дата");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Организация");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "СтруктурноеПодразделение");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "Контрагент");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "УчитыватьНДС");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеРеквизитаДокумента(Правила, "УчитыватьКПН");
	
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ТребуетсяУчетРасчетов", "ВидОперации, ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ТребуетсяУчетАвансов",  "ВидОперации, ДоговорКонтрагента");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ОтражатьВБухУчете",     "ОтражатьВБухгалтерскомУчете");
	СчетаУчетаВДокументах.ДобавитьВПравилоОписаниеДополнительныхДанных(Правила, "ОтложитьНачислениеНДС", "ДокументОснование");

КонецПроцедуры

Процедура ДополнитьДанныеЗаполненияСчетовУчета(ДанныеЗаполнения) Экспорт
	
	Если ДанныеЗаполнения.Свойство("ТребуетсяУчетРасчетов") Или ДанныеЗаполнения.Свойство("ТребуетсяУчетАвансов") Тогда
		
		ОсобенностиДокумента = ОсобенностиУчетаРасчетов(ДанныеЗаполнения.ВидОперации, ДанныеЗаполнения.ДоговорКонтрагента);
		
		ЗаполнитьЗначенияСвойств(ДанныеЗаполнения, ОсобенностиДокумента);
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ОтражатьВБухУчете") Тогда
		
		ДанныеЗаполнения.ОтражатьВБухУчете = ДанныеЗаполнения.ОтражатьВБухгалтерскомУчете;
		
	КонецЕсли;
	
	Если ДанныеЗаполнения.Свойство("ОтложитьНачислениеНДС") Тогда
		
		ДокументОснование          = ДанныеЗаполнения.ДокументОснование;
		ОтложитьНачислениеНДС = Ложь;
		
		Если ЗначениеЗаполнено(ДокументОснование)
			И Метаданные.Документы[ДокументОснование.Метаданные().Имя].Реквизиты.Найти("ОтложитьНачислениеНДС") <> Неопределено Тогда
			ОтложитьНачислениеНДС = ДокументОснование.ОтложитьНачислениеНДС;
		КонецЕсли;
		
		ДанныеЗаполнения.ОтложитьНачислениеНДС = ОтложитьНачислениеНДС;
		
	КонецЕсли;

КонецПроцедуры

Функция ОсобенностиУчетаРасчетов(ВидОперации, ДоговорКонтрагента) Экспорт
	
	ОсобенностиДокумента = УправлениеВзаиморасчетамиСервер.НовыйОсобенностиУчетаРасчетовДокумента();
	
	Если ВидОперации = Перечисления.ВидыОперацийВЦеляхНДС.КорректировкаПрочее Тогда
		ОсобенностиДокумента.ТребуетсяУчетРасчетов = Ложь;
		ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
	КонецЕсли;
	
	Если Не ОсобенностиДокумента.ТребуетсяУчетРасчетов Тогда
		ОсобенностиДокумента.ТребуетсяУчетАвансов = Ложь;
		ОсобенностиДокумента.ТребуетсяУчетСроковОплаты = Ложь;
	КонецЕсли;
	
	Возврат ОсобенностиДокумента;
	
КонецФункции

#Область УстаревшиеПроцедурыИФункции

// Заполняет счета учета расчетов с контрагентом в шапке документа
//
// Параметры:
//  Объект         	  - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                      вид договора контрагента, признак комиссионной торговли и т.п.)
//
Процедура ЗаполнитьСчетаУчетаРасчетов(Объект) Экспорт
	
	ЗаполнятьСчетаРасчетов = Истина;
	Если ЗначениеЗаполнено(Объект.ДокументОснование) Тогда
		МетаданныеДокумента = Объект.ДокументОснование.Метаданные();
		
		Если (ОбщегоНазначенияБК.ЕстьРеквизитДокумента("ОтложитьНачислениеНДС",МетаданныеДокумента) И Объект.ДокументОснование.ОтложитьНачислениеНДС) Тогда 
			Объект.СчетУчетаРасчетовСКонтрагентом  = ПланыСчетов.Типовой.НалогНаДобавленнуюСтоимостьОтложенный;
			Объект.СчетУчетаРасчетовПоАвансам      = ПланыСчетов.Типовой.ПустаяСсылка();
			ЗаполнятьСчетаРасчетов = Ложь;
			
		КонецЕсли;
	КонецЕсли;
	
	Если ЗаполнятьСчетаРасчетов Тогда
		СчетаУчета = УправлениеВзаиморасчетамиСервер.ПолучитьСчетаРасчетовСКонтрагентом(Объект.Организация, Объект.Контрагент, Объект.ДоговорКонтрагента);
		
		Объект.СчетУчетаРасчетовСКонтрагентом = СчетаУчета.СчетРасчетовПокупателя;
		Объект.СчетУчетаРасчетовПоАвансам 	  = СчетаУчета.СчетАвансовПокупателя;
	КонецЕсли;

КонецПроцедуры

// Заполняет счета учета номенклатуры в табличной части документа
//
// Параметры:
//  Объект         	  - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                      вид договора контрагента, признак комиссионной торговли и т.п.)
//  ИмяТабличнойЧасти - Строка - имя табличной части документа
//
Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(Объект, ИмяТабличнойЧасти) Экспорт
	
	КолонкаНоменклатура = "Номенклатура";
	
	Если ИмяТабличнойЧасти = "НМА" Тогда
		 КолонкаНоменклатура = "НематериальныйАктив";
	ИначеЕсли ИмяТабличнойЧасти = "ОС" Тогда
		 КолонкаНоменклатура = "ОсновноеСредство";
	КонецЕсли; 
	
	ТабличнаяЧасть = Объект[ИмяТабличнойЧасти];
	
	ДанныеОбъекта = Новый Структура("Дата, Организация, ВидОперации, УчитыватьКПН, УчитыватьНДС, ОтражатьВБухгалтерскомУчете");
	ЗаполнитьЗначенияСвойств(ДанныеОбъекта, Объект);
		
	Если ИмяТабличнойЧасти = "ОС" Тогда
		СоответствиеСчетовУчета = УправлениеВнеоборотнымиАктивамиСервер.ПолучитьСчетаУчетаСпискаОС(
			ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, КолонкаНоменклатура, Истина), ДанныеОбъекта.Дата);
		
		Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
			СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти[КолонкаНоменклатура]);
			ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, СчетаУчета);
		КонецЦикла;
		
	ИначеЕсли ИмяТабличнойЧасти = "НМА" Тогда 
		СоответствиеСчетовУчета = УправлениеВнеоборотнымиАктивамиСервер.ПолучитьСчетаУчетаСпискаНМА(
			ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, КолонкаНоменклатура, Истина), ДанныеОбъекта.Дата);
		
		Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
			СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти[КолонкаНоменклатура]);
			СчетаУчета.Вставить("СчетУчетаНДСПоРеализации", ПланыСчетов.Типовой.НалогНаДобавленнуюСтоимость);
			ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, СчетаУчета);
		КонецЦикла;
		      
	Иначе
		СоответствиеСчетовУчета = ПроцедурыБухгалтерскогоУчета.ПолучитьСчетаУчетаСпискаНоменклатуры(
			ДанныеОбъекта.Организация, ОбщегоНазначения.ВыгрузитьКолонку(ТабличнаяЧасть, КолонкаНоменклатура, Истина), ДанныеОбъекта.Дата);
			
		Для Каждого СтрокаТабличнойЧасти Из ТабличнаяЧасть Цикл
			СчетаУчета = СоответствиеСчетовУчета.Получить(СтрокаТабличнойЧасти[КолонкаНоменклатура]);
			ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, СчетаУчета);
		КонецЦикла;
		
	КонецЕсли; 
		
КонецПроцедуры

// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - структура сведений о номенклатуре, либо стуктура счетов учета
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта, СтрокаТабличнойЧасти, СведенияОНоменклатуре, ВключаяЗаполненные = Истина) Экспорт

	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОНоменклатуре.Свойство("СчетаУчета") Тогда
		СчетаУчета = СведенияОНоменклатуре.СчетаУчета;
	ИначеЕсли СведенияОНоменклатуре.Свойство("СчетУчетаБУ") Тогда
		СчетаУчета = СведенияОНоменклатуре;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если Не ЗначениеЗаполнено(СчетаУчета.СчетУчетаНДСПоРеализации) Или (СчетаУчета.СчетУчетаНДСПоРеализации = ПланыСчетов.Типовой.ПустаяСсылка()) Тогда		
		СчетаУчета.СчетУчетаНДСПоРеализации  = ПланыСчетов.Типовой.НалогНаДобавленнуюСтоимость;		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетУчетаНДСПоРеализации) И ДанныеОбъекта.ОтражатьВБухгалтерскомУчете Тогда
		Если ВключаяЗаполненные ИЛИ НЕ ЗначениеЗаполнено(СтрокаТабличнойЧасти.СчетУчетаНДСПоРеализации) Тогда				
			СтрокаТабличнойЧасти.СчетУчетаНДСПоРеализации  = СчетаУчета.СчетУчетаНДСПоРеализации;
		КонецЕсли;    		
	Иначе
		СтрокаТабличнойЧасти.СчетУчетаНДСПоРеализации  = ПланыСчетов.Типовой.ПустаяСсылка();			
	КонецЕсли;
	
КонецПроцедуры

// Производит заполнение и установку реквизитов налогового учета и НДС в табличной части
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//
Процедура ЗаполнитьРеквизитыНалоговогоУчета(ДанныеОбъекта, СтрокаТабличнойЧасти, ИмяТабличнойЧасти) Экспорт
	
	МетаданныеДокумента = ДанныеОбъекта.Ссылка.Метаданные();
	ОбработкаТабличныхЧастей.ЗаполнитьНДСВидРеализацииТабЧасти(СтрокаТабличнойЧасти, ИмяТабличнойЧасти, МетаданныеДокумента, Пользователи.ТекущийПользователь());		
	
КонецПроцедуры

#КонецОбласти

#КонецОбласти

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// См. ЗапретРедактированияРеквизитовОбъектовПереопределяемый.ПриОпределенииОбъектовСЗаблокированнымиРеквизитами.
Функция ПолучитьБлокируемыеРеквизитыОбъекта() Экспорт
	
	БлокируемыеРеквизиты = Новый Массив;
	
	Если ОбщегоНазначенияБККлиентСервер.ЭтоПростаяВерсияКонфигурации() Тогда
		БлокируемыеРеквизиты.Добавить("Дата");
		БлокируемыеРеквизиты.Добавить("Номер");
		БлокируемыеРеквизиты.Добавить("ВалютаДокумента");
		БлокируемыеРеквизиты.Добавить("ВидОперации");
		БлокируемыеРеквизиты.Добавить("ДокументОснование; ДокументОснование"); //сделано специально для формы Дополнительно
		БлокируемыеРеквизиты.Добавить("Контрагент");
		БлокируемыеРеквизиты.Добавить("ДоговорКонтрагента");
		БлокируемыеРеквизиты.Добавить("КурсВзаиморасчетов");
		БлокируемыеРеквизиты.Добавить("КратностьВзаиморасчетов");
		БлокируемыеРеквизиты.Добавить("Организация; СтруктурноеПодразделениеОрганизация");
		БлокируемыеРеквизиты.Добавить("СуммаДокумента");
		БлокируемыеРеквизиты.Добавить("СтруктурноеПодразделение");
		БлокируемыеРеквизиты.Добавить("ОтражатьВБухгалтерскомУчете");
		
		// таб. часть Товары
		БлокируемыеРеквизиты.Добавить("Товары; ТоварыПодборТМЗ, ТоварыИзменитьТМЗ,
			|ТоварыСкопироватьСтроки, ТоварыВставитьСтроки, ТоварыКонтекстноеМенюСкопироватьСтроки, ТоварыКонтекстноеМенюВставитьСтроки");
		БлокируемыеРеквизиты.Добавить("Товары.Номенклатура; ТоварыНоменклатура");
		БлокируемыеРеквизиты.Добавить("Товары.Количество; ТоварыКоличество");
		БлокируемыеРеквизиты.Добавить("Товары.ЕдиницаИзмерения; ТоварыЕдиницаИзмерения");
		БлокируемыеРеквизиты.Добавить("Товары.Цена; ТоварыЦена");
		БлокируемыеРеквизиты.Добавить("Товары.СтавкаНДС; ТоварыСтавкаНДС");
		БлокируемыеРеквизиты.Добавить("Товары.Сумма; ТоварыСумма, ТоварыВсего");
		БлокируемыеРеквизиты.Добавить("Товары.СуммаНДС; ТоварыСуммаНДС");
		БлокируемыеРеквизиты.Добавить("Товары.ОборотПоРеализации; ТоварыОборотПоРеализации");
		БлокируемыеРеквизиты.Добавить("Товары.ГТД; ТоварыГТД");
		БлокируемыеРеквизиты.Добавить("Товары.Коэффициент; ТоварыКоэффициент");
		
		// таб. часть Услуги
		БлокируемыеРеквизиты.Добавить("Услуги; УслугиПодборУслуги, УслугиИзменитьУслуги,
			|УслугиСкопироватьСтроки, УслугиВставитьСтроки, УслугиКонтекстноеМенюСкопироватьСтроки, УслугиКонтекстноеМенюВставитьСтроки");
		БлокируемыеРеквизиты.Добавить("Услуги.Номенклатура; УслугиНоменклатура");
		БлокируемыеРеквизиты.Добавить("Услуги.Количество; УслугиКоличество");
		БлокируемыеРеквизиты.Добавить("Услуги.Цена; УслугиЦена");
		БлокируемыеРеквизиты.Добавить("Услуги.Сумма; УслугиСумма, УслугиВсего");
		БлокируемыеРеквизиты.Добавить("Услуги.СуммаНДС; УслугиСуммаНДС");
		БлокируемыеРеквизиты.Добавить("Услуги.СтавкаНДС; УслугиСтавкаНДС");
		БлокируемыеРеквизиты.Добавить("Услуги.ОборотПоРеализации; УслугиОборотПоРеализации");
		
		// таб. часть ОС
		БлокируемыеРеквизиты.Добавить("ОС; ОСПодборОС, ОСИзменитьОС");
		БлокируемыеРеквизиты.Добавить("ОС.ОсновноеСредство; ОСОсновноеСредство");
		БлокируемыеРеквизиты.Добавить("ОС.СтавкаНДС; ОССтавкаНДС");
		БлокируемыеРеквизиты.Добавить("ОС.Сумма; ОССумма, ОСВсего");
		БлокируемыеРеквизиты.Добавить("ОС.СуммаНДС; ОССуммаНДС");
		БлокируемыеРеквизиты.Добавить("ОС.ОборотПоРеализации; ОСОборотПоРеализации");
		БлокируемыеРеквизиты.Добавить("ОС.ГТД; ОСГТД");
		
		// таб. часть НМА
		БлокируемыеРеквизиты.Добавить("НМА; ПодборНМА, НМАИзменитьНМА");
		БлокируемыеРеквизиты.Добавить("НМА.НематериальныйАктив; НМАНематериальныйАктив");
		БлокируемыеРеквизиты.Добавить("НМА.СтавкаНДС; НМАСтавкаНДС");
		БлокируемыеРеквизиты.Добавить("НМА.Сумма; НМАСумма, НМАВсего");
		БлокируемыеРеквизиты.Добавить("НМА.СуммаНДС; НМАСуммаНДС");
		БлокируемыеРеквизиты.Добавить("НМА.ОборотПоРеализации; НМАОборотПоРеализации");
		
	КонецЕсли;
	
	Возврат БлокируемыеРеквизиты;
	
КонецФункции

// Конец СтандартныеПодсистемы.ЗапретРедактированияРеквизитовОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ЗаполнитьДокументПоДокументуОснованию(Объект, Основание, ЗаполнятьШапку = Истина) Экспорт

	Если ЗаполнятьШапку Тогда
		
		МетаданныеДокумента = Основание.ПолучитьОбъект().Метаданные();
		//если не заполнена дата подписания в РС "Сведения по актам выполненных работ", то документ не вводим
		Если ОбщегоНазначенияБК.ЕстьРеквизитДокумента("ОтложитьНачислениеНДС", МетаданныеДокумента) И Основание.ОтложитьНачислениеНДС Тогда	 
			
			ДатаПодписанияПоДаннымРегистра = УчетНДСИАкциза.ПолучитьДатуПодписанияПоАктамВыполненныхРабот(Основание);
			
			Если ДатаПодписанияПоДаннымРегистра = Дата(1,1,1) Тогда
				ТекстСообщения = НСтр("ru = 'Не указана дата подписания! Документ не может быть введен!'");
				ВызватьИсключение(ТекстСообщения);
			Иначе
				Объект.Дата = ДатаПодписанияПоДаннымРегистра;
			КонецЕсли; 
			
		Иначе 
			
		Если Основание.Дата <> КонецДня(Основание.Дата) Тогда
			Объект.Дата = Основание.Дата+1;
		Иначе
			Объект.Дата = Основание.Дата;
		КонецЕсли;
		
		КонецЕсли;

		Объект.ДокументОснование = Основание.Ссылка;
		
		// Заполним реквизиты из стандартного набора по документу основанию.
		ЗаполнениеДокументов.ЗаполнитьШапкуДокументаПоОснованию(Объект, Основание);
		МетаданныеДокумента = Основание.ПолучитьОбъект().Метаданные();	
		
		Если ОбщегоНазначенияБК.ЕстьРеквизитДокумента("ОтложитьНачислениеНДС", МетаданныеДокумента) И Объект.ДокументОснование.ОтложитьНачислениеНДС Тогда		
			Объект.СчетУчетаРасчетовСКонтрагентом  = ПланыСчетов.Типовой.НалогНаДобавленнуюСтоимостьОтложенный;
			Объект.СчетУчетаРасчетовПоАвансам      = ПланыСчетов.Типовой.ПустаяСсылка();
		КонецЕсли;
		
		// Документн не имеет смысла без данного признака
		Объект.УчитыватьНДС = Истина;	
		
	КонецЕсли;
	
	//заполним табличные части
	УчетНДСИАкциза.ЗаполнитьТабличныеЧастиИзДокументаОснования(Объект, Основание);
	
	СчетаУчетаВДокументах.ЗаполнитьПередОтображениемПользователю(Объект);
	
КонецПроцедуры  


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",	Константы.ВалютаРегламентированногоУчета.Получить());
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Если НЕ УчетнаяПолитикаСервер.Существует(Реквизиты.Организация, Реквизиты.Период, "БУ", Истина, ДокументСсылка)
		ИЛИ НЕ УчетнаяПолитикаСервер.Существует(Реквизиты.Организация, Реквизиты.Период, "НУ", Истина, ДокументСсылка) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат ПараметрыПроведения;
	КонецЕсли;
	
	Реквизиты.Вставить("РасчетыВВалюте", Реквизиты.ВалютаВзаиморасчетов <> Реквизиты.ВалютаРегламентированногоУчета);
	Запрос.УстановитьПараметр("РасчетыВВалюте",  Реквизиты.РасчетыВВалюте);
		
	Если Реквизиты.РасчетыВВалюте Тогда
		ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты);
	Иначе
		Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
		Если НЕ ПустаяСтрока(Запрос.Текст) Тогда 
			Результат    = Запрос.ВыполнитьПакет();
		КонецЕсли;
	КонецЕсли;
	
	ОрганизацияПлательщикНалогаНаПрибыль 			= ПроцедурыНалоговогоУчета.ПолучитьПризнакПлательщикаНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль 	= ОрганизацияПлательщикНалогаНаПрибыль И ПроцедурыНалоговогоУчета.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	НеобходимостьОтраженияВНУ 						= ОрганизацияПлательщикНалогаНаПрибыль И Реквизиты.УчитыватьКПН И (ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль ИЛИ Реквизиты.ВидУчетаНУ = Справочники.ВидыУчетаНУ.НУ);
			
	Если ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями") Тогда
		
		Налогоплательщик = ПроцедурыНалоговогоУчетаВызовСервераПовтИсп.ПолучитьНалогоплательщикаСтруктурнойЕдиницы(Реквизиты.СтруктурноеПодразделение,
																	Реквизиты.Организация,
																	Перечисления.РазделыНалоговогоУчета.НДС);
	Иначе
		Налогоплательщик = Реквизиты.Организация;															
	КонецЕсли;	
	
	Реквизиты.Вставить("Налогоплательщик", Налогоплательщик);	
	Реквизиты.Вставить("НеобходимостьОтраженияВНУ", 					 НеобходимостьОтраженияВНУ);
			
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(НеобходимостьОтраженияВНУ					  , "НеобходимостьОтраженияВНУ");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль, "ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(Налогоплательщик		 					  , "Налогоплательщик");
		
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаУчастникиСовместнойДеятельности(НомераТаблиц, ПараметрыПроведения, Реквизиты);
					                  	
	СодержаниеДокумента = НСтр("ru = '%1 НДС %2: %3'", ОбщегоНазначения.КодОсновногоЯзыка());	
	КорректировкаСодержание = ?(Реквизиты.КорректировкаОборота, НСтр("ru = 'Корректировка'", ОбщегоНазначения.КодОсновногоЯзыка()), "");
	
	Запрос.УстановитьПараметр("СинонимТовары",	      НСтр("ru = 'Товары'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("СинонимУслуги",	      НСтр("ru = 'Услуги'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("СинонимОС",	          НСтр("ru = 'ОС'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("СинонимНМА",	          НСтр("ru = 'НМА'", ОбщегоНазначения.КодОсновногоЯзыка()));
	Запрос.УстановитьПараметр("СодержаниеТовары",     СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СодержаниеДокумента,
															КорректировкаСодержание, НСтр("ru = '(Товары)'", ОбщегоНазначения.КодОсновногоЯзыка()), Реквизиты.Комментарий));
	Запрос.УстановитьПараметр("СодержаниеУслуги",     СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СодержаниеДокумента,
															КорректировкаСодержание, НСтр("ru = '(Услуги)'", ОбщегоНазначения.КодОсновногоЯзыка()), Реквизиты.Комментарий));
	Запрос.УстановитьПараметр("СодержаниеОС",     СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СодержаниеДокумента,
															КорректировкаСодержание, НСтр("ru = '(ОС)'", ОбщегоНазначения.КодОсновногоЯзыка()), Реквизиты.Комментарий));     															
	Запрос.УстановитьПараметр("СодержаниеНМА",     СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(СодержаниеДокумента,
															КорректировкаСодержание, НСтр("ru = '(НМА)'", ОбщегоНазначения.КодОсновногоЯзыка()), Реквизиты.Комментарий));
														
	Запрос.УстановитьПараметр("НеобходимостьОтраженияВНУ",	                  НеобходимостьОтраженияВНУ);
		
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда 
		Результат = Запрос.ВыполнитьПакет();
		Для Каждого НомерТаблицы Из НомераТаблиц Цикл
			ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
		КонецЦикла;
	КонецЕсли;
	
	СтруктураТаблицДокумента = Новый Структура;
	СтруктураТаблицДокумента.Вставить("ТаблицаДокумента", ПараметрыПроведения.ТаблицаНДС);
	ПараметрыПроведения.Вставить("СтруктураТаблицДокумента", СтруктураТаблицДокумента);
	
	Возврат ПараметрыПроведения;

КонецФункции 

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСоставДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	МАКСИМУМ(СоставДокумента.ЕстьТовары) КАК ЕстьТовары,
	|	МАКСИМУМ(СоставДокумента.ЕстьУслуги) КАК ЕстьУслуги,
	|	МАКСИМУМ(СоставДокумента.ЕстьОС) КАК ЕстьОС,
	|	МАКСИМУМ(СоставДокумента.ЕстьНМА) КАК ЕстьНМА,
	|	МАКСИМУМ(СоставДокумента.ЕстьУчастникиСовместнойДеятельности) КАК ЕстьУчастникиСовместнойДеятельности
	|ПОМЕСТИТЬ СоставДокумента
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ИСТИНА КАК ЕстьТовары,
	|		ЛОЖЬ КАК ЕстьУслуги,
	|		ЛОЖЬ КАК ЕстьОС,
	|		ЛОЖЬ КАК ЕстьНМА,
	|		ЛОЖЬ КАК ЕстьУчастникиСовместнойДеятельности
	|	ИЗ
	|		Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.Товары КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ЛОЖЬ,
	|		ИСТИНА,
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ЛОЖЬ
	|	ИЗ
	|		Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.Услуги КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ИСТИНА,
	|		ЛОЖЬ,
	|		ЛОЖЬ
	|	ИЗ
	|		Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.ОС КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ИСТИНА,
	|		ЛОЖЬ
	|	ИЗ
	|		Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.НМА КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.УчастникиСовместнойДеятельности КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка) КАК СоставДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.Организация КАК Налогоплательщик,
	|	Реквизиты.СтруктурноеПодразделение,
	|	ВЫБОР
	|		КОГДА Реквизиты.Организация.ГоловнаяОрганизация ЕСТЬ NULL 
	|				ИЛИ Реквизиты.Организация.ГоловнаяОрганизация = ЗНАЧЕНИЕ(Справочник.Организации.ПустаяСсылка)
	|			ТОГДА Реквизиты.Организация
	|		ИНАЧЕ Реквизиты.Организация.ГоловнаяОрганизация
	|	КОНЕЦ КАК ГоловнаяОрганизация,
	|	Реквизиты.УчитыватьКПН,
	|	Реквизиты.ВидУчетаНУ,
	|	Реквизиты.ВидОперации,
	|	ВЫБОР
	|		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийВЦеляхНДС.КорректировкаОборотаПоРеализации)
	|			ТОГДА ИСТИНА
	|		ИНАЧЕ ЛОЖЬ
	|	КОНЕЦ КАК КорректировкаОборота,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ДоговорСовместнойДеятельности, НЕОПРЕДЕЛЕНО) КАК ПризнакДоговорСовместнойДеятельности,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВедениеВзаиморасчетов, НЕОПРЕДЕЛЕНО) КАК ВедениеВзаиморасчетов,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВидДоговора, НЕОПРЕДЕЛЕНО) КАК ВидДоговора,
	|	ЕСТЬNULL(Реквизиты.ДоговорКонтрагента.ВалютаВзаиморасчетов, НЕОПРЕДЕЛЕНО) КАК ВалютаВзаиморасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.УчитыватьНДС,
	|	Реквизиты.СуммаВключаетНДС,
	|	Реквизиты.КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента,
	|	&ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	ЛОЖЬ КАК НеобходимостьОтраженияВНУ,
	|	ЛОЖЬ КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
	|	Реквизиты.ОтражатьВБухгалтерскомУчете КАК ОтражатьВБухгалтерскомУчете,
	|	Реквизиты.Комментарий КАК Комментарий,
	|	Реквизиты.ДокументОснование КАК ДокументОснование
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация,
	|	Реквизиты.Налогоплательщик,
	|	Реквизиты.СтруктурноеПодразделение,
	|	Реквизиты.ГоловнаяОрганизация,
	|	Реквизиты.УчитыватьКПН,
	|	Реквизиты.ВидУчетаНУ,
	|	Реквизиты.ВидОперации,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ПризнакДоговорСовместнойДеятельности,
	|	Реквизиты.ВедениеВзаиморасчетов,
	|	Реквизиты.ВидДоговора,
	|	Реквизиты.ВалютаВзаиморасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам,
	|	Реквизиты.УчитыватьНДС,
	|	Реквизиты.СуммаВключаетНДС,
	|	Реквизиты.КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов,
	|	Реквизиты.ВалютаДокумента,
	|	Реквизиты.ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	Реквизиты.НеобходимостьОтраженияВНУ КАК НеобходимостьОтраженияВНУ,
	|	Реквизиты.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
	|	ЕСТЬNULL(СоставДокумента.ЕстьТовары, ЛОЖЬ) КАК ЕстьТовары,
	|	ЕСТЬNULL(СоставДокумента.ЕстьУслуги, ЛОЖЬ) КАК ЕстьУслуги,
	|	ЕСТЬNULL(СоставДокумента.ЕстьОС, ЛОЖЬ) КАК ЕстьОС,
	|	ЕСТЬNULL(СоставДокумента.ЕстьНМА, ЛОЖЬ) КАК ЕстьНМА,
	|	ЕСТЬNULL(СоставДокумента.ЕстьУчастникиСовместнойДеятельности, ЛОЖЬ) КАК ЕстьУчастникиСовместнойДеятельности,
	|	Реквизиты.Комментарий,
	|	Реквизиты.КорректировкаОборота,
	|	Реквизиты.ОтражатьВБухгалтерскомУчете,
	|	ВЫБОР
	|		КОГДА Реквизиты.КорректировкаОборота
	|			ТОГДА ""КорректировкаНДС""
	|		ИНАЧЕ ""НДС""
	|	КОНЕЦ КАК ИмяРегистраНДС,
	|	Реквизиты.ДокументОснование КАК ДокументОснование
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоставДокумента КАК СоставДокумента
	|		ПО (ИСТИНА)";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты)

	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьТовары Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаТовары", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаТовары.Ссылка,
		|	ТаблицаТовары.НомерСтроки,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.СтавкаНДС,
		|	ТаблицаТовары.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаТовары.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма,
		|	ТаблицаТовары.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаТовары.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК  СуммаВзаиморасчетов,
		|	ТаблицаТовары.СуммаНДС КАК СуммаНДС,
		|	ТаблицаТовары.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаТовары.СчетУчетаНДСПоРеализации КАК СчетУчетаНДС,
		|	ТаблицаТовары.ОборотПоРеализации КАК ОборотПоРеализации,
		|	ТаблицаТовары.ОборотПоРеализации КАК ОборотПоРеализацииВзаиморасчетов,		
		|	ТаблицаТовары.НДСВидОперацииРеализации
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.Товары КАК ТаблицаТовары
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаТовары.Ссылка = Реквизиты.Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	    	
	Если Реквизиты.ЕстьУслуги Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаУслуги", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаУслуги.Ссылка КАК Ссылка,
		|	ТаблицаУслуги.НомерСтроки КАК НомерСтроки,
		|	ТаблицаУслуги.Номенклатура КАК Номенклатура,
		|	ТаблицаУслуги.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаУслуги.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма,
		|	ТаблицаУслуги.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаУслуги.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаУслуги.СуммаНДС КАК СуммаНДС,
		|	ТаблицаУслуги.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаУслуги.СчетУчетаНДСПоРеализации КАК СчетУчетаНДС,
		|	ТаблицаУслуги.ОборотПоРеализации КАК ОборотПоРеализации,
		|	ТаблицаУслуги.ОборотПоРеализации КАК ОборотПоРеализацииВзаиморасчетов,
		|	ТаблицаУслуги.НДСВидОперацииРеализации КАК НДСВидОперацииРеализации
		|ПОМЕСТИТЬ ТаблицаУслуги
		|ИЗ
		|	Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.Услуги КАК ТаблицаУслуги
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаУслуги.Ссылка = Реквизиты.Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	Если Реквизиты.ЕстьОС Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаОС", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаОС.Ссылка,
		|	ТаблицаОС.НомерСтроки,
		|	ТаблицаОС.ОсновноеСредство,
		|	ТаблицаОС.СтавкаНДС,
	    |	ТаблицаОС.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаОС.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма,
		|	ТаблицаОС.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаОС.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаОС.СуммаНДС КАК СуммаНДС,
		|	ТаблицаОС.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаОС.СчетУчетаНДСПоРеализации КАК СчетУчетаНДС,
		|	ТаблицаОС.ОборотПоРеализации КАК ОборотПоРеализации,
		|	ТаблицаОС.ОборотПоРеализации КАК ОборотПоРеализацииВзаиморасчетов,		
		|	ТаблицаОС.НДСВидОперацииРеализации
		|ПОМЕСТИТЬ ТаблицаОС
		|ИЗ
		|	Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.ОС КАК ТаблицаОС
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаОС.Ссылка = Реквизиты.Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	Если Реквизиты.ЕстьНМА Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаНМА", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаНМА.Ссылка КАК Ссылка,
		|	ТаблицаНМА.НомерСтроки КАК НомерСтроки,
		|	ТаблицаНМА.НематериальныйАктив КАК НематериальныйАктив,
		|	ТаблицаНМА.СтавкаНДС КАК СтавкаНДС,
		|	ТаблицаНМА.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаНМА.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК Сумма,
		|	ТаблицаНМА.Сумма - ВЫБОР
		|		КОГДА Реквизиты.СуммаВключаетНДС
		|			ТОГДА ТаблицаНМА.СуммаНДС
		|		ИНАЧЕ 0
		|	КОНЕЦ КАК СуммаВзаиморасчетов,
		|	ТаблицаНМА.СуммаНДС КАК СуммаНДС,
		|	ТаблицаНМА.СуммаНДС КАК СуммаНДСВзаиморасчетов,
		|	ТаблицаНМА.СчетУчетаНДСПоРеализации КАК СчетУчетаНДС,
		|	ТаблицаНМА.ОборотПоРеализации КАК ОборотПоРеализации,
		|	ТаблицаНМА.ОборотПоРеализации КАК ОборотПоРеализацииВзаиморасчетов,
		|	ТаблицаНМА.НДСВидОперацииРеализации КАК НДСВидОперацииРеализации
		|ПОМЕСТИТЬ ТаблицаНМА
		|ИЗ
		|	Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.НМА КАК ТаблицаНМА
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО ТаблицаНМА.Ссылка = Реквизиты.Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	 		
	Если Реквизиты.ЕстьУчастникиСовместнойДеятельности Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаУчастникиСовместнойДеятельности", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаУчастникиСовместнойДеятельности.НомерСтроки,
		|	ТаблицаУчастникиСовместнойДеятельности.УчастникСовместнойДеятельности,
		|	ТаблицаУчастникиСовместнойДеятельности.ДоляУчастия
		|ПОМЕСТИТЬ ТаблицаУчастникиСовместнойДеятельности
		|ИЗ
		|	Документ.РегистрацияПрочихОперацийПоРеализованнымТоварамВЦеляхНДС.УчастникиСовместнойДеятельности КАК ТаблицаУчастникиСовместнойДеятельности
		|ГДЕ
		|	ТаблицаУчастникиСовместнойДеятельности.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

Процедура ПодготовитьТаблицыДокументаРасчетыВВалюте(Запрос, Реквизиты)
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты);
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат    = Запрос.ВыполнитьПакет();
	КонецЕсли;
	
	ТекстЗапроса = "";
	Если Реквизиты.ЕстьТовары Тогда
		СуммыТаблицыТовары = Результат[НомераТаблиц["СуммыТаблицыТовары"]].Выгрузить();
		УправлениеВзаиморасчетамиСервер.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыТовары, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыТовары", СуммыТаблицыТовары);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеТовары(НомераТаблиц, Реквизиты);
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда
		СуммыТаблицыУслуги = Результат[НомераТаблиц["СуммыТаблицыУслуги"]].Выгрузить();
		УправлениеВзаиморасчетамиСервер.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыУслуги, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыУслуги", СуммыТаблицыУслуги);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеУслуги(НомераТаблиц, Реквизиты);
	КонецЕсли;

	Если Реквизиты.ЕстьОС Тогда
		СуммыТаблицыОС = Результат[НомераТаблиц["СуммыТаблицыОС"]].Выгрузить();
		УправлениеВзаиморасчетамиСервер.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыОС, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыОС", СуммыТаблицыОС);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеОС(НомераТаблиц, Реквизиты);
	КонецЕсли;
	
	Если Реквизиты.ЕстьНМА Тогда
		СуммыТаблицыНМА = Результат[НомераТаблиц["СуммыТаблицыНМА"]].Выгрузить();
		УправлениеВзаиморасчетамиСервер.ПодготовитьТаблицуДокументаРасчетыВВалюте(СуммыТаблицыНМА, Реквизиты);
		Запрос.УстановитьПараметр("СуммыТаблицыНМА", СуммыТаблицыНМА);
		ТекстЗапроса = ТекстЗапроса + ТекстЗапросаРасчетыВВалютеНМА(НомераТаблиц, Реквизиты);
	КонецЕсли;

	Запрос.Текст = ТекстЗапроса;
	Если НЕ ПустаяСтрока(Запрос.Текст) Тогда
		Результат    = Запрос.ВыполнитьПакет();
	КонецЕсли;
	
КонецПроцедуры

Функция ТекстЗапросаВременныеТаблицыДокументаРасчетыВВалюте(НомераТаблиц, Реквизиты)

	ТекстЗапроса = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты);
	
	Если Реквизиты.ЕстьТовары Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаТовары", "ПОМЕСТИТЬ ВременнаяТаблицаТовары");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаТовары.Ссылка = &Ссылка", "ТаблицаТовары.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыТовары", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаТовары.НомерСтроки,
		|	ВременнаяТаблицаТовары.СтавкаНДС,
		|	ВременнаяТаблицаТовары.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаТовары.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаТовары.ОборотПоРеализацииВзаиморасчетов,		
		|	ВременнаяТаблицаТовары.Сумма,
		|	ВременнаяТаблицаТовары.СуммаНДС,
		|	ВременнаяТаблицаТовары.ОборотПоРеализации
		|ИЗ
		|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаУслуги", "ПОМЕСТИТЬ ВременнаяТаблицаУслуги");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаУслуги.Ссылка = &Ссылка", "ТаблицаУслуги.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыУслуги", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаУслуги.НомерСтроки,
		|	ВременнаяТаблицаУслуги.СтавкаНДС,
		|	ВременнаяТаблицаУслуги.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаУслуги.ОборотПоРеализацииВзаиморасчетов,		
		|	ВременнаяТаблицаУслуги.Сумма,
		|	ВременнаяТаблицаУслуги.СуммаНДС,
		|	ВременнаяТаблицаУслуги.ОборотПоРеализации
		|ИЗ
		|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	Если Реквизиты.ЕстьОС Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаОС", "ПОМЕСТИТЬ ВременнаяТаблицаОС");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаОС.Ссылка = &Ссылка", "ТаблицаОС.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыОС", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаОС.НомерСтроки,
		|	ВременнаяТаблицаОС.СтавкаНДС,
		|	ВременнаяТаблицаОС.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаОС.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаОС.ОборотПоРеализацииВзаиморасчетов,		
		|	ВременнаяТаблицаОС.Сумма,
		|	ВременнаяТаблицаОС.СуммаНДС,
		|	ВременнаяТаблицаОС.ОборотПоРеализации
		|ИЗ
		|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Если Реквизиты.ЕстьНМА Тогда
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ПОМЕСТИТЬ ТаблицаНМА", "ПОМЕСТИТЬ ВременнаяТаблицаНМА");
		ТекстЗапроса = СтрЗаменить(ТекстЗапроса, "ТаблицаНМА.Ссылка = &Ссылка", "ТаблицаНМА.Ссылка = &Ссылка
		|
		|ИНДЕКСИРОВАТЬ ПО
		|НомерСтроки");
		НомераТаблиц.Вставить("СуммыТаблицыНМА", НомераТаблиц.Количество());
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ВременнаяТаблицаНМА.НомерСтроки,
		|	ВременнаяТаблицаНМА.СтавкаНДС,
		|	ВременнаяТаблицаНМА.СуммаВзаиморасчетов,
		|	ВременнаяТаблицаНМА.СуммаНДСВзаиморасчетов,
		|	ВременнаяТаблицаНМА.ОборотПоРеализацииВзаиморасчетов,		
		|	ВременнаяТаблицаНМА.Сумма,
		|	ВременнаяТаблицаНМА.СуммаНДС,
		|	ВременнаяТаблицаНМА.ОборотПоРеализации		
		|ИЗ
		|	ВременнаяТаблицаНМА КАК ВременнаяТаблицаНМА"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаРасчетыВВалютеТовары(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("ТаблицаТовары", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыТовары.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыТовары.СуммаВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыТовары.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыТовары.Сумма,
	|	СуммыТаблицыТовары.СуммаНДС,
	|	СуммыТаблицыТовары.ОборотПоРеализации	
	|ПОМЕСТИТЬ СуммыТаблицыТовары
	|ИЗ
	|	&СуммыТаблицыТовары КАК СуммыТаблицыТовары
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаТовары.Ссылка,
	|	ВременнаяТаблицаТовары.НомерСтроки,
	|	ВременнаяТаблицаТовары.Номенклатура,
	|	СуммыТаблицыТовары.СуммаВзаиморасчетов,
	|	СуммыТаблицыТовары.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыТовары.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыТовары.Сумма,
	|	СуммыТаблицыТовары.СуммаНДС,
	|	СуммыТаблицыТовары.ОборотПоРеализации,
	|	ВременнаяТаблицаТовары.СтавкаНДС,
	|	ВременнаяТаблицаТовары.СчетУчетаНДС,
	|	ВременнаяТаблицаТовары.НДСВидОперацииРеализации	
	|ПОМЕСТИТЬ ТаблицаТовары
	|ИЗ
	|	ВременнаяТаблицаТовары КАК ВременнаяТаблицаТовары
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыТовары КАК СуммыТаблицыТовары
	|		ПО ВременнаяТаблицаТовары.НомерСтроки = СуммыТаблицыТовары.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеУслуги(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("ТаблицаУслуги", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыУслуги.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыУслуги.Сумма,
	|	СуммыТаблицыУслуги.СуммаНДС,
	|	СуммыТаблицыУслуги.ОборотПоРеализации
	|ПОМЕСТИТЬ СуммыТаблицыУслуги
	|ИЗ
	|	&СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаУслуги.Ссылка,
	|	ВременнаяТаблицаУслуги.НомерСтроки,
	|	ВременнаяТаблицаУслуги.Номенклатура,
	|	СуммыТаблицыУслуги.СуммаВзаиморасчетов,
	|	СуммыТаблицыУслуги.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыУслуги.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыУслуги.Сумма,
	|	СуммыТаблицыУслуги.СуммаНДС,
	|	СуммыТаблицыУслуги.ОборотПоРеализации,	
	|	ВременнаяТаблицаУслуги.СтавкаНДС,
	|	ВременнаяТаблицаУслуги.СчетУчетаНДС,
	|	ВременнаяТаблицаУслуги.НДСВидОперацииРеализации
	|ПОМЕСТИТЬ ТаблицаУслуги
	|ИЗ
	|	ВременнаяТаблицаУслуги КАК ВременнаяТаблицаУслуги
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыУслуги КАК СуммыТаблицыУслуги
	|		ПО ВременнаяТаблицаУслуги.НомерСтроки = СуммыТаблицыУслуги.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеОС(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("ТаблицаОС", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыОС.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыОС.СуммаВзаиморасчетов,
	|	СуммыТаблицыОС.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОС.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыОС.Сумма,
	|	СуммыТаблицыОС.СуммаНДС,
	|	СуммыТаблицыОС.ОборотПоРеализации	
	|ПОМЕСТИТЬ СуммыТаблицыОС
	|ИЗ
	|	&СуммыТаблицыОС КАК СуммыТаблицыОС
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаОС.Ссылка,
	|	ВременнаяТаблицаОС.НомерСтроки,
	|	ВременнаяТаблицаОС.ОсновноеСредство,
	|	СуммыТаблицыОС.СуммаВзаиморасчетов,
	|	СуммыТаблицыОС.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыОС.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыОС.Сумма,
	|	СуммыТаблицыОС.СуммаНДС,
	|	СуммыТаблицыОС.ОборотПоРеализации,
	|	ВременнаяТаблицаОС.СтавкаНДС,
	|	ВременнаяТаблицаОС.СчетУчетаНДС,
	|	ВременнаяТаблицаОС.НДСВидОперацииРеализации	
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	ВременнаяТаблицаОС КАК ВременнаяТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыОС КАК СуммыТаблицыОС
	|		ПО ВременнаяТаблицаОС.НомерСтроки = СуммыТаблицыОС.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаРасчетыВВалютеНМА(НомераТаблиц, Реквизиты)

	НомераТаблиц.Вставить("ТаблицаНМА", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	СуммыТаблицыНМА.НомерСтроки КАК НомерСтроки,
	|	СуммыТаблицыНМА.СуммаВзаиморасчетов,
	|	СуммыТаблицыНМА.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыНМА.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыНМА.Сумма,
	|	СуммыТаблицыНМА.СуммаНДС,
	|	СуммыТаблицыНМА.ОборотПоРеализации	
	|ПОМЕСТИТЬ СуммыТаблицыНМА
	|ИЗ
	|	&СуммыТаблицыНМА КАК СуммыТаблицыНМА
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	НомерСтроки
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ВременнаяТаблицаНМА.Ссылка,
	|	ВременнаяТаблицаНМА.НомерСтроки,
	|	ВременнаяТаблицаНМА.НематериальныйАктив,
	|	СуммыТаблицыНМА.СуммаВзаиморасчетов,
	|	СуммыТаблицыНМА.СуммаНДСВзаиморасчетов,
	|	СуммыТаблицыНМА.ОборотПоРеализацииВзаиморасчетов,	
	|	СуммыТаблицыНМА.Сумма,
	|	СуммыТаблицыНМА.СуммаНДС,
	|	СуммыТаблицыНМА.ОборотПоРеализации,
	|	ВременнаяТаблицаНМА.СтавкаНДС,
	|	ВременнаяТаблицаНМА.СчетУчетаНДС,
	|	ВременнаяТаблицаНМА.НДСВидОперацииРеализации
	|ПОМЕСТИТЬ ТаблицаНМА
	|ИЗ
	|	ВременнаяТаблицаНМА КАК ВременнаяТаблицаНМА
	|		ЛЕВОЕ СОЕДИНЕНИЕ СуммыТаблицыНМА КАК СуммыТаблицыНМА
	|		ПО ВременнаяТаблицаНМА.НомерСтроки = СуммыТаблицыНМА.НомерСтроки";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции 

Функция ТекстЗапросаНДС(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	ТекстЗапроса = "";
		
	Если Реквизиты.ЕстьТовары Тогда 
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	""ТоварыНДС"" КАК ИмяСписка,
		|	&СинонимТовары КАК СинонимСписка,
		|	ТаблицаТовары.НомерСтроки,
		|	ТаблицаТовары.Номенклатура КАК ТМЗ,
		|	ТаблицаТовары.Сумма КАК СуммаБезНДС,
		|	ТаблицаТовары.Сумма КАК СуммаКорректировкиОборота,
		|	ТаблицаТовары.ОборотПоРеализации КАК ОборотПоРеализации,
		|	ТаблицаТовары.ОборотПоРеализацииВзаиморасчетов КАК ОборотПоРеализацииВал,		
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаТовары.СуммаНДС * -1
		|		ИНАЧЕ ТаблицаТовары.СуммаНДС
		|	КОНЕЦ КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаТовары.СуммаНДСВзаиморасчетов * -1
		|		ИНАЧЕ ТаблицаТовары.СуммаНДСВзаиморасчетов
		|	КОНЕЦ КАК СуммаНДСВал,
		|	ТаблицаТовары.СтавкаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаТовары.СчетУчетаНДС
		|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
		|	КОНЕЦ КАК Счет,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаТовары.СчетУчетаНДС
		|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
		|	КОНЕЦ КАК КорСчет,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК Субконто1,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
		|		ИНАЧЕ Реквизиты.ДоговорКонтрагента
		|	КОНЕЦ КАК Субконто2,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Ссылка
		|	КОНЕЦ КАК Субконто3,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК КорСубконто1,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
		|		ИНАЧЕ Реквизиты.ДоговорКонтрагента
		|	КОНЕЦ КАК КорСубконто2,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Ссылка
		|	КОНЕЦ КАК КорСубконто3,
		|	ТаблицаТовары.НДСВидОперацииРеализации КАК ВидОперацииРеализации,
		|	ТаблицаТовары.НДСВидОперацииРеализации КАК ВидОперацииКорректировки,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыНДС.НДС) КАК ВидНалогаНДС,
		|	&СодержаниеТовары КАК Содержание,
		|	0 КАК Порядок
		|ИЗ
		|	ТаблицаТовары КАК ТаблицаТовары
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)";
	КонецЕсли;
	
	Если Реквизиты.ЕстьУслуги Тогда 
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда 
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;

	   ТекстЗапроса = ТекстЗапроса + 
	   "ВЫБРАТЬ
	   |	""УслугиНДС"" КАК ИмяСписка,
	   |	&СинонимТовары КАК СинонимСписка,
	   |	ТаблицаУслуги.НомерСтроки,
	   |	ТаблицаУслуги.Номенклатура КАК ТМЗ,
	   |	ТаблицаУслуги.Сумма КАК СуммаБезНДС,
	   |	ТаблицаУслуги.Сумма КАК СуммаКорректировкиОборота,
	   |	ТаблицаУслуги.ОборотПоРеализации КАК ОборотПоРеализации,
	   |	ТаблицаУслуги.ОборотПоРеализацииВзаиморасчетов КАК ОборотПоРеализацииВал,		
	   |	ВЫБОР
	   |		КОГДА Реквизиты.КорректировкаОборота
	   |			ТОГДА ТаблицаУслуги.СуммаНДС * -1
	   |		ИНАЧЕ ТаблицаУслуги.СуммаНДС
	   |	КОНЕЦ КАК СуммаНДС,
	   |	ВЫБОР
	   |		КОГДА Реквизиты.КорректировкаОборота
	   |			ТОГДА ТаблицаУслуги.СуммаНДСВзаиморасчетов * -1
	   |		ИНАЧЕ ТаблицаУслуги.СуммаНДСВзаиморасчетов
	   |	КОНЕЦ КАК СуммаНДСВал,
	   |	ТаблицаУслуги.СтавкаНДС,
	   |	ВЫБОР
	   |		КОГДА Реквизиты.КорректировкаОборота
	   |			ТОГДА ТаблицаУслуги.СчетУчетаНДС
	   |		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
	   |	КОНЕЦ КАК Счет,
	   |	ВЫБОР
	   |		КОГДА НЕ Реквизиты.КорректировкаОборота
	   |			ТОГДА ТаблицаУслуги.СчетУчетаНДС
	   |		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
	   |	КОНЕЦ КАК КорСчет,
	   |	ВЫБОР
	   |		КОГДА Реквизиты.КорректировкаОборота
	   |			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
	   |		ИНАЧЕ Реквизиты.Контрагент
	   |	КОНЕЦ КАК Субконто1,
	   |	ВЫБОР
	   |		КОГДА Реквизиты.КорректировкаОборота
	   |			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
	   |		ИНАЧЕ ВЫБОР
	   |			КОГДА НЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом = ЗНАЧЕНИЕ(ПланСчетов.Типовой.НалогНаДобавленнуюСтоимостьОтложенный)
	   |				ТОГДА Реквизиты.ДоговорКонтрагента
	   |			ИНАЧЕ Реквизиты.ДокументОснование
	   |		КОНЕЦ
	   |	КОНЕЦ КАК Субконто2, 
	   |	ВЫБОР
	   |		КОГДА Реквизиты.КорректировкаОборота
	   |			ТОГДА НЕОПРЕДЕЛЕНО
	   |		ИНАЧЕ Реквизиты.Ссылка
	   |	КОНЕЦ КАК Субконто3,
	   |	ВЫБОР
	   |		КОГДА НЕ Реквизиты.КорректировкаОборота
	   |			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
	   |		ИНАЧЕ Реквизиты.Контрагент
	   |	КОНЕЦ КАК КорСубконто1,
	   |	ВЫБОР
	   |		КОГДА НЕ Реквизиты.КорректировкаОборота
	   |			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
	   |		ИНАЧЕ Реквизиты.ДоговорКонтрагента
	   |	КОНЕЦ КАК КорСубконто2,
	   |	ВЫБОР
	   |		КОГДА НЕ Реквизиты.КорректировкаОборота
	   |			ТОГДА НЕОПРЕДЕЛЕНО
	   |		ИНАЧЕ Реквизиты.Ссылка
	   |	КОНЕЦ КАК КорСубконто3,
	   |	ТаблицаУслуги.НДСВидОперацииРеализации КАК ВидОперацииРеализации,
	   |	ТаблицаУслуги.НДСВидОперацииРеализации КАК ВидОперацииКорректировки, 
	   |	ЗНАЧЕНИЕ(Перечисление.ВидыНДС.НДС) КАК ВидНалогаНДС,
	   |	&СодержаниеУслуги КАК Содержание,
	   |	1 КАК Порядок
	   |ИЗ
	   |	ТаблицаУслуги КАК ТаблицаУслуги
	   |		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	   |		ПО (ИСТИНА) ";
	КонецЕсли;

	Если Реквизиты.ЕстьОС Тогда 
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда 
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	""ОСНДС"" КАК ИмяСписка,
		|	&СинонимОС КАК СинонимСписка,
		|	ТаблицаОС.НомерСтроки,
		|	ТаблицаОС.ОсновноеСредство КАК ТМЗ,
		|	ТаблицаОС.Сумма КАК СуммаБезНДС,
		|	ТаблицаОС.Сумма КАК СуммаКорректировкиОборота, 		
		|	ТаблицаОС.ОборотПоРеализации КАК ОборотПоРеализации,
		|	ТаблицаОС.ОборотПоРеализацииВзаиморасчетов КАК ОборотПоРеализацииВал,	
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаОС.СуммаНДС * -1
		|		ИНАЧЕ ТаблицаОС.СуммаНДС
		|	КОНЕЦ КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаОС.СуммаНДСВзаиморасчетов * -1
		|		ИНАЧЕ ТаблицаОС.СуммаНДСВзаиморасчетов
		|	КОНЕЦ КАК СуммаНДСВал,
		|	ТаблицаОС.СтавкаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаОС.СчетУчетаНДС
		|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
		|	КОНЕЦ КАК Счет,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаОС.СчетУчетаНДС
		|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
		|	КОНЕЦ КАК КорСчет,
		|	ВЫБОР
		|		КОГДА  Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК Субконто1,
		|	ВЫБОР
		|		КОГДА  Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
		|		ИНАЧЕ Реквизиты.ДоговорКонтрагента
		|	КОНЕЦ КАК Субконто2,
		|	ВЫБОР
		|		КОГДА  Реквизиты.КорректировкаОборота
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Ссылка
		|	КОНЕЦ КАК Субконто3,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК КорСубконто1,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
		|		ИНАЧЕ Реквизиты.ДоговорКонтрагента
		|	КОНЕЦ КАК КорСубконто2,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Ссылка
		|	КОНЕЦ КАК КорСубконто3,
		|	ТаблицаОС.НДСВидОперацииРеализации КАК ВидОперацииРеализации,
		|	ТаблицаОС.НДСВидОперацииРеализации КАК ВидОперацииКорректировки,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыНДС.НДС) КАК ВидНалогаНДС,
		|	&СодержаниеОС КАК Содержание,
		|	2 КАК Порядок
		|ИЗ
		|	ТаблицаОС КАК ТаблицаОС
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА) ";		
	КонецЕсли;
	
	Если Реквизиты.ЕстьНМА Тогда 
		Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда 
			ТекстЗапроса = ТекстЗапроса + "
			|
			|ОБЪЕДИНИТЬ ВСЕ
			|";
		КонецЕсли;
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	""НМАНДС"" КАК ИмяСписка,
		|	&СинонимНМА КАК СинонимСписка,
		|	ТаблицаНМА.НомерСтроки,
		|	ТаблицаНМА.НематериальныйАктив КАК ТМЗ,
		|	ТаблицаНМА.Сумма КАК СуммаБезНДС,
		|	ТаблицаНМА.Сумма КАК СуммаКорректировкиОборота, 		
		|	ТаблицаНМА.ОборотПоРеализации КАК ОборотПоРеализации,
		|	ТаблицаНМА.ОборотПоРеализацииВзаиморасчетов КАК ОборотПоРеализацииВал,		
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаНМА.СуммаНДС * -1
		|		ИНАЧЕ ТаблицаНМА.СуммаНДС
		|	КОНЕЦ КАК СуммаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаНМА.СуммаНДСВзаиморасчетов * -1
		|		ИНАЧЕ ТаблицаНМА.СуммаНДСВзаиморасчетов
		|	КОНЕЦ КАК СуммаНДСВал,
		|	ТаблицаНМА.СтавкаНДС,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаНМА.СчетУчетаНДС
		|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
		|	КОНЕЦ КАК Счет,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ТаблицаНМА.СчетУчетаНДС
		|		ИНАЧЕ Реквизиты.СчетУчетаРасчетовСКонтрагентом
		|	КОНЕЦ КАК КорСчет,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК Субконто1,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
		|		ИНАЧЕ Реквизиты.ДоговорКонтрагента
		|	КОНЕЦ КАК Субконто2,
		|	ВЫБОР
		|		КОГДА Реквизиты.КорректировкаОборота
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Ссылка
		|	КОНЕЦ КАК Субконто3,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Справочник.НалогиСборыОтчисления.НалогНаДобавленнуюСтоимость)
		|		ИНАЧЕ Реквизиты.Контрагент
		|	КОНЕЦ КАК КорСубконто1,
		|	ВЫБОР
		|		КОГДА НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА ЗНАЧЕНИЕ(Перечисление.ВидыПлатежейВБюджетИФонды.Налог)
		|		ИНАЧЕ Реквизиты.ДоговорКонтрагента
		|	КОНЕЦ КАК КорСубконто2,
		|	ВЫБОР
		|		КОГДА  НЕ Реквизиты.КорректировкаОборота
		|			ТОГДА НЕОПРЕДЕЛЕНО
		|		ИНАЧЕ Реквизиты.Ссылка
		|	КОНЕЦ КАК КорСубконто3,
		|	ТаблицаНМА.НДСВидОперацииРеализации КАК ВидОперацииРеализации,
		|	ТаблицаНМА.НДСВидОперацииРеализации КАК ВидОперацииКорректировки,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыНДС.НДС) КАК ВидНалогаНДС,
		|	&СодержаниеНМА КАК Содержание,
		|	3 КАК Порядок
		|ИЗ
		|	ТаблицаНМА КАК ТаблицаНМА
		|		ЛЕВОЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)	";		
	КонецЕсли;
	
	Если НЕ ПустаяСтрока(ТекстЗапроса) Тогда 
		НомераТаблиц.Вставить("ТаблицаНДС",	НомераТаблиц.Количество());

		ТекстЗапроса = ТекстЗапроса + "
		|УПОРЯДОЧИТЬ ПО
		|	Порядок,
		|	НомерСтроки" + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	Иначе 
		ПараметрыПроведения.Вставить("ТаблицаНДС", Неопределено);
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Функция ТекстЗапросаЗачетАвансов(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийВЦеляхНДС.КорректировкаПрочее ИЛИ НЕ Реквизиты.ОтражатьВБухгалтерскомУчете Тогда
		ПараметрыПроведения.Вставить("ЗачетАвансовРеквизиты",        Неопределено);
		ПараметрыПроведения.Вставить("ЗачетАвансовТаблицаДокумента", Неопределено);
		Возврат "";
	КонецЕсли;
	
	НомераТаблиц.Вставить("ВременнаяТаблицаСуммВзаиморасчетов", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовРеквизиты",        НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ЗачетАвансовТаблицаДокумента", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	СУММА(ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов) КАК СуммаВзаиморасчетов,
	|	СУММА(ТаблицаСуммВзаиморасчетов.Сумма) КАК Сумма
	|ПОМЕСТИТЬ ТаблицаСуммВзаиморасчетов
	|ИЗ
	|	(ВЫБРАТЬ
	|		0 КАК СуммаВзаиморасчетов,
	|		0 КАК Сумма "
	+ ?(Реквизиты.ЕстьТовары, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаТовары.СуммаНДСВзаиморасчетов,
	|		ТаблицаТовары.СуммаНДС
	|	ИЗ
	|		ТаблицаТовары КАК ТаблицаТовары", "")
	+ ?(Реквизиты.ЕстьУслуги, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаУслуги.СуммаНДСВзаиморасчетов,
	|		ТаблицаУслуги.СуммаНДС
	|	ИЗ
	|		ТаблицаУслуги КАК ТаблицаУслуги", "")  
	+ ?(Реквизиты.ЕстьОС, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаОС.СуммаНДСВзаиморасчетов,
	|		ТаблицаОС.СуммаНДС
	|	ИЗ
	|		ТаблицаОС КАК ТаблицаОС", "") 
	+ ?(Реквизиты.ЕстьНМА, "
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ
	|		ТаблицаНМА.СуммаНДСВзаиморасчетов,
	|		ТаблицаНМА.СуммаНДС
	|	ИЗ
	|		ТаблицаНМА КАК ТаблицаНМА", "") 	+ ") КАК ТаблицаСуммВзаиморасчетов
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.ВалютаДокумента КАК ВалютаДокумента,
	|	Реквизиты.ВидУчетаНУ КАК ВидУчетаНУ,
	|	""Выбытие"" КАК НаправлениеДвижения,
	|	ЛОЖЬ КАК ЭтоВозврат,
	|	&НеобходимостьОтраженияВНУ КАК НеобходимостьОтраженияВНУ,
	|	Реквизиты.КурсВзаиморасчетов КАК КурсВзаиморасчетов,
	|	Реквизиты.КратностьВзаиморасчетов КАК КратностьВзаиморасчетов
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.Ссылка КАК ДокументРасчетов,
	|	Реквизиты.СчетУчетаРасчетовСКонтрагентом КАК СчетРасчетов,
	|	Реквизиты.СчетУчетаРасчетовПоАвансам КАК СчетАвансов,
	|	Реквизиты.Контрагент,
	|	Реквизиты.ДоговорКонтрагента,
	|	Реквизиты.ВидДоговора КАК ВидДоговора,
	|	Реквизиты.ВедениеВзаиморасчетов,
	|	Реквизиты.ВалютаВзаиморасчетов КАК ВалютаВзаиморасчетов,
	|	&РасчетыВВалюте КАК РасчетыВВалюте,
	|	НЕОПРЕДЕЛЕНО КАК Сделка,
	|	ТаблицаСуммВзаиморасчетов.Сумма КАК СуммаРегл,
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов КАК СуммаВзаиморасчетов
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаСуммВзаиморасчетов КАК ТаблицаСуммВзаиморасчетов
	|		ПО (ИСТИНА)
	|ГДЕ
	|	ТаблицаСуммВзаиморасчетов.СуммаВзаиморасчетов ЕСТЬ НЕ NULL "
	+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
	Возврат ТекстЗапроса;
		
КонецФункции

Функция ТекстЗапросаУчастникиСовместнойДеятельности(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	ТекстЗапроса = "";
	
	Если Реквизиты.ЕстьУчастникиСовместнойДеятельности Тогда 
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаУчастникиСовместнойДеятельности.НомерСтроки,
		|	ТаблицаУчастникиСовместнойДеятельности.УчастникСовместнойДеятельности КАК УчастникСовместнойДеятельности,
		|	ТаблицаУчастникиСовместнойДеятельности.ДоляУчастия КАК ДоляУчастия
		|ИЗ
		|	ТаблицаУчастникиСовместнойДеятельности КАК ТаблицаУчастникиСовместнойДеятельности
		|УПОРЯДОЧИТЬ ПО
		|	НомерСтроки" + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		НомераТаблиц.Вставить("ТаблицаУчастникиСовместнойДеятельности",	НомераТаблиц.Количество());
	Иначе
		ПараметрыПроведения.Вставить("ТаблицаУчастникиСовместнойДеятельности", Неопределено);
		Возврат ТекстЗапроса;
	КонецЕсли;
	
	Возврат ТекстЗапроса;
	
КонецФункции

Процедура ВыполнитьКонтрольОтложенногоНДСКНачислению(Отказ, ТаблицаНДС, ТаблицаРеквизиты) Экспорт
	
	Реквизиты = ТаблицаРеквизиты[0];
	
	ДокументОснование = Реквизиты.ДокументОснование;
	
	СуммаНДС = ТаблицаНДС.Итог("СуммаНДС");
	
	Если СуммаНДС = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Запрос       = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	ТиповойОстатки.Организация,
	               |	ТиповойОстатки.СтруктурноеПодразделение,
	               |	ТиповойОстатки.Счет,
	               |	- ТиповойОстатки.СуммаОстаток КАК СуммаНДС
	               |ИЗ
	               |	РегистрБухгалтерии.Типовой.Остатки(
	               |			&ПериодОстатков,
	               |			Счет = &Счет,
	               |			&ВидыСубконто,
	               |			Организация = &Организация
	               |				И СтруктурноеПодразделение = &СтруктурноеПодразделение
	               |				И Субконто1 = &Контрагент
	               |				И Субконто2 = &ДокументРеализации) КАК ТиповойОстатки";
	
	Запрос.УстановитьПараметр("Счет", ПланыСчетов.Типовой.НалогНаДобавленнуюСтоимостьОтложенный);
	Запрос.УстановитьПараметр("ПериодОстатков", Новый Граница(Реквизиты.Период, ВидГраницы.Включая));
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение", Реквизиты.СтруктурноеПодразделение);
	
	ВидыСубконто = Новый Массив;
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоТиповые.Контрагенты);
	ВидыСубконто.Добавить(ПланыВидовХарактеристик.ВидыСубконтоТиповые.ДокументыРеализации);
	Запрос.УстановитьПараметр("ВидыСубконто", ВидыСубконто);
	
	Запрос.УстановитьПараметр("Контрагент", Реквизиты.Контрагент);
	Запрос.УстановитьПараметр("ДокументРеализации", ДокументОснование);
	
	Результат = Запрос.Выполнить();
	
	ВыборкаРезультата = Результат.Выбрать();
	
	ПревышениеСуммыНДС = СуммаНДС;

	Если ВыборкаРезультата.Следующий() Тогда
		
		ПревышениеСуммыНДС = ПревышениеСуммыНДС - ВыборкаРезультата.СуммаНДС;
		
	КонецЕсли;
	
	Если ПревышениеСуммыНДС > 0 Тогда
		
		Если ЗначениеЗаполнено(ДокументОснование) Тогда
			ПредставлениеОснования = ДокументОснование;
		Иначе
			ПредставлениеОснования = "<...>";
		КонецЕсли;
		
		ТекстОшибки = НСтр("ru='Сумма НДС к начислению превышает сумму отложенного налога по документу ""%1"" на %2.'");
		ТекстОшибки = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
			ТекстОшибки,
			ПредставлениеОснования,
			Формат(ПревышениеСуммыНДС, "ЧЦ=15; ЧДЦ=2; ЧРД='.'"));
			
		ОбщегоНазначения.СообщитьПользователю(ТекстОшибки);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецЕсли