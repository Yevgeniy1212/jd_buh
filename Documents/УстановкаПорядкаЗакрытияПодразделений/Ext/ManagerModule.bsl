#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	Запрос.УстановитьПараметр("Ссылка", ДокументСсылка);
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента();
	Результат = Запрос.Выполнить();
	ПараметрыПроведения.Вставить("ТаблицаРеквизитов", Результат.Выгрузить());
	
	Реквизиты = ПараметрыПроведения.ТаблицаРеквизитов[0];	
	Запрос.УстановитьПараметр("Организация", Реквизиты.Организация);
	
	НомераТаблиц = Новый Структура;
	Запрос.Текст = ТекстЗапросаТаблицаПодразделения(НомераТаблиц);
		
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;

КонецФункции 

Функция ТекстЗапросаРеквизитыДокумента()
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	НАЧАЛОПЕРИОДА(Реквизиты.Дата,МЕСЯЦ) КАК Период,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение	
	|ИЗ
	|	Документ.УстановкаПорядкаЗакрытияПодразделений КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка";

	Возврат ТекстЗапроса; 
	
КонецФункции

Функция ТекстЗапросаТаблицаПодразделения(НомераТаблиц)

	НомераТаблиц.Вставить("ТаблицаПодразделения", НомераТаблиц.Количество());

	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Подразделения.Ссылка КАК Подразделение,
	|	ВЫБОР 
	|		КОГДА ПорядокЗакрытия.НомерПередела ЕСТЬ NULL 
	|		ТОГДА 0
	|	ИНАЧЕ
	|		ПорядокЗакрытия.НомерПередела
	|	КОНЕЦ КАК НомерПередела
	|ИЗ
	|	Справочник.ПодразделенияОрганизаций КАК Подразделения
	|	
	|	ЛЕВОЕ СОЕДИНЕНИЕ (
	|		ВЫБРАТЬ
	|			УстановкаПорядкаЗакрытие.Подразделение КАК Подразделение,
	|			УстановкаПорядкаЗакрытие.НомерСтроки КАК НомерПередела
	|		ИЗ
	|			Документ.УстановкаПорядкаЗакрытияПодразделений.ПорядокЗакрытия КАК УстановкаПорядкаЗакрытие
	|		ГДЕ
	|			УстановкаПорядкаЗакрытие.Ссылка = &Ссылка
	|		) КАК ПорядокЗакрытия
	|	ПО
	|		Подразделения.Ссылка = ПорядокЗакрытия.Подразделение
	|ГДЕ
	|	НЕ Подразделения.ПометкаУдаления
	|	И Подразделения.Владелец = &Организация";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ ФОРМИРОВАНИЯ ДВИЖЕНИЙ

Процедура СформироватьДвиженияПорядокЗакрытияПодразделений(Таблица, ТаблицаРеквизитов, Движения, Отказ) Экспорт
	
	Если Таблица.Количество() = 0 Тогда
		Возврат;
	КонецЕсли;
	
	Реквизиты = ТаблицаРеквизитов[0];
	
	Для каждого СтрокаПодразделения Из Таблица Цикл

		Движение = Движения.ПорядокЗакрытияПодразделенийОрганизаций.Добавить();
		
		Движение.Период 	  			   = Реквизиты.Период;		
		Движение.Организация  			   = Реквизиты.Организация;
		Движение.СтруктурноеПодразделение  = Реквизиты.СтруктурноеПодразделение;
		Движение.Подразделение 			   = СтрокаПодразделения.Подразделение;
		Движение.НомерПередела   	  	   = СтрокаПодразделения.НомерПередела; 	
		
	КонецЦикла;
	
	Движения.ПорядокЗакрытияПодразделенийОрганизаций.Записывать = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецЕсли