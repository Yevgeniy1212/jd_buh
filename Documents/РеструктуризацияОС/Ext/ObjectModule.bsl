#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область УправлениеДоступом

// Процедура ЗаполнитьНаборыЗначенийДоступа по свойствам объекта заполняет наборы значений доступа
// в таблице с полями:
//    НомерНабора     - Число                                     (необязательно, если набор один),
//    ВидДоступа      - ПланВидовХарактеристикСсылка.ВидыДоступа, (обязательно),
//    ЗначениеДоступа - Неопределено, СправочникСсылка или др.    (обязательно),
//    Чтение          - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Добавление      - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Изменение       - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//    Удаление        - Булево (необязательно, если набор для всех прав) устанавливается для одной строки набора,
//
//  Вызывается из процедуры УправлениеДоступомСлужебный.ЗаписатьНаборыЗначенийДоступа(),
// если объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьНаборыЗначенийДоступа" и
// из таких же процедур объектов, у которых наборы значений доступа зависят от наборов этого
// объекта (в этом случае объект зарегистрирован в "ПодпискаНаСобытие.ЗаписатьЗависимыеНаборыЗначенийДоступа").
//
// Параметры:
//  Таблица      - ТабличнаяЧасть,
//                 РегистрСведенийНаборЗаписей.НаборыЗначенийДоступа,
//                 ТаблицаЗначений, возвращаемая УправлениеДоступом.ТаблицаНаборыЗначенийДоступа().
//
Процедура ЗаполнитьНаборыЗначенийДоступа(Таблица) Экспорт
	
	УчетТоваров.ЗаполнитьНаборыПоОрганизацииСтрутурномуПодразделениюСкладу(ЭтотОбъект, Таблица, "Организация", "СтруктурноеПодразделение", "Склад");
	
КонецПроцедуры

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда
		Если ДанныеЗаполнения.Свойство("Автор") Тогда
			ДанныеЗаполнения.Удалить("Автор");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;

	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, ОбщегоНазначенияБКВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ УчитыватьКПН Тогда
		ВидУчетаНУ = Справочники.ВидыУчетаНУ.ПустаяСсылка();
	КонецЕсли;
	              	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	УправлениеВнеоборотнымиАктивамиСервер.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОС", Новый Структура("ОсновноеСредство"), Отказ);
	
	ОрганизацияПлательщикНалогаНаПрибыль 			= ПроцедурыНалоговогоУчета.ПолучитьПризнакПлательщикаНалогаНаПрибыль(Организация, Дата);
	ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль 	= ОрганизацияПлательщикНалогаНаПрибыль И ПроцедурыНалоговогоУчета.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль(Организация, Дата);
	НеобходимостьОтраженияВНУ 						= ОрганизацияПлательщикНалогаНаПрибыль И УчитыватьКПН И (ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль ИЛИ ВидУчетаНУ = Справочники.ВидыУчетаНУ.НУ);

	МассивНепроверяемыхРеквизитов = Новый Массив();

	Если ОС.Количество() > 0 
		ИЛИ Товары.Количество() > 0
		ИЛИ Прочее.Количество() > 0 Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОС");
		МассивНепроверяемыхРеквизитов.Добавить("Товары");
		МассивНепроверяемыхРеквизитов.Добавить("Прочее");
		
	КонецЕсли;
	
	Если Товары.Количество() = 0 Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("Склад");
		
	КонецЕсли;
	
	
	Если ВидОперации <> Перечисления.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("СтоимостьЧастичногоСписания");

	КонецЕсли;	
	
	
	Если НЕ НеобходимостьОтраженияВНУ Тогда

		МассивНепроверяемыхРеквизитов.Добавить("ОС.СчетУчетаНУ");
		МассивНепроверяемыхРеквизитов.Добавить("ОС.СтоимостьНУ");
		МассивНепроверяемыхРеквизитов.Добавить("ВидУчетаНУ");

	КонецЕсли;
	
	
	Если НЕ УправлениеВнеоборотнымиАктивамиСервер.ВедетсяАналитическийУчетОСПоПодразделениям(Дата) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОС.ПодразделениеОрганизации");
		
	КонецЕсли;
	
	Если НЕ УправлениеВнеоборотнымиАктивамиСервер.ВедетсяАналитическийУчетОСПоМОЛ(Дата) Тогда
		
		МассивНепроверяемыхРеквизитов.Добавить("ОС.МОЛОрганизации");
		
	КонецЕсли;
		
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	

КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ОбработкаПроведения(Отказ, РежимПроведения)
	
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект, Ложь);
	
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.РеструктуризацияОС.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Реквизиты = ПараметрыПроведения.Реквизиты[0];
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	ОбщегоНазначенияБКВызовСервера.ДобавитьКолонкуВТаблицуЗначений(ПараметрыПроведения.Реквизиты, "НомерЖурнала", НСтр("ru = 'ОС'", ОбщегоНазначения.КодОсновногоЯзыка()));   	
	
	УчетОС.ПроверитьСоответствиеОСОрганизации(
			ПараметрыПроведения.ТаблицаПоОС,
			ПараметрыПроведения.Реквизиты, Отказ);
	
	УчетОС.ПроверитьВозможностьИзмененияСостоянияОС(
			ПараметрыПроведения.ТаблицаПоОС,
			ПараметрыПроведения.Реквизиты, Отказ);
	
	УчетОС.ПроверитьВозможностьЧастичногоСписанияОС(
			ПараметрыПроведения.ТаблицаПоОС,
			ПараметрыПроведения.Реквизиты, Отказ);
	
	Если Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеструктуризацияОС.ПолноеСписание ИЛИ  НЕ Реквизиты.ПрименятьПараметрыАмортизацииВТекМесяце Тогда
		
		ТаблицаАмортизацииОС = УчетОС.ПодготовитьТаблицуАмортизацияОСБухРегл(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаПоОС, Истина);
		
		ТаблицаОСРаспределениеАмортизацииПоНаправлениямРегл = УправлениеВнеоборотнымиАктивамиСервер.ПодготовитьТаблицуРаспределениеАмортизацииПоНаправлениямРегл(ТаблицаАмортизацииОС, ПараметрыПроведения.Реквизиты, Отказ);
		
		УправлениеВнеоборотнымиАктивамиСервер.СформироватьДвиженияАмортизацииПоНаправлениямРегл(ТаблицаОСРаспределениеАмортизацииПоНаправлениямРегл, ПараметрыПроведения.Реквизиты,
				Движения, Отказ);
		
	КонецЕсли;
	
	ТаблицаПараметровАмортизации = УчетОС.ПодготовитьТаблицуПараметрыАмортизацииЧастичногоСписания(ПараметрыПроведения.ТаблицаПараметрыАмортизации,ТаблицаАмортизацииОС) ;
	
	УчетОС.СформироватьДвиженияПараметрыАмортизацииОСБухгалтерскийУчет(ТаблицаПараметровАмортизации,
				Движения, Отказ);
	
	ТаблицаПоИсходномуОС   = УчетОС.ПодготовитьТаблицуПараметрыРеструктуризацииОС(ПараметрыПроведения.Реквизиты, ПараметрыПроведения.ТаблицаПоОС, ТаблицаОСРаспределениеАмортизацииПоНаправлениямРегл, ПараметрыПроведения.ТаблицаСтоимостьОСНУ);
	
	ТаблицаСписываемойСтоимостиПоНУ = УчетОС.ПодготовитьТаблицуСписываемойСтоимостиПоНУПриРеструктуризацииОС(ПараметрыПроведения.Реквизиты, ТаблицаПоИсходномуОС, ПараметрыПроведения.ТаблицаСтоимостьОСНУ);
	
	УчетОС.ПроверитьКорректностьСуммДокументаРеструктуризацияОС(ПараметрыПроведения, ТаблицаПоИсходномуОС, Отказ);
	
	ОбщегоНазначенияБКВызовСервера.ДобавитьКолонкуВТаблицуЗначений(ПараметрыПроведения.Реквизиты, "НомерЖурнала", НСтр("ru = 'ОС'", ОбщегоНазначения.КодОсновногоЯзыка()));   	

	ТаблицаОбъектыНалоговогоУчетаФА = УчетОС.ПодготовитьТаблицуОбъектовНалоговогоУчетаФА(ПараметрыПроведения.ТаблицаОбъектыНалоговогоУчетаФА,ПараметрыПроведения.Реквизиты);
	
	УправлениеВнеоборотнымиАктивамиСервер.СформироватьДвиженияОбъектыНалоговогоУчетаФА(ТаблицаОбъектыНалоговогоУчетаФА,ПараметрыПроведения.Реквизиты,
		 		Движения, Отказ);

	УправлениеВнеоборотнымиАктивамиСервер.СформироватьДвиженияФАУчитываемыхОтдельно(ПараметрыПроведения.ТаблицаФАУчитываемыеОтдельно,ПараметрыПроведения.Реквизиты, 
		 		Движения, Отказ);
	
	УчетОС.СформироватьДвиженияПоСписаниюНакопленнойАмортизации(ПараметрыПроведения.Реквизиты, ТаблицаПоИсходномуОС,
				Движения, Отказ);
				
	УчетОС.СформироватьДвиженияПоСписаниюОстаточнойСтоимости(ПараметрыПроведения.Реквизиты, ТаблицаПоИсходномуОС, ПараметрыПроведения.ТаблицаПоНовымОС, ПараметрыПроведения.ТаблицаПрочее,
				Движения, Отказ);
			
	УчетОС.СформироватьДвиженияИзменениеСостоянияОС(ПараметрыПроведения.ТаблицаСостоянияОС,ПараметрыПроведения.Реквизиты,
		 		Движения, Отказ);
		 
	УчетОС.СформироватьДвиженияРегистрацияСобытияОС(ПараметрыПроведения.ТаблицаСобытияОС,ПараметрыПроведения.Реквизиты,
		 		Движения, Отказ);
		 
	УчетОС.СформироватьДвиженияИзменениеПризнакаНачисленияАмортизацииОС(ПараметрыПроведения.ТаблицаНачислениеАмортизацииОСБухгалтерскийУчет,ПараметрыПроведения.Реквизиты,
		 		Движения, Отказ);
		 
	УчетОС.СформироватьДвиженияСоставОС(ПараметрыПроведения.ТаблицаСоставОС, ПараметрыПроведения.Реквизиты,
				Движения, Отказ);
				
	УчетОС.СформироватьДвиженияИзменениеЗемельногоНалога(ПараметрыПроведения.ТаблицаОбъектыЗемельногоНалога, ПараметрыПроведения.Реквизиты,
				Движения, Отказ);
				
	УчетОС.СформироватьДвиженияОбъектыИмущественногоНалога(ПараметрыПроведения.ТаблицаОбъектыИмущественногоНалога, ПараметрыПроведения.Реквизиты,
				Движения, Отказ);
								
	УчетОС.СформироватьДвиженияИзменениеТранспортногоНалога(ПараметрыПроведения.ТаблицаОбъектыТранспортногоНалога, ПараметрыПроведения.Реквизиты,
				Движения, Отказ);            

	УчетОС.СформироватьДвиженияРаспределенияПереоценкиОСПриРеструктуризацииОС(ПараметрыПроведения.Реквизиты, ТаблицаПоИсходномуОС, ПараметрыПроведения.ТаблицаПереоценкиВА, ПараметрыПроведения.ТаблицаПоНовымОС,
	    		Движения, Отказ); 			

	УчетОС.СформироватьДвижениеСписанияСтоимостиВыбытияПриРеструктуризацииОСНУ(ТаблицаПоИсходномуОС, ТаблицаСписываемойСтоимостиПоНУ, 
				ПараметрыПроведения.ТаблицаПоНовымОС, ПараметрыПроведения.ТаблицаПоТоварам, ПараметрыПроведения.ТаблицаПрочее,ПараметрыПроведения.РеквизитыСтоимостьОС,
				Движения, Отказ);

	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	    		

КонецПроцедуры

#КонецЕсли

 