#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

// Заполняет счета учета номенклатуры в строке табличной части документа
//
// Параметры:
//  ДанныеОбъекта         - структура данных объекта, используемых при заполнении счетов учета (вид операции,
//                          вид договора контрагента, признак комиссионной торговли и т.п.)
//  СтрокаТабличнойЧасти  - строка табличной части документа
//  ИмяТабличнойЧасти     - имя табличной части документа
//  СведенияОНоменклатуре - структура сведений о номенклатуре, либо стуктура счетов учета
//
Процедура ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(ДанныеОбъекта = Неопределено, СтрокаТабличнойЧасти, СведенияОНоменклатуре) Экспорт

	Если СведенияОНоменклатуре = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если СведенияОНоменклатуре.Свойство("СчетаУчета") Тогда
		// СведенияОНоменклатуре - структура сведений номенклатуры
		СчетаУчета = СведенияОНоменклатуре.СчетаУчета;
	ИначеЕсли СведенияОНоменклатуре.Свойство("СчетУчетаБУ") Тогда
		// СведенияОНоменклатуре - структура счетов учета номенклатуры
		СчетаУчета = СведенияОНоменклатуре;
	Иначе
		Возврат;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетУчетаБУ) Тогда
		СтрокаТабличнойЧасти.СчетУчетаБУ = СчетаУчета.СчетУчетаБУ;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(СчетаУчета.СчетУчетаНУ) Тогда
		СтрокаТабличнойЧасти.СчетУчетаНУ = СчетаУчета.СчетУчетаНУ;
	КонецЕсли;
	
КонецПроцедуры

Процедура ЗаполнитьСчетаУчетаВТабличнойЧасти(ДанныеОбъекта, Объект) Экспорт
	
	СведенияОСпискеНоменклатуры = ОбработкаТабличныхЧастей.ПолучитьСведенияОСпискеНоменклатуры(
		ОбщегоНазначения.ВыгрузитьКолонку(Объект.Товары, "Номенклатура", Истина), ДанныеОбъекта);

	Для Каждого ТекСтрока Из Объект.Товары Цикл
		
		СведенияОНоменклатуре = СведенияОСпискеНоменклатуры.Получить(ТекСтрока.Номенклатура);
		ЗаполнитьСчетаУчетаВСтрокеТабличнойЧасти(Неопределено, ТекСтрока, СведенияОНоменклатуре)
		
	КонецЦикла; 		
		
КонецПроцедуры 

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ


////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка"						  , ДокументСсылка);
	Запрос.УстановитьПараметр("СинонимОС"				      , НСтр("ru = 'ОС'", ОбщегоНазначения.КодОсновногоЯзыка()));	
	Запрос.УстановитьПараметр("ВалютаРегламентированногоУчета",	Константы.ВалютаРегламентированногоУчета.Получить());
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	Если НЕ УчетнаяПолитикаСервер.Существует(Реквизиты.Организация, Реквизиты.Период, "БУ", Истина, ДокументСсылка) 
		ИЛИ НЕ УчетнаяПолитикаСервер.Существует(Реквизиты.Организация, Реквизиты.Период, "НУ", Истина, ДокументСсылка) Тогда
		Отказ = Истина;
	КонецЕсли;
	
	Если Отказ Тогда
		Возврат ПараметрыПроведения;
	КонецЕсли;           	
	
	ОрганизацияПлательщикНалогаНаПрибыль 			= ПроцедурыНалоговогоУчета.ПолучитьПризнакПлательщикаНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль 	= ОрганизацияПлательщикНалогаНаПрибыль И ПроцедурыНалоговогоУчета.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	
	НеобходимостьОтраженияВНУ 						= ОрганизацияПлательщикНалогаНаПрибыль И Реквизиты.УчитыватьКПН И (ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль ИЛИ Реквизиты.ВидУчетаНУ = Справочники.ВидыУчетаНУ.НУ);
	ВедениеУчетаВременныхразницБалансовымМетодом    = УчетнаяПолитикаСервер.ВедетсяУчетВременныхРазницБалансовымМетодом(Реквизиты.Организация, Реквизиты.Период);
   	ПоддержкаРаботыСоСтруктурнымиПодразделениями 	= ПолучитьФункциональнуюОпцию("ПоддержкаРаботыСоСтруктурнымиПодразделениями");	
	ВедетсяАналитическийУчетОСПоМОЛ 				= УправлениеВнеоборотнымиАктивамиСервер.ВедетсяАналитическийУчетОСПоМОЛ(Реквизиты.Период); 
	ВедетсяАналитическийУчетОСПоПодразделениям 		= УправлениеВнеоборотнымиАктивамиСервер.ВедетсяАналитическийУчетОСПоПодразделениям(Реквизиты.Период);
	ПрименятьПараметрыАмортизацииВТекМесяце 		= УправлениеВнеоборотнымиАктивамиСервер.ПолучитьПризнакПримененияПараметровАмортизацииВТекМесяце(Реквизиты.Организация, Реквизиты.Период); 

	Реквизиты.Вставить("ОрганизацияПлательщикНалогаНаПрибыль", 			 ОрганизацияПлательщикНалогаНаПрибыль);
	Реквизиты.Вставить("ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль", ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль);
	Реквизиты.Вставить("НеобходимостьОтраженияВНУ", 					 НеобходимостьОтраженияВНУ);
	Реквизиты.Вставить("ПоддержкаРаботыСоСтруктурнымиПодразделениями", 	 ПоддержкаРаботыСоСтруктурнымиПодразделениями);
    Реквизиты.Вставить("ПрименятьПараметрыАмортизацииВТекМесяце", 	 	 ПрименятьПараметрыАмортизацииВТекМесяце);
                                                                 	
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(НеобходимостьОтраженияВНУ, 						"НеобходимостьОтраженияВНУ");	
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ПоддержкаРаботыСоСтруктурнымиПодразделениями, 	"ПоддержкаРаботыСоСтруктурнымиПодразделениями");	
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ПрименятьПараметрыАмортизацииВТекМесяце, 		"ПрименятьПараметрыАмортизацииВТекМесяце");	

	Запрос.УстановитьПараметр("НеобходимостьОтраженияВНУ", НеобходимостьОтраженияВНУ);
	Запрос.УстановитьПараметр("ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль", ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль);
	Запрос.УстановитьПараметр("ВедениеУчетаВременныхразницБалансовымМетодом", ВедениеУчетаВременныхразницБалансовымМетодом);	
	Запрос.УстановитьПараметр("СубконтоОС",		ПланыВидовХарактеристик.ВидыСубконтоТиповые.ОсновныеСредства);
	
	
	//Запрос.УстановитьПараметр("Организация",   Реквизиты.Организация);
	Запрос.УстановитьПараметр("Период", Реквизиты.Период);
	Запрос.УстановитьПараметр("СнятоСУчета",   Перечисления.ВидыСостоянийОС.СнятоСУчета);
	Запрос.УстановитьПараметр("ПринятыйКУчету", Перечисления.ВидыСостоянийФА.ПринятКУчету);
	
	СостоянияФА = Новый СписокЗначений();	
	СостоянияФА.Добавить(Перечисления.ВидыСостоянийФА.ПринятКУчету);
	СостоянияФА.Добавить(Перечисления.ВидыСостоянийФА.Выбыл);	
	Запрос.УстановитьПараметр("СостоянияФА", СостоянияФА);		

	НомераТаблиц = Новый Структура;
	Запрос.Текст =   ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты)
					+ ТекстЗапросаИсходноеОС(НомераТаблиц,  Реквизиты)
					+ ТекстЗапросаТаблицаПоОС(НомераТаблиц,ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаТаблицаПоПрочее(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаТаблицаПоТоварам(НомераТаблиц, ПараметрыПроведения, Реквизиты)
					+ УчетОС.ТекстЗапросаРаспределениеАмортизацииПоНаправлениямРегл(НомераТаблиц)
					+ ТекстЗапросаСостоянияОС(НомераТаблиц,ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаСобытияОС(НомераТаблиц, Реквизиты)
					+ ТекстЗапросаСоставОС(НомераТаблиц,ПараметрыПроведения, Реквизиты)					
					+ ТекстЗапросаНачислениеАмортизацииОСБухгалтерскийУчет(НомераТаблиц,ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаПоМестнымНалогам(НомераТаблиц,Реквизиты) 
					+ ТекстЗапросаПараметрыАмортизацииОС(НомераТаблиц,ПараметрыПроведения, Реквизиты) 
					+ ТекстЗапросаТаблицыПереоценкиОС(НомераТаблиц, Реквизиты)
					+ ТекстЗапросаОбъектыНалоговогоУчетаФА(НомераТаблиц,ПараметрыПроведения, Реквизиты)
					+ ТекстЗапросаФАУчитываемыеОтдельно(НомераТаблиц, ПараметрыПроведения, Реквизиты) 
					+ ТекстЗапросаСписаниеСтоимостиОСНУ(НомераТаблиц, ПараметрыПроведения, Реквизиты) ;
	         	
	Результат = Запрос.ВыполнитьПакет();
	
	Для Каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	
	// подготовим таблицу по исходному ОС
	//дополним необходимыми данными
	
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ВедетсяАналитическийУчетОСПоМОЛ				   , "ВедетсяАналитическийУчетОСПоМОЛ");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ВедетсяАналитическийУчетОСПоПодразделениям	   , "ВедетсяАналитическийУчетОСПоПодразделениям");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(НеобходимостьОтраженияВНУ					   , "НеобходимостьОтраженияВНУ");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ВедениеУчетаВременныхРазницБалансовымМетодом   , "ВедениеУчетаВременныхРазницБалансовымМетодом");

	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)
	
	НомераТаблиц.Вставить("СоставДокумента", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты", НомераТаблиц.Количество());
	
	ТекстЗапроса = 
	"ВЫБРАТЬ
	|	МАКСИМУМ(СоставДокумента.ЕстьОС) КАК ЕстьОС,
	|	МАКСИМУМ(СоставДокумента.ЕстьТовары) КАК ЕстьТовары,
	|	МАКСИМУМ(СоставДокумента.ЕстьПрочее) КАК ЕстьПрочее
	|ПОМЕСТИТЬ СоставДокумента
	|ИЗ
	|	(ВЫБРАТЬ ПЕРВЫЕ 1
	|		ИСТИНА КАК ЕстьОС,
	|		ЛОЖЬ КАК ЕстьТовары,
	|		ЛОЖЬ КАК ЕстьПрочее
	|	ИЗ
	|		Документ.РеструктуризацияОС.ОС КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ЛОЖЬ,
	|		ИСТИНА,
	|		ЛОЖЬ
	|	ИЗ
	|		Документ.РеструктуризацияОС.Товары КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка
	|	
	|	ОБЪЕДИНИТЬ ВСЕ
	|	
	|	ВЫБРАТЬ ПЕРВЫЕ 1
	|		ЛОЖЬ,
	|		ЛОЖЬ,
	|		ИСТИНА
	|	ИЗ
	|		Документ.РеструктуризацияОС.Прочее КАК ТаблицаДокумента
	|	ГДЕ
	|		ТаблицаДокумента.Ссылка = &Ссылка) КАК СоставДокумента
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	""РеструктуризацияОС"" КАК ВидДокумента,
	|	&СинонимОС КАК ИмяСписка,
	|	Реквизиты.Ссылка КАК Ссылка,
	|	Реквизиты.Дата КАК Дата,
	|	&ВалютаРегламентированногоУчета КАК ВалютаРегламентированногоУчета,
	|	Реквизиты.Организация,
	|	Реквизиты.СобытиеОС КАК СобытиеОС,
	|	Реквизиты.ВидОперации,
	|	Реквизиты.ИсходноеОсновноеСредство,
	|	Реквизиты.КоэффициентАмортизацииБУ,
	|	Реквизиты.КоэффициентУскоренияБУ,
	|	Реквизиты.КоэффициентЧастичногоСписания,
	|	Реквизиты.ЛиквидационнаяСтоимостьЧастичногоСписания,
	|	Реквизиты.ОбъемПродукцииРаботБУ,
	|	Реквизиты.Склад,
	|	Реквизиты.СнятьСУчетаПоНалогам,
	|	Реквизиты.СрокПолезногоИспользованияБУ,
	|	Реквизиты.СтоимостьЧастичногоСписания,
	|	Реквизиты.СтоимостьЧастичногоСписанияНУ,
	|	Реквизиты.АмортизацияЧастичногоСписания,
	|	Реквизиты.ФактОбъемПродукцииРаботБУ,
	|	Реквизиты.ФактСрокИспользованияБУ,
	|	Реквизиты.ВидУчетаНУ,
	|	Реквизиты.СтруктурноеПодразделение,
	|	Реквизиты.СтруктурноеПодразделение КАК КорСтруктурноеПодразделение,
	|	Реквизиты.УчитыватьКПН,
	|	ЛОЖЬ КАК НеобходимостьОтраженияВНУ,
	|	ЛОЖЬ КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
	|	ЛОЖЬ КАК ПрименятьПараметрыАмортизацииВТекМесяце,
	|	ЛОЖЬ КАК ПоддержкаРаботыСоСтруктурнымиПодразделениями,
	|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета) КАК Состояние,
	|	ЛОЖЬ КАК ВедениеУчетаВременныхРазницБалансовымМетодом,
	|	ЛОЖЬ КАК ВедетсяАналитическийУчетОСПоПодразделениям,
	|	ЛОЖЬ КАК ВедетсяАналитическийУчетОСПоМОЛ,
	|	НЕОПРЕДЕЛЕНО КАК ВидРегламентнойОперации,
	|	"""" КАК Содержание,
	|	Реквизиты.Номер
	|ПОМЕСТИТЬ Реквизиты
	|ИЗ
	|	Документ.РеструктуризацияОС КАК Реквизиты
	|ГДЕ
	|	Реквизиты.Ссылка = &Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	Реквизиты.ВидДокумента,
	|	Реквизиты.ИмяСписка,
	|	Реквизиты.Ссылка КАК Регистратор,
	|	Реквизиты.Ссылка КАК Объект,
	|	Реквизиты.Дата КАК Период,
	|	Реквизиты.ВалютаРегламентированногоУчета,
	|	Реквизиты.Организация,
	|	Реквизиты.Состояние,
	|	Реквизиты.СобытиеОС,
	|	Реквизиты.ВидОперации,
	|	Реквизиты.ИсходноеОсновноеСредство,
	|	Реквизиты.КоэффициентАмортизацииБУ,
	|	Реквизиты.КоэффициентУскоренияБУ,
	|	Реквизиты.АмортизацияЧастичногоСписания,
	|	Реквизиты.ЛиквидационнаяСтоимостьЧастичногоСписания,
	|	Реквизиты.Склад,
	|	Реквизиты.СнятьСУчетаПоНалогам,
	|	Реквизиты.СрокПолезногоИспользованияБУ,
	|	Реквизиты.СтоимостьЧастичногоСписания,
	|	Реквизиты.СтоимостьЧастичногоСписанияНУ,
	|	Реквизиты.ФактОбъемПродукцииРаботБУ,
	|	Реквизиты.ФактСрокИспользованияБУ,
	|	Реквизиты.ВидУчетаНУ,
	|	Реквизиты.СтруктурноеПодразделение,
	|	Реквизиты.УчитыватьКПН,
	|	Реквизиты.НеобходимостьОтраженияВНУ,
	|	Реквизиты.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
	|	Реквизиты.ПрименятьПараметрыАмортизацииВТекМесяце,
	|	Реквизиты.Номер,
	|	Реквизиты.ПоддержкаРаботыСоСтруктурнымиПодразделениями,
	|	Реквизиты.ВедениеУчетаВременныхРазницБалансовымМетодом,
	|	Реквизиты.ВедетсяАналитическийУчетОСПоПодразделениям,
	|	Реквизиты.ВедетсяАналитическийУчетОСПоМОЛ,
	|	Реквизиты.ВидРегламентнойОперации,
	|	Реквизиты.Содержание,
	|	СоставДокумента.ЕстьОС,
	|	СоставДокумента.ЕстьТовары,
	|	СоставДокумента.ЕстьПрочее
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ЛЕВОЕ СОЕДИНЕНИЕ СоставДокумента КАК СоставДокумента
	|		ПО (ИСТИНА)";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц, Реквизиты)
	
	ТекстЗапроса = "";
	Если Реквизиты.ЕстьОС Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаОС", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса + 
		"ВЫБРАТЬ
		|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
		|	ТаблицаОС.Ссылка КАК Ссылка,
		|	ТаблицаОС.ОсновноеСредство,
		|	ТаблицаОС.ОсновноеСредство.Код КАК Код,
		|	ТаблицаОС.СтоимостьБУ,
		|	ТаблицаОС.СтоимостьНУ,
		|	ТаблицаОС.СчетУчетаБУ,
		|	ТаблицаОС.СчетУчетаНУ,
		|	ТаблицаОС.ПодразделениеОрганизации,
		|	ТаблицаОС.МОЛОрганизации
		|ПОМЕСТИТЬ ТаблицаПоНовымОС
		|ИЗ
		|	Документ.РеструктуризацияОС.ОС КАК ТаблицаОС
		|ГДЕ
		|	ТаблицаОС.Ссылка = &Ссылка"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();		
	КонецЕсли;
	
	Если Реквизиты.ЕстьТовары Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаТовары", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Количество,
		|	ТаблицаТовары.Цена,
		|	ТаблицаТовары.Сумма,
		|	ТаблицаТовары.СуммаНУ,
		|	ТаблицаТовары.СчетУчетаНУ
		|ПОМЕСТИТЬ ТаблицаТовары
		|ИЗ
		|	Документ.РеструктуризацияОС.Товары КАК ТаблицаТовары
		|ГДЕ
		|	ТаблицаТовары.Ссылка = &Ссылка"  
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	
	Если Реквизиты.ЕстьПрочее Тогда 
		НомераТаблиц.Вставить("ВременнаяТаблицаПрочее", НомераТаблиц.Количество());
		
		ТекстЗапроса = ТекстЗапроса +
		"ВЫБРАТЬ
		|	ТаблицаПрочее.НомерСтроки КАК НомерСтроки,
		|	ТаблицаПрочее.Ссылка КАК Ссылка,
		|	ТаблицаПрочее.Сумма,
		|	ТаблицаПрочее.СуммаНУ,
		|	ТаблицаПрочее.СчетСписанияБУ,
		|	ТаблицаПрочее.СчетСписанияНУ,
		|	ТаблицаПрочее.СубконтоСписанияБУ1,
		|	ТаблицаПрочее.СубконтоСписанияБУ2,
		|	ТаблицаПрочее.СубконтоСписанияБУ3,
		|	ТаблицаПрочее.СубконтоСписанияНУ1,
		|	ТаблицаПрочее.СубконтоСписанияНУ2,
		|	ТаблицаПрочее.СубконтоСписанияНУ3
		|ПОМЕСТИТЬ ТаблицаПрочее
		|ИЗ
		|	Документ.РеструктуризацияОС.Прочее КАК ТаблицаПрочее
		|ГДЕ
		|	ТаблицаПрочее.Ссылка = &Ссылка"  
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;

	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаИсходноеОС(НомераТаблиц, Реквизиты)
	
	НомераТаблиц.Вставить("ВременнаяТаблицаПоОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ТаблицаПоОС", НомераТаблиц.Количество());
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Реквизиты.ИсходноеОсновноеСредство КАК ОсновноеСредство,
	               |	""0"" КАК НомерСтроки,
	               |	Реквизиты.ИсходноеОсновноеСредство.Код КАК КодОС,
	               |	Реквизиты.Ссылка,
	               |	Реквизиты.КоэффициентАмортизацииБУ,
	               |	Реквизиты.КоэффициентУскоренияБУ,
	               |	Реквизиты.ОбъемПродукцииРаботБУ,
	               |	Реквизиты.СрокПолезногоИспользованияБУ,
	               |	Реквизиты.ФактОбъемПродукцииРаботБУ,
	               |	Реквизиты.ФактСрокИспользованияБУ,
	               |	Реквизиты.СтоимостьЧастичногоСписания,
	               |	СчетаУчетаОССрезПоследних.СчетУчетаБУ КАК СчетУчетаБУ,
	               |	СчетаУчетаОССрезПоследних.СчетНачисленияАмортизацииБУ КАК СчетНачисленияАмортизацииБУ,
	               |	СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних.СпособОтраженияРасходовПоАмортизации КАК Направление_БУ,
	               |	ВЫБОР
	               |		КОГДА ОС_СнятыеСУчета.ОсновноеСредство ЕСТЬ NULL 
	               |			ТОГДА ЛОЖЬ
	               |		ИНАЧЕ ИСТИНА
	               |	КОНЕЦ КАК СнятоСУчета,
	               |	ЕСТЬNULL(ОбъектыИмущественногоНалогаСрезПоследних.НачислятьНалог, ЛОЖЬ) КАК ОбъектИмущественногоНалога,
	               |	ОбъектыИмущественногоНалогаСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаИмущественногоНалога,
	               |	ЕСТЬNULL(ОбъектыТранспортногоНалогаСрезПоследних.НачислятьНалог, ЛОЖЬ) КАК ОбъектТранспортногоНалога,
	               |	ОбъектыТранспортногоНалогаСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаТранспортногоНалога,
	               |	ЕСТЬNULL(ОбъектыЗемельногоНалогаСрезПоследних.НачислятьНалог, ЛОЖЬ) КАК ОбъектЗемельногоНалога,
	               |	ОбъектыЗемельногоНалогаСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаЗемельногоНалога,
	               |	МестонахождениеОС.Местонахождение КАК ПодразделениеОрганизации,
	               |	МестонахождениеОС.МОЛ КАК МОЛОрганизации,
	               |	ЕСТЬNULL(СтоимостьОС_БУ.СуммаОстатокДт, 0) КАК СтоимостьБУ,
	               |	ВЫБОР
	               |		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание)
	               |			ТОГДА Реквизиты.АмортизацияЧастичногоСписания
	               |		ИНАЧЕ ЕСТЬNULL(АмортизацияОС_БУ.СуммаОстатокКт, 0)
	               |	КОНЕЦ КАК АмортизацияБУ,
	               |	ЕСТЬNULL(АмортизацияОС_БУ.СуммаОстатокКт, 0) КАК НакопленнаяАмортизацияБУ,
	               |	ВЫБОР
	               |		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание)
	               |			ТОГДА Реквизиты.АмортизацияЧастичногоСписания
	               |		ИНАЧЕ ЕСТЬNULL(АмортизацияОС_БУ.СуммаОстатокКт, 0)
	               |	КОНЕЦ КАК СуммаАмортизации,
	               |	0 КАК АмортизацияЗаМесяцБУ,
	               |	ВЫБОР
	               |		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеструктуризацияОС.ПолноеСписание)
	               |			ТОГДА ЕСТЬNULL(СтоимостьОС_БУ.СуммаОстатокДт, 0) - ЕСТЬNULL(АмортизацияОС_БУ.СуммаОстатокКт, 0)
	               |		ИНАЧЕ Реквизиты.СтоимостьЧастичногоСписания
	               |	КОНЕЦ КАК СписываемаяСтоимостьБУ,
	               |	ВЫБОР
	               |		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеструктуризацияОС.ПолноеСписание)
	               |			ТОГДА 1
	               |		ИНАЧЕ ВЫБОР
	               |				КОГДА ЕСТЬNULL(СтоимостьОС_БУ.СуммаОстатокДт, 0) = 0
	               |					ТОГДА 0
	               |				ИНАЧЕ (Реквизиты.СтоимостьЧастичногоСписания + Реквизиты.АмортизацияЧастичногоСписания) / ЕСТЬNULL(СтоимостьОС_БУ.СуммаОстатокДт, 0)
	               |			КОНЕЦ
	               |	КОНЕЦ КАК КоэффициентПереоценки,
	               |	ПараметрыАмортизацииОС.ЛиквидационнаяСтоимость КАК ЛиквидационнаяСтоимостьБУ,
	               |	Реквизиты.ЛиквидационнаяСтоимостьЧастичногоСписания КАК ЛиквидационнаяСтоимостьЧастичногоСписания,
	               |	Реквизиты.СнятьСУчетаПоНалогам,
	               |	Реквизиты.СтоимостьЧастичногоСписанияНУ,
	               |	Реквизиты.СобытиеОС КАК СобытиеОС,
	               |	ВЫБОР
	               |		КОГДА Реквизиты.ВидОперации = ЗНАЧЕНИЕ(Перечисление.ВидыОперацийРеструктуризацияОС.ПолноеСписание)
	               |			ТОГДА ЕСТЬNULL(СтоимостьОС_НУ.СуммаОстатокДт, 0)
	               |		ИНАЧЕ Реквизиты.СтоимостьЧастичногоСписанияНУ
	               |	КОНЕЦ КАК СтоимостьНУ,
	               |	СтоимостьОС_НУ.ВидУчета КАК ВидУчетаНУ,
	               |	СчетаУчетаОССрезПоследних.СчетУчетаНУ,
	               |	ЛОЖЬ КАК ПризнакФА
	               |ПОМЕСТИТЬ ТаблицаОС
	               |ИЗ
	               |	Реквизиты КАК Реквизиты
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПервоначальныеСведенияОСБухгалтерскийУчет.СрезПоследних(
	               |				&Период,
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
	               |					И ОсновноеСредство В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)) КАК ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = ПервоначальныеСведенияОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаОС.СрезПоследних(
	               |				&Период,
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
	               |					И ОсновноеСредство В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)) КАК СчетаУчетаОССрезПоследних
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = СчетаУчетаОССрезПоследних.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	               |				&Период,
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
	               |					И ОсновноеСредство В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)) КАК СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = СпособыОтраженияРасходовПоАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ (ВЫБРАТЬ РАЗЛИЧНЫЕ
	               |			СостоянияОСОрганизацииСрезПоследних.ОсновноеСредство КАК ОсновноеСредство
	               |		ИЗ
	               |			РегистрСведений.СостоянияОСОрганизаций КАК СостоянияОСОрганизацииСрезПоследних
	               |		ГДЕ
	               |			СостоянияОСОрганизацииСрезПоследних.ДатаСостояния < &Период
	               |			И СостоянияОСОрганизацииСрезПоследних.Организация В
	               |					(ВЫБРАТЬ
	               |						Реквизиты.Организация
	               |					ИЗ
	               |						Реквизиты)
	               |			И СостоянияОСОрганизацииСрезПоследних.ОсновноеСредство В
	               |					(ВЫБРАТЬ
	               |						Реквизиты.ИсходноеОсновноеСредство
	               |					ИЗ
	               |						Реквизиты)
	               |			И СостоянияОСОрганизацииСрезПоследних.Состояние = &СнятоСУчета) КАК ОС_СнятыеСУчета
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = ОС_СнятыеСУчета.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыИмущественногоНалога.СрезПоследних(
	               |				&Период,
	               |				ОбъектНалогообложения В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)
	               |					И Налогоплательщик В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)) КАК ОбъектыИмущественногоНалогаСрезПоследних
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = ОбъектыИмущественногоНалогаСрезПоследних.ОбъектНалогообложения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыЗемельногоНалога.СрезПоследних(
	               |				&Период,
	               |				ОбъектНалогообложения В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)
	               |					И Налогоплательщик В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)) КАК ОбъектыЗемельногоНалогаСрезПоследних
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = ОбъектыЗемельногоНалогаСрезПоследних.ОбъектНалогообложения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ОбъектыТранспортногоНалога.СрезПоследних(
	               |				&Период,
	               |				ОбъектНалогообложения В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)
	               |					И Налогоплательщик В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)) КАК ОбъектыТранспортногоНалогаСрезПоследних
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = ОбъектыТранспортногоНалогаСрезПоследних.ОбъектНалогообложения
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.МестонахождениеОСБухгалтерскийУчет.СрезПоследних(
	               |				&Период,
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
	               |					И ОсновноеСредство В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)) КАК МестонахождениеОС
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = МестонахождениеОС.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Остатки(
	               |				&Период,
	               |				,
	               |				&СубконтоОС,
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
	               |					И СтруктурноеПодразделение В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.СтруктурноеПодразделение
	               |						ИЗ
	               |							Реквизиты)
	               |					И Субконто1 В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)) КАК СтоимостьОС_БУ
	               |		ПО (СчетаУчетаОССрезПоследних.СчетУчетаБУ = СтоимостьОС_БУ.Счет)
	               |			И Реквизиты.ИсходноеОсновноеСредство = СтоимостьОС_БУ.Субконто1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Остатки(
	               |				&Период,
	               |				,
	               |				&СубконтоОС,
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
	               |					И СтруктурноеПодразделение В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.СтруктурноеПодразделение
	               |						ИЗ
	               |							Реквизиты)
	               |					И Субконто1 В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)) КАК АмортизацияОС_БУ
	               |		ПО (СчетаУчетаОССрезПоследних.СчетНачисленияАмортизацииБУ = АмортизацияОС_БУ.Счет)
	               |			И Реквизиты.ИсходноеОсновноеСредство = АмортизацияОС_БУ.Субконто1
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	               |				&Период,
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
	               |					И ОсновноеСредство В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.ИсходноеОсновноеСредство
	               |						ИЗ
	               |							Реквизиты)) КАК ПараметрыАмортизацииОС
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = ПараметрыАмортизацииОС.ОсновноеСредство
	               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Налоговый.Остатки(
	               |				&Период,
	               |				Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Налоговый.ВнеоборотныеАктивы)),
	               |				ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.ВнеоборотныеАктивы),
	               |				Организация В
	               |						(ВЫБРАТЬ
	               |							Реквизиты.Организация
	               |						ИЗ
	               |							Реквизиты)
				   //|					И СтруктурноеПодразделение В
				   //|						(ВЫБРАТЬ
				   //|							Реквизиты.СтруктурноеПодразделение
				   //|						ИЗ
				   //|							Реквизиты)
	               |					И Субконто1 В
	               |						(ВЫБРАТЬ
	               |							ВЫРАЗИТЬ(Реквизиты.ИсходноеОсновноеСредство КАК Справочник.ОсновныеСредства)
	               |						ИЗ
	               |							Реквизиты)
	               |					И ВидУчета = ЗНАЧЕНИЕ(Справочник.ВидыУчетаНУ.НУ)) КАК СтоимостьОС_НУ
	               |		ПО Реквизиты.ИсходноеОсновноеСредство = СтоимостьОС_НУ.Субконто1
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
	               |	ТаблицаОС.ОсновноеСредство,
	               |	ТаблицаОС.НомерСтроки,
	               |	ТаблицаОС.КодОС,
	               |	ТаблицаОС.КоэффициентАмортизацииБУ,
	               |	ТаблицаОС.КоэффициентУскоренияБУ,
	               |	ТаблицаОС.ОбъемПродукцииРаботБУ,
	               |	ТаблицаОС.СрокПолезногоИспользованияБУ,
	               |	ТаблицаОС.ФактОбъемПродукцииРаботБУ,
	               |	ТаблицаОС.ФактСрокИспользованияБУ,
	               |	ТаблицаОС.СчетУчетаБУ,
	               |	ТаблицаОС.СчетНачисленияАмортизацииБУ,
	               |	ТаблицаОС.Направление_БУ,
	               |	ТаблицаОС.СнятоСУчета,
	               |	ТаблицаОС.ОбъектИмущественногоНалога,
	               |	ТаблицаОС.СтруктурнаяЕдиницаИмущественногоНалога,
	               |	ТаблицаОС.ОбъектТранспортногоНалога,
	               |	ТаблицаОС.СтруктурнаяЕдиницаТранспортногоНалога,
	               |	ТаблицаОС.ОбъектЗемельногоНалога,
	               |	ТаблицаОС.СтруктурнаяЕдиницаЗемельногоНалога,
	               |	ТаблицаОС.ПодразделениеОрганизации,
	               |	ТаблицаОС.МОЛОрганизации,
	               |	ТаблицаОС.СтоимостьБУ,
	               |	ТаблицаОС.АмортизацияБУ,
				   |	ТаблицаОС.НакопленнаяАмортизацияБУ,
	               |	ТаблицаОС.СуммаАмортизации,
	               |	ТаблицаОС.АмортизацияЗаМесяцБУ,
	               |	ТаблицаОС.ЛиквидационнаяСтоимостьБУ,
	               |	ТаблицаОС.ЛиквидационнаяСтоимостьЧастичногоСписания,
	               |	ТаблицаОС.СнятьСУчетаПоНалогам,
	               |	ТаблицаОС.СтоимостьЧастичногоСписания,
	               |	ТаблицаОС.СтоимостьЧастичногоСписанияНУ,
	               |	ТаблицаОС.СтоимостьНУ,
	               |	ТаблицаОС.ВидУчетаНУ,
	               |	ТаблицаОС.СчетУчетаНУ,
	               |	ТаблицаОС.СписываемаяСтоимостьБУ,
	               |	ТаблицаОС.СобытиеОС КАК СобытиеОС,
	               |	ТаблицаОС.КоэффициентПереоценки КАК КоэффициентПереоценки,
	               |	ТаблицаОС.ПризнакФА КАК ПризнакФА
	               |ИЗ
	               |	ТаблицаОС КАК ТаблицаОС";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаТаблицаПоОС(НомераТаблиц,  ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьОС Тогда
		ПараметрыПроведения.Вставить("ТаблицаПоНовымОС", Неопределено);
		Возврат "";
	Иначе
		НомераТаблиц.Вставить("ТаблицаПоНовымОС", НомераТаблиц.Количество());
		ТекстЗапроса = "
		|ВЫБРАТЬ 
		|	ТаблицаПоНовымОС.НомерСтроки КАК НомерСтроки,
		|	ТаблицаПоНовымОС.Ссылка КАК Ссылка,
		|	ТаблицаПоНовымОС.ОсновноеСредство,
		|	ТаблицаПоНовымОС.ОсновноеСредство.Код КАК Код,
		|	ТаблицаПоНовымОС.СтоимостьБУ,
		|	ТаблицаПоНовымОС.СтоимостьНУ,
		|	ТаблицаПоНовымОС.СчетУчетаБУ,
		|	ТаблицаПоНовымОС.СчетУчетаНУ,
		|	ТаблицаПоНовымОС.ПодразделениеОрганизации,
		|	ТаблицаПоНовымОС.МОЛОрганизации,
		|	0 КАК СуммаПереоценкиСтоимости,
		|	0 КАК СуммаПереоценкиАмортизации
		|ИЗ
		|	 ТаблицаПоНовымОС";
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаТаблицаПоПрочее(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьПрочее Тогда
		
		ПараметрыПроведения.Вставить("ТаблицаПрочее", Неопределено);
		Возврат "";
	Иначе
		НомераТаблиц.Вставить("ТаблицаПрочее", НомераТаблиц.Количество());
		ТекстЗапроса = 	"ВЫБРАТЬ
		|	ТаблицаПрочее.НомерСтроки КАК НомерСтроки,
		|	ТаблицаПрочее.Ссылка КАК Ссылка,
		|	ТаблицаПрочее.Сумма,
		|	ТаблицаПрочее.СуммаНУ,
		|	ТаблицаПрочее.СчетСписанияБУ,
		|	ТаблицаПрочее.СчетСписанияНУ,
		|	ТаблицаПрочее.СубконтоСписанияБУ1,
		|	ТаблицаПрочее.СубконтоСписанияБУ2,
		|	ТаблицаПрочее.СубконтоСписанияБУ3,
		|	ТаблицаПрочее.СубконтоСписанияНУ1,
		|	ТаблицаПрочее.СубконтоСписанияНУ2,
		|	ТаблицаПрочее.СубконтоСписанияНУ3
		|ИЗ
		|	ТаблицаПрочее ";
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаТаблицаПоТоварам(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	Если Не Реквизиты.ЕстьТовары Тогда
		
		ПараметрыПроведения.Вставить("ТаблицаПоТоварам", Неопределено);
		Возврат "";
	Иначе
		
		НомераТаблиц.Вставить("ТаблицаПоТоварам", НомераТаблиц.Количество());
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ТаблицаТовары.НомерСтроки КАК НомерСтроки,
		|	ТаблицаТовары.Ссылка КАК Ссылка,
		|	ТаблицаТовары.Номенклатура,
		|	ТаблицаТовары.Количество,
		|	ТаблицаТовары.СчетУчетаНУ,
		|	ТаблицаТовары.Цена,
		|	ТаблицаТовары.Сумма,
		|	ТаблицаТовары.СуммаНУ
		|ИЗ
		|	 ТаблицаТовары ";
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаСостоянияОС(НомераТаблиц,ПараметрыПроведения, Реквизиты)
	
	Если  Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание Тогда
		ПараметрыПроведения.Вставить("ТаблицаСостоянияОС", Неопределено);
		Возврат "";
	Иначе
		
		НомераТаблиц.Вставить("ТаблицаСостоянияОС", НомераТаблиц.Количество());
		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийОС.СнятоСУчета) КАК СостояниеОС
		|ИЗ
		|	ТаблицаОС КАК ТаблицаОС" ;
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаСобытияОС(НомераТаблиц, Реквизиты)
	
	НомераТаблиц.Вставить("ТаблицаСобытияОС", НомераТаблиц.Количество());
	
	ТекстЗапроса = "
	|ВЫБРАТЬ
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,	
	|	ТаблицаОС.СобытиеОС  КАК Событие,
	|	""0"" КАК СуммаЗатратБУ
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС" ;
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаСоставОС(НомераТаблиц,ПараметрыПроведения, Реквизиты)
	
	Если  Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание Тогда
		ПараметрыПроведения.Вставить("ТаблицаСоставОС", Неопределено);
		Возврат "";
	Иначе      
		
		НомераТаблиц.Вставить("ТаблицаСоставОС", НомераТаблиц.Количество());
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	СоставОС.ОсновноеСредство,
		|	СоставОС.ВСоставеОС,
		|	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийВСоставеОС.ИсключеноИзСостава) КАК СостояниеВСоставеОС				   
		|ИЗ
		|	РегистрСведений.СоставОС.СрезПоследних(
		|		&Период,
		|		ОсновноеСредство В (ВЫБРАТЬ ОсновноеСредство Из ТаблицаОС)
		|			И СостояниеВСоставеОС = Значение(Перечисление.ВидыСостоянийВСоставеОС.ВключеноВСостав)) КАК СоставОС";
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	КонецЕсли;
	
КонецФункции

Функция ТекстЗапросаНачислениеАмортизацииОСБухгалтерскийУчет(НомераТаблиц,ПараметрыПроведения, Реквизиты)
	
	//ТекстЗапроса = "";
	Если  Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание Тогда
		ПараметрыПроведения.Вставить("ТаблицаНачислениеАмортизацииОСБухгалтерскийУчет", Неопределено); 	
	Иначе      
		НомераТаблиц.Вставить("ТаблицаНачислениеАмортизацииОСБухгалтерскийУчет", НомераТаблиц.Количество());
		  		
		ТекстЗапроса = "
		|ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство,
		|	ЛОЖЬ КАК НачислятьАмортизацию
		|ИЗ
		|	ТаблицаОС КАК ТаблицаОС
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаОС.НомерСтроки"
		 + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	 КонецЕсли;
	 
	Возврат ТекстЗапроса;	
	
КонецФункции

Функция ТекстЗапросаПараметрыАмортизацииОС(НомераТаблиц,ПараметрыПроведения, Реквизиты)
	
	Если  НЕ Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание Тогда
		ПараметрыПроведения.Вставить("ТаблицаПараметрыАмортизации", Неопределено); 	
	Иначе      
		НомераТаблиц.Вставить("ТаблицаПараметрыАмортизации", НомераТаблиц.Количество());
		
		ТекстЗапроса =
		"ВЫБРАТЬ
		|	""ОС"" КАК ИмяСписка,
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.Номер КАК Номер,
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Организация КАК Организация,
		|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
		|	""0"" КАК НомерСтроки,
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	ТаблицаОС.СрокПолезногоИспользованияБУ КАК СрокПолезногоИспользования,
		|	ЕСТЬNULL(ТаблицаОС.СрокПолезногоИспользованияБУ, 0) - ЕСТЬNULL(ТаблицаОС.ФактСрокИспользованияБУ, 0) КАК СрокИспользованияДляВычисленияАмортизации,
		|	ТаблицаОС.ОбъемПродукцииРаботБУ КАК  ОбъемПродукцииРабот,
		|	ТаблицаОС.ОбъемПродукцииРаботБУ - ТаблицаОС.ФактОбъемПродукцииРаботБУ КАК ОбъемПродукцииРаботДляВычисленияАмортизации ,
		|	ТаблицаОС.КоэффициентАмортизацииБУ КАК КоэффициентАмортизации,
		|	ТаблицаОС.КоэффициентУскоренияБУ КАК КоэффициентУскорения,
		|	ТаблицаОС.ЛиквидационнаяСтоимостьБУ - ТаблицаОС.ЛиквидационнаяСтоимостьЧастичногоСписания КАК ЛиквидационнаяСтоимость,
		|	ТаблицаОС.СтоимостьБУ - ТаблицаОС.НакопленнаяАмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ - ТаблицаОС.СтоимостьЧастичногоСписания 
		|	- (ТаблицаОС.ЛиквидационнаяСтоимостьБУ - ТаблицаОС.ЛиквидационнаяСтоимостьЧастичногоСписания) КАК СтоимостьДляВычисленияАмортизации 
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
		|		ПО (ИСТИНА)";
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

	КонецЕсли;
	
	
КонецФункции
			   
Функция ТекстЗапросаОбъектыНалоговогоУчетаФА(НомераТаблиц, ПараметрыПроведения, Реквизиты)
	
	ТекстЗапроса = "";
	
	Если  НЕ Реквизиты.НеобходимостьОтраженияВНУ  Тогда
		//ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание 
		//ИЛИ НЕ Реквизиты.СнятьСУчетаПоНалогам Тогда		
		ПараметрыПроведения.Вставить("ТаблицаОбъектыНалоговогоУчетаФА", Неопределено);	
		ПараметрыПроведения.Вставить("ОбъектыНалоговогоУчетаФА", Неопределено);	
	Иначе
		НомераТаблиц.Вставить("ОбъектыНалоговогоУчетаФА", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ТаблицаОбъектыНалоговогоУчетаФА", НомераТаблиц.Количество());

		ТекстЗапроса = "ВЫБРАТЬ
		               |	ВЫРАЗИТЬ(ОбъектыНалоговогоУчетаФА.ФиксированныйАктив КАК Справочник.ОсновныеСредства) КАК ОсновноеСредство,
		               |	ОбъектыНалоговогоУчетаФА.ГруппаНУ,
		               |	ОбъектыНалоговогоУчетаФА.ПорядокПогашенияСтоимостиНУ,
		               |	ФиксированныеАктивыУчитываемыеОтдельноСрезПоследних.ФиксированныйАктив,
		               |	ФиксированныеАктивыУчитываемыеОтдельноСрезПоследних.ВидЛьготыФА,
		               |	ВЫБОР
		               |		КОГДА ОбъектыНалоговогоУчетаФА.ФиксированныйАктив ЕСТЬ NULL 
		               |			ТОГДА ЛОЖЬ
		               |		ИНАЧЕ ИСТИНА
		               |	КОНЕЦ КАК ПризнакФА,
		               |	ВЫБОР
		               |		КОГДА НЕ ФиксированныеАктивыУчитываемыеОтдельноСрезПоследних.ФиксированныйАктив ЕСТЬ NULL 
		               |			ТОГДА ИСТИНА
		               |		ИНАЧЕ ЛОЖЬ
		               |	КОНЕЦ КАК ПризнакОтдельногоУчетаФА
		               |ПОМЕСТИТЬ ОбъектыНалоговогоУчетаФА
		               |ИЗ
		               |	РегистрСведений.ОбъектыНалоговогоУчетаФА.СрезПоследних(
		               |			&Период,
		               |			Организация В
		               |					(ВЫБРАТЬ
		               |						Реквизиты.Организация
		               |					ИЗ
		               |						Реквизиты)
		               |				И ФиксированныйАктив В
		               |					(ВЫБРАТЬ
		               |						ВЫРАЗИТЬ(Реквизиты.ИсходноеОсновноеСредство КАК Справочник.ОсновныеСредства) КАК ОсновноеСредство
		               |					ИЗ
		               |						Реквизиты)) КАК ОбъектыНалоговогоУчетаФА
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ФиксированныеАктивыУчитываемыеОтдельно.СрезПоследних(
		               |				&Период,
		               |				Организация В
		               |						(ВЫБРАТЬ
		               |							Реквизиты.Организация
		               |						ИЗ
		               |							Реквизиты)
		               |					И ФиксированныйАктив В
		               |						(ВЫБРАТЬ
		               |							Реквизиты.ИсходноеОсновноеСредство
		               |						ИЗ
		               |							Реквизиты)) КАК ФиксированныеАктивыУчитываемыеОтдельноСрезПоследних
		               |		ПО ОбъектыНалоговогоУчетаФА.Организация = ФиксированныеАктивыУчитываемыеОтдельноСрезПоследних.Организация
		               |			И ОбъектыНалоговогоУчетаФА.ФиксированныйАктив = ФиксированныеАктивыУчитываемыеОтдельноСрезПоследних.ФиксированныйАктив
		               |			И (ФиксированныеАктивыУчитываемыеОтдельноСрезПоследних.Событие = ЗНАЧЕНИЕ(Перечисление.СобытияФАУчитываемыхОтдельно.ПринятиеКУчету))
		               |;
		               |
		               |////////////////////////////////////////////////////////////////////////////////
		               |ВЫБРАТЬ
		               |	Реквизиты.Дата КАК Период,
		               |	Реквизиты.Организация,
		               |	ОбъектыНалоговогоУчетаФА.ОсновноеСредство КАК ФиксированныйАктив,
		               |	ОбъектыНалоговогоУчетаФА.ГруппаНУ,
		               |	ОбъектыНалоговогоУчетаФА.ПорядокПогашенияСтоимостиНУ,
		               |	ЗНАЧЕНИЕ(Перечисление.ВидыСостоянийФА.Выбыл) КАК СостояниеФиксированногоАктива
		               |ИЗ
		               |	ОбъектыНалоговогоУчетаФА КАК ОбъектыНалоговогоУчетаФА
		               |		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		               |		ПО (ИСТИНА)
		               |ГДЕ
		               |	ОбъектыНалоговогоУчетаФА.ПризнакФА"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
	КонецЕсли;

Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаФАУчитываемыеОтдельно(НомераТаблиц,ПараметрыПроведения,Реквизиты)
	
	ТекстЗапроса = "";
	
	Если  НЕ Реквизиты.НеобходимостьОтраженияВНУ 
		ИЛИ Реквизиты.ВидОперации = Перечисления.ВидыОперацийРеструктуризацияОС.ЧастичноеСписание 
		ИЛИ НЕ Реквизиты.СнятьСУчетаПоНалогам Тогда		
		ПараметрыПроведения.Вставить("ТаблицаФАУчитываемыеОтдельно", Неопределено);		
	Иначе  
		НомераТаблиц.Вставить("ТаблицаФАУчитываемыеОтдельно", НомераТаблиц.Количество());		
		
		ТекстЗапроса = "ВЫБРАТЬ
		|	ОбъектыНалоговогоУчетаФА.ФиксированныйАктив,
		|	ОбъектыНалоговогоУчетаФА.ВидЛьготыФА,
		|	ЗНАЧЕНИЕ(Перечисление.СобытияФАУчитываемыхОтдельно.Выбытие) КАК Событие
		|ИЗ
		|	ОбъектыНалоговогоУчетаФА КАК ОбъектыНалоговогоУчетаФА
		|ГДЕ
		|	ОбъектыНалоговогоУчетаФА.ПризнакФА
		|	И ОбъектыНалоговогоУчетаФА.ПризнакОтдельногоУчетаФА"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
	КонецЕсли;
	
	Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаПоМестнымНалогам(НомераТаблиц,Реквизиты)
	
   		НомераТаблиц.Вставить("ТаблицаОбъектыИмущественногоНалога", НомераТаблиц.Количество());		
		НомераТаблиц.Вставить("ТаблицаОбъектыТранспортногоНалога", НомераТаблиц.Количество());		
		НомераТаблиц.Вставить("ТаблицаОбъектыЗемельногоНалога", НомераТаблиц.Количество());		
		
		ТекстЗапроса = 
		"ВЫБРАТЬ
		|	ОбъектыИмущественногоНалогаСрезПоследних.ОбъектНалогообложения КАК ОбъектНалогообложения,
		|	ЛОЖЬ КАК НачислятьНалог,
		|	Реквизиты.Организация КАК Местонахождение,
		|	ОбъектыИмущественногоНалогаСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаИмущественногоНалога
		|ИЗ
		|	РегистрСведений.ОбъектыИмущественногоНалога.СрезПоследних(
		|			&Период,
		|			ОбъектНалогообложения В
		|					(ВЫБРАТЬ
		|						ТаблицаОС.ОсновноеСредство
		|					ИЗ
		|						ТаблицаОС КАК ТаблицаОС)
		|				И Налогоплательщик В
		|					(ВЫБРАТЬ
		|						Реквизиты.Организация
		|					ИЗ
		|						Реквизиты КАК Реквизиты)) КАК ОбъектыИмущественногоНалогаСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
		|		ПО (ТаблицаОС.ОсновноеСредство = ОбъектыИмущественногоНалогаСрезПоследних.ОбъектНалогообложения)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ОбъектыИмущественногоНалогаСрезПоследних.НачислятьНалог
		|	И ТаблицаОС.СнятьСУчетаПоНалогам
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаОС.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОбъектНалогообложения,
		|	ЛОЖЬ КАК НачислятьНалог,
		|	ОбъектыТранспортногоНалогаСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаТранспортногоНалога,
		|	Реквизиты.Организация КАК Местонахождение
		|ИЗ
		|	РегистрСведений.ОбъектыТранспортногоНалога.СрезПоследних(
		|			&Период,
		|			ОбъектНалогообложения В
		|					(ВЫБРАТЬ
		|						ТаблицаОС.ОсновноеСредство
		|					ИЗ
		|						ТаблицаОС КАК ТаблицаОС)
		|				И Налогоплательщик В
		|					(ВЫБРАТЬ
		|						Реквизиты.Организация
		|					ИЗ
		|						Реквизиты КАК Реквизиты)) КАК ОбъектыТранспортногоНалогаСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
		|		ПО (ТаблицаОС.ОсновноеСредство = ОбъектыТранспортногоНалогаСрезПоследних.ОбъектНалогообложения)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ОбъектыТранспортногоНалогаСрезПоследних.НачислятьНалог
		|	И ТаблицаОС.СнятьСУчетаПоНалогам
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаОС.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОбъектНалогообложения,
		|	ЛОЖЬ КАК НачислятьНалог,
		|	ОбъектыЗемельногоНалогаСрезПоследних.СтруктурнаяЕдиница КАК СтруктурнаяЕдиницаЗемельногоНалога,
		|	Реквизиты.Организация КАК Местонахождение
		|ИЗ
		|	РегистрСведений.ОбъектыЗемельногоНалога.СрезПоследних(
		|			&Период,
		|			ОбъектНалогообложения В
		|					(ВЫБРАТЬ
		|						ТаблицаОС.ОсновноеСредство
		|					ИЗ
		|						ТаблицаОС КАК ТаблицаОС)
		|				И Налогоплательщик В
		|					(ВЫБРАТЬ
		|						Реквизиты.Организация
		|					ИЗ
		|						Реквизиты КАК Реквизиты)) КАК ОбъектыЗемельногоНалогаСрезПоследних
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
		|		ПО (ТаблицаОС.ОсновноеСредство = ОбъектыЗемельногоНалогаСрезПоследних.ОбъектНалогообложения)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|ГДЕ
		|	ОбъектыЗемельногоНалогаСрезПоследних.НачислятьНалог
		|	И ТаблицаОС.СнятьСУчетаПоНалогам
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаОС.НомерСтроки"
		+ ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
		Возврат ТекстЗапроса;

КонецФункции

Функция ТекстЗапросаТаблицыПереоценкиОС(НомераТаблиц, Реквизиты)
	
		НомераТаблиц.Вставить("ТаблицаПереоценкиВА", НомераТаблиц.Количество());		
		ТекстЗапроса = "ВЫБРАТЬ
		               |	ТаблицаОС.ОсновноеСредство КАК ВнеоборотныйАктив,
		               |	ВЫБОР
		               |		КОГДА ТиповойОстатки.Субконто1 ЕСТЬ NULL 
		               |			ТОГДА ЛОЖЬ
		               |		ИНАЧЕ ИСТИНА
		               |	КОНЕЦ КАК ПереоценкаНаСчетах,
		               |	ВЫБОР
		               |		КОГДА ПереоценкаВнеоборотныхАктивовОстатки.ВнеоборотныйАктив ЕСТЬ NULL 
		               |			ТОГДА ЛОЖЬ
		               |		ИНАЧЕ ИСТИНА
		               |	КОНЕЦ КАК ПереоценкаНаРегистрах,
		               |	ТиповойОстатки.СуммаОстатокКт КАК Сумма,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки,
		               |	ЕСТЬNULL(ПереоценкаВнеоборотныхАктивовОстатки.СуммаПереоценкиСтоимостиОстаток, 0) * (ВЫРАЗИТЬ(ТаблицаОС.КоэффициентПереоценки КАК ЧИСЛО)) КАК СуммаПереоценкиСтоимости,
		               |	ЕСТЬNULL(ПереоценкаВнеоборотныхАктивовОстатки.СуммаПереоценкиАмортизацииОстаток, 0) * (ВЫРАЗИТЬ(ТаблицаОС.КоэффициентПереоценки КАК ЧИСЛО)) КАК СуммаПереоценкиАмортизации,
		               |	ЗНАЧЕНИЕ(ПланСчетов.Типовой.НераспределеннаяПрибыльНепокрытыйУбытокОтчетногоГода) КАК СчетКт,
		               |	ЗНАЧЕНИЕ(ПланСчетов.Типовой.РезервНаПереоценкуОсновныхСредств) КАК СчетДт,
		               |	ЗНАЧЕНИЕ(Перечисление.ВидыИзмененийВСобственномКапитале.ПереоценкаОсновныхСредств) КАК СубконтоДт2,
		               |	ТаблицаОС.ОсновноеСредство КАК СубконтоДт1,
		               |	ТаблицаОС.СписываемаяСтоимостьБУ КАК СписываемаяСтоимостьБУ,
		               |	ТаблицаОС.КоэффициентПереоценки КАК КоэффициентПереоценки,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СчетРасходовБУ КАК СчетРасходовБУ,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоРасходовБУ1 КАК СубконтоРасходовБУ1,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоРасходовБУ2 КАК СубконтоРасходовБУ2,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоРасходовБУ3 КАК СубконтоРасходовБУ3,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СчетДоходовБУ КАК СчетДоходовБУ,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоДоходовБУ1 КАК СубконтоДоходовБУ1,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоДоходовБУ2 КАК СубконтоДоходовБУ2,
		               |	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоДоходовБУ3 КАК СубконтоДоходовБУ3
		               |ИЗ
		               |	ТаблицаОС КАК ТаблицаОС
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Остатки(
		               |				&Период,
		               |				Счет = ЗНАЧЕНИЕ(ПланСчетов.Типовой.РезервНаПереоценкуОсновныхСредств),
		               |				,
		               |				Организация В
		               |						(ВЫБРАТЬ
		               |							Реквизиты.Организация
		               |						ИЗ
		               |							Реквизиты)
		               |					И СтруктурноеПодразделение В
		               |						(ВЫБРАТЬ
		               |							Реквизиты.СтруктурноеПодразделение
		               |						ИЗ
		               |							Реквизиты)
		               |					И Субконто1 В
		               |						(ВЫБРАТЬ
		               |							ТаблицаОС.ОсновноеСредство
		               |						ИЗ
		               |							ТаблицаОС)) КАК ТиповойОстатки
		               |		ПО ТаблицаОС.ОсновноеСредство = ТиповойОстатки.Субконто1
		               |		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ПереоценкаВнеоборотныхАктивов.Остатки(
		               |				&Период,
		               |				ВнеоборотныйАктив В
		               |						(ВЫБРАТЬ
		               |							ТаблицаОС.ОсновноеСредство
		               |						ИЗ
		               |							ТаблицаОС)
		               |					И Организация В
		               |						(ВЫБРАТЬ
		               |							Реквизиты.Организация
		               |						ИЗ
		               |							Реквизиты)
		               |					И СтруктурноеПодразделение В
		               |						(ВЫБРАТЬ
		               |							Реквизиты.СтруктурноеПодразделение
		               |						ИЗ
		               |							Реквизиты)) КАК ПереоценкаВнеоборотныхАктивовОстатки
		               |		ПО ТаблицаОС.ОсновноеСредство = ПереоценкаВнеоборотныхАктивовОстатки.ВнеоборотныйАктив";  
		
		Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаСписаниеСтоимостиОСНУ(НомераТаблиц, ПараметрыПроведения,Реквизиты)
	
	
	ТекстЗапроса = "";
	
	Если  НЕ Реквизиты.НеобходимостьОтраженияВНУ  Тогда
		ПараметрыПроведения.Вставить("РеквизитыСтоимостьОС", Неопределено);	
		ПараметрыПроведения.Вставить("ТаблицаСтоимостьОСНУ", Неопределено);	
	Иначе
		НомераТаблиц.Вставить("РеквизитыСтоимостьОС", НомераТаблиц.Количество());
		НомераТаблиц.Вставить("ТаблицаСтоимостьОСНУ", НомераТаблиц.Количество());
		
		ТекстЗапроса =  "ВЫБРАТЬ
		|	Реквизиты.Ссылка КАК Регистратор,
		|	Реквизиты.Дата КАК Период,
		|	Реквизиты.ВалютаРегламентированногоУчета КАК ВалютаВзаиморасчетов,
		|	Реквизиты.Организация,
		|	Реквизиты.СтруктурноеПодразделение,
		|	Реквизиты.ВидУчетаНУ,
		|	Реквизиты.Склад,
		|	ЛОЖЬ КАК ЭтоПередачаСтруктурномуПодразделению,
		|	&НеобходимостьОтраженияВНУ КАК НеобходимостьОтраженияВНУ,
		|	&ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль КАК ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль,
		|	&ВедениеУчетаВременныхРазницБалансовымМетодом КАК ВедениеУчетаВременныхРазницБалансовымМетодом
		|ИЗ
		|	Реквизиты КАК Реквизиты
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
		|	СтоимостьВА_НУ.Счет КАК СчетНУ,
		|	ЕСТЬNULL(СтоимостьВА_НУ.СуммаОстатокДт, 0) КАК СтоимостьНУ,
		|	СтоимостьВА_НУ.ВидУчета КАК ВидУчетаНУ,
		|	ЕСТЬNULL(ОбъектыНалоговогоУчетаФА.ПризнакФА, ЛОЖЬ) КАК ПризнакФА,
		|	ОбъектыНалоговогоУчетаФА.ГруппаНУ,
		|	ОбъектыНалоговогоУчетаФА.ПорядокПогашенияСтоимостиНУ КАК ПорядокПогашенияСтоимостиНУ,
		|	ОбъектыНалоговогоУчетаФА.ВидЛьготыФА КАК ВидЛьготыФА,
		|	ОбъектыНалоговогоУчетаФА.ПризнакОтдельногоУчетаФА
		|ИЗ
		|	РегистрБухгалтерии.Налоговый.Остатки(
		|			&Период,
		|			Счет В ИЕРАРХИИ (ЗНАЧЕНИЕ(ПланСчетов.Налоговый.ВнеоборотныеАктивы)),
		|			ЗНАЧЕНИЕ(ПланВидовХарактеристик.ВидыСубконтоТиповые.ВнеоборотныеАктивы),
		|			Организация В
		|					(ВЫБРАТЬ
		|						Реквизиты.Организация
		|					ИЗ
		|						Реквизиты)
		|				И Субконто1 В
		|					(ВЫБРАТЬ
		|						ТаблицаОС.ОсновноеСредство
		|					ИЗ
		|						ТаблицаОС)) КАК СтоимостьВА_НУ
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
		|		ПО (ТаблицаОС.ОсновноеСредство = СтоимостьВА_НУ.Субконто1)
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
		|		ПО (ИСТИНА)
		|		ЛЕВОЕ СОЕДИНЕНИЕ ОбъектыНалоговогоУчетаФА КАК ОбъектыНалоговогоУчетаФА
		|		ПО СтоимостьВА_НУ.Субконто1 = ОбъектыНалоговогоУчетаФА.ОсновноеСредство
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицаОС.НомерСтроки";
		
	КонецЕсли;
	
 Возврат  ТекстЗапроса;
 
КонецФункции

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
	
КонецПроцедуры

#КонецЕсли
 