#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, СтандартнаяОбработка)
	
	ТипДанныхЗаполнения = ТипЗнч(ДанныеЗаполнения);
	
	Если ТипДанныхЗаполнения = Тип("Структура") Тогда 
		Если ДанныеЗаполнения.Свойство("Автор") Тогда
			ДанныеЗаполнения.Удалить("Автор");
		КонецЕсли;
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, ДанныеЗаполнения);
	КонецЕсли;

	ЗаполнениеДокументов.ЗаполнитьШапкуДокумента(ЭтотОбъект, ОбщегоНазначенияБКВызовСервераПовтИсп.ПолучитьВалютуРегламентированногоУчета());

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив();
    
    Если НЕ ПроцедурыНалоговогоУчета.ПолучитьПризнакПлательщикаНалогаНаПрибыль(Организация, Дата)
        ИЛИ НЕ УчитыватьКПН Тогда
        
        МассивНепроверяемыхРеквизитов.Добавить("ВидУчетаНУ");
                
    КонецЕсли;

    ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
	
	УправлениеВнеоборотнымиАктивамиСервер.ПроверитьОтсутствиеДублейВТабличнойЧасти(ЭтотОбъект, "ОС", Новый Структура("ОсновноеСредство"), Отказ);
	
	// Проверка, если остаточная стоимость = 0, тогда не проводить документ, если выбран метод переоценки Пропорциональная переоценка накопленной амортизации.
	Для Каждого СтрокаОС Из ОС Цикл
		
		Если (СтрокаОС.СтоимостьБУ - СтрокаОС.АмортизацияБУ - СтрокаОС.АмортизацияЗаМесяцБУ = 0) И МетодПереоценки = Перечисления.МетодыПереоценкиВнеоборотныхАктивов.ПропорциональнаяПереоценкаНакопленнойАмортизации Тогда
			
			ТекстСообщения = НСтр("ru='Остаточная стоимость ОС <%1> равна 0. В данном случае рекомендуется выбрать метод переоценки ""Списание амортизации при переоценке"".'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения,
				СтрокаОС.ОсновноеСредство);
				
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , "Объект", Отказ);

			Отказ = Истина;
		КонецЕсли;
	КонецЦикла;	

КонецПроцедуры

Процедура ОбработкаПроведения(Отказ, РежимПроведения)

	// ПОДГОТОВКА ПРОВЕДЕНИЯ ПО ДАННЫМ ДОКУМЕНТА
	ПроведениеСервер.ПодготовитьНаборыЗаписейКПроведению(ЭтотОбъект);
	
	Если РучнаяКорректировка Тогда
		Возврат;
	КонецЕсли;

	ПараметрыПроведения = Документы.ПереоценкаВнеоборотныхАктивов.ПодготовитьПараметрыПроведения(Ссылка, Отказ);
	
	Если Отказ Тогда
		Возврат;
	КонецЕсли;
	
	// Если вдруг не удалось получить параметры проведения и не установлен флаг Отказ, то просто выйдем из проведения
	Если ПараметрыПроведения = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	// Таблица ОС 
	ТаблицаДвиженийПараметровАмортизацииОСБухгалтерскийУчет = УчетОС.ПодготовитьТаблицыДвиженийПараметровАмортизацииОСБухгалтерскийУчет(ПараметрыПроведения.ТаблицаПараметрыАмортизацииОСБухгалтерскийУчет,
																				ПараметрыПроведения.Реквизиты, Отказ);

	// ФОРМИРОВАНИЕ ДВИЖЕНИЙ
	УчетОС.СформироватьДвиженияРегистрацияСобытияОС(ПараметрыПроведения.ТаблицаСобытияОСОрганизаций,
		                                        ПараметрыПроведения.Реквизиты,
												Движения, Отказ);
												
											
	УчетОС.СформироватьДвиженияПараметрыАмортизацииОСБухгалтерскийУчет(ТаблицаДвиженийПараметровАмортизацииОСБухгалтерскийУчет,
		                                        Движения, Отказ);
												
	УправлениеВнеоборотнымиАктивамиСервер.СформироватьДвиженияПереоценкаВнеоборотныхАктивов(ПараметрыПроведения.СписокПереоцененныхОC,
												ПараметрыПроведения.ТаблицаДокументовПереоценкиВА,
												ПараметрыПроведения.Реквизиты,
												Движения, Отказ);											

КонецПроцедуры

Процедура ПередЗаписью(Отказ, РежимЗаписи, РежимПроведения)
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	Если НЕ УчитыватьКПН Тогда
		ВидУчетаНУ = Справочники.ВидыУчетаНУ.ПустаяСсылка();
	КонецЕсли;	              	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	Дата = НачалоДня(ОбщегоНазначения.ТекущаяДатаПользователя());
	
	Для Каждого СтрокаОС Из ОС Цикл  
		СтрокаОС.АмортизацияБУ                    = 0;
		СтрокаОС.АмортизацияЗаМесяцБУ             = 0;
		СтрокаОС.ЛиквидационнаяСтоимостьБУ        = 0;
		СтрокаОС.ПереоцененнаяСтоимостьБУ         = 0;
		СтрокаОС.СписываемаяСуммаРезерваЗаМесяцБУ = 0;
		СтрокаОС.СтоимостьБУ                      = 0;
	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#КонецЕсли