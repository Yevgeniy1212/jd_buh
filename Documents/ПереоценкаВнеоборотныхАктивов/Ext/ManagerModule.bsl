#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Процедура ЗаполнитьТабличнуюЧастьДляСпискаОС(Объект, ПоддержкаРаботыСоСтруктурнымиПодразделениями, ПрименятьПараметрыАмортизацииВТекМесяце) Экспорт 
	
	СписокОС = Объект.ОС.Выгрузить(,"ОсновноеСредство");
	
	ТаблицаАмортизацииБух = Новый ТаблицаЗначений();

	Запрос   = Новый Запрос;
	Запрос.УстановитьПараметр("Организация"    , Объект.Организация);
	Запрос.УстановитьПараметр("СписокОС"       , СписокОС);
	Запрос.УстановитьПараметр("Период"         , Объект.Дата);
	Запрос.УстановитьПараметр("СубконтоОС"     , ПланыВидовХарактеристик.ВидыСубконтоТиповые.ОсновныеСредства);
	Запрос.УстановитьПараметр("ПериодОстатков" , Новый Граница(Объект.Дата, ВидГраницы.Включая));

	Если ПоддержкаРаботыСоСтруктурнымиПодразделениями Тогда
		УсловиеСтруктурноеПодразделение = " И СтруктурноеПодразделение = &СтруктурноеПодразделение ";		
		Запрос.УстановитьПараметр("СтруктурноеПодразделение", Объект.СтруктурноеПодразделение);
	Иначе 
		УсловиеСтруктурноеПодразделение = "";		
	КонецЕсли;
	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	СтоимостьОС_БУ.Субконто1 КАК ОсновноеСредство,
	|   ЕстьNULL(СтоимостьОС_БУ.СуммаОстатокДт,0) КАК СтоимостьБУ,
	|	ЕстьNULL(АмортизацияОС_БУ.СуммаОстатокКт,0) КАК АмортизацияБУ,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ЛиквидационнаяСтоимость, 0) КАК ЛиквидационнаяСтоимостьБУ
	|ИЗ
	|	РегистрСведений.СчетаУчетаОС.СрезПоследних(
	|			&Период,
	|			Организация = &Организация " + УсловиеСтруктурноеПодразделение + "
	|				И ОсновноеСредство В (&СписокОС)) КАК СчетаУчетаОсновныхСредствСрезПоследних
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Остатки(
	|				&ПериодОстатков,
	|				,
	|				&СубконтоОС,
	|				Организация = &Организация " + УсловиеСтруктурноеПодразделение + "
	|					И Субконто1 В (&СписокОС)) КАК СтоимостьОС_БУ
	|		ПО СчетаУчетаОсновныхСредствСрезПоследних.ОсновноеСредство = СтоимостьОС_БУ.Субконто1
	|			И СчетаУчетаОсновныхСредствСрезПоследних.СчетУчетаБУ = СтоимостьОС_БУ.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрБухгалтерии.Типовой.Остатки(
	|				&ПериодОстатков,
	|				,
	|				&СубконтоОС,
	|				Организация = &Организация " + УсловиеСтруктурноеПодразделение + "
	|					И Субконто1 В (&СписокОС)) КАК АмортизацияОС_БУ
	|		ПО СчетаУчетаОсновныхСредствСрезПоследних.ОсновноеСредство = АмортизацияОС_БУ.Субконто1
	|			И СчетаУчетаОсновныхСредствСрезПоследних.СчетНачисленияАмортизацииБУ = АмортизацияОС_БУ.Счет
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|				&Период,
	|				Организация = &Организация " + УсловиеСтруктурноеПодразделение + "
	|					И ОсновноеСредство В (&СписокОС)) КАК ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних
	|		ПО СчетаУчетаОсновныхСредствСрезПоследних.ОсновноеСредство = ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство";
	
	ТЗ_БУ = Запрос.Выполнить().Выгрузить();
	
	// амортизация за месяц и списваемая сумма резерва за месяц заполняем
	// только в том случае, если отключена настройка учетной политики "Применять ... в тек. месяце"
	//
	Если НЕ ПрименятьПараметрыАмортизацииВТекМесяце Тогда
		ТаблицаАмортизацииБух = УчетОС.ПодготовитьТаблицуАмортизацияОС(Объект.Дата, Объект.Организация, Объект.СтруктурноеПодразделение, СписокОС);
		
		// получим таблицу, которая будет содержать стоимость и амортизацию основного средства
		ТаблицаТекДанныеОСБУ = Новый ТаблицаЗначений;
		ТаблицаТекДанныеОСБУ.Колонки.Добавить("ОС");
		ТаблицаТекДанныеОСБУ.Колонки.Добавить("АмортизацияЗаМесяц");
		ТаблицаТекДанныеОСБУ.Колонки.Добавить("ОстаточнаяСтоимость");
		ТаблицаТекДанныеОСБУ = УправлениеВнеоборотнымиАктивамиСервер.ПолучитьТекДанныеОСБУ(Объект.Дата, Объект.Организация, Объект.СтруктурноеПодразделение, СписокОС);
		
		// создадим таблицу, которая будет содержать суммы списания резерва по переоценке
		ТаблицаРезерваКСписанию = Новый ТаблицаЗначений;
		
		Для Каждого СтрокаОС ИЗ Объект.ОС Цикл
			СтрокаТекДанныеОСБУ = ТаблицаТекДанныеОСБУ.Найти(СтрокаОС.ОсновноеСредство,"ОС"); 
			
			Если СтрокаТекДанныеОСБУ <> Неопределено Тогда
				// смотрим, есть ли амортизация за месяц
				Если СтрокаТекДанныеОСБУ.АмортизацияЗаМесяц = 0 Тогда
					СтрокаТаблицаАмортизацииБУ 			   = ТаблицаАмортизацииБух.Найти(СтрокаОС.ОсновноеСредство,"ОС");
					СтрокаТекДанныеОСБУ.АмортизацияЗаМесяц = ?(СтрокаТаблицаАмортизацииБУ = Неопределено,0,СтрокаТаблицаАмортизацииБУ.Бух);
				КонецЕсли;
										
			КонецЕсли;	
			
		КонецЦикла;	
	    ТаблицаРезерваКСписанию = УправлениеВнеоборотнымиАктивамиСервер.РасчетСуммыСписываемогоРезерваПоПереоценке(Объект.Дата, Объект.Организация, ТаблицаТекДанныеОСБУ,);

	КонецЕсли;
	
	Для каждого СтрокаТЧ Из Объект.ОС Цикл

		// В соответствующие поля строки запишем данные из запроса
		СтрокаТЗБУ = ТЗ_БУ.Найти(СтрокаТЧ.ОсновноеСредство,"ОсновноеСредство");

		Если СтрокаТЗБУ = Неопределено Тогда
			СтрокаТЧ.СтоимостьБУ 			   = 0;
			СтрокаТЧ.АмортизацияБУ 			   = 0;
			СтрокаТЧ.АмортизацияЗаМесяцБУ 	   = 0;
			СтрокаТЧ.ЛиквидационнаяСтоимостьБУ = 0;
		Иначе
    		СтрокаТЧ.СтоимостьБУ          	   = СтрокаТЗБУ.СтоимостьБУ;
			СтрокаТЧ.АмортизацияБУ        	   = СтрокаТЗБУ.АмортизацияБУ;
			СтрокаТЧ.ЛиквидационнаяСтоимостьБУ = СтрокаТЗБУ.ЛиквидационнаяСтоимостьБУ;
			
			Если Не ПрименятьПараметрыАмортизацииВТекМесяце Тогда
				СтрокаТаблицаАмортизацииБух   = ТаблицаАмортизацииБух.Найти(СтрокаТЧ.ОсновноеСредство,"ОС");
				СтрокаТЧ.АмортизацияЗаМесяцБУ = ?(СтрокаТаблицаАмортизацииБух = Неопределено,0,СтрокаТаблицаАмортизацииБух.Бух);
				
				СтрокаТаблицаРзерваКСписанию			  = ТаблицаРезерваКСписанию.Найти(СтрокаТЧ.ОсновноеСредство,"ОС");
				СтрокаТЧ.СписываемаяСуммаРезерваЗаМесяцБУ = ?(СтрокаТаблицаРзерваКСписанию = Неопределено,0,СтрокаТаблицаРзерваКСписанию.СуммаРезерваКСписанию);
			КонецЕсли;
		КонецЕсли;

	КонецЦикла;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// Проведение

Функция ПодготовитьПараметрыПроведения(ДокументСсылка, Отказ) Экспорт
	
	ПараметрыПроведения = Новый Структура;
	
	Запрос = Новый Запрос;
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	Запрос.УстановитьПараметр("Ссылка"						  , ДокументСсылка);
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаРеквизитыДокумента(НомераТаблиц);
	Результат = Запрос.ВыполнитьПакет();
	ТаблицаРеквизиты = Результат[НомераТаблиц["Реквизиты"]].Выгрузить();
	ПараметрыПроведения.Вставить("Реквизиты", ТаблицаРеквизиты);
	
	Реквизиты = ОбщегоНазначения.СтрокаТаблицыЗначенийВСтруктуру(ТаблицаРеквизиты[0]);
	
	ВедетсяАналитическийУчетОСПоМОЛ 				= УправлениеВнеоборотнымиАктивамиСервер.ВедетсяАналитическийУчетОСПоМОЛ(Реквизиты.Период); 
	ВедетсяАналитическийУчетОСПоПодразделениям 		= УправлениеВнеоборотнымиАктивамиСервер.ВедетсяАналитическийУчетОСПоПодразделениям(Реквизиты.Период);
	
	ОрганизацияПлательщикНалогаНаПрибыль 			= ПроцедурыНалоговогоУчета.ПолучитьПризнакПлательщикаНалогаНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль 	= ОрганизацияПлательщикНалогаНаПрибыль И ПроцедурыНалоговогоУчета.ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль(Реквизиты.Организация, Реквизиты.Период);
	НеобходимостьОтраженияВНУ 						= ОрганизацияПлательщикНалогаНаПрибыль ИЛИ ПоддержкаУчетаВременныхРазницПоНалогуНаПрибыль;
	
	ВедениеУчетаВременныхРазницБалансовымМетодом    = УчетнаяПолитикаСервер.ВедетсяУчетВременныхРазницБалансовымМетодом(Реквизиты.Организация, Реквизиты.Период);
	
	ПрименятьПараметрыАмортизацииВТекМесяце 		= УправлениеВнеоборотнымиАктивамиСервер.ПолучитьПризнакПримененияПараметровАмортизацииВТекМесяце(Реквизиты.Организация, Реквизиты.Период);
		
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ВедетсяАналитическийУчетОСПоМОЛ				   , "ВедетсяАналитУчетОсПоМОЛ");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ВедетсяАналитическийУчетОСПоПодразделениям	   , "ВедетсяАналитУчетОсПоПодразделениям");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(НеобходимостьОтраженияВНУ					   , "НеобходимостьОтраженияВНУ");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ВедениеУчетаВременныхРазницБалансовымМетодом   , "ВедениеУчетаВременныхРазницБалансовымМетодом");
	ПараметрыПроведения.Реквизиты.ЗаполнитьЗначения(ПрименятьПараметрыАмортизацииВТекМесяце   	   , "ПрименятьПараметрыАмортизацииВТекМесяце");
	
	ВедетсяАналитическийУчетОС = ВедетсяАналитическийУчетОСПоМОЛ ИЛИ ВедетсяАналитическийУчетОСПоПодразделениям;
	
	Запрос.УстановитьПараметр("Организация"				  , Реквизиты.Организация);
	Запрос.УстановитьПараметр("Период"   				  , Реквизиты.Период);
	Запрос.УстановитьПараметр("СтруктурноеПодразделение"  , Реквизиты.СтруктурноеПодразделение);
	Запрос.УстановитьПараметр("ПрименятьПараметрыАмортизацииВТекМесяце"  , ПрименятьПараметрыАмортизацииВТекМесяце);
	
	НомераТаблиц = Новый Структура;
	
	Запрос.Текст = ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)
					+ ТекстЗапросаСобытияОСОрганизаций(НомераТаблиц)
					+ ТекстЗапросаПараметрыАмортизацииОСБухгалтерскийУчет(НомераТаблиц)
					+ ТекстЗапросаСписокПереоцененныхОC(НомераТаблиц)
					+ ТекстЗапросаТаблицаДокументовПереоценкиВА(НомераТаблиц);
	
	Результат = Запрос.ВыполнитьПакет();
	
	Для каждого НомерТаблицы Из НомераТаблиц Цикл
		ПараметрыПроведения.Вставить(НомерТаблицы.Ключ, Результат[НомерТаблицы.Значение].Выгрузить());
	КонецЦикла;
	
	Возврат ПараметрыПроведения;

КонецФункции

Функция ТекстЗапросаРеквизитыДокумента(НомераТаблиц)

	НомераТаблиц.Вставить("ВременнаяТаблицаРеквизиты", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("Реквизиты"				 , НомераТаблиц.Количество());
	
	ТекстЗапроса = "ВЫБРАТЬ
	               |	Реквизиты.Ссылка КАК Регистратор,
				   |	Реквизиты.Ссылка КАК Ссылка,
	               |	Реквизиты.Дата КАК Период,
				   |	Реквизиты.Дата КАК Дата,
				   |	Реквизиты.Номер КАК Номер,
	               |	Реквизиты.ВидУчетаНУ КАК ВидУчета,
	               |	Реквизиты.МетодПереоценки КАК МетодПереоценки,
	               |	Реквизиты.Организация КАК Организация,
	               |	Реквизиты.СобытиеОС КАК СобытиеОС,
	               |	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	               |	Реквизиты.СубконтоДоходовБУ1 КАК СубконтоДоходовБУ1,
	               |	Реквизиты.СубконтоДоходовБУ2 КАК СубконтоДоходовБУ2,
	               |	Реквизиты.СубконтоДоходовБУ3 КАК СубконтоДоходовБУ3,
	               |	Реквизиты.СубконтоДоходовНУ1 КАК СубконтоДоходовНУ1,
	               |	Реквизиты.СубконтоДоходовНУ2 КАК СубконтоДоходовНУ2,
	               |	Реквизиты.СубконтоДоходовНУ3 КАК СубконтоДоходовНУ3,
	               |	Реквизиты.СубконтоРасходовБУ1 КАК СубконтоРасходовБУ1,
	               |	Реквизиты.СубконтоРасходовБУ2 КАК СубконтоРасходовБУ2,
	               |	Реквизиты.СубконтоРасходовБУ3 КАК СубконтоРасходовБУ3,
	               |	Реквизиты.СубконтоРасходовНУ1 КАК СубконтоРасходовНУ1,
	               |	Реквизиты.СубконтоРасходовНУ2 КАК СубконтоРасходовНУ2,
	               |	Реквизиты.СубконтоРасходовНУ3 КАК СубконтоРасходовНУ3,
	               |	Реквизиты.СчетДоходовБУ КАК СчетДоходовБУ,
	               |	Реквизиты.СчетДоходовНУ КАК СчетДоходовНУ,
	               |	Реквизиты.СчетРасходовБУ КАК СчетРасходовБУ,
	               |	Реквизиты.СчетРасходовНУ КАК СчетРасходовНУ,
	               |	Реквизиты.УчитыватьКПН КАК УчитыватьКПН,
				   |	ЛОЖЬ КАК ВедетсяАналитУчетОсПоПодразделениям,
	               |	ЛОЖЬ КАК ВедетсяАналитУчетОсПоМОЛ,
	               |	ЛОЖЬ КАК ПоддержкаРаботыСоСтруктурнымиПодразделениями,
	               |	ЛОЖЬ КАК НеобходимостьОтраженияВНУ,
	               |	ЛОЖЬ КАК ВедениеУчетаВременныхРазницБалансовымМетодом,
				   |    ЛОЖЬ КАК ПрименятьПараметрыАмортизацииВТекМесяце
	               |ПОМЕСТИТЬ Реквизиты
	               |ИЗ
	               |	Документ.ПереоценкаВнеоборотныхАктивов КАК Реквизиты
	               |ГДЕ
	               |	Реквизиты.Ссылка = &Ссылка
	               |;
	               |
	               |////////////////////////////////////////////////////////////////////////////////
	               |ВЫБРАТЬ
				   |	Реквизиты.Регистратор,
				   |	Реквизиты.Ссылка,
	               |	Реквизиты.Период,
				   |	Реквизиты.Дата,
				   |	Реквизиты.Номер,
	               |	Реквизиты.ВидУчета,
	               |	Реквизиты.МетодПереоценки,
	               |	Реквизиты.Организация,
	               |	Реквизиты.СобытиеОС,
	               |	Реквизиты.СтруктурноеПодразделение,
	               |	Реквизиты.СубконтоДоходовБУ1,
	               |	Реквизиты.СубконтоДоходовБУ2,
	               |	Реквизиты.СубконтоДоходовБУ3,
	               |	Реквизиты.СубконтоДоходовНУ1,
	               |	Реквизиты.СубконтоДоходовНУ2,
	               |	Реквизиты.СубконтоДоходовНУ3,
	               |	Реквизиты.СубконтоРасходовБУ1,
	               |	Реквизиты.СубконтоРасходовБУ2,
	               |	Реквизиты.СубконтоРасходовБУ3,
	               |	Реквизиты.СубконтоРасходовНУ1,
	               |	Реквизиты.СубконтоРасходовНУ2,
	               |	Реквизиты.СубконтоРасходовНУ3,
	               |	Реквизиты.СчетДоходовБУ,
	               |	Реквизиты.СчетДоходовНУ,
	               |	Реквизиты.СчетРасходовБУ,
	               |	Реквизиты.СчетРасходовНУ,
	               |	Реквизиты.УчитыватьКПН,
				   |	Реквизиты.ВедетсяАналитУчетОсПоПодразделениям,
	               |	Реквизиты.ВедетсяАналитУчетОсПоМОЛ,
	               |	Реквизиты.ПоддержкаРаботыСоСтруктурнымиПодразделениями,
	               |	Реквизиты.НеобходимостьОтраженияВНУ,
	               |	Реквизиты.ВедениеУчетаВременныхРазницБалансовымМетодом,
				   |	Реквизиты.ПрименятьПараметрыАмортизацииВТекМесяце
	               |ИЗ
	               |	Реквизиты КАК Реквизиты";

	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
	
КонецФункции

Функция ТекстЗапросаВременныеТаблицыДокумента(НомераТаблиц)

	
	НомераТаблиц.Вставить("вт_ТаблицаОС", НомераТаблиц.Количество());
	НомераТаблиц.Вставить("ВременнаяТаблицаОС", НомераТаблиц.Количество());
 	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ТаблицаОС.Ссылка КАК Ссылка,
	|	ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	ТаблицаОС.СтоимостьБУ КАК СтоимостьБУ,
	|	ТаблицаОС.АмортизацияБУ КАК АмортизацияБУ,
	|	ТаблицаОС.АмортизацияЗаМесяцБУ КАК АмортизацияЗаМесяцБУ,
	|	ТаблицаОС.ПереоцененнаяСтоимостьБУ КАК ПереоцененнаяСтоимостьБУ,
	|	ТаблицаОС.ЛиквидационнаяСтоимостьБУ КАК ЛиквидационнаяСтоимостьБУ,
	|	ТаблицаОС.СписываемаяСуммаРезерваЗаМесяцБУ КАК СписываемаяСуммаРезерваЗаМесяцБУ,
	|	ВЫБОР
	|		КОГДА &ПрименятьПараметрыАмортизацииВТекМесяце
	|			ТОГДА ТаблицаОС.ПереоцененнаяСтоимостьБУ - (ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ)
	|		ИНАЧЕ ТаблицаОС.ПереоцененнаяСтоимостьБУ - (ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ)
	|	КОНЕЦ КАК СуммаПереоценки,
	|	ВЫБОР
	|		КОГДА ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ <> 0
	|			ТОГДА ТаблицаОС.ПереоцененнаяСтоимостьБУ * (ТаблицаОС.СтоимостьБУ / (ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ)) - ТаблицаОС.СтоимостьБУ
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК СуммаПереоценкиСтоимости,
	|	ВЫБОР
	|		КОГДА &ПрименятьПараметрыАмортизацииВТекМесяце
	|			ТОГДА ВЫБОР
	|					КОГДА ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ <> 0
	|						ТОГДА ТаблицаОС.ПереоцененнаяСтоимостьБУ * (ТаблицаОС.СтоимостьБУ / (ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ) - 1) - ТаблицаОС.АмортизацияБУ
	|					ИНАЧЕ 0
	|				КОНЕЦ
	|		ИНАЧЕ ВЫБОР
	|				КОГДА ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ <> 0
	|					ТОГДА ТаблицаОС.ПереоцененнаяСтоимостьБУ * (ТаблицаОС.СтоимостьБУ / (ТаблицаОС.СтоимостьБУ - ТаблицаОС.АмортизацияБУ - ТаблицаОС.АмортизацияЗаМесяцБУ) - 1) - (ТаблицаОС.АмортизацияБУ + ТаблицаОС.АмортизацияЗаМесяцБУ)
	|				ИНАЧЕ 0
	|			КОНЕЦ
	|	КОНЕЦ КАК СуммаПереоценкиАмортизации
	|ПОМЕСТИТЬ вт_ТаблицаОС
	|ИЗ
	|	Документ.ПереоценкаВнеоборотныхАктивов.ОС КАК ТаблицаОС
	|ГДЕ
	|	ТаблицаОС.Ссылка = &Ссылка
	|
	|ИНДЕКСИРОВАТЬ ПО
	|	Ссылка
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	вт_ТаблицаОС.Ссылка КАК Ссылка,
	|	вт_ТаблицаОС.НомерСтроки КАК НомерСтроки,
	|	вт_ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	вт_ТаблицаОС.СтоимостьБУ КАК СтоимостьБУ,
	|	вт_ТаблицаОС.АмортизацияБУ КАК АмортизацияБУ,
	|	вт_ТаблицаОС.АмортизацияЗаМесяцБУ КАК АмортизацияЗаМесяцБУ,
	|	вт_ТаблицаОС.ПереоцененнаяСтоимостьБУ КАК ПереоцененнаяСтоимостьБУ,
	|	вт_ТаблицаОС.ЛиквидационнаяСтоимостьБУ КАК ЛиквидационнаяСтоимостьБУ,
	|	вт_ТаблицаОС.СписываемаяСуммаРезерваЗаМесяцБУ КАК СписываемаяСуммаРезерваЗаМесяцБУ,
	|	вт_ТаблицаОС.СуммаПереоценки КАК СуммаПереоценки,
	|	вт_ТаблицаОС.СуммаПереоценкиСтоимости КАК СуммаПереоценкиСтоимости,
	|	вт_ТаблицаОС.СуммаПереоценкиАмортизации КАК СуммаПереоценкиАмортизации,
	|	ЕСТЬNULL(НачислениеАмортизацииОСБухгалтерскийУчетСрезПоследних.НачислятьАмортизацию, ЛОЖЬ) КАК НачислятьАмортизацию
	|ПОМЕСТИТЬ ТаблицаОС
	|ИЗ
	|	вт_ТаблицаОС КАК вт_ТаблицаОС
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.НачислениеАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							вт_ТаблицаОС.ОсновноеСредство
	|						ИЗ
	|							вт_ТаблицаОС КАК вт_ТаблицаОС)) КАК НачислениеАмортизацииОСБухгалтерскийУчетСрезПоследних
	|		ПО вт_ТаблицаОС.ОсновноеСредство = НачислениеАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();

КонецФункции

Функция ТекстЗапросаСобытияОСОрганизаций(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаСобытияОСОрганизаций", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Регистратор КАК Регистратор,
	|	Реквизиты.Организация КАК Организация,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	Реквизиты.СобытиеОС КАК СобытиеОС,
	|	Реквизиты.Номер КАК НомерДокумента,
	|	0 КАК СуммаЗатратБУ
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
	|		ПО (ИСТИНА)
	|
	| ГДЕ ТаблицаОС.СуммаПереоценки <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаПараметрыАмортизацииОСБухгалтерскийУчет(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаПараметрыАмортизацииОСБухгалтерскийУчет", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Регистратор КАК Регистратор,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период КАК ДатаПоследнихПараметровАмортизации,
	|	(ГОД(Реквизиты.Период) - ГОД(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период)) * 12 + (МЕСЯЦ(Реквизиты.Период) - МЕСЯЦ(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период)) КАК СрокИспользованияПослеПоследнихПараметровАмортизации,
	|	ТаблицаОС.ПереоцененнаяСтоимостьБУ - ТаблицаОС.ЛиквидационнаяСтоимостьБУ КАК СтоимостьДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокПолезногоИспользования, 0) КАК СрокПолезногоИспользования,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРабот, 0) КАК ОбъемПродукцииРабот,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации, 0) КАК СрокИспользованияДляВычисленияАмортизацииДляРасчета,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации, 0) - ((ГОД(Реквизиты.Период) - ГОД(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период)) * 12 + (МЕСЯЦ(Реквизиты.Период) - МЕСЯЦ(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период))) КАК СрокИспользованияДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОбъемПродукцииРаботДляВычисленияАмортизации, 0) КАК ОбъемПродукцииРаботДляВычисленияАмортизации,
	|	ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.КоэффициентУскорения, 0) КАК КоэффициентУскорения,
	|	ВЫБОР
	|		КОГДА ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации, 0) - ((ГОД(Реквизиты.Период) - ГОД(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период)) * 12 + (МЕСЯЦ(Реквизиты.Период) - МЕСЯЦ(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период))) <> 0
	|			ТОГДА ВЫРАЗИТЬ(12 / (ЕСТЬNULL(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.СрокИспользованияДляВычисленияАмортизации, 0) - ((ГОД(Реквизиты.Период) - ГОД(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период)) * 12 + (МЕСЯЦ(Реквизиты.Период) - МЕСЯЦ(ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.Период)))) * 100 КАК ЧИСЛО(15, 2))
	|		ИНАЧЕ 0
	|	КОНЕЦ КАК КоэффициентАмортизации,
	|	ТаблицаОС.ЛиквидационнаяСтоимостьБУ КАК ЛиквидационнаяСтоимость,
	|	ЕСТЬNULL(ВыработкаОСОбороты.КоличествоОборот, 0) КАК ОбъемВыработанныхПродукцииРабот
	|ИЗ
	|	ТаблицаОС КАК ТаблицаОС
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Реквизиты КАК Реквизиты
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.ПараметрыАмортизацииОСБухгалтерскийУчет.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|					И ОсновноеСредство В
	|						(ВЫБРАТЬ
	|							ТаблицаОС.ОсновноеСредство
	|						ИЗ
	|							ТаблицаОС)) КАК ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних
	|		ПО ТаблицаОС.ОсновноеСредство = ПараметрыАмортизацииОСБухгалтерскийУчетСрезПоследних.ОсновноеСредство
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.ВыработкаОС.Обороты(
	|				,
	|				&Период,
	|				,
	|				ОсновноеСредство В
	|					(ВЫБРАТЬ
	|						ТаблицаОС.ОсновноеСредство
	|					ИЗ
	|						ТаблицаОС)) КАК ВыработкаОСОбороты
	|		ПО ТаблицаОС.ОсновноеСредство = ВыработкаОСОбороты.ОсновноеСредство
	|ГДЕ
	|	ТаблицаОС.СуммаПереоценки <> 0
	|	и ТаблицаОС.НачислятьАмортизацию = ИСТИНА
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаСписокПереоцененныхОC(НомераТаблиц)
	
	НомераТаблиц.Вставить("СписокПереоцененныхОC", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	Реквизиты.Период КАК Период,
	|	Реквизиты.Регистратор КАК Регистратор,
	|	ТаблицаОС.НомерСтроки,
	|	ТаблицаОС.ОсновноеСредство КАК ОсновноеСредство,
	|	Реквизиты.Организация КАК Организация,
	|	Реквизиты.СтруктурноеПодразделение КАК СтруктурноеПодразделение,
	|	ТаблицаОС.СтоимостьБУ,
	|	ТаблицаОС.АмортизацияБУ,
	|	ТаблицаОС.АмортизацияЗаМесяцБУ,
	|	ТаблицаОС.ПереоцененнаяСтоимостьБУ,
	|	ТаблицаОС.ЛиквидационнаяСтоимостьБУ,
	|	ТаблицаОС.СписываемаяСуммаРезерваЗаМесяцБУ,
	|	ТаблицаОС.СуммаПереоценки,
	|	ТаблицаОС.СуммаПереоценкиСтоимости,
	|	ТаблицаОС.СуммаПереоценкиАмортизации,
    |	СчетаУчетаОССрезПоследних.СчетУчетаБУ,
	|	СчетаУчетаОССрезПоследних.СчетНачисленияАмортизацииБУ
	|ИЗ
	|	Реквизиты КАК Реквизиты
	|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ ТаблицаОС КАК ТаблицаОС
	|		ПО (ИСТИНА)
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СчетаУчетаОС.СрезПоследних(
	|				&Период,
	|				Организация = &Организация
	|					И ОсновноеСредство В (Выбрать ТаблицаОС.ОсновноеСредство ИЗ ТаблицаОС)) КАК СчетаУчетаОССрезПоследних
	|		ПО ТаблицаОС.ОсновноеСредство = СчетаУчетаОССрезПоследних.ОсновноеСредство
	|
	| ГДЕ ТаблицаОС.СуммаПереоценки <> 0
	|
	|УПОРЯДОЧИТЬ ПО
	|	ТаблицаОС.НомерСтроки";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции

Функция ТекстЗапросаТаблицаДокументовПереоценкиВА(НомераТаблиц)
	
	НомераТаблиц.Вставить("ТаблицаДокументовПереоценкиВА", НомераТаблиц.Количество());
	
	ТекстЗапроса =
	"ВЫБРАТЬ
	|	ПереоценкаВнеоборотныхАктивовОстатки.ВнеоборотныйАктив КАК ВнеоборотныйАктив,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки КАК ДокументПереоценки,
	|	ПереоценкаВнеоборотныхАктивовОстатки.СуммаПереоценкиСтоимостиОстаток КАК СуммаПереоценкиСтоимости,
	|	ПереоценкаВнеоборотныхАктивовОстатки.СуммаПереоценкиАмортизацииОстаток КАК СуммаПереоценкиАмортизации,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СчетДоходовБУ КАК СчетДоходовБУ,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоДоходовБУ1 КАК СубконтоДоходовБУ1,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоДоходовБУ2 КАК СубконтоДоходовБУ2,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоДоходовБУ3 КАК СубконтоДоходовБУ3,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СчетРасходовБУ КАК СчетРасходовБУ,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоРасходовБУ1 КАК СубконтоРасходовБУ1,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоРасходовБУ2 КАК СубконтоРасходовБУ2,
	|	ПереоценкаВнеоборотныхАктивовОстатки.ДокументПереоценки.СубконтоРасходовБУ3 КАК СубконтоРасходовБУ3
	|ИЗ
	|	РегистрНакопления.ПереоценкаВнеоборотныхАктивов.Остатки(
	|		&Период,
	|		ВнеоборотныйАктив В (Выбрать ТаблицаОС.ОсновноеСредство ИЗ ТаблицаОС)
	|		    И Организация = &Организация И СтруктурноеПодразделение = &СтруктурноеПодразделение) КАК ПереоценкаВнеоборотныхАктивовОстатки
	|
	|УПОРЯДОЧИТЬ ПО
	|	ДокументПереоценки
	//|ИТОГИ
	//|	СУММА(СуммаПереоценкиСтоимостиОстаток),
	//|	СУММА(СуммаПереоценкиАмортизацииОстаток)
	//|ПО
	//|	ВнеоборотныйАктив
	|";
	
	Возврат ТекстЗапроса + ОбщегоНазначенияБКВызовСервера.ТекстРазделителяЗапросовПакета();
		
КонецФункции


#КонецЕсли

