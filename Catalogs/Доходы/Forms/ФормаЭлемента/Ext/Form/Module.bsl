
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
        КодыСтрокДекларацииПоНалогуНаПрибыль.Отбор, 
        "ВидДоходаРасхода", 
        Объект.Ссылка, 
        ВидСравненияКомпоновкиДанных.Равно, 
        "Вид дохода расхода", 
        Истина ,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный
    ); 
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
        СведенияОСтавкахРозничногоНалога.Отбор, 
        "ВидДохода", 
        Объект.Ссылка, 
        ВидСравненияКомпоновкиДанных.Равно, 
        "Вид дохода", 
        Истина ,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.Обычный
    );
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ КодыСтрокДекларацииПоНалогуНаПрибыль

&НаКлиенте
Процедура КодыСтрокДекларацииПоНалогуНаПрибыльПриАктивизацииСтроки(Элемент)
	
	КодСтроки = "";
	Если Элемент.ТекущиеДанные <> Неопределено Тогда
		КодСтроки = Строка(Элемент.ТекущиеДанные.КодСтроки);
	КонецЕсли;
	
	ОбновитьПредставлениеЭлемента("КодСтрокиДекларации",КодСтроки);

КонецПроцедуры

&НаКлиенте
Процедура КодыСтрокДекларацииПоНалогуНаПрибыльПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
						
		ТекстВопроса = НСтр("ru = 'Перед редактированием данных по Декларации необходимо записать элемент.
		|Записать?'");
		Режим = РежимДиалогаВопрос.ДаНет;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаписатьВФорме", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		Отказ = Истина;

	Иначе 
		
		ЗначенияЗаполнения = Новый Структура("ВидДоходаРасхода", Объект.Ссылка);
		Параметр = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура СведенияОСтавкахРозничногоНалогаПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если НЕ ЗначениеЗаполнено(Объект.Ссылка) Тогда
						
		ТекстВопроса = НСтр("ru = 'Перед редактированием данных по Декларации необходимо записать элемент.
		|Записать?'");
		Режим = РежимДиалогаВопрос.ДаНет; 
				
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаЗаписатьВФорме", ЭтотОбъект, Параметры);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		Отказ = Истина;

	Иначе 
		
		ЗначенияЗаполнения = Новый Структура("ВидДохода", Объект.Ссылка);
		Параметр = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
		
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ОбновитьПредставлениеЭлемента(ИмяОбновляемогоЭлемента, КодСтроки)
	
	Если ИмяОбновляемогоЭлемента = "КодСтрокиДекларации" Тогда
				
		Если ПустаяСтрока(СтрЗаменить(КодСтроки, ".", "")) Тогда
			Элементы.ДекорацияРасшифровкаКодСтроки.Заголовок = НСтр("ru ='<не указано>'");
		Иначе
			
			Если МакетКодовСтрок.ВысотаТаблицы = 0 Тогда
				
				МакетКодовСтрок = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КодыСтрокНалоговыхДеклараций");
				МакетКодовСтрок.КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
				
			КонецЕсли;
			
			ОбластьСтрок = МакетКодовСтрок.Области.Найти("НалогНаПрибыль_Доходы");
			
			НаименованиеСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(МакетКодовСтрок, ОбластьСтрок, КодСтроки);
			Если ПустаяСтрока(НаименованиеСтроки) Тогда
				Элементы.ДекорацияРасшифровкаКодСтроки.Заголовок = СтрШаблон(НСтр("ru ='строка с кодом %1 не найдена.'"), СокрЛП(КодСтроки));
			Иначе
				Элементы.ДекорацияРасшифровкаКодСтроки.Заголовок = НаименованиеСтроки;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;
	       
КонецПроцедуры // ОбновитьПредставлениеЭлемента()

&НаКлиенте
Процедура ПослеЗакрытияВопросаЗаписатьВФорме(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		// записываем объет
		Если Записать() Тогда
			
			Если ЭтотОбъект.ТекущийЭлемент.Имя = "КодыСтрокДекларацииПоНалогуНаПрибыль" Тогда
				
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			        КодыСтрокДекларацииПоНалогуНаПрибыль.Отбор, 
			        "ВидДоходаРасхода", 
			        Объект.Ссылка, 
			        ВидСравненияКомпоновкиДанных.Равно, 
			        "Вид дохода расхода", 
			        Истина
			    );        
				ЗначенияЗаполнения = Новый Структура("ВидДоходаРасхода", Объект.Ссылка);
				ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
				ОткрытьФорму("РегистрСведений.КодыСтрокДекларацииПоНалогуНаПрибыль.ФормаЗаписи", ПараметрыФормы, ЭтаФорма);    
			
			ИначеЕсли ЭтотОбъект.ТекущийЭлемент.Имя = "СведенияОСтавкахРозничногоНалога" Тогда
 
				ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбора(
			        СведенияОСтавкахРозничногоНалога.Отбор, 
			        "ВидДохода", 
			        Объект.Ссылка, 
			        ВидСравненияКомпоновкиДанных.Равно, 
			        "Вид дохода", 
			        Истина
		    	);        
		
				ЗначенияЗаполнения = Новый Структура("ВидДохода", Объект.Ссылка);
				ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
				ОткрытьФорму("РегистрСведений.СведенияОСтавкахРозничногоНалога.ФормаЗаписи", ПараметрыФормы, ЭтаФорма); 
				
			КонецЕсли;		
		КонецЕсли;
	КонецЕсли;	
	
КонецПроцедуры	
