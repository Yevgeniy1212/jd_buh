
#Область ОбработчикиСобытийЭлементовТаблицыФормыПрофессиональныеНавыки

&НаКлиенте
Процедура ПрофессиональныеНавыкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидКлассификатора",  "ПрофессиональныеНавыки");
	ПараметрыФормы.Вставить("Заголовок",   НСтр("ru = 'Специальные навыки'"));
	ПараметрыФормы.Вставить("Уровень",     1);
	ПараметрыФормы.Вставить("ВыборГруппы", Истина);
	
	ОткрытьФорму("Обработка.ОбменЭТД.Форма.ФормаПодбораКлассификатора",
		ПараметрыФормы,
		,
		,
		,
		,
		Новый ОписаниеОповещения("ОбработатьВыборСпециальныхНавыков",
		ЭтотОбъект));

КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормыОбщиеНавыки

&НаКлиенте
Процедура ОбщиеНавыкиПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("ВидКлассификатора",  "ОбщиеНавыки");
	ПараметрыФормы.Вставить("Заголовок",   НСтр("ru = 'Общие навыки'"));
	ПараметрыФормы.Вставить("Уровень",     3);
	ПараметрыФормы.Вставить("ВыборГруппы", Истина);
	
	ОткрытьФорму("Обработка.ОбменЭТД.Форма.ФормаПодбораКлассификатора",
		ПараметрыФормы,
		,
		,
		,
		,
		Новый ОписаниеОповещения("ОбработатьВыборОбщихНавыков",
		ЭтотОбъект));

КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ОбработатьВыборСпециальныхНавыков(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> неопределено Тогда
		ОбработатьВыборСпециальныхНавыковНаСервере(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборСпециальныхНавыковНаСервере(Результат, ДополнительныеПараметры)
	
	Если Результат <> Неопределено Тогда
		
		ТаблицаНавыков = ПолучитьИзВременногоХранилища(Результат);
		
		Если Тип("ТаблицаЗначений") = ТипЗнч(ТаблицаНавыков) Тогда
			
			Для Каждого СтрокаТаблицы Из ТаблицаНавыков Цикл
				НоваяСтрока = Объект.ПрофессиональныеНавыки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			КонецЦикла;
			Модифицированность = Истина;
		
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработатьВыборОбщихНавыков(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат <> неопределено Тогда
		ОбработатьВыборОбщихНавыковНаСервере(Результат, ДополнительныеПараметры);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ОбработатьВыборОбщихНавыковНаСервере(Результат, ДополнительныеПараметры)
	
	Если Результат <> Неопределено Тогда
		
		ТаблицаНавыков = ПолучитьИзВременногоХранилища(Результат);
		
		Если Тип("ТаблицаЗначений") = ТипЗнч(ТаблицаНавыков) Тогда
			
			Для Каждого СтрокаТаблицы Из ТаблицаНавыков Цикл
				НоваяСтрока = Объект.ОбщиеНавыки.Добавить();
				ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТаблицы);
			КонецЦикла;
			Модифицированность = Истина;
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти


