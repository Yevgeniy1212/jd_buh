
////////////////////////////////////////////////////////////////////////////////
//ОБРАБОТЧИКИ СОБЫТИЙ  ФОРМЫ

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если  ВРег(ИсточникВыбора.ИмяФормы) = ВРег("Справочник.НоменклатураГСВС.Форма.ФормаВыбора") Тогда
		
		УстановитьКодТНВЭД(ВыбранноеЗначение);
		
		Модифицированность = Истина;
		
	КонецЕсли;

	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ЗаполнитьСписокВыбораПоляНаименование();
	СформироватьСписокВыбораПризнакаПроисхождения();
	УстановитьУсловноеОформление();
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
//ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте

Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
	
КонецПроцедуры

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерСтрокиГТДПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура СпособПроисхожденияТовараПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
	Если Объект.СпособПроисхожденияТовара  =  ПредопределенноеЗначение("Перечисление.СпособыПроисхожденияТоваров.СТ1") Тогда
		Объект.СтранаПроисхожденияТовара = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.Казахстан"); 
	Иначе
		Если Объект.СтранаПроисхожденияТовара = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.Казахстан") Тогда
			Объект.СтранаПроисхожденияТовара = ПредопределенноеЗначение("Справочник.КлассификаторСтранМира.ПустаяСсылка");
		КонецЕсли;				
	КонецЕсли;
	
	ОбновитьНаименование();
	
	Объект.ПризнакПроисхождения = Неопределено;
	СформироватьСписокВыбораПризнакаПроисхождения();
	
КонецПроцедуры

&НаКлиенте
Процедура НомерСертификатаПроисхожденияТовараПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ДатаСертификатаПроисхожденияТовараПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура СтранаПроисхожденияПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура КодТНВЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
		
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		  , Истина);
	СтруктураОтбора = Новый Структура;
	СтруктураОтбора.Вставить("ТипКодаГСВС", ПредопределенноеЗначение("Перечисление.ТипыКодовГСВС.ТНВЭД"));
	ПараметрыФормы.Вставить("Отбор", СтруктураОтбора);
	ПараметрыФормы.Вставить("ТекущийКодТНВЭД"	  , ?(НЕ ЗначениеЗаполнено(Объект.КодТНВЭД), Неопределено, СокрЛП(Объект.КодТНВЭД)));
	
	ОткрытьФорму("Справочник.НоменклатураГСВС.ФормаВыбора", ПараметрыФормы, ЭтаФорма, Истина);

КонецПроцедуры

&НаКлиенте
Процедура КодТНВЭДПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакПроисхожденияПриИзменении(Элемент)
	
	ОбновитьНаименование();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Элементы = Форма.Элементы;	
	Объект = Форма.Объект;
	ВидимостьДанныхГТД = НЕ(Объект.СпособПроисхожденияТовара = ПредопределенноеЗначение("Перечисление.СпособыПроисхожденияТоваров.СТ1"));
	
	Элементы.ДатаСертификатаПроисхожденияТовара.Видимость = НЕ ВидимостьДанныхГТД;
	
	Элементы.Код.Заголовок = ?(ВидимостьДанныхГТД, НСтр("ru = 'Номер ГТД'"), НСтр("ru = 'Номер сертификата'"));
	Элементы.НомерСтрокиГТД.Видимость = ВидимостьДанныхГТД;	
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьСписокВыбораПоляНаименование()
	ВариантыНаименованийНомераГТД = Справочники.НомераГТД.ВариантыНаименованийНомераГТД(Объект);
	Элементы.Наименование.СписокВыбора.ЗагрузитьЗначения(ВариантыНаименованийНомераГТД);	
КонецПроцедуры

&НаСервере
Процедура ОбновитьНаименование()
		
	ВариантыНаименованийНомераГТД = Справочники.НомераГТД.ВариантыНаименованийНомераГТД(Объект);
	Элементы.Наименование.СписокВыбора.ЗагрузитьЗначения(ВариантыНаименованийНомераГТД);
	Объект.Наименование = ВариантыНаименованийНомераГТД[ВариантыНаименованийНомераГТД.ВГраница()];
		
КонецПроцедуры

&НаСервере
//Процедура формирует список выбора для признака происхождения в зависимости от способа происхождения.
//
Процедура СформироватьСписокВыбораПризнакаПроисхождения()
	
	ПредставленияПризнаков = НомераГТДКлиентСервер.ПредставленияПризнаковПроисхождения();
	
	Элементы.ПризнакПроисхождения.РежимВыбораИзСписка = Истина;
	Элементы.ПризнакПроисхождения.СписокВыбора.Очистить();
	Если ЗначениеЗаполнено(Объект.СпособПроисхожденияТовара) Тогда
		Если Объект.СпособПроисхожденияТовара = ПредопределенноеЗначение("Перечисление.СпособыПроисхожденияТоваров.СТ1") Тогда
			Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("3", ПредставленияПризнаков[2]);
			Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("4", ПредставленияПризнаков[3]);
			Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("5", ПредставленияПризнаков[4]);
		ИначеЕсли Объект.СпособПроисхожденияТовара = ПредопределенноеЗначение("Перечисление.СпособыПроисхожденияТоваров.ЕТТЕАЭС")
			ИЛИ Объект.СпособПроисхожденияТовара = ПредопределенноеЗначение("Перечисление.СпособыПроисхожденияТоваров.ВТО")
			ИЛИ Объект.СпособПроисхожденияТовара = ПредопределенноеЗначение("Перечисление.СпособыПроисхожденияТоваров.ТС") Тогда
			Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("1", ПредставленияПризнаков[0]);
			Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("2", ПредставленияПризнаков[1]);
			Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("5", ПредставленияПризнаков[4]);
		КонецЕсли;
	Иначе
		Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("1", ПредставленияПризнаков[0]);
		Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("2", ПредставленияПризнаков[1]);
		Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("3", ПредставленияПризнаков[2]);
		Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("4", ПредставленияПризнаков[3]);
		Элементы.ПризнакПроисхождения.СписокВыбора.Добавить("5", ПредставленияПризнаков[4]);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьУсловноеОформление()
	
	//устанавливаем условное оформление для реквизита Признак происхождения
	Для Каждого ЭлемСЗ Из Элементы.ПризнакПроисхождения.СписокВыбора Цикл
		ЭлементУО = УсловноеОформление.Элементы.Добавить();
		
		// оформляемое поле
		ОформляемоеПоле = ЭлементУО.Поля.Элементы.Добавить();
		ОформляемоеПоле.Поле = Новый ПолеКомпоновкиДанных(Элементы.ПризнакПроисхождения.Имя);
		// отбор
		ОтборУО = ЭлементУО.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
		ОтборУО.ЛевоеЗначение = Новый ПолеКомпоновкиДанных("Объект.ПризнакПроисхождения");
		ОтборУО.ВидСравнения = ВидСравненияКомпоновкиДанных.Равно;
		ОтборУО.ПравоеЗначение = ЭлемСЗ.Значение;
		// оформление
		ЭлементУО.Оформление.УстановитьЗначениеПараметра("Текст", ЭлемСЗ.Представление);
		
	КонецЦикла;
	
	Элементы.РегистрационныйНомерЭСФ.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьОбменЭСФ");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьКодТНВЭД(НомеклатураГСВС)
	
	Объект.КодТНВЭД = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(НомеклатураГСВС, "КодГСВС");
	
КонецПроцедуры
