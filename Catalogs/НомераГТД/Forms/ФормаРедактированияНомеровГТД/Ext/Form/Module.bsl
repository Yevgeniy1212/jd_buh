
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Блок должен быть сверху, так как переменные используются ниже.
	Дата = Параметры.Дата;
	Организация = Параметры.Организация;
	Ссылка = Параметры.Ссылка;
	СтруктурноеПодразделение = Параметры.СтруктурноеПодразделение;
	ДокументМодифицирован   = Параметры.ДокументМодифицирован;
	
	ЭтаФорма.ТолькоПросмотр = Параметры.ТолькоПросмотр;
	Элементы.ДеревоНомеровГТДКнопкаИзменить.Доступность = НЕ Параметры.ТолькоПросмотр;
	
	// Если помещаемое значение не привязать к уникальному идентификатору, то со временем оно будет удалено.
	ТаблицаТовары = ПолучитьИзВременногоХранилища(Параметры.АдресТовары);
	КопияТаблицыТовары = ТаблицаТовары.Скопировать();
	АдресТовары = ПоместитьВоВременноеХранилище(КопияТаблицыТовары, ЭтаФорма.УникальныйИдентификатор);
	
	// Если помещаемое значение не привязать к уникальному идентификатору, то со временем оно будет удалено.
	ТаблицаНомераГТД = ПолучитьИзВременногоХранилища(Параметры.АдресНомераГТД);
	КопияТаблицыНомераГТД = ТаблицаНомераГТД.Скопировать();
	АдресНомераГТД = ПоместитьВоВременноеХранилище(КопияТаблицыНомераГТД, ЭтаФорма.УникальныйИдентификатор);
	
	ЕстьРеквизитНовыйНомерГТД = (ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПеремещениеТоваров"));
	ЕстьРеквизитКоэффициент = (НЕ ТаблицаТовары.Колонки.Найти("Коэффициент") = Неопределено);
	ЗаполнятьОстатки = Истина;
	
	Если Параметры.Свойство("ЗаполнятьОстатки") Тогда
		ЗаполнятьОстатки = Параметры.ЗаполнятьОстатки;
	КонецЕсли; 
	
	ВедетсяУчетПоСкладам  = ?(ПланыСчетов.Типовой.Товары.ВидыСубконто.Найти(ПланыВидовХарактеристик.ВидыСубконтоТиповые.Склады, "ВидСубконто") = Неопределено, Ложь, Истина);	
			
	ЕстьРеквизитСклад       = (Параметры.Свойство("Склад", Склад) <> Неопределено И ЗначениеЗаполнено(Склад));	
		
	ЗаполнитьДеревоНомеровГТД(АдресТовары, АдресНомераГТД);
	
	Элементы.НовыйНомерГТД.Видимость = ЕстьРеквизитНовыйНомерГТД;		

КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Элементы.КартинкаВнимание.Видимость = ДокументМодифицирован;
	Элементы.НадписьДокументМодифицирован.Видимость = ДокументМодифицирован;	
	Элементы.ДеревоНомеровГТДГруппаЗаполнить.Видимость = ЗаполнятьОстатки;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПеренестиВДокумент(Команда)
	
	АдресНомеровГТД = ПоместитьНомераГТДВХранилище();
	Закрыть(КодВозвратаДиалога.OK);
	ОповеститьОВыборе(АдресНомеровГТД);
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНомераГТДСначалаЗаполненными(Команда)
	
	Если ПодсистемаОценкаПроизводительностиСуществует() Тогда
		КлючеваяОперация = "Справочник ""источники происхождения"" (заполнение сначала заполненными)";
		ОбщегоНазначенияКлиент.ОбщийМодуль("ОценкаПроизводительностиКлиент").НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;

	ЗаполнитьНомераГТДНаСервере(НомераГТДКлиентСервер.СпособЗаполнения_СначалаЗаполненными());
	
КонецПроцедуры

&НаКлиенте
Процедура ЗаполнитьНомераГТДСначалаПустыми(Команда)
	
	Если ПодсистемаОценкаПроизводительностиСуществует() Тогда
		КлючеваяОперация = "Справочник ""источники происхождения"" (заполнение сначала пустыми)";
		ОбщегоНазначенияКлиент.ОбщийМодуль("ОценкаПроизводительностиКлиент").НачатьЗамерВремени(Истина, КлючеваяОперация);
	КонецЕсли;
	
	ЗаполнитьНомераГТДНаСервере(НомераГТДКлиентСервер.СпособЗаполнения_СначалаПустыми());
	
КонецПроцедуры

&НаСервере
Процедура ЗаполнитьНомераГТДНаСервере(Знач СпособЗаполнения)
		
	НомерГТД = НоваяТаблицаНомераГТД();
	Объект = Новый Структура;
	//Объект.Вставить("Товары", ПолучитьИзВременногоХранилища(АдресТовары));
	Объект.Вставить("НомераГТД", НомерГТД);
	Объект.Вставить("Дата", Дата);
	Объект.Вставить("Ссылка", Ссылка);
	Объект.Вставить("Организация", Организация);
	Объект.Вставить("СтруктурноеПодразделение", СтруктурноеПодразделение);
	Объект.Вставить("Склад", Склад);
	
	НомераГТДСервер.ЗаполнитьТаблицуНомераГТД(Объект,ПолучитьИзВременногоХранилища(АдресТовары),НомерГТД, СпособЗаполнения, Истина);	
	
	АдресНомераГТД = ПоместитьВоВременноеХранилище(Объект.НомераГТД, ЭтаФорма.УникальныйИдентификатор);	
	ЗаполнитьДеревоНомеровГТД(АдресТовары, АдресНомераГТД);	
	УдалитьИзВременногоХранилища(АдресНомераГТД);
	
	Модифицированность = Истина;

	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийТаблицыФормы

&НаКлиенте
Процедура ДеревоНомеровГТДВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	// Номенклатуру запрещается изменять всегда.
	Если ЭтаФорма.ТолькоПросмотр ИЛИ Поле.Имя = "Номенклатура" Тогда
		
		ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные[Поле.Имя]);
		СтандартнаяОбработка = Ложь;
		
	ИначеЕсли Поле.Имя = "ХарактеристикаНоменклатуры" ИЛИ Поле.Имя = "СерияНоменклатуры" Тогда
		
		// В документе СчетФактураВыданный разрешается редактировать Характеристки и Серии
		// в данной форме, так как в таблице Товары отсутствуют Характеристки и Серии.
		Если ТипЗнч(Ссылка) <> Тип("ДокументСсылка.СчетФактураВыданный") Тогда
			ПоказатьЗначение(Неопределено, Элемент.ТекущиеДанные[Поле.Имя]);
			СтандартнаяОбработка = Ложь;			
		КонецЕсли;
	
	КонецЕсли;

	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНомеровГТДПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
			
	Если Элемент.ТекущиеДанные = Неопределено Тогда
		// В таблице Товары нет ни одной строки.
		Отказ = Истина;
		Возврат;
	КонецЕсли;
	
	СтрокаРодитель = Элемент.ТекущиеДанные.ПолучитьРодителя();
	
	Если НЕ Копирование И СтрокаРодитель <> Неопределено Тогда
	 
		Отказ = Истина;
		Элемент.ТекущаяСтрока = СтрокаРодитель.ПолучитьИдентификатор();
		Элемент.ДобавитьСтроку();
		
	ИначеЕсли Копирование И СтрокаРодитель = Неопределено Тогда
	 
		Отказ = Истина;
		
	КонецЕсли;

КонецПроцедуры

&НаКлиенте
Процедура ДеревоНомеровГТДПередУдалением(Элемент, Отказ)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	Если СтрокаТаблицы.ПолучитьРодителя() = Неопределено Тогда
		ПоказатьПредупреждение(Неопределено, НСтр("ru = 'Корневую строку дерева удалять нельзя.'"));
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНомеровГТДПослеУдаления(Элемент)
	РодительскаяСтрока = РодительскаяСтрока(Элементы.ДеревоНомеровГТД.ТекущиеДанные);
	ЗаполнитьПолеИнформация(РодительскаяСтрока);
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНомеровГТДПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	СтрокаТаблицы = Элемент.ТекущиеДанные;
	СтрокаРодитель = СтрокаТаблицы.ПолучитьРодителя();
	
	Если НоваяСтрока И НЕ Копирование Тогда
			
		СуммаКоличествДочернихСтрок = 0;
		ДочерниеСтроки = СтрокаРодитель.ПолучитьЭлементы();
		Для Каждого ДочерняяСтрока Из ДочерниеСтроки Цикл
			СуммаКоличествДочернихСтрок = СуммаКоличествДочернихСтрок + ДочерняяСтрока.Количество;
		КонецЦикла;
		
		СтрокаТаблицы.НомерСтроки   = 0;
		СтрокаТаблицы.Номенклатура  = СтрокаРодитель.Номенклатура;		
		СтрокаТаблицы.Коэффициент   = СтрокаРодитель.Коэффициент;
		СтрокаТаблицы.Количество    = СтрокаРодитель.Количество - СуммаКоличествДочернихСтрок;
		СтрокаТаблицы.КлючСвязи	    = СтрокаРодитель.КлючСвязи;
		СтрокаТаблицы.НомерГТД      = ПредопределенноеЗначение("Справочник.НомераГТД.ПустаяСсылка");
		СтрокаТаблицы.НовыйНомерГТД = ПредопределенноеЗначение("Справочник.НомераГТД.ПустаяСсылка");		
		СтрокаТаблицы.Склад         = СтрокаРодитель.Склад;		
		СтрокаТаблицы.ЭтоРодительскаяСтрока = Ложь;
		
	КонецЕсли;
	
	ЗаполнитьПолеИнформация(СтрокаРодитель);
	
КонецПроцедуры

&НаКлиенте
Процедура ДеревоНомеровГТДНомерГТДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	ПараметрыВыбора = ПараметрыВыбораНомераГТД(Элементы.ДеревоНомеровГТД.ТекущаяСтрока, "НовыйНомерГТД");	
	НомераГТДКлиент.ТоварыНомерГТДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка, ПараметрыВыбора);

	
КонецПроцедуры

&НаКлиенте
Процедура КоличествоПриИзменении(Элемент)
	РодительскаяСтрока = РодительскаяСтрока(Элементы.ДеревоНомеровГТД.ТекущиеДанные);
	ЗаполнитьПолеИнформация(РодительскаяСтрока);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФУнкции

Функция НоваяТаблицаНомераГТД()
	
	НомераГТД = Новый ТаблицаЗначений;
	
	КвалификаторЧисла = Новый КвалификаторыЧисла(15, 3, ДопустимыйЗнак.Любой);
	ТипЧисло = Новый ОписаниеТипов("Число", КвалификаторЧисла);
	
	НомераГТД.Колонки.Добавить("НомерСтроки", ТипЧисло);
	НомераГТД.Колонки.Добавить("КлючСвязи", ТипЧисло);
	НомераГТД.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.Номенклатура"));	
	НомераГТД.Колонки.Добавить("НомерГТД", Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	НомераГТД.Колонки.Добавить("Коэффициент", ТипЧисло);
	НомераГТД.Колонки.Добавить("Количество", ТипЧисло);
	
	Если ТипЗнч(Ссылка) = Тип("ДокументСсылка.ПеремещениеТоваров") Тогда
		НомераГТД.Колонки.Добавить("НовыйНомерГТД", Новый ОписаниеТипов("СправочникСсылка.НомераГТД"));
	КонецЕсли;
		
	НомераГТД.Колонки.Добавить("Склад", Новый ОписаниеТипов("СправочникСсылка.Склады"));
	
	Возврат НомераГТД;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьДеревоНомеровГТД(АдресТовары, АдресНомераГТД)
	
	ОценкаПроизводительностиСуществует = ПодсистемаОценкаПроизводительностиСуществует();
	Если ОценкаПроизводительностиСуществует Тогда
		ВремяНачалаЗамера = ОбщегоНазначения.ОбщийМодуль("ОценкаПроизводительности").НачатьЗамерВремени();		
	КонецЕсли;
	
	ДеревоНомеровГТД.ПолучитьЭлементы().Очистить();
	
	// Получить таблицы значений, переданные из документа.
	ТабЗначТовары = ПолучитьИзВременногоХранилища(АдресТовары);	
	ТабЗначНомераГТД = ПолучитьИзВременногоХранилища(АдресНомераГТД); 
	
	// Заполнить ДеревоНомеровГТД.
	Для Каждого СтрокаТабЗначТовары Из ТабЗначТовары Цикл
		
		РодительскаяСтрокаДерева = ДеревоНомеровГТД.ПолучитьЭлементы().Добавить();	
		РодительскаяСтрокаДерева.НомерСтроки = СтрокаТабЗначТовары.НомерСтроки;
		РодительскаяСтрокаДерева.Номенклатура = СтрокаТабЗначТовары.Номенклатура;
						
		РодительскаяСтрокаДерева.Склад = Склад;			
				
		Если ЕстьРеквизитКоэффициент Тогда
			РодительскаяСтрокаДерева.Коэффициент = СтрокаТабЗначТовары.Коэффициент;
		Иначе
			РодительскаяСтрокаДерева.Коэффициент = 1;
		КонецЕсли;
		
		РодительскаяСтрокаДерева.НомерГТД = Неопределено;
		
		РодительскаяСтрокаДерева.Количество = СтрокаТабЗначТовары.Количество;
		РодительскаяСтрокаДерева.КлючСвязи = СтрокаТабЗначТовары.КлючСвязи;
		РодительскаяСтрокаДерева.ЭтоРодительскаяСтрока = Истина;
		
		// Предполагается, что в таблице Товары может быть строка, а в таблице НомераГТД строки нет.
		// Также предполагается, что ситуация, когда в таблице НомераГТД есть строка с определенным ключом,
		// а в таблице Товары строки с таким ключом нет - невозможна.
		ПараметрыОтбора = Новый Структура();
		ПараметрыОтбора.Вставить("КлючСвязи", СтрокаТабЗначТовары.КлючСвязи);
		МассивСтрокНомеровГТД = ТабЗначНомераГТД.НайтиСтроки(ПараметрыОтбора);
			
		Для Каждого СтрокаНомерГТД Из МассивСтрокНомеровГТД Цикл
			
			ДочернииСтроки = РодительскаяСтрокаДерева.ПолучитьЭлементы();
			
			ДочерняяСтрока = ДочернииСтроки.Добавить();
			
			ДочерняяСтрока.НомерСтроки  = Неопределено;
			ДочерняяСтрока.Номенклатура = РодительскаяСтрокаДерева.Номенклатура;
							
			Если ЕстьРеквизитКоэффициент Тогда
				ДочерняяСтрока.Коэффициент = РодительскаяСтрокаДерева.Коэффициент;
			Иначе
				ДочерняяСтрока.Коэффициент = 1;
			КонецЕсли;
			
			Если ЕстьРеквизитСклад Тогда
				ДочерняяСтрока.Склад = РодительскаяСтрокаДерева.Склад;
			КонецЕсли;      
			
			ДочерняяСтрока.НомерГТД = СтрокаНомерГТД.НомерГТД;
			
			ДочерняяСтрока.Количество = СтрокаНомерГТД.Количество;
			ДочерняяСтрока.КлючСвязи  = СтрокаНомерГТД.КлючСвязи;
			
			Если ЕстьРеквизитНовыйНомерГТД Тогда
				ДочерняяСтрока.НовыйНомерГТД = СтрокаНомерГТД.НовыйНомерГТД;
			КонецЕсли;
			
		КонецЦикла;
		
	КонецЦикла;
	
	// Заполнить колонку Информация.
	РодительскиеСтрокиДерева = ДеревоНомеровГТД.ПолучитьЭлементы();
	Для Каждого РодительскаяСтрока Из РодительскиеСтрокиДерева Цикл
		ЗаполнитьПолеИнформация(ДеревоНомеровГТД);
	КонецЦикла;
	
	Если ОценкаПроизводительностиСуществует Тогда
		ОбщегоНазначения.ОбщийМодуль("ОценкаПроизводительности").ЗакончитьЗамерВремени("Справочник ""источники происхождения"" (Заполнение дерева номеров гтд)", ВремяНачалаЗамера);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ПоместитьНомераГТДВХранилище()
	
	// Создать новую ТабЗначНомераГТД и задать ее колонки.
	ТабЗначНомераГТД = Новый ТаблицаЗначений();
	ДеревоЗначНомераГТД = РеквизитФормыВЗначение("ДеревоНомеровГТД");
	Для Каждого КолонкаДерева Из ДеревоЗначНомераГТД.Колонки Цикл
		Если КолонкаДерева.Имя <> "НомерСтроки" Тогда
			ТабЗначНомераГТД.Колонки.Добавить(КолонкаДерева.Имя, КолонкаДерева.ТипЗначения);
		КонецЕсли;
	КонецЦикла;
	
	РодительскиеСтрокиДерева = ДеревоНомеровГТД.ПолучитьЭлементы();	
	Для Каждого РодительскаяСтрока Из РодительскиеСтрокиДерева Цикл
		
		ДочерниеСтрокиДерева = РодительскаяСтрока.ПолучитьЭлементы();
		
		Для Каждого ДочерняяСтрока Из ДочерниеСтрокиДерева Цикл
			
			СтрокаНомераГТД = ТабЗначНомераГТД.Добавить();	
			СтрокаНомераГТД.КлючСвязи = ДочерняяСтрока.КлючСвязи;
			СтрокаНомераГТД.Номенклатура = ДочерняяСтрока.Номенклатура;								
			СтрокаНомераГТД.НомерГТД = ДочерняяСтрока.НомерГТД;
			СтрокаНомераГТД.Коэффициент = ДочерняяСтрока.Коэффициент;
			СтрокаНомераГТД.Количество = ДочерняяСтрока.Количество;
			Если ЕстьРеквизитНовыйНомерГТД Тогда
				СтрокаНомераГТД.НовыйНомерГТД = ДочерняяСтрока.НовыйНомерГТД;
			КонецЕсли; 			
			Если ВедетсяУчетПоСкладам Тогда
				СтрокаНомераГТД.Склад = ДочерняяСтрока.Склад;
			КонецЕсли;                                      							
		КонецЦикла;
		
	КонецЦикла;
	
	АдресНомераГТД = ПоместитьВоВременноеХранилище(ТабЗначНомераГТД);
	
	Возврат АдресНомераГТД;
	
КонецФункции

&НаКлиенте
Функция ПараметрыВыбораНомераГТД(ТекущаяСтрока,ИмяРеквизитаНомерГТД)
	
	ПараметрыВыбора = НомераГТДКлиент.ПустыеПараметрыВыбораНомераГТД();
	
	ПараметрыВыбора.Ссылка = Ссылка;
	ПараметрыВыбора.Дата = Дата;	
	ПараметрыВыбора.Организация = Организация;
	ПараметрыВыбора.СтруктурноеПодразделение = СтруктурноеПодразделение;
	ПараметрыВыбора.ПоказыватьТолькоОстатки = Истина;
	
	ТекущиеДанные = ДеревоНомеровГТД.НайтиПоИдентификатору(ТекущаяСтрока);
	ПараметрыВыбора.Товар = ТекущиеДанные.Номенклатура;
	
	Если ВедетсяУчетПоСкладам И ЕстьРеквизитСклад Тогда
		ПараметрыВыбора.Склад = Склад;
	КонецЕсли;    	
	
	ПараметрыВыбора.НомерГТД = ТекущиеДанные[ИмяРеквизитаНомерГТД];
	
	Возврат ПараметрыВыбора; 	
КонецФункции

&НаКлиентеНаСервереБезКонтекста
Процедура ЗаполнитьПолеИнформация(ДеревоНомеровГТД)
	
	КоллекцияРодителей = ДеревоНомеровГТД.ПолучитьЭлементы();
	
	Для Каждого СтрокаРодитель Из КоллекцияРодителей Цикл
		
		СуммаКоличествДочернихСтрок = 0;
		ДочерниеСтроки = СтрокаРодитель.ПолучитьЭлементы();
		Для Каждого ДочерняяСтрока Из ДочерниеСтроки Цикл
			СуммаКоличествДочернихСтрок = СуммаКоличествДочернихСтрок + ДочерняяСтрока.Количество;
		КонецЦикла;
		
		Если СтрокаРодитель.Количество > СуммаКоличествДочернихСтрок Тогда
			СтрокаРодитель.Информация = "Недостаточно: " + Формат(СтрокаРодитель.Количество - СуммаКоличествДочернихСтрок, "ЧДЦ=3");
		ИначеЕсли СтрокаРодитель.Количество < СуммаКоличествДочернихСтрок Тогда
			СтрокаРодитель.Информация = "Превышение: " + Формат(СуммаКоличествДочернихСтрок - СтрокаРодитель.Количество, "ЧДЦ=3");
		Иначе
			СтрокаРодитель.Информация = "";
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция РодительскаяСтрока(СтрокаДерева)
	
	РодительскаяСтрока = Неопределено;
	
	Если СтрокаДерева.ПолучитьРодителя() = Неопределено Тогда
		РодительскаяСтрока = СтрокаДерева;
	Иначе
		РодительскаяСтрока = СтрокаДерева.ПолучитьРодителя();	
	КонецЕсли;
	
	Возврат РодительскаяСтрока;
	
КонецФункции

&НаСервереБезКонтекста
Функция ПодсистемаОценкаПроизводительностиСуществует()
		
	Подсистема = Метаданные.НайтиПоПолномуИмени("Подсистема.СтандартныеПодсистемы.Подсистема.ОценкаПроизводительности");
	
	Возврат ?(Подсистема = Неопределено, Ложь, Истина);
	
КонецФункции

#КонецОбласти