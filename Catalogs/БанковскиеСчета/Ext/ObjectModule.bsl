#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		
		Если НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда 
			ВалютаДенежныхСредств = Константы.ВалютаРегламентированногоУчета.Получить();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации")И НЕ ЭтоНовый() Тогда
		Если Не Справочники.БанковскиеСчета.ИспользуетсяНесколькоБанковскихСчетовОрганизации(Владелец)
			И ПометкаУдаления Тогда
				
			ТекстСообщения = НСтр("ru = 'Нельзя удалить единственный банковский счет, т.к. он используется для автозаполнения документов.'"); 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка,, "Объект", Отказ);	
		КонецЕсли;	
	КонецЕсли;	


КонецПроцедуры


Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.Организации") Тогда
		Справочники.БанковскиеСчета.ПроверитьУстановитьЗначениеОпцииИспользоватьНесколькоБанковскихСчетовОрганизации(Владелец);
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция СуществуютСсылки() Экспорт

	Запрос = Новый Запрос();

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1	
	|	ТиповойОбороты.Счет
	|ИЗ
	|	РегистрБухгалтерии.Типовой.Обороты(, , , , , Субконто1 = &Счет ИЛИ Субконто2 = &Счет ИЛИ Субконто3 = &Счет, , ) КАК ТиповойОбороты"; 	

	Запрос.УстановитьПараметр("Счет", Ссылка);

	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

#КонецЕсли
