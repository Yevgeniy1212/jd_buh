
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("АвтоТест") Тогда // Возврат при получении формы для анализа.
		Возврат;
	КонецЕсли;
	
	Если Параметры.Ключ.Пустая() Тогда
		НомерТекущегоГода = Год(ТекущаяДатаСеанса());
		СоздатьСтрокиТаблицыДанныхГрафика();
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура СоздатьСтрокиТаблицыДанныхГрафика()
	
	Для НомерМесяца = 1 По 12 Цикл
		
		ДобавитьСтрокуВТаблицуДанныхГрафика(НомерМесяца);	
			
	КонецЦикла;		
	
КонецПроцедуры

&НаСервере
Процедура ДобавитьСтрокуВТаблицуДанныхГрафика(НомерМесяца)
	СтрокаТаблицы = ДанныеГрафика.Добавить();
	СтрокаТаблицы.НомерМесяца = НомерМесяца;
	СтрокаТаблицы.МесяцПредставление = ПредставлениеМесяца(НомерТекущегоГода, НомерМесяца);	
КонецПроцедуры	

&НаКлиентеНаСервереБезКонтекста
Функция ПредставлениеМесяца(НомерТекущегоГода, НомерМесяца)
	Возврат Формат(Дата(НомерТекущегоГода, НомерМесяца, 1), "ДФ=ММММ");;	
КонецФункции

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	Если НомерТекущегоГода = 0 Тогда
		НомерТекущегоГода = Год(ТекущаяДатаСеанса());
	КонецЕсли;
	Если ДанныеГрафика.Количество() = 0 Тогда
		СоздатьСтрокиТаблицыДанныхГрафика();
	КонецЕсли;
	
	ПрочитатьДанныеГрафикаВФорму();
		
КонецПроцедуры

&НаСервере
Процедура ПрочитатьДанныеГрафикаВФорму()
	
	ОчиститьДанныеГрафика();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	киб_ДанныеГрафиковРаботыСотрудников.ГрафикРаботы КАК ГрафикРаботы,
		|	киб_ДанныеГрафиковРаботыСотрудников.Месяц КАК Месяц,
		|	киб_ДанныеГрафиковРаботыСотрудников.ИтогДни КАК ИтогДни,
		|	киб_ДанныеГрафиковРаботыСотрудников.ИтогЧасы КАК ИтогЧасы
		|ИЗ
		|	РегистрСведений.киб_ДанныеГрафиковРаботыСотрудников КАК киб_ДанныеГрафиковРаботыСотрудников
		|ГДЕ
		|	киб_ДанныеГрафиковРаботыСотрудников.ГрафикРаботы = &ГрафикРаботы
		|	И киб_ДанныеГрафиковРаботыСотрудников.Месяц МЕЖДУ &ДатаНачала И &ДатаОкончания
		|	И киб_ДанныеГрафиковРаботыСотрудников.Месяц < &ДатаОкончания
		|
		|УПОРЯДОЧИТЬ ПО
		|	Месяц";
	
	Запрос.УстановитьПараметр("ГрафикРаботы", Объект.Ссылка);
	Запрос.УстановитьПараметр("ДатаНачала", Дата(НомерТекущегоГода, 1, 1));
	Запрос.УстановитьПараметр("ДатаОкончания", Дата(НомерТекущегоГода + 1, 1, 1));
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		СтруктураПоиска = Новый Структура("НомерМесяца", Месяц(Выборка.Месяц));
		НайденныеСтроки = ДанныеГрафика.НайтиСтроки(СтруктураПоиска);
		Если НайденныеСтроки.Количество() > 0 Тогда
			ЗаполняемаяСтрока = НайденныеСтроки[0];
			ЗаполнитьЗначенияСвойств(ЗаполняемаяСтрока, Выборка);
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры	 

&НаСервере
Процедура ОчиститьДанныеГрафика()
	Для Каждого СтрокаДанныхГрафика Из ДанныеГрафика Цикл
		СтрокаДанныхГрафика.ИтогДни = 0;
		СтрокаДанныхГрафика.ИтогЧасы = 0;
	КонецЦикла;	
КонецПроцедуры	  

&НаКлиенте
Процедура НомерТекущегоГодаРегулирование(Элемент, Направление, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	Если МодифицированностьДанныхГрафика Тогда
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(
							НСтр("ru = 'Данные графика за %1 год были изменены. Сохранить данные графика?'"),
							Формат(НомерТекущегоГода, "ЧГ="));
		Оповещение = Новый ОписаниеОповещения("НомерТекущегоГодаРегулированиеЗавершение", ЭтотОбъект, Направление);					
		ПоказатьВопрос(Оповещение, ТекстВопроса, РежимДиалогаВопрос.ДаНет, , КодВозвратаДиалога.Да);					
	Иначе 
		НомерТекущегоГодаРегулированиеЗавершение(Неопределено, Направление);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура НомерТекущегоГодаРегулированиеЗавершение(Ответ, Направление) Экспорт 
	
	Если Ответ = КодВозвратаДиалога.Да Тогда
		Записать();
		
		Если МодифицированностьДанныхГрафика Тогда
			Возврат;
		КонецЕсли;	
	КонецЕсли;
	
	МодифицированностьДанныхГрафика = Ложь;
	
	НомерТекущегоГода = НомерТекущегоГода + 1 * Направление;
	ПрочитатьДанныеГрафикаВФорму();
	
КонецПроцедуры

&НаКлиенте
Процедура ДанныеГрафикаПриИзменении(Элемент)
	МодифицированностьДанныхГрафика = Истина;
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)  


КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	Для Каждого Стр Из ДанныеГрафика Цикл
		Месяц = Дата(НомерТекущегоГода, Стр.НомерМесяца, 1);
		МЗ = РегистрыСведений.киб_ДанныеГрафиковРаботыСотрудников.СоздатьМенеджерЗаписи();
		МЗ.ГрафикРаботы = Объект.Ссылка;
		МЗ.Месяц = Месяц;
		МЗ.ИтогДни = Стр.ИтогДни;
		МЗ.ИтогЧасы = Стр.ИтогЧасы;
		МЗ.Записать();
	КонецЦикла;
	
	
	МодифицированностьДанныхГрафика = Ложь;
КонецПроцедуры
