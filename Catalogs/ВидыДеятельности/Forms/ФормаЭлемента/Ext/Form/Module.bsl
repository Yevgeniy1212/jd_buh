////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбновитьПредставлениеЭлемента("КодСтрокиОКЭД");
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ФормаВыбораИзКлассификатора") Тогда
		
		Если ВРег(ИсточникВыбора.ИмяМакета) = ВРег("КодыОКЭД") Тогда
			
			Если ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда 
				Объект.КодОКЭД = ВыбранноеЗначение;
			Иначе 
				Объект.КодОКЭД 									  	 = ВыбранноеЗначение.КодСтроки;
				Элементы.ДекорацияРасшифровкаКодСтрокиОКЭД.Заголовок = ВыбранноеЗначение.Наименование;
				Объект.ПолноеНаименование = ВыбранноеЗначение.Наименование;								
			КонецЕсли;
			
			Модифицированность = Истина;
				
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура КодОКЭДНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		  , Истина);
	ПараметрыФормы.Вставить("ИмяМакета"			  , "КодыОКЭД");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	"Классификатор");
	ПараметрыФормы.Вставить("ПолучатьПолныеДанные", Истина);
	ПараметрыФормы.Вставить("ТекущийКодСтроки"	  , ?(НЕ ЗначениеЗаполнено(Объект.КодОКЭД), Неопределено, СокрЛП(Объект.КодОКЭД)));
	ПараметрыФормы.Вставить("ЯзыкМакета", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	ФормаВыбораКода = ПолучитьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, ЭтаФорма, Истина);
	
	ФормаВыбораКода.Открыть();

КонецПроцедуры

&НаКлиенте
Процедура КодОКЭДПриИзменении(Элемент)
	
	ОбновитьПредставлениеЭлемента("КодСтрокиОКЭД");

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
	
&НаСервере
Процедура ОбновитьПредставлениеЭлемента(ИмяОбновляемогоЭлемента)
	
	Если ИмяОбновляемогоЭлемента = "КодСтрокиОКЭД" Тогда
		
		Если ПустаяСтрока(СтрЗаменить(Объект.КодОКЭД, ".", "")) Тогда
			Элементы.ДекорацияРасшифровкаКодСтрокиОКЭД.Заголовок = НСтр("ru ='<не указано>'");
		Иначе
		
			мМакетКодовСтрокОКЭД = УправлениеПечатью.МакетПечатнойФормы("ОбщийМакет.ПФ_MXL_КодыОКЭД");
			мМакетКодовСтрокОКЭД.КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	 		мОбластьСтрокОКЭД 	 = мМакетКодовСтрокОКЭД.Области.Найти("Классификатор");
			
			НаименованиеСтроки = РегламентированнаяОтчетность.ПолучитьНаименованиеСтрокиКлассификатораПоКоду(мМакетКодовСтрокОКЭД, мОбластьСтрокОКЭД, Объект.КодОКЭД, 1, 2);
			Если ПустаяСтрока(НаименованиеСтроки) Тогда
				Элементы.ДекорацияРасшифровкаКодСтрокиОКЭД.Заголовок = СтрШаблон(НСтр("ru ='строка с кодом %1 не найдена.'"), СокрЛП(Объект.КодОКЭД));
			Иначе
				Элементы.ДекорацияРасшифровкаКодСтрокиОКЭД.Заголовок = НаименованиеСтроки;
			КонецЕсли;
			
		КонецЕсли;
	
	КонецЕсли;
		
КонецПроцедуры // ОбновитьПредставлениеЭлемента()

