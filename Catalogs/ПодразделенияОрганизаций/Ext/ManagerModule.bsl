#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт
	
	Ограничение.Текст =
	"ПрисоединитьДополнительныеТаблицы
	|ЭтотСписок КАК ЭтотСписок
	|
	|ЛЕВОЕ СОЕДИНЕНИЕ Справочник.Организации КАК ОбособленныеПодразделения 
	|	ПО ОбособленныеПодразделения.ГоловнаяОрганизация = ЭтотСписок.Владелец.ГоловнаяОрганизация
	|;
	|РазрешитьЧтение
	|ГДЕ
	|	ЗначениеРазрешено(ОбособленныеПодразделения.Ссылка)
	|
	|;
	|РазрешитьИзменениеЕслиРазрешеноЧтение
	|ГДЕ
	|  ЗначениеРазрешено(ЭтотСписок.Владелец)";
	
КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

// В процедуре обрабатывается выбор подразделений организаций в зависимости от установленных параметров выбора.
// В части применения для структурных подразделений справедливы следующие параметры выбора:
// 	- ВыбиратьСтруктурныеЕдиницы - сигнализирует о том, что происходит выбор именно подразделения с флагом "Является структурным подразделением" или организации
//	- Отбор.Владелец - связь параметров выбора необходимо добавлять если необходимо, чтобы при выборе СП выполнялась фильтрация по организации
//  - ВыбиратьПодразделенияОрганизации - параметр используется, когда также необходима обработка подбора не укладывающаяся в стандартное поведение
Процедура ОбработкаПолученияДанныхВыбора(ДанныеВыбора, Параметры, СтандартнаяОбработка)
	
	Если Параметры.Свойство("ВыбиратьСтруктурныеЕдиницы") И Параметры.ВыбиратьСтруктурныеЕдиницы Тогда
		
		СтандартнаяОбработка = Ложь;
		
		Запрос = Новый Запрос;
		
		СтрокаПоиска = Неопределено;
		
		Параметры.Свойство("СтрокаПоиска", СтрокаПоиска);
		
		ТекстУсловияПоОрганизации 	= "";
		ТекстУсловияПоПодразделению = "";
		
		Если СтрокаПоиска <> Неопределено Тогда 
			ТекстУсловияПоОрганизации = " ГДЕ Организации.Наименование ПОДОБНО &ШаблонНаименования ";
			ТекстУсловияПоПодразделению = " И ПодразделенияОрганизаций.Наименование ПОДОБНО &ШаблонНаименования ";
		КонецЕсли;
		
		Если Параметры.Отбор.Свойство("Владелец") Тогда 
			ТекстУсловияПоОрганизации 	= ТекстУсловияПоОрганизации   + ?(ЗначениеЗаполнено(ТекстУсловияПоОрганизации), " И ", " ГДЕ ") + " Организации.Ссылка = &Владелец ";
			ТекстУсловияПоПодразделению = ТекстУсловияПоПодразделению + " И ПодразделенияОрганизаций.Владелец = &Владелец ";
			
			Запрос.УстановитьПараметр("Владелец", Параметры.Отбор.Владелец);
		КонецЕсли;
		
		ЗначенияНастроекПользователя = ПользователиБКВызовСервераПовтИсп.ЗначенияНастроекПользователя(ПользователиКлиентСервер.АвторизованныйПользователь(), "УчетПоВсемОрганизациям");
		Если НЕ ЗначенияНастроекПользователя.УчетПоВсемОрганизациям Тогда
			ТекстУсловияПоОрганизации = ТекстУсловияПоОрганизации + "
			|	" + ?(ЗначениеЗаполнено(ТекстУсловияПоОрганизации), " И ", " ГДЕ ") + " Организации.Ссылка = &ОсновнаяОрганизация";
			
			ТекстУсловияПоПодразделению = ТекстУсловияПоПодразделению + "
			|	И ПодразделенияОрганизаций.Владелец = &ОсновнаяОрганизация";
			
			Запрос.УстановитьПараметр("ОсновнаяОрганизация", Справочники.Организации.ОрганизацияПоУмолчанию());
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	Организации.Ссылка КАК Ссылка,
		|	Организации.ПометкаУдаления КАК ПометкаУдаления
		|ИЗ
		|	Справочник.Организации КАК Организации
		|" + ТекстУсловияПоОрганизации + "
		|
		|ОБЪЕДИНИТЬ ВСЕ
		|
		|ВЫБРАТЬ 
		|	ПодразделенияОрганизаций.Ссылка,
		|	ПодразделенияОрганизаций.ПометкаУдаления
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.ЯвляетсяСтруктурнымПодразделением = ИСТИНА"
		+ ТекстУсловияПоПодразделению;
		
		Запрос.УстановитьПараметр("ШаблонНаименования", "%" + СтрокаПоиска + "%");
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений();
		
		Пока Выборка.Следующий() Цикл 
			Если Выборка.ПометкаУдаления Тогда
				СтруктураЗначение = Новый Структура("Значение,ПометкаУдаления", Выборка.Ссылка, Выборка.ПометкаУдаления);
				ДанныеВыбора.Добавить(СтруктураЗначение,,,БиблиотекаКартинок.ПомеченныйНаУдалениеЭлемент);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Ссылка,);
			КонецЕсли;			
		КонецЦикла;
		
	КонецЕсли;
	
	Если Параметры.Свойство("ВыбиратьПодразделенияОрганизации") И Параметры.ВыбиратьПодразделенияОрганизации Тогда
		
		СтандартнаяОбработка = Ложь;
		СтрокаПоиска = Неопределено;
		
		Параметры.Свойство("СтрокаПоиска", СтрокаПоиска);
		
		Запрос = Новый Запрос;
		Запрос.УстановитьПараметр("ШаблонНаименования", "%" + СтрокаПоиска + "%");
		
		ТекстУсловий = "";
		Для Каждого ТекЭлемент Из Параметры.Отбор Цикл
			
			ТекстСравнения = " = &%Ключ%";
			Если ТипЗнч(ТекЭлемент.Значение) = Тип("Массив") И НЕ ТекЭлемент.Ключ = "Ссылка" Тогда 
				ТекстСравнения = " В(&%Ключ%)";
			ИначеЕсли ТекЭлемент.Ключ = "Ссылка" Тогда 
				ТекстСравнения = " В ИЕРАРХИИ(&%Ключ%)";
			КонецЕсли;
			
			ТекстСравнения = СтрЗаменить(ТекстСравнения, "%Ключ%", ТекЭлемент.Ключ);
			ТекстУсловий   = ТекстУсловий + " И " + ТекЭлемент.Ключ + ТекстСравнения;
			
			Запрос.УстановитьПараметр(ТекЭлемент.Ключ, ТекЭлемент.Значение);
			
		КонецЦикла;
		
		ЗначенияНастроекПользователя = ПользователиБКВызовСервераПовтИсп.ЗначенияНастроекПользователя(ПользователиКлиентСервер.АвторизованныйПользователь(), "УчетПоВсемОрганизациям");
		Если НЕ ЗначенияНастроекПользователя.УчетПоВсемОрганизациям Тогда
			ТекстУсловий = ТекстУсловий + "
			|	" + ?(ЗначениеЗаполнено(ТекстУсловий), " И ", " ГДЕ ") + " ПодразделенияОрганизаций.Владелец = &ОсновнаяОрганизация";
			
			Запрос.УстановитьПараметр("ОсновнаяОрганизация", Справочники.Организации.ОрганизацияПоУмолчанию());
		КонецЕсли;
		
		Запрос.Текст = "ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ПодразделенияОрганизаций.Ссылка,
		|	ПодразделенияОрганизаций.ПометкаУдаления
		|ИЗ
		|	Справочник.ПодразделенияОрганизаций КАК ПодразделенияОрганизаций
		|ГДЕ
		|	ПодразделенияОрганизаций.Наименование ПОДОБНО &ШаблонНаименования
		|	" + ТекстУсловий;
		
		Выборка = Запрос.Выполнить().Выбрать();
		ДанныеВыбора = Новый СписокЗначений();
		
		Пока Выборка.Следующий() Цикл 
			Если Выборка.ПометкаУдаления Тогда
				СтруктураЗначение = Новый Структура("Значение,ПометкаУдаления", Выборка.Ссылка, Выборка.ПометкаУдаления);
				ДанныеВыбора.Добавить(СтруктураЗначение,,,БиблиотекаКартинок.ПомеченныйНаУдалениеЭлемент);
			Иначе
				ДанныеВыбора.Добавить(Выборка.Ссылка,);
			КонецЕсли;			
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

////////////////////////////////////////////////////////////////////////////////
// Интерфейс для работы с подсистемой Печать.

// Заполняет список команд печати.
// 
// Параметры:
//   КомандыПечати - ТаблицаЗначений - состав полей см. в функции УправлениеПечатью.СоздатьКоллекциюКомандПечати
//
Процедура ДобавитьКомандыПечати(КомандыПечати) Экспорт
		
КонецПроцедуры

#КонецЕсли