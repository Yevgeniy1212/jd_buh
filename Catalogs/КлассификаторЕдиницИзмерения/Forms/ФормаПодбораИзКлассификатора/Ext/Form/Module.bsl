
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.КлассификаторЕдиницИзмерения.ПФ_MXL_КлассификаторЕдиницИзмерения");
	Макет.КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();
	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ПолеТабличногоДокумента.Очистить();
	ПолеТабличногоДокумента.Вывести(Макет);

	ПолеТабличногоДокумента.ФиксацияСверху = ПолеТабличногоДокумента.Области.ОбластьРасшифровки.Верх - 1;

	ПолеТабличногоДокумента.ОтображатьЗаголовки = Ложь;
	ПолеТабличногоДокумента.ОтображатьСетку     = Ложь;
	ПолеТабличногоДокумента.ТолькоПросмотр      = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ЗакрытьФормуКлассификатора" И Источник = ЭтаФорма Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТабличныйДокумент = ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКодЧисловой         = ТабличныйДокумент.Области.КодЧисловой;
	ОбластьКодЭСФ			   = ТабличныйДокумент.Области.КодЭСФ;
	ОбластьНаименованиеКраткое = ТабличныйДокумент.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное  = ТабличныйДокумент.Области.НаименованиеПолное;

	СтруктураДанных = Новый Структура;

	СтруктураДанных.Вставить("КодЧисловой", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.Право).Текст);
	СтруктураДанных.Вставить("КодЭСФ", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЭСФ.Лево, ТекущаяОбласть.Низ, ОбластьКодЭСФ.Право).Текст);
	СтруктураДанных.Вставить("НаименованиеКраткое", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст);
	СтруктураДанных.Вставить("НаименованиеПолное", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное. Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеПолное. Право).Текст);
	
	Ссылка = ПолучитьСуществующуюЕдиницуИзмерения(СтруктураДанных.КодЧисловой);
	
	Если НЕ Ссылка.Пустая() Тогда
		ТекстВопроса = НСтр("ru='В справочнике ""Классификатор единиц измерения"" уже существует элемент с кодом %1! Открыть существующий?'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, 
			СтруктураДанных.КодЧисловой);
		
		Данные = Новый Структура("Ссылка, СтруктураДанных", Ссылка, СтруктураДанных);
		
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриПодборе", ЭтотОбъект, Данные);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	Иначе
		СоздатьНовуюЕдиницуИзмерения(СтруктураДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриПодборе(Результат, Параметры) Экспорт
	                       
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ОткрытьФорму("Справочник.КлассификаторЕдиницИзмерения.ФормаОбъекта", Новый Структура("Ключ", Параметры.Ссылка), ЭтаФорма);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		СоздатьНовуюЕдиницуИзмерения(Параметры.СтруктураДанных);
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ПолучитьСуществующуюЕдиницуИзмерения(КодЧисловой)
	
	Возврат Справочники.КлассификаторЕдиницИзмерения.НайтиПоКоду(КодЧисловой);
		
КонецФункции

&НаКлиенте
Процедура СоздатьНовуюЕдиницуИзмерения(СтруктураДанных)
	
	// Создание нового банка.
	ФормаНовогоЭлемента = ПолучитьФорму("Справочник.КлассификаторЕдиницИзмерения.ФормаОбъекта",, ЭтаФорма);

	ОбъектЗаполнения = ФормаНовогоЭлемента.Объект;
	
	ОбъектЗаполнения.Код                = СтруктураДанных.КодЧисловой;
	ОбъектЗаполнения.КодЭСФ             = СтруктураДанных.КодЭСФ;
	ОбъектЗаполнения.Наименование       = СтрПолучитьСтроку(СтруктураДанных.НаименованиеКраткое, 1);
	ОбъектЗаполнения.НаименованиеПолное = СтрПолучитьСтроку(СтруктураДанных.НаименованиеПолное, 1);
			
	ФормаНовогоЭлемента.Открыть();
	
КонецПроцедуры
