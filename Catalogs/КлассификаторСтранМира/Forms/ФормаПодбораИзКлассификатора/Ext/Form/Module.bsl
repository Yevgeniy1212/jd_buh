
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.КлассификаторСтранМира.ПФ_MXL_КлассификаторСтранМира");
	Макет.КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();

	Макет.Параметры.Расшифровка = Истина; // чтобы работала расшифровка

	ПолеТабличногоДокумента.Очистить();
	ПолеТабличногоДокумента.Вывести(Макет);

	ПолеТабличногоДокумента.ФиксацияСверху      = 3;

	ПолеТабличногоДокумента.ОтображатьЗаголовки = Ложь;
	ПолеТабличногоДокумента.ОтображатьСетку     = Ложь;
	ПолеТабличногоДокумента.ТолькоПросмотр      = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)

	Если ИмяСобытия = "ЗакрытьФормуКлассификатора" И Источник = ЭтаФорма Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура ПолеТабличногоДокументаОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;

	ТабличныйДокумент = ПолеТабличногоДокумента;
	ТекущаяОбласть    = ТабличныйДокумент.ТекущаяОбласть;

	ОбластьКодЧисловой         	= ТабличныйДокумент.Области.КодЧисловой;
	ОбластьАльфа2			   	= ТабличныйДокумент.Области.КодАльфа2;
	ОбластьАльфа3			   	= ТабличныйДокумент.Области.КодАльфа3;
	ОбластьНаименованиеКраткое 	= ТабличныйДокумент.Области.НаименованиеКраткое;
	ОбластьНаименованиеПолное  	= ТабличныйДокумент.Области.НаименованиеПолное;
	ОбластьТаможенныйСоюз 		= ТабличныйДокумент.Области.ТаможенныйСоюз;

	СтруктураДанных = Новый Структура;

	СтруктураДанных.Вставить("КодЧисловой", 		ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьКодЧисловой.Лево, ТекущаяОбласть.Низ, ОбластьКодЧисловой.Право).Текст);
	СтруктураДанных.Вставить("КодАльфа2", 			ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьАльфа2.Лево, ТекущаяОбласть.Низ, ОбластьАльфа2.Право).Текст);
	СтруктураДанных.Вставить("КодАльфа3", 			ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьАльфа3.Лево, ТекущаяОбласть.Низ, ОбластьАльфа3.Право).Текст);
	СтруктураДанных.Вставить("НаименованиеКраткое", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеКраткое.Лево, ТекущаяОбласть.Низ, ОбластьНаименованиеКраткое.Право).Текст);
	СтруктураДанных.Вставить("НаименованиеПолное", 	ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьНаименованиеПолное.Лево,ТекущаяОбласть.Низ, ОбластьНаименованиеПолное. Право).Текст);
	СтруктураДанных.Вставить("УчастникТаможенногоСоюза", ТабличныйДокумент.Область(ТекущаяОбласть.Верх, ОбластьТаможенныйСоюз.Лево,ТекущаяОбласть.Низ, ОбластьТаможенныйСоюз.Право).Текст);
	
	КодДляПоиска = "";
	
	Ссылка = ПолучитьСуществующуюСтрану(СтруктураДанных.КодЧисловой);
	
	Если НЕ Ссылка.Пустая() Тогда
		
		ТекстВопроса = НСтр("ru='В справочнике ""Классификатор стран мира"" уже существует элемент с кодом %1! Открыть существующий?'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, 
			СтруктураДанных.КодЧисловой);
		
		Данные = Новый Структура("Ссылка, СтруктураДанных", Ссылка, СтруктураДанных);
		
		Режим = РежимДиалогаВопрос.ДаНетОтмена;
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаПриПодборе", ЭтотОбъект, Данные);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	Иначе
		СоздатьНовуюСтрану(СтруктураДанных);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаПриПодборе(Результат, Параметры) Экспорт
	                       
	Если Результат = КодВозвратаДиалога.Да Тогда
		
		ОткрытьФорму("Справочник.КлассификаторСтранМира.ФормаОбъекта", Новый Структура("Ключ", Параметры.Ссылка), ЭтаФорма);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда
		
		СоздатьНовуюСтрану(Параметры.СтруктураДанных);
		
	КонецЕсли;
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ПолучитьСуществующуюСтрану(КодЧисловой)
	
	Возврат Справочники.КлассификаторСтранМира.НайтиПоКоду(КодЧисловой);
		
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьСтрануУчастникаТС(УчастникТаможенногоСоюза)
	
	Возврат Справочники.СтраныУчастникиТаможенногоСоюза[УчастникТаможенногоСоюза];
	
КонецФункции

&НаКлиенте
Процедура СоздатьНовуюСтрану(СтруктураДанных)
	
	// Создание нового банка.
	ФормаНовогоЭлемента = ПолучитьФорму("Справочник.КлассификаторСтранМира.ФормаОбъекта",, ЭтаФорма);

	ОбъектЗаполнения = ФормаНовогоЭлемента.Объект;
	
	ОбъектЗаполнения.Код 		= СтруктураДанных.КодЧисловой;
	ОбъектЗаполнения.КодАльфа2 	= СтруктураДанных.КодАльфа2;
	ОбъектЗаполнения.КодАльфа3	= СтруктураДанных.КодАльфа3;
	ОбъектЗаполнения.Наименование	= СтруктураДанных.НаименованиеКраткое;
	ОбъектЗаполнения.НаименованиеПолное	= СтруктураДанных.НаименованиеПолное;
		
	Если НЕ ЗначениеЗаполнено(ОбъектЗаполнения.УчастникТаможенногоСоюза) Тогда
		Попытка      			
			ОбъектЗаполнения.УчастникТаможенногоСоюза = ПредопределенноеЗначение("Справочник.СтраныУчастникиТаможенногоСоюза." + СтруктураДанных.УчастникТаможенногоСоюза);			
		Исключение   					
		КонецПопытки;	
	КонецЕсли;
	
	ФормаНовогоЭлемента.Открыть();
	
КонецПроцедуры
