
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Макет = УправлениеПечатью.МакетПечатнойФормы("Справочник.ОснованияУвольненияИзОрганизации.ПФ_MXL_СписокОснованийУвольнения");
	Макет.КодЯзыка = ОбщегоНазначения.КодОсновногоЯзыка();

	Макет.Параметры.Расшифровка = Истина;

	СписокОснованийУвольнения.Очистить();
	СписокОснованийУвольнения.Вывести(Макет);

	СписокОснованийУвольнения.ФиксацияСверху      = 3;

	СписокОснованийУвольнения.ОтображатьЗаголовки = Ложь;
	СписокОснованийУвольнения.ОтображатьСетку     = Ложь;
	СписокОснованийУвольнения.ТолькоПросмотр      = Истина;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокОснованийУвольненияОбработкаРасшифровки(Элемент, Расшифровка, СтандартнаяОбработка)
	
	СтандартнаяОбработка = Ложь;
	
	// Получение значений полей выбранной строки.
	ТекущаяОбласть    = СписокОснованийУвольнения.ТекущаяОбласть;
	
	ОбластьКод          = СписокОснованийУвольнения.Области.Код;
	ОбластьНаименование = СписокОснованийУвольнения.Области.Наименование;
	ОбластьСтатья	    = СписокОснованийУвольнения.Области.Статья;
	
	Если ТекущаяОбласть.Низ = ТекущаяОбласть.Верх Тогда
		
		Код          = СписокОснованийУвольнения.Область(ТекущаяОбласть.Верх, ОбластьКод.         Лево, ТекущаяОбласть.Низ, ОбластьКод.         Право).Текст;
		Наименование = СписокОснованийУвольнения.Область(ТекущаяОбласть.Верх, ОбластьНаименование.Лево, ТекущаяОбласть.Низ, ОбластьНаименование.Право).Текст;
		Статья       = СписокОснованийУвольнения.Область(ТекущаяОбласть.Верх, ОбластьСтатья.Лево, ТекущаяОбласть.Низ, ОбластьСтатья.Право).Текст;
		
		ПолноеНаименование = Лев(СокрЛП(Статья + ". " + Наименование), 150);
		
		// Проверка наличия выбранного элемента.
		Результат = ОснованиеУвольненияСуществует(ПолноеНаименование);
		Если Результат.ОснованиеУвольненияСуществует Тогда 
			
			ТекстВопроса = НСтр("ru = 'В справочнике ""Основания увольнения из организации"" уже существует элемент с наименованием ""%1""! Открыть существующий?'");
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, ПолноеНаименование);
			
			ДопПараметры = Новый Структура("Ссылка, Наименование, Код", Результат.Ссылка, ПолноеНаименование, Код);
			ОписаниеОповещения = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОткрытияСуществующегоЭлемента", ЭтотОбъект, ДопПараметры);
			ПоказатьВопрос( ОписаниеОповещения, 
							СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, ПолноеНаименование),
							РежимДиалогаВопрос.ДаНетОтмена,
							,
							КодВозвратаДиалога.Отмена);
							
		Иначе 
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("Наименование, Код", ПолноеНаименование, Код));
			ОткрытьФорму("Справочник.ОснованияУвольненияИзОрганизации.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, ПолноеНаименование);
		КонецЕсли;
		
	Иначе
		
		//Форма = ПолучитьОбщуюФорму("ФормаПодбораИзКлассификатора", ЭтаФорма);
		//Если Форма.Открыта() Тогда
		//	Форма.СписокВыбора.Очистить();
		//Иначе			
		//	СтуктураКолонок = Новый Структура();
		//	СтуктураКолонок.Вставить("Код", Новый Структура("Заголовок, Ширина", "Код", 5));
		//	СтуктураКолонок.Вставить("Наименование", Новый Структура("Заголовок, Ширина", "Наименование"));
		//	СтуктураКолонок.Вставить("Видимость", Новый Структура("Заголовок, Ширина", ""));	
		//	Форма.ТипСправочника = "ОснованияУвольненияИзОрганизации";
		//	Форма.НастроитьФорму(СтуктураКолонок);
		//КонецЕсли;	
		//
		//СписокВыбора =  Форма.СписокВыбора;		
		//
		//Для ТекущаяСтрока = ТекущаяОбласть.Верх по ТекущаяОбласть.Низ Цикл
		//	
		//	Код				= СписокОснованийУвольнения.Область(ТекущаяСтрока, ОбластьКод.			Лево, ТекущаяСтрока, ОбластьКод.			Право).Текст;
		//	Наименование	= СписокОснованийУвольнения.Область(ТекущаяСтрока, ОбластьНаименование.	Лево, ТекущаяСтрока, ОбластьНаименование.	Право).Текст;
		//	Статья			= СписокОснованийУвольнения.Область(ТекущаяСтрока, ОбластьСтатья.		Лево, ТекущаяСтрока, ОбластьСтатья.			Право).Текст;				
		//	
		//	ЭтоЧисло = Истина;
		//	Попытка
		//		КодКакЧисло = Число(Код);
		//	Исключение
		//		ЭтоЧисло = Ложь;
		//	КонецПопытки;
		//	
		//	ЕСли ЭтоЧисло Тогда
		//		СтрокаПодбора = СписокВыбора.Добавить();
		//		СтрокаПодбора.Код			= Код;
		//		СтрокаПодбора.Наименование	= Статья + ". " + Наименование;
		//		СтрокаПодбора.Видимость		= Справочники.ОснованияУвольненияИзОрганизации.НайтиПоКоду(Код).Пустая();
		//		СтрокаПодбора.Переносить	= СтрокаПодбора.Видимость;
		//	КонецЕсли;
		//	
		//КонецЦикла;
		//
		//Форма.Открыть();
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервереБезКонтекста
Функция ОснованиеУвольненияСуществует(СтрокаПоиска)
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ОснованияУвольненияИзОрганизации.Ссылка
	|ИЗ
	|	Справочник.ОснованияУвольненияИзОрганизации КАК ОснованияУвольненияИзОрганизации
	|ГДЕ
	|	ОснованияУвольненияИзОрганизации.Наименование = &СтрокаПоиска";
	
	Запрос.УстановитьПараметр("СтрокаПоиска", СтрокаПоиска);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда 
		Возврат Новый Структура("ОснованиеУвольненияСуществует, Ссылка", Истина, Выборка.Ссылка);
	КонецЕсли;
	
	Возврат Новый Структура("ОснованиеУвольненияСуществует, Ссылка", Ложь, Неопределено);
		
КонецФункции

&НаКлиенте
Процедура ПослеЗакрытияВопросаОткрытияСуществующегоЭлемента(Результат, ДопПараметры) Экспорт
	
	Если Результат = КодВозвратаДиалога.Отмена Тогда 
		Возврат;
		
	ИначеЕсли Результат = КодВозвратаДиалога.Да Тогда
		ПараметрыФормы = Новый Структура("Ключ", ДопПараметры.Ссылка);
		ОткрытьФорму("Справочник.ОснованияУвольненияИзОрганизации.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, ДопПараметры.Ссылка);
		
	ИначеЕсли Результат = КодВозвратаДиалога.Нет Тогда 
		ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", Новый Структура("Наименование, Код", ДопПараметры.Наименование, ДопПараметры.Код));
		ОткрытьФорму("Справочник.ОснованияУвольненияИзОрганизации.ФормаОбъекта", ПараметрыФормы, ЭтаФорма, ДопПараметры.Наименование);
		
	КонецЕсли;
	
КонецПроцедуры
