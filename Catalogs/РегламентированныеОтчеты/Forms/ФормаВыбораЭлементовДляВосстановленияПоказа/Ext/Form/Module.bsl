&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// Пропускаем инициализацию, чтобы гарантировать получение формы при передаче параметра "АвтоТест".
	Если Параметры.Свойство("АвтоТест") Тогда
		Возврат;
	КонецЕсли;
	
	Запрос = Новый Запрос("ВЫБРАТЬ РАЗРЕШЕННЫЕ
	                      |	СкрытыеРегламентированныеОтчеты.Ссылка КАК Ссылка
	                      |ИЗ
	                      |	Справочник.РегламентированныеОтчеты КАК СкрытыеРегламентированныеОтчеты
	                      |ГДЕ
	                      |	СкрытыеРегламентированныеОтчеты.НеПоказыватьВСписке");
		
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
		
	Если РезультатЗапроса.Количество() = 0 Тогда
				
		Сообщение = Новый СообщениеПользователю;

		Сообщение.Текст = НСтр("ru='Отсутствуют элементы для восстановления!'");

		Сообщение.Сообщить();

		Отказ = Истина;
		
		Возврат;
		
	КонецЕсли;
	            	
	Для Каждого СтрТаблЗнач Из РезультатЗапроса Цикл
		
		Стр = ТаблицаЗначений.Добавить();
		Стр.Ссылка       = СтрТаблЗнач.Ссылка;
		Стр.Код          = СтрТаблЗнач.Ссылка.Код;
		Стр.Наименование = СтрТаблЗнач.Ссылка.Наименование;
		
	КонецЦикла;
					
КонецПроцедуры

&НаСервере
// Процедура установки/снятия флажков.
//
Процедура УстановитьИлиСнятьФлажки(Флаг)
	
	Для Каждого Стр Из ТаблицаЗначений Цикл
		
		Стр.Пометка = Флаг;
				
	КонецЦикла;
		
КонецПроцедуры

&НаКлиенте
Процедура СнятьФлажки(Команда)
	
	УстановитьИлиСнятьФлажки(Ложь);
	
КонецПроцедуры

&НаКлиенте
Процедура УстановитьФлажки(Команда)
	
	УстановитьИлиСнятьФлажки(Истина);
		
КонецПроцедуры

&НаСервере
Процедура ВосстановитьСервер()
	
	НачатьТранзакцию();
	
	Для Каждого Стр Из ТаблицаЗначений Цикл
		
		Если Стр.Пометка = Истина Тогда
			
			Попытка
				ОтчетОбъект = Стр.Ссылка.ПолучитьОбъект();
				ОтчетОбъект.НеПоказыватьВСписке = Ложь;
				ОтчетОбъект.Записать();				
			Исключение
								
				Сообщение = Новый СообщениеПользователю;
				
				Сообщение.Текст = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(НСтр("ru='Не удалось изменить признак отображения отчета. %1'"), ОписаниеОшибки());
				
				Сообщение.Сообщить();
                         				
			КонецПопытки;
			
		КонецЕсли;
		
	КонецЦикла;
	
	ЗафиксироватьТранзакцию();
			
КонецПроцедуры

&НаКлиенте
Процедура Восстановить(Команда)
	
	ВосстановитьСервер();
	Оповестить("ОбновитьФорму", ВладелецФормы, ЭтаФорма);
	Закрыть();
		
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Отказ = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ТаблицаЗначенийПередУдалением(Элемент, Отказ)
	
	Отказ = Истина;
	
КонецПроцедуры
