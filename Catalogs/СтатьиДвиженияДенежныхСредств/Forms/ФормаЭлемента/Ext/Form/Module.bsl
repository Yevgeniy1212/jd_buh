&НаКлиенте
Перем СоответствиеРазрезовДеятельности;
&НаКлиенте
Перем СоответствиеВидовДвижения;
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	СоответствиеРазрезовДеятельности =  Новый Соответствие;
	СоответствиеРазрезовДеятельности.Вставить(ПредопределенноеЗначение("Перечисление.РазрезыДеятельности.Инвестиционная"), ПредопределенноеЗначение("Справочник.ДвиженияДенежныхСредств.ДвижениеОтИнвестиционнойДеятельности"));
	СоответствиеРазрезовДеятельности.Вставить(ПредопределенноеЗначение("Перечисление.РазрезыДеятельности.Финансовая"), ПредопределенноеЗначение("Справочник.ДвиженияДенежныхСредств.ДвижениеОтФинансовойДеятельности"));
	СоответствиеРазрезовДеятельности.Вставить(ПредопределенноеЗначение("Перечисление.РазрезыДеятельности.Операционная"), ПредопределенноеЗначение("Справочник.ДвиженияДенежныхСредств.ДвижениеОтОперационнойДеятельности") );	

	СоответствиеВидовДвижения =  Новый Соответствие;
	СоответствиеВидовДвижения.Вставить(ПредопределенноеЗначение("Справочник.ДвиженияДенежныхСредств.ДвижениеОтИнвестиционнойДеятельности"), ПредопределенноеЗначение("Перечисление.РазрезыДеятельности.Инвестиционная"));
	СоответствиеВидовДвижения.Вставить(ПредопределенноеЗначение("Справочник.ДвиженияДенежныхСредств.ДвижениеОтФинансовойДеятельности"), ПредопределенноеЗначение("Перечисление.РазрезыДеятельности.Финансовая"));
	СоответствиеВидовДвижения.Вставить(ПредопределенноеЗначение("Справочник.ДвиженияДенежныхСредств.ДвижениеОтОперационнойДеятельности"), ПредопределенноеЗначение("Перечисление.РазрезыДеятельности.Операционная"));	

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура РазрезДеятельностиПриИзменении(Элемент)
	
	РазрезДеятельностиПриИзмененииНаСервере(СоответствиеРазрезовДеятельности);
	
КонецПроцедуры

&НаКлиенте
Процедура ДвижениеДенежныхСредствНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если НЕ Объект.РазрезДеятельности.Пустая() Тогда	
		
		СтандартнаяОбработка = Ложь;
		СтруктураПараметров = Новый Структура ("ТекущаяСтрока");
		ФормаВыбора = ПолучитьФорму("Справочник.ДвиженияДенежныхСредств.ФормаВыбора", СтруктураПараметров, Элемент, Истина);
		ОтборПоДеятельности = СоответствиеРазрезовДеятельности[Объект.РазрезДеятельности];				
		ФормаВыбора.УстановитьРодителя(ОтборПоДеятельности);
		ФормаВыбора.Открыть();
		
	КонецЕсли; 
	
КонецПроцедуры

&НаКлиенте
Процедура ДвижениеДенежныхСредствПриИзменении(Элемент)
	
	ДвижениеДенежныхСредствПриИзмененииНаСервере(СоответствиеВидовДвижения);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура РазрезДеятельностиПриИзмененииНаСервере(СоответствиеРазрезовДеятельности)
	
	Если НЕ Объект.РазрезДеятельности.Пустая() 
		И НЕ Объект.ДвижениеДенежныхСредств.Пустая() Тогда		
		
		Если НЕ	Объект.ДвижениеДенежныхСредств.ПринадлежитЭлементу(СоответствиеРазрезовДеятельности[Объект.РазрезДеятельности]) Тогда
			ТекстСообщения = НСтр("ru='Движение денежных средств не соответствует выбранному разрезу деятельности!'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.ДвижениеДенежныхСредств");
		    Объект.ДвижениеДенежныхСредств = Справочники.ДвиженияДенежныхСредств.ПустаяСсылка();           			
		КонецЕсли; 	
	КонецЕсли; 
	
КонецПроцедуры

&НаСервере
Процедура ДвижениеДенежныхСредствПриИзмененииНаСервере(СоответствиеВидовДвижения)
	
	Если НЕ Объект.ДвижениеДенежныхСредств.Пустая() 
		И НЕ  Объект.РазрезДеятельности = СоответствиеВидовДвижения[Объект.ДвижениеДенежныхСредств.Родитель] Тогда	
		ТекстСообщения = НСтр("ru='Разрез деятельности не соответствует выбранному движению денежных средств!'");
	    ОбщегоНазначения.СообщитьПользователю(ТекстСообщения,, "Объект.РазрезДеятельности");
		Объект.РазрезДеятельности = СоответствиеВидовДвижения[Объект.ДвижениеДенежныхСредств.Родитель];
	КонецЕсли; 

КонецПроцедуры







