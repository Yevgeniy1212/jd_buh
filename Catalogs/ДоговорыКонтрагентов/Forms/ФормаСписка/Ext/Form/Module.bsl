
&НаКлиенте
Перем УстановкаОсновногоДоговораВыполнена;

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов	

	Если Параметры.Отбор.Свойство("Владелец") Тогда
		
		Если Параметры.Свойство("ВладелецПодразделение") Тогда
			Владелец = Параметры.ВладелецПодразделение;
		Иначе
			Владелец = Параметры.Отбор.Владелец;
		КонецЕсли;
		
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Владелец) Тогда
		
		ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.Контрагенты);
		ОсновнойДоговорКонтрагента = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Владелец, "ОсновнойДоговорКонтрагента");
		
	КонецЕсли;

	Элементы.ПолеВладелец.Видимость = ЗначениеЗаполнено(Владелец);
	Элементы.ИспользоватьОсновным.Видимость = ЗначениеЗаполнено(Владелец) И ДоступноИспользоватьОсновным;

	Элементы.Основной.Видимость = ЗначениеЗаполнено(Владелец);
	
	ОбщегоНазначенияБК.ФормаСпискаПриСозданииНаСервере(ЭтотОбъект, Отказ, СтандартнаяОбработка);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "УстановкаОсновногоДоговора" Тогда
		ОсновнойДоговорКонтрагента = Параметр.ОсновнойДоговорКонтрагента;
		УправлениеФормойКлиент();
		Элементы.Список.Обновить();
	Конецесли;

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	ГрупповоеИзменениеОбъектовКлиент.ИзменитьВыделенные(Элементы.Список);

КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено
		Или Не Элементы.Список.ТекущиеДанные.Свойство("Ссылка")
		Или Не Элементы.Список.ТекущиеДанные.Свойство("Владелец") Тогда
		Возврат;
	КонецЕсли;
	
	Если Элементы.Список.ТекущиеДанные.Ссылка = ОсновнойДоговорКонтрагента Тогда
		ОсновнойДоговорКонтрагента = ПредопределенноеЗначение("Справочник.ДоговорыКонтрагентов.ПустаяСсылка");
	Иначе
		ОсновнойДоговорКонтрагента = Элементы.Список.ТекущиеДанные.Ссылка;
	КонецЕсли;
	
	УстановкаОсновногоДоговораВыполнена = Ложь;
	
	СтруктураПараметров = Новый Структура();
	Если ТипЗнч(Владелец) = Тип("СправочникСсылка.ПодразделенияОрганизаций") Тогда		
		СтруктураПараметров.Вставить("КонтрагентОрганизация",  Владелец);
	Иначе
		СтруктураПараметров.Вставить("КонтрагентОрганизация",  Элементы.Список.ТекущиеДанные.Владелец);
	КонецЕсли;
	СтруктураПараметров.Вставить("ОсновнойДоговорКонтрагента", ОсновнойДоговорКонтрагента);
	
	Оповестить("УстановкаОсновногоДоговора", СтруктураПараметров);
	
	// Если форма владельца закрыта, то запишем основной договор самостоятельно.
	Если Не УстановкаОсновногоДоговораВыполнена Тогда
		УстановитьОсновнойДоговор(СтруктураПараметров);
		Элементы.Список.Обновить();
	КонецЕсли;
	
	УправлениеФормойКлиент();

КонецПроцедуры

&НаСервереБезКонтекста
Процедура УстановитьОсновнойДоговор(СтруктураПараметров)
	
	Справочники.ДоговорыКонтрагентов.УстановитьОсновнойДоговор(
	СтруктураПараметров.КонтрагентОрганизация,
	СтруктураПараметров.ОсновнойДоговорКонтрагента);
	
КонецПроцедуры

&НаКлиенте
Процедура УправлениеФормойКлиент()
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ДоступноИспользоватьОсновным И ТекущиеДанные <> Неопределено Тогда
		Если ТекущиеДанные.Свойство("Ссылка") Тогда
			Элементы.ИспользоватьОсновным.Пометка = ТекущиеДанные.Ссылка = ОсновнойДоговорКонтрагента;
		КонецЕсли;
		
		Если ТекущиеДанные.Свойство("ПометкаУдаления") И ТекущиеДанные.ПометкаУдаления Тогда
			Элементы.ИспользоватьОсновным.Доступность = Ложь;
		Иначе
			Элементы.ИспользоватьОсновным.Доступность = Истина;
		КонецЕсли;
	Иначе
		Элементы.ИспользоватьОсновным.Доступность = Ложь;
	КонецЕсли;
	 
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦ ФОРМЫ

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.НачатьОбновлениеКоманд(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	УправлениеФормойКлиент();

КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
     ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Элементы.Список);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
     ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
     ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Элементы.Список);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
     ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Элементы.Список);
КонецПроцедуры

// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
