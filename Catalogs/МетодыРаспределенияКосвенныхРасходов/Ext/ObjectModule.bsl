#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если Справочники.Организации.ИспользуетсяНесколькоОрганизаций() Тогда
		Возврат;
	Иначе
		
		Если ДанныеЗаполнения = Неопределено Тогда
			ДанныеЗаполнения = Новый Структура;
		ИначеЕсли ТипЗнч(ДанныеЗаполнения) <> Тип("Структура") Тогда
			Возврат;
		КонецЕсли;
			
		Если НЕ ДанныеЗаполнения.Свойство("Организация") Тогда
			ОрганизацияПоУмолчанию = Справочники.Организации.ОрганизацияПоУмолчанию();
			ДанныеЗаполнения.Вставить("Организация", ОрганизацияПоУмолчанию);
		КонецЕсли;
			
	КонецЕсли;
	
КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	Если БазаРаспределения = Перечисления.БазыРаспределенияКосвенныхРасходов.Процентом Тогда 
		
		ИтоговыйПроцент = АналитикаРаспределения.Итог("ПроцентРаспределения");
		
		Если ИтоговыйПроцент <> 100 Тогда 
			
			ОтменаРедактирования = Истина;
			
			ТекстСообщения = НСтр("ru = 'Ошибка в заполнении графы ""Процент распределения""'");
			Поле = "АналитикаРаспределения.ПроцентРаспределения";
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, Поле, "Объект", Отказ);
			
		КонецЕсли;
		
		
		Для Каждого СтрокаТЧ Из АналитикаРаспределения Цикл 
			
			Если ЗначениеЗаполнено(СтрокаТЧ.СчетЗакрытияБУ) Тогда 
				
				КоличествоСубконто = СтрокаТЧ.СчетЗакрытияБУ.Ссылка.ВидыСубконто.Количество();
				
				Для Н = 1 По КоличествоСубконто Цикл

					Если ТипЗнч(СтрокаТЧ["СубконтоБУ"+Н]) <> Тип("СправочникСсылка.СтатьиЗатрат") Тогда 
				    	Отказ = НЕ ЗначениеЗаполнено(СтрокаТЧ["СубконтоБУ"+Н]);
					КонецЕсли;
					
				КонецЦикла;
				
				Если Отказ Тогда
					
					ТекстСообщения = НСтр("ru = 'Для метода распределения базы ""Процентом"" необходимо заполнить аналитику распределения косвенных расходов'");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
					Прервать;
					
				КонецЕсли;
				
			КонецЕсли;
			
			Если ЗначениеЗаполнено(СтрокаТЧ.СчетЗакрытияНУ) Тогда 
				
				КоличествоСубконто = СтрокаТЧ.СчетЗакрытияНУ.Ссылка.ВидыСубконто.Количество();
				
				Для Н = 1 По КоличествоСубконто Цикл

					Если ТипЗнч(СтрокаТЧ["СубконтоНУ"+Н]) <> Тип("СправочникСсылка.СтатьиЗатрат") Тогда 
				    	Отказ = НЕ ЗначениеЗаполнено(СтрокаТЧ["СубконтоНУ"+Н]);
					КонецЕсли;
				
				КонецЦикла;
				
				Если Отказ Тогда 
					
					ТекстСообщения = НСтр("ru = 'Для метода распределения базы ""Процентом"" необходимо заполнить аналитику распределения косвенных расходов'");
					ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект, , , Отказ);
					Прервать;
					
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЦикла;
		
		// Не должно быть абсолютно одинаковой аналитики, отличающейся только процентом распределения
		ТаблицаАналитики = АналитикаРаспределения.Выгрузить();
		ТаблицаАналитики.Свернуть("СчетЗакрытияБУ,СубконтоБУ1,СубконтоБУ2,СубконтоБУ3,СчетЗакрытияНУ,СубконтоНУ1,СубконтоНУ2,СубконтоНУ3,СтруктурноеПодразделение");
		
		Если АналитикаРаспределения.Количество() <> ТаблицаАналитики.Количество() Тогда
			ТекстСообщения = НСтр("ru = 'Для метода распределения базы ""Процентом"" не должно быть строк с одинаковой аналитикой распределения косвенных расходов'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,, "Объект", Отказ);
		КонецЕсли;
		
	Иначе 
		
		ТаблицаАналитики = АналитикаРаспределения.Выгрузить();
		ТаблицаАналитики.Свернуть("СчетЗакрытияБУ,СчетЗакрытияНУ",);
		
		ЕстьОдинаковыеСчета            = АналитикаРаспределения.Количество() <> ТаблицаАналитики.Количество();
		ЕстьНезаполненноеПодразделение = Ложь;
		ЕстьПроизводственныеСчета      = Ложь;
		ЕстьНепроизводственныеСчета    = Ложь;
		
		Для Каждого СтрокаТЧ Из АналитикаРаспределения Цикл
			Если ЕстьОдинаковыеСчета И ЕстьНезаполненноеПодразделение И (ЕстьПроизводственныеСчета И ЕстьНепроизводственныеСчета) Тогда 
				Отказ = Истина;
				Прервать;
			КонецЕсли;
			
			// Выполняем проверку на заполненность подразделения, если указаны одинаковые счета таковое еще не обнаружено.
			Если ЕстьОдинаковыеСчета И НЕ ЕстьНезаполненноеПодразделение
				И ТипЗнч(СтрокаТЧ.СубконтоБУ1) = Тип("СправочникСсылка.ПодразделенияОрганизаций")
				И НЕ ЗначениеЗаполнено(СтрокаТЧ.СубконтоБУ1) ИЛИ НЕ ЗначениеЗаполнено(СтрокаТЧ.СубконтоНУ1) Тогда 
				
				ЕстьНезаполненноеПодразделение = Истина;
			КонецЕсли;
			
			// Выполняем проверку на наличие производственных и непроизводственных счетов
			Если НЕ ЕстьПроизводственныеСчета Тогда 
				ЕстьПроизводственныеСчета = ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.СчетЯвляетсяПроизводственным(СтрокаТЧ.СчетЗакрытияБУ) 
											ИЛИ ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.СчетЯвляетсяПроизводственнымНУ(СтрокаТЧ.СчетЗакрытияНУ);
			КонецЕсли;
				
			Если НЕ ЕстьНепроизводственныеСчета Тогда 
				ЕстьНепроизводственныеСчета = НЕ (ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.СчетЯвляетсяПроизводственным(СтрокаТЧ.СчетЗакрытияБУ) 
											ИЛИ ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.СчетЯвляетсяПроизводственнымНУ(СтрокаТЧ.СчетЗакрытияНУ));
			КонецЕсли;
		КонецЦикла;
		
		Если (ЕстьОдинаковыеСчета И ЕстьНезаполненноеПодразделение) ИЛИ (ЕстьПроизводственныеСчета И ЕстьНепроизводственныеСчета) Тогда 
			Отказ = Истина;
		КонецЕсли;

		Если ЕстьОдинаковыеСчета И ЕстьНезаполненноеПодразделение Тогда
			ТекстСообщения = НСтр("ru = 'В аналитике распределения косвенных затрат существуют одинаковые счета закрытия, для данных записей необходимо заполнить подразделение организации'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,, "Объект", Отказ);
		КонецЕсли;
		
		Если ЕстьПроизводственныеСчета И ЕстьНепроизводственныеСчета Тогда 
			ТекстСообщения = НСтр("ru = 'Для выбранного способа распределения косвенных затрат в аналитике распределения нельзя указывать одновременно производственные и непроизводственные счета закрытия'");
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,, "Объект", Отказ);
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#КонецЕсли
