#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда
	
Перем ПрошлыйИзмененныйРодительОбъектаДоступа;

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		
		Если НЕ ЗначениеЗаполнено(ВалютаДенежныхСредств) Тогда 
			ВалютаДенежныхСредств = Константы.ВалютаРегламентированногоУчета.Получить();
		КонецЕсли;
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа И НЕ ЭтоНовый() Тогда
		// Проверяем изменения учетных реквизитов - счет учета и валюта учета
		Если ВалютаДенежныхСредств <> Ссылка.ВалютаДенежныхСредств Тогда
			СуществуютСсылки = ЭтотОбъект.СуществуютСсылки();
			Если СуществуютСсылки И ВалютаДенежныхСредств <> Ссылка.ВалютаДенежныхСредств Тогда		
				ТекстСообщения = НСтр("ru = 'Существуют документы, проведенные по кассе %1.
							|Реквизит ""Валюта денежных средств"" не может быть изменен, элемент не записан'");
				ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, 
					Наименование);
				ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, ЭтотОбъект,,, Отказ);
			КонецЕсли;				
		ИначеЕсли Не Справочники.Кассы.ИспользуетсяНесколькоКасс(Ссылка.Владелец)
			И ПометкаУдаления Тогда
				
			ТекстСообщения = НСтр("ru = 'Нельзя удалить единственную кассу, т.к. она используется для автозаполнения документов.'"); 
			ОбщегоНазначения.СообщитьПользователю(ТекстСообщения, Ссылка,, "Объект", Отказ);	
		КонецЕсли;	
	КонецЕсли;	


КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;

	Справочники.Кассы.ПроверитьЗначениеОпцииИспользоватьНесколькоКасс(Ссылка.Владелец);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

Функция СуществуютСсылки() Экспорт

	Запрос = Новый Запрос();

	Запрос.Текст = 
	"ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 1	
	|	ТиповойОбороты.Счет
	|ИЗ
	|	РегистрБухгалтерии.Типовой.Обороты(, , , , , Субконто1 = &Касса ИЛИ Субконто2 = &Касса ИЛИ Субконто3 = &Касса, , ) КАК ТиповойОбороты"; 	

	Запрос.УстановитьПараметр("Касса", Ссылка);

	Возврат НЕ Запрос.Выполнить().Пустой();

КонецФункции

#КонецЕсли

