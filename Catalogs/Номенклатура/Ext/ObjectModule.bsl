#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

////////////////////////////////////////////////////////////////////////////////
// ПРОГРАММНЫЙ ИНТЕРФЕЙС

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если НЕ ЭтоГруппа Тогда
		НастройкиПользователя = ПользователиБКВызовСервераПовтИсп.ЗначенияНастроекПользователя(Пользователи.ТекущийПользователь(), "ОсновнаяЕдиницаПоКлассификатору,ОсновнаяСтавкаНДС,ОсновнойВидПодакцизногоТМЗ,ОсновнаяСтавкаАкциза");
		
		БазоваяЕдиницаИзмерения = НастройкиПользователя.ОсновнаяЕдиницаПоКлассификатору;
		
		//НДС
		СтавкаНДС               = НастройкиПользователя.ОсновнаяСтавкаНДС;
		
		//Акциз
		ВидПодакцизногоТМЗ     	=  НастройкиПользователя.ОсновнойВидПодакцизногоТМЗ;
		СтавкаАкциза    	   	=  НастройкиПользователя.ОсновнаяСтавкаАкциза;
		
		НоменклатурнаяГруппа = ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.ОсновнаяНоменклатурнаяГруппа();
	КонецЕсли;
	
КонецПроцедуры

Процедура ПередЗаписью(Отказ)

	Если ОбменДанными.Загрузка Тогда
		Возврат;
	КонецЕсли;
	
	Если НЕ ЭтоГруппа Тогда

		СуществуютСсылки = Неопределено;

		Если Услуга <> Ссылка.Услуга 
			 И Справочники.Номенклатура.СуществуютСсылки(СуществуютСсылки, ЭтотОбъект) Тогда

			ТекстСообщения = НСтр("ru='Номенклатура ""%1"" участвует в товародвижении.
			                 |Признак услуги не может быть изменен!'");
			ОбщегоНазначения.СообщитьПользователю(СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СокрЛП(Ссылка)),,,,Отказ); 
			
		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

Процедура ОбработкаПроверкиЗаполнения(Отказ, ПроверяемыеРеквизиты)
	
	МассивНепроверяемыхРеквизитов = Новый Массив;
	
	Если НЕ ЭтоГруппа И Услуга Тогда
		МассивНепроверяемыхРеквизитов.Добавить("БазоваяЕдиницаИзмерения");
	КонецЕсли;
	
	ОбщегоНазначения.УдалитьНепроверяемыеРеквизитыИзМассива(ПроверяемыеРеквизиты, МассивНепроверяемыхРеквизитов);
    
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

#КонецЕсли
