
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Отбор") Тогда
		Если Параметры.Отбор.Свойство("Владелец") Тогда
			Номенклатура = Параметры.Отбор.Владелец;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

// СтандартныеПодсистемы.Печать
&НаКлиенте
Процедура Подключаемый_ВыполнитьКомандуПечати(Команда)
	УправлениеПечатьюКлиент.ВыполнитьПодключаемуюКомандуПечати(Команда, ЭтотОбъект, Элементы.Список);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Печать

&НаКлиенте
Процедура ИзменитьВыделенные(Команда)
	
	общ_ПереопределениеПроцедурБСПКлиент.ИзменитьВыделенные(ЭтотОбъект,Команда);

КонецПроцедуры

&НаКлиенте
Процедура НоменклатураПриИзменении(Элемент)
	
	УстановитьОтбор(Номенклатура);
	 
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура УстановитьОтбор(Значение)
	Список.Отбор.Элементы.Очистить();
	ЭлементОтбора = Список.Отбор.Элементы.Добавить(Тип("ЭлементОтбораКомпоновкиДанных"));
	ЭлементОтбора.ЛевоеЗначение  = Новый ПолеКомпоновкиДанных("Владелец");	
	ЭлементОтбора.ВидСравнения   = ВидСравненияКомпоновкиДанных.Равно;
	ЭлементОтбора.Использование  = ЗначениеЗаполнено(Значение);
	ЭлементОтбора.ПравоеЗначение = Значение;
	
	Элементы.Владелец.Видимость = Не ЗначениеЗаполнено(Значение);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	УстановитьОтбор(Номенклатура)
КонецПроцедуры

&НаСервереБезКонтекста
Процедура ПолучитьСвойстваИЗначение(Ссылка, ТД)
	//ТД = Новый ТабличныйДокумент;
	ТД.Очистить();
	Если Не Ссылка = Неопределено Тогда
		Запрос = Новый Запрос;
		Запрос.Текст =
		"ВЫБРАТЬ
		|	си_ЗначенияСвойствОбъектов.Свойство,
		|	си_ЗначенияСвойствОбъектов.Значение
		|ИЗ
		|	РегистрСведений.си_ЗначенияСвойствОбъектов КАК си_ЗначенияСвойствОбъектов
		|ГДЕ
		|	си_ЗначенияСвойствОбъектов.Объект = &Объект";
		Запрос.УстановитьПараметр("Объект", Ссылка);
		Результат = Запрос.Выполнить();
		Выборка = результат.Выбрать();
		
		Макет = Справочники.си_ХарактеристикиСпецодеждыИИнвентаря.ПолучитьМакет("Макет");
		ОбластьШапка = Макет.ПолучитьОбласть("ОбластьШапка");
		ОбластьСтрока = Макет.ПолучитьОбласть("ОбластьСтрока");
		ТД.Вывести(ОбластьШапка);
		Пока Выборка.Следующий() Цикл
			ОбластьСтрока.Параметры.Свойство = Выборка.Свойство;
			ОбластьСтрока.Параметры.Значение = Выборка.Значение;
			ТД.Вывести(ОбластьСтрока);
		КонецЦикла;
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		Возврат;
	КонецЕсли;
	ПолучитьСвойстваИЗначение(ТекущиеДанные.Ссылка,СвойстваИЗначения);
КонецПроцедуры

#КонецОбласти
