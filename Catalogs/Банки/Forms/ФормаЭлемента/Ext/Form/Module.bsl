
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	
	Если НЕ ЭтаФорма.ВладелецФормы = Неопределено Тогда 
		ПараметрОповещения = Новый Структура("Ссылка, Контрагент", Объект.Ссылка, Объект.Контрагент);
	
		Оповестить("ИзмененБанк", ПараметрОповещения);
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура БИКДоРеформыБанковскихСчетовПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.КодВПлатежнойСистемеДоРеформыБанковскихСчетов) Тогда
		
		Объект.КодВПлатежнойСистемеДоРеформыБанковскихСчетов = УправлениеДенежнымиСредствамиКлиентСервер.ПолучитьКодБанкаВПлатежнойСистеме(Объект.БИКДоРеформыБанковскихСчетов);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура БИКПриИзменении(Элемент)
	
	Если НЕ ЗначениеЗаполнено(Объект.КодВПлатежнойСистеме) Тогда
		
		Объект.КодВПлатежнойСистеме = УправлениеДенежнымиСредствамиКлиентСервер.ПолучитьКодБанкаВПлатежнойСистеме(Объект.БИК);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура АдресНачалоВыбора(Элемент)
	
	ДополнительныеПараметры = Новый Структура("ФормаВладелец,ИмяРеквизита", ЭтаФорма, "Основание");
	Оповещение = Новый ОписаниеОповещения("АдресЗавершениеВвода", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(
		Оповещение,
		Объект.Адрес,
		НСтр("ru='Адрес'"));
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныНачалоВыбора(Элемент)
	
	ДополнительныеПараметры = Новый Структура("ФормаВладелец,ИмяРеквизита", ЭтаФорма, "Основание");
	Оповещение = Новый ОписаниеОповещения("ТелефоныЗавершениеВвода", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(
		Оповещение,
		Объект.Телефоны,
		НСтр("ru='Телефоны'"));
	
КонецПроцедуры

&НаКлиенте
Процедура АдресЗавершениеВвода(Строка, Параметры) Экспорт

	Если Строка <> Неопределено Тогда
		
		Объект.Адрес = Строка;
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТелефоныЗавершениеВвода(Строка, Параметры) Экспорт

	Если Строка <> Неопределено Тогда
		
		Объект.Телефоны = Строка;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПроверитьБИН(Команда)
	
	СообщенияПроверки = "";
	РегламентированныеДанныеКлиентСервер.ИИНБИНСоответствуетТребованиям(Объект.ИдентификационныйНомер, СообщенияПроверки);
	ОбщегоНазначенияКлиент.СообщитьПользователю(СообщенияПроверки);
	
КонецПроцедуры

&НаКлиенте
Процедура ПроверитьРНН(Команда)
	
	СообщенияПроверки = "";
	РегламентированныеДанныеКлиентСервер.РННСоответствуетТребованиям(Объект.РНН, СообщенияПроверки);
	ОбщегоНазначенияКлиент.СообщитьПользователю(СообщенияПроверки);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ
