#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	РежимПовторногоВыбораВидаОрганизации = Параметры.РежимПовторногоВыбораВидаОрганизации;
	РежимПовторногоВыбораСистемыНалогообложения = Параметры.РежимПовторногоВыбораСистемыНалогообложения;
	
	//РежимПростогоИнтерфейса = Параметры.Свойство("РежимПростогоИнтерфейса");
	
	Если Параметры.Свойство("ВидОрганизации") Тогда
		ВидОрганизации = Параметры.ВидОрганизации;
	КонецЕсли;
	
	ПараметрыУчетнойПолитики = Новый Структура;
	ПараметрыУчетнойПолитики.Вставить("РежимНалогообложения", Перечисления.РежимНалогообложения.ПустаяСсылка());
	
	Если Параметры.Свойство("РежимНалогообложения") Тогда
		ПараметрыУчетнойПолитики.Вставить("РежимНалогообложения", Параметры.РежимНалогообложения);
	КонецЕсли;
	
	ДатаНормативныхСведений = ТекущаяДатаСеанса();
	
	//ФорматнаяСтрока = ФорматнаяСтрокаПредельныхДоходовДляСпецрежима(ДатаНормативныхСведений);
	//
	//// Ограничение по доходам
	//ТекстОграниченияДоходыУСН = СтрШаблон(НСтр("ru='%1 %2'"),
	//	Формат(КонтрольПраваПримененияСпецрежима.ГраницаДоходовОграничивающаяПравоПримененияУСН(ДатаНормативныхСведений), ФорматнаяСтрока),
	//	НСтр("ru='млн руб'"));
	//ТекстОграниченияДоходыПСН = СтрШаблон(НСтр("ru='%1 %2'"),
	//	Формат(КонтрольПраваПримененияСпецрежима.ГраницаДоходовОграничивающаяПравоПримененияПСН(), ФорматнаяСтрока),
	//	НСтр("ru='млн руб'"));
	//
	//Элементы.НадписьДоходыУСНИП.Заголовок    = ТекстОграниченияДоходыУСН;
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	Заголовок = ПолучитьЗаголовокФормы();
	
	Если РежимПовторногоВыбораВидаОрганизации Тогда
		ИнициализироватьФормуВРежимеПовторногоВыбораВидаОрганизации();
	ИначеЕсли РежимПовторногоВыбораСистемыНалогообложения Тогда
		ИнициализироватьФормуВРежимеПовторногоВыбораСистемыНалогообложения();
	ИначеЕсли РежимПростогоИнтерфейса Тогда
		ИнициализироватьФормуВРежимеПростогоИнтерфейса();
	Иначе
		Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "ОткрытиеФормыНовойОрганизации" 
	   И Открыта() Тогда
		Закрыть();
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ВыбратьИП(Команда)
	
	ВидОрганизации = "ИндивидуальныйПредприниматель";
	
	Если РежимПовторногоВыбораВидаОрганизации Тогда
		Закрыть(ВидОрганизации);
	КонецЕсли;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияИП;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьЮЛ(Команда)
	
	ВидОрганизации = "ЮридическоеЛицо";
	
	Если РежимПовторногоВыбораВидаОрганизации Тогда
		Закрыть(ВидОрганизации);
	КонецЕсли;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаСистемаНалогообложенияЮЛ;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияПатент(Команда)
	
	СистемаНалогообложения = "Патент";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияУпрощеннаяДекларация(Команда)
	
	СистемаНалогообложения = "УпрощеннаяДекларация";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияФиксированныйВычет(Команда)
	
	СистемаНалогообложения = "ФиксированныйВычет";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура СистемаНалогообложенияОбщеустановленный(Команда)
	
	СистемаНалогообложения = "Общеустановленный";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры  

&НаКлиенте
Процедура СистемаНалогообложенияРозничныйНалог(Команда)
	
	СистемаНалогообложения = "РозничныйНалог";
	
	ОткрытьФормуОрганизации();
	
КонецПроцедуры

&НаКлиенте
Процедура Назад(Команда)
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	
	Заголовок = ПолучитьЗаголовокФормы();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФормы

&НаКлиентеНаСервереБезКонтекста
Функция ТекущаяСтраницаПоВидуОрганизации(ВидОрганизации, РежимПовторногоВыбораСистемыНалогообложения)
	
	Если ВидОрганизации = "ЮридическоеЛицо" Тогда
		ТекущаяСтраницаФормы = "СтраницаСистемаНалогообложенияЮЛ";
	ИначеЕсли ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
		ТекущаяСтраницаФормы = "СтраницаСистемаНалогообложенияИП";
	Иначе
		ТекущаяСтраницаФормы = "СтраницаВидОрганизации";
	КонецЕсли;
	Возврат ТекущаяСтраницаФормы;
	
КонецФункции

&НаКлиенте
Функция ПолучитьЗаголовокФормы()
	
	Если РежимПовторногоВыбораСистемыНалогообложения Тогда
		
		ЗаголовокФормы = НСтр("ru='Система налогообложения'");
		
	Иначе
	
		ЗаголовокФормы = НСтр("ru = 'Вид организации'");
		
		Если Элементы.СтраницыФормы.ТекущаяСтраница <> Элементы.СтраницаВидОрганизации Тогда
			Если ВидОрганизации = "ЮридическоеЛицо" Тогда
				ЗаголовокФормы = НСтр("ru = 'Юридическое лицо'");
			ИначеЕсли ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
				ЗаголовокФормы = НСтр("ru = 'Индивидуальный предприниматель'");
			КонецЕсли;
		КонецЕсли;
		
	КонецЕсли;
	
	Возврат ЗаголовокФормы;
	
КонецФункции


&НаКлиенте
Процедура ОткрытьФормуОрганизации()
	
	СтруктураУчетнойПолитики = ПолучитьСтруктуруУчетнойПолитики(ВидОрганизации,
		СистемаНалогообложения);
	
	Модифицированность = Ложь;
	
	Если РежимПовторногоВыбораСистемыНалогообложения Тогда
		
		Закрыть(СтруктураУчетнойПолитики);
		
	Иначе
	
		ЗначенияЗаполнения = Новый Структура;
		Если ВидОрганизации = "ИндивидуальныйПредприниматель" Тогда
			ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо",
				ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ФизЛицо"));
		Иначе
			ЗначенияЗаполнения.Вставить("ЮридическоеФизическоеЛицо",
				ПредопределенноеЗначение("Перечисление.ЮрФизЛицо.ЮрЛицо"));
		КонецЕсли;
		
		СтруктураПараметров = Новый Структура;
		СтруктураПараметров.Вставить("ЗначенияЗаполнения",       ЗначенияЗаполнения);
		СтруктураПараметров.Вставить("СтруктураУчетнойПолитики", СтруктураУчетнойПолитики);
		
		ОткрытьФорму("Справочник.Организации.Форма.ФормаЭлемента", СтруктураПараметров, ВладелецФормы);
		
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ИнициализироватьФормуВРежимеПовторногоВыбораВидаОрганизации()
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы.СтраницаВидОрганизации;
	
	КнопкаПоУмолчанию = Новый Соответствие;
	КнопкаПоУмолчанию["ЮридическоеЛицо"]               = "ВыбратьЮЛ";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"] = "ВыбратьИП";
	
	Если ЗначениеЗаполнено(ВидОрганизации) Тогда
		ЭлементФормыПоУмолчанию = Элементы[КнопкаПоУмолчанию[ВидОрганизации]];
	КонецЕсли;
	Если ЭлементФормыПоУмолчанию <> Неопределено Тогда
		ЭлементФормыПоУмолчанию.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьФормуВРежимеПовторногоВыбораСистемыНалогообложения()

	Если ПараметрыУчетнойПолитики.РежимНалогообложения = ПредопределенноеЗначение("Перечисление.РежимНалогообложения.Патент") Тогда
		СистемаНалогообложения = "Патент";
	ИначеЕсли ПараметрыУчетнойПолитики.РежимНалогообложения = ПредопределенноеЗначение("Перечисление.РежимНалогообложения.УпрощеннаяДекларация") Тогда
		СистемаНалогообложения = "УпрощеннаяДекларация";
	ИначеЕсли ПараметрыУчетнойПолитики.РежимНалогообложения = ПредопределенноеЗначение("Перечисление.РежимНалогообложения.ФиксированныйВычет") Тогда
		СистемаНалогообложения = "ФиксированныйВычет";
	ИначеЕсли ПараметрыУчетнойПолитики.РежимНалогообложения = ПредопределенноеЗначение("Перечисление.РежимНалогообложения.РозничныйНалог") Тогда
		СистемаНалогообложения = "РозничныйНалог";	
	Иначе
		СистемаНалогообложения = "Общеустановленный";
	КонецЕсли;
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы[ТекущаяСтраницаПоВидуОрганизации(ВидОрганизации, Истина)];
	Элементы.НазадЮЛ.Доступность = Ложь;
	Элементы.НазадЮЛ.ЦветТекста = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаФормы");
	Элементы.НазадИП.Доступность = Ложь;
	Элементы.НазадИП.ЦветТекста = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаФормы");
	
	КнопкаПоУмолчанию = Новый Соответствие;
	КнопкаПоУмолчанию["ЮридическоеЛицо"]                                               = Новый Соответствие;
	КнопкаПоУмолчанию["ЮридическоеЛицо"]["УпрощеннаяДекларация"]                       = "СистемаНалогообложенияУпрощеннаяДекларацияЮЛ";
	КнопкаПоУмолчанию["ЮридическоеЛицо"]["ФиксированныйВычет"]                         = "СистемаНалогообложенияФиксированныйВычетЮЛ";
	КнопкаПоУмолчанию["ЮридическоеЛицо"]["Общеустановленный"]                          = "СистемаНалогообложенияОбщеустановленныйЮЛ";
	КнопкаПоУмолчанию["ЮридическоеЛицо"]["РозничныйНалог"]                             = "СистемаНалогообложенияРозничныйНалогЮЛ";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]                                 = Новый Соответствие;
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["Патент"]                       = "СистемаНалогообложенияПатентИП";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["УпрощеннаяДекларация"]         = "СистемаНалогообложенияУпрощеннаяДекларацияИП";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["ФиксированныйВычет"]           = "СистемаНалогообложенияФиксированныйВычетИП";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["Общеустановленный"]            = "СистемаНалогообложенияОбщеустановленныйИП";
	КнопкаПоУмолчанию["ИндивидуальныйПредприниматель"]["РозничныйНалог"]               = "СистемаНалогообложенияРозничныйНалогИП";
	
	ЭлементФормыПоУмолчанию = Элементы[КнопкаПоУмолчанию[ВидОрганизации][СистемаНалогообложения]];
	Если ЭлементФормыПоУмолчанию <> Неопределено Тогда
		ЭлементФормыПоУмолчанию.АктивизироватьПоУмолчанию = Истина;
	КонецЕсли;
	
	Если НЕ ОбщегоНазначенияБККлиентСервер.ЭтоПростаяВерсияКонфигурации() Тогда
		//Видимость элементов по патенту
		Элементы.НадписьПатентИП.Видимость = Ложь;
		Элементы.НадписьДоходыПатентИП.Видимость = Ложь;
		Элементы.НадписьНППатентИП.Видимость = Ложь;
		Элементы.НадписьУплачиваетсяПатентИП.Видимость = Ложь;
		Элементы.НадписьФНОПатентИП.Видимость = Ложь;
		Элементы.НадписьДоходыПатентИП.Видимость = Ложь;
		Элементы.НадписьРаботникиПатентИП.Видимость = Ложь;
		Элементы.СистемаНалогообложенияПатентИП.Видимость = Ложь; 
		Элементы.НадписьПрочиеПатентИП.Видимость = Ложь;
		
		//Видимость элементов по фиксированному вычету ИП
		Элементы.НадписьФиксированныйВычетИП.Видимость = Ложь;
		Элементы.НадписьДоходыФиксированныйВычетИП.Видимость = Ложь;
		Элементы.НадписьФНОФиксированныйВычетИП.Видимость = Ложь;
		Элементы.НадписьДоходыФиксированныйВычетИП.Видимость = Ложь;
		Элементы.НадписьРаботникиФиксированныйВычетИП.Видимость = Ложь;
		Элементы.СистемаНалогообложенияФиксированныйВычетИП.Видимость = Ложь;
		Элементы.НадписьПрочиеФиксированныйВычетИП.Видимость = Ложь;
		
		//Видимость элементов по фиксированному вычету ЮЛ
		Элементы.НадписьФиксированныйВычетЮЛ.Видимость = Ложь;
		Элементы.НадписьДоходыФиксированныйВычетЮЛ.Видимость = Ложь;
		Элементы.НадписьФНОФиксированныйВычетЮЛ.Видимость = Ложь;
		Элементы.НадписьДоходыФиксированныйВычетЮЛ.Видимость = Ложь;
		Элементы.НадписьРаботникиФиксированныйВычетЮЛ.Видимость = Ложь;
		Элементы.СистемаНалогообложенияФиксированныйВычетЮЛ.Видимость = Ложь;
		Элементы.НадписьПрочиеФиксированныйВычетЮЛ.Видимость = Ложь;
		
		Элементы.НадписьСпециальныеРежимыИП.Видимость = Ложь;
		Элементы.НадписьСпециальныеРежимыЮЛ.Видимость = Ложь;
		//ИП
		Элементы.НадписьОбщеустановленныйИП.АвтоМаксимальнаяШирина = Ложь;
		Элементы.НадписьОбщеустановленныйИП.Ширина = 20;
		Элементы.НадписьОбщеустановленныйИП.РастягиватьПоГоризонтали = Ложь;
		Элементы.НадписьОбщеустановленныйИП.РастягиватьПоВертикали = Ложь;
		
		Элементы.НадписьРозничныйНалогИП.АвтоМаксимальнаяШирина = Ложь;
		Элементы.НадписьРозничныйНалогИП.Ширина = 20;
		Элементы.НадписьРозничныйНалогИП.РастягиватьПоГоризонтали = Ложь;
		Элементы.НадписьРозничныйНалогИП.РастягиватьПоВертикали = Ложь;
		//ЮЛ
		Элементы.НадписьОбщеустановленныйЮЛ.АвтоМаксимальнаяШирина = Ложь;
		Элементы.НадписьОбщеустановленныйЮЛ.Ширина = 25;
		Элементы.НадписьОбщеустановленныйЮЛ.РастягиватьПоГоризонтали = Ложь;
		Элементы.НадписьОбщеустановленныйЮЛ.РастягиватьПоВертикали = Ложь; 
		
		Элементы.НадписьРозничныйНалогЮЛ.АвтоМаксимальнаяШирина = Ложь;
		Элементы.НадписьРозничныйНалогЮЛ.Ширина = 25;
		Элементы.НадписьРозничныйНалогЮЛ.РастягиватьПоГоризонтали = Ложь;
		Элементы.НадписьРозничныйНалогЮЛ.РастягиватьПоВертикали = Ложь;
		
		Элементы.НадписьУплачиваетсяФиксированныйВычетИП.Ширина = 20;
		Элементы.НадписьУплачиваетсяФиксированныйВычетЮЛ.Ширина = 25;
		
		Элементы.НадписьНПФиксОбщИП.Ширина = 20;
		Элементы.НадписьНПФиксОбщЮЛ.Ширина = 25;
		
		Элементы.ДекорацияОграничениеИП.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		Элементы.ДекорацияОграничениеЮЛ.ГоризонтальноеПоложение = ГоризонтальноеПоложениеЭлемента.Лево;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ИнициализироватьФормуВРежимеПростогоИнтерфейса()
	
	ВидОрганизации = "ИндивидуальныйПредприниматель";
	СистемаНалогообложения = "Патент";
	
	Элементы.СтраницыФормы.ТекущаяСтраница = Элементы[ТекущаяСтраницаПоВидуОрганизации(ВидОрганизации, Истина)];
	Элементы.НазадЮЛ.Доступность = Ложь;
	Элементы.НазадЮЛ.ЦветТекста = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаФормы");
	Элементы.НазадИП.Доступность = Ложь;
	Элементы.НазадИП.ЦветТекста = ОбщегоНазначенияКлиент.ЦветСтиля("ЦветФонаФормы");
	
	ЭлементФормыПоУмолчанию = Элементы.СистемаНалогообложенияПатентИП;
	ЭлементФормыПоУмолчанию.АктивизироватьПоУмолчанию = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьСтруктуруУчетнойПолитики(ВидОрганизации, СистемаНалогообложения)
	
	УчетнаяПолитика = Новый Структура;
	
	УчетнаяПолитика.Вставить("УчетнаяПолитикаБухгалтерскийУчет",
		РегистрыСведений.УчетнаяПолитикаБухгалтерскийУчет.СтруктураУчетнойПолитики(ВидОрганизации, СистемаНалогообложения));
	
	УчетнаяПолитика.Вставить("УчетнаяПолитикаНалоговыйУчет",
		РегистрыСведений.УчетнаяПолитикаНалоговыйУчет.СтруктураУчетнойПолитики(ВидОрганизации, СистемаНалогообложения));
	
	Возврат УчетнаяПолитика;
	
КонецФункции

&НаСервереБезКонтекста
Функция ФорматнаяСтрокаПредельныхДоходовДляСпецрежима(ДатаФормата)

	ФорматнаяСтрока = "ЧДЦ=; ЧС=6";
	
	Возврат ФорматнаяСтрока;

КонецФункции

#КонецОбласти
