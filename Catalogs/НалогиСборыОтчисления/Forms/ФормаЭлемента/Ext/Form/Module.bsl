
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ  ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	Элементы.ОтноситьНаВычетыВПределахУплаченнойСуммыНалога.ТолькоПросмотр = ТолькоПросмотр;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ВРег(ИсточникВыбора.ИмяФормы) = ВРег("ОбщаяФорма.ФормаВыбораИзКлассификатора") Тогда
		
		Если ВРег(ИсточникВыбора.ИмяМакета) = ВРег("КодыНазначенияПлатежей") Тогда 
			Если  ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
				
				Объект.КодНазначенияПлатежа = ВыбранноеЗначение;
			Иначе
				Объект.КодНазначенияПлатежа = ВыбранноеЗначение.КодСтроки;
			КонецЕсли;
			
			
		ИначеЕсли ВРег(ИсточникВыбора.ИмяМакета) = ВРег("КодыБюджетнойКлассификации") Тогда 
			
			Если  ТипЗнч(ВыбранноеЗначение) <> Тип("Структура") Тогда
				
				Объект.КодБК = ВыбранноеЗначение;
			Иначе
				Объект.КодБК = ВыбранноеЗначение.КодСтроки;
			КонецЕсли;
			
		КонецЕсли;
		
		Модифицированность = Истина;

	КонецЕсли;	

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПрочитатьНастройкиВидовНалоговВНалоговомУчете(ТекущийОбъект.Ссылка);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	РегистрыСведений.НастройкиВидовНалоговВНалоговомУчете.УстановитьНастройки(ТекущийОбъект.Ссылка,
		Новый Структура("ОтноситьНаВычетыВПределахУплаченнойСуммыНалога", ОтноситьНаВычетыВПределахУплаченнойСуммыНалога),
		ТекущаяДатаСеанса());
	
КонецПроцедуры


////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СчетУчетаРасчетовСКонтрагентомБУПриИзменении(Элемент)
	
	Объект.СчетУчетаРасчетовСКонтрагентомНУ = ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.ПреобразоватьСчетаБУвСчетНУ(Новый Структура("СчетБУ", Объект.СчетУчетаРасчетовСКонтрагентомБУ));

КонецПроцедуры

&НаКлиенте
Процедура КодБКНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		  , Истина);
	ПараметрыФормы.Вставить("ИмяМакета"			  , "КодыБюджетнойКлассификации");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	"Классификатор");
	ПараметрыФормы.Вставить("ПолучатьПолныеДанные", Истина);
	ПараметрыФормы.Вставить("ТекущийКодСтроки"	  , ?(НЕ ЗначениеЗаполнено(Объект.КодБК), Неопределено, СокрЛП(Объект.КодБК)));
	ПараметрыФормы.Вставить("ЯзыкМакета", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());

	ФормаВыбораКода = ПолучитьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, ЭтаФорма, Истина);
	
	ФормаВыбораКода.Открыть();

КонецПроцедуры

&НаКлиенте
Процедура КодНазначенияПлатежаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("РежимВыбора"		  , Истина);
	ПараметрыФормы.Вставить("ИмяМакета"			  , "КодыНазначенияПлатежей");
	ПараметрыФормы.Вставить("ИмяСекции"			  ,	"Классификатор");
	ПараметрыФормы.Вставить("ПолучатьПолныеДанные", Истина);
	ПараметрыФормы.Вставить("ТекущийКодСтроки"	  , ?(НЕ ЗначениеЗаполнено(Объект.КодНазначенияПлатежа), Неопределено, СокрЛП(Объект.КодНазначенияПлатежа)));
	ПараметрыФормы.Вставить("ЯзыкМакета", ОбщегоНазначенияКлиент.КодОсновногоЯзыка());
	ФормаВыбораКода = ПолучитьФорму("ОбщаяФорма.ФормаВыбораИзКлассификатора", ПараметрыФормы, ЭтаФорма, Истина);
	
	ФормаВыбораКода.Открыть();


КонецПроцедуры

&НаКлиенте
Процедура НазначениеПлатежаНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ДополнительныеПараметры = Новый Структура("ФормаВладелец,ИмяРеквизита", ЭтаФорма, "НазначениеПлатежа");
	Оповещение = Новый ОписаниеОповещения("НазначениеПлатежаЗавершениеВвода", ЭтотОбъект, ДополнительныеПараметры);
	
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияМногострочногоТекста(
		Оповещение,
		Объект.НазначениеПлатежа,
		НСтр("ru='Назначение платежа'"));

КонецПроцедуры
	
&НаКлиенте
Процедура СтатьяЗатратНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	

	
КонецПроцедуры

&НаКлиенте
Процедура ОтноситьНаВычетыВПределахУплаченнойСуммыНалогаПриИзменении(Элемент)
	
	Модифицированность = Истина;
	
КонецПроцедуры

&НаКлиенте
Процедура ДекорацияИсторияНажатие(Элемент)
	
	Если Не ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	Отбор = Новый Структура("ВидНалога", Объект.Ссылка);
	
	ОписаниеОповещения = Новый ОписаниеОповещения("ПоказатьИсториюЗавершение", ЭтотОбъект);
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("Отбор"         , Отбор);
	ПараметрыФормы.Вставить("ТолькоПросмотр", ТолькоПросмотр);
	
	ОткрытьФорму("РегистрСведений.НастройкиВидовНалоговВНалоговомУчете.ФормаСписка", ПараметрыФормы, ЭтотОбъект, , , , ОписаниеОповещения);
	
КонецПроцедуры

&НаКлиенте
Процедура ПоказатьИсториюЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	ПрочитатьНастройкиВидовНалоговВНалоговомУчете(Объект.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура НазначениеПлатежаЗавершениеВвода(Строка, Параметры) Экспорт

	Если Строка <> Неопределено Тогда
		
		Объект.НазначениеПлатежа = Строка;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНастройкиВидовНалоговВНалоговомУчете(Ссылка)
	
	НастройкиНалога = РегистрыСведений.НастройкиВидовНалоговВНалоговомУчете.ПолучитьНастройки(Ссылка);
	НастройкиНалога.Свойство("ОтноситьНаВычетыВПределахУплаченнойСуммыНалога", ОтноситьНаВычетыВПределахУплаченнойСуммыНалога);
	
КонецПроцедуры





