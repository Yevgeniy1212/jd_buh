
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Карт-счет физ. лица: %1'"), Объект.Владелец.Наименование);
	
	Если Параметры.Ключ.Пустая() Тогда
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.УправлениеДоступом
	Если ОбщегоНазначения.ПодсистемаСуществует("СтандартныеПодсистемы.УправлениеДоступом") Тогда
		МодульУправлениеДоступом = ОбщегоНазначения.ОбщийМодуль("УправлениеДоступом");
		МодульУправлениеДоступом.ПослеЗаписиНаСервере(ЭтотОбъект, ТекущийОбъект, ПараметрыЗаписи);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.УправлениеДоступом
	
	ЭтаФорма.Заголовок = СтрШаблон(НСтр("ru = 'Карт-счет физ. лица: %1'"), Объект.Владелец.Наименование);
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура НеЯвляетсяВладельцемСчетаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура БанкПриИзменении(Элемент)
	
	Объект.Наименование = УправлениеДенежнымиСредствамиКлиентСервер.СформироватьНаименованиеКартСчета(Объект);
	
КонецПроцедуры

&НаКлиенте
Процедура НомерСчетаПриИзменении(Элемент)
	
	Объект.Наименование = УправлениеДенежнымиСредствамиКлиентСервер.СформироватьНаименованиеКартСчета(Объект);
	
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.ФизЛицоВладелецСчета.Видимость = Объект.НеЯвляетсяВладельцемСчета;
	Элементы.ГруппаРеквизитыВладельцаСчета.Видимость = Объект.НеЯвляетсяВладельцемСчета И (Не ПустаяСтрока(Объект.УдалитьФамилия)
		Или Не ПустаяСтрока(Объект.УдалитьИмя) Или Не ПустаяСтрока(Объект.УдалитьОтчество)
		Или Не ПустаяСтрока(Объект.УдалитьИдентификационныйКодЛичности) Или Не ПустаяСтрока(Объект.УдалитьРНН));
	
КонецПроцедуры

#КонецОбласти
