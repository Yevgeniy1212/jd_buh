
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ВерсионированиеОбъектов
	ВерсионированиеОбъектов.ПриСозданииНаСервере(ЭтаФорма);
	// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов
	
	// СтандартныеПодсистемы.Свойства
	ДополнительныеПараметры = Новый Структура;
	ДополнительныеПараметры.Вставить("ИмяЭлементаДляРазмещения", "ГруппаДополнительныеРеквизиты");
	УправлениеСвойствами.ПриСозданииНаСервере(ЭтотОбъект, ДополнительныеПараметры);
	// Конец СтандартныеПодсистемы.Свойства	
	
	Если Объект.Ссылка.Пустая() Тогда
		УправлениеФормой(ЭтаФорма);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПриЧтенииНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
	ЗаполнитьСоставГруппы();
	
КонецПроцедуры

&НаСервере
Процедура ПослеЗаписиНаСервере(ТекущийОбъект, ПараметрыЗаписи)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_Номенклатура" И Параметр.Свойство("НоменклатурнаяГруппа") И Параметр.НоменклатурнаяГруппа = Объект.Ссылка Тогда 
		ЗаполнитьСоставГруппы();
	КонецЕсли;

	// СтандартныеПодсистемы.Свойства
	Если УправлениеСвойствамиКлиент.ОбрабатыватьОповещения(ЭтотОбъект, ИмяСобытия, Параметр)Тогда
		ОбновитьЭлементыДополнительныхРеквизитов();
		УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	КонецЕсли;
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствамиКлиент.ПослеЗагрузкиДополнительныхРеквизитов(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.Свойства
	
КонецПроцедуры

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ОбработкаПроверкиЗаполнения(ЭтотОбъект, Отказ, ПроверяемыеРеквизиты);
	// Конец СтандартныеПодсистемы.Свойства 
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	// СтандартныеПодсистемы.Свойства
	УправлениеСвойствами.ПередЗаписьюНаСервере(ЭтотОбъект, ТекущийОбъект);
	// Конец СтандартныеПодсистемы.Свойства 

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокГруппыПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Отказ = Истина;
	
	Если Копирование Тогда
		ПоказатьПредупреждение(, НСтр("ru='Ввод новой записи копированием запрещен!'"));
	КонецЕсли;
	
	ОписаниеОповещениОВыборе = Новый ОписаниеОповещения("НоменклатураОкончаниеВыбора", ЭтотОбъект);
	ПараметрыФормы = Новый Структура("МножественныйВыбор, ЗакрыватьПриВыборе", Ложь, Истина);
	ОткрытьФорму("Справочник.Номенклатура.ФормаВыбора", ПараметрыФормы, ЭтотОбъект, Объект.Ссылка,,, ОписаниеОповещениОВыборе, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура СписокГруппыВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(Элемент.ТекущиеДанные.Номенклатура) Тогда
		
		СтандартнаяОбработка = Ложь;
		
		ПараметрыОткрытия = Новый Структура("Ключ", Элемент.ТекущиеДанные.Номенклатура); 
		ОткрытьФорму("Справочник.Номенклатура.Форма.ФормаЭлемента", ПараметрыОткрытия); 
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура СписокГруппыПередУдалением(Элемент, Отказ)
	
	Номенклатура = Элемент.ТекущиеДанные.Номенклатура;
	
	Если ЗначениеЗаполнено(Номенклатура) Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр("ru='Исключить номенклатуру %1 из номенклатурной группы %2?'");
		ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, 
			Номенклатура, Объект.Ссылка);
		                                 
		Режим = РежимДиалогаВопрос.ДаНет;
		ПараметрыВопроса = Новый Структура("Объект", Номенклатура);
		Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаУдалениеНоменклатурнойГруппы", ЭтотОбъект, ПараметрыВопроса);
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
		
	КонецЕсли;
		
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ОбновитьСоставГруппы(Команда)
	
	ЗаполнитьСоставГруппы();
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаКлиенте
Процедура Подключаемый_СвойстваВыполнитьКоманду(ЭлементИлиКоманда, НавигационнаяСсылка = Неопределено, СтандартнаяОбработка = Неопределено)
	УправлениеСвойствамиКлиент.ВыполнитьКоманду(ЭтотОбъект, ЭлементИлиКоманда, СтандартнаяОбработка);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)

	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.СписокГруппы.Доступность = НЕ Объект.Ссылка.Пустая();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаУстановкиНоменклатурнойГруппы(Результат, ПараметрыВопроса) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;	
	КонецЕсли;

	ЗаписатьНоменклатуруНаСервере(ПараметрыВопроса.ОбъектНоменклатуры, Объект.Ссылка);
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаУдалениеНоменклатурнойГруппы(Результат, ПараметрыВопроса) Экспорт
	
	Если Результат = КодВозвратаДиалога.Нет Тогда
		Возврат;	
	КонецЕсли;

	ЗаписатьНоменклатуруНаСервере(ПараметрыВопроса.Объект, ПредопределенноеЗначение("Справочник.НоменклатурныеГруппы.ПустаяСсылка"));
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ПолучитьДанныеНоменклатуры(Номеклатура)
	
	Возврат Новый Структура("ЭтоГруппа, НоменклатурнаяГруппа", Номеклатура.ЭтоГруппа, Номеклатура.НоменклатурнаяГруппа);
	
КонецФункции

&НаСервере
Функция ЗаписатьНоменклатуруНаСервере(Номеклатура, ЗначениеНомГруппы)
	
    Попытка
	
		ОбъектНоменклатуры = Номеклатура.ПолучитьОбъект();
		ОбъектНоменклатуры.НоменклатурнаяГруппа = ЗначениеНомГруппы;
		ОбъектНоменклатуры.Записать();
		
		ЗаполнитьСоставГруппы();
		
		Возврат Истина;
		
	Исключение

		Возврат Ложь;
		
	КонецПопытки;
	
КонецФункции

&НаСервере
Процедура ЗаполнитьСоставГруппы()
	
	Запрос = Новый Запрос;
    Запрос.УстановитьПараметр("НоменклатурнаяГруппа", Объект.Ссылка);
    
    Запрос.Текст = 
    "ВЫБРАТЬ РАЗРЕШЕННЫЕ
    |	Номенклатура.Код,
    |	Номенклатура.Ссылка КАК Номенклатура
    |ИЗ
    |	Справочник.Номенклатура КАК Номенклатура
    |ГДЕ
    |	Номенклатура.НоменклатурнаяГруппа.Ссылка = &НоменклатурнаяГруппа
    |
    |УПОРЯДОЧИТЬ ПО
    |	Номенклатура";
    
    ТаблицаТоваров = Запрос.Выполнить().Выгрузить();  
    
    СписокГруппы.Загрузить(ТаблицаТоваров);
	
КонецПроцедуры

&НаКлиенте
Процедура НоменклатураОкончаниеВыбора(Результат, ДополнительныеПараметры) Экспорт
	
	Если Результат = Неопределено Тогда 
		Возврат;
	КонецЕсли;
	
	ДанныеНоменклатуры = ПолучитьДанныеНоменклатуры(Результат);
	
	Если ДанныеНоменклатуры.ЭтоГруппа Тогда
		
		ПоказатьПредупреждение(, НСтр("ru='В состав номенклатурной группы могут включаться только элементы номенклатуры!'"));
		
	Иначе
		
		Если ЗначениеЗаполнено(ДанныеНоменклатуры.НоменклатурнаяГруппа) Тогда
			
			СтандартнаяОбработка = Ложь;
			
			ТекстВопроса = НСтр("ru='Выбранная номенклатура %1 уже входит в группу %2. Включить ее в текущую номенклатурную группу?'");
			ТекстВопроса = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстВопроса, 
				Результат, ДанныеНоменклатуры.НоменклатурнаяГруппа);
			                                 
			Режим = РежимДиалогаВопрос.ДаНет;
			ПараметрыВопроса = Новый Структура("Объект, ОбъектНоменклатуры", Объект, Результат);
			Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияВопросаУстановкиНоменклатурнойГруппы", ЭтотОбъект, ПараметрыВопроса);
			ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
			
		Иначе 
			
			ЗаписатьНоменклатуруНаСервере(Результат, Объект.Ссылка);
			
		КонецЕсли;

	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.Свойства
&НаСервере
Процедура ОбновитьЭлементыДополнительныхРеквизитов()
    УправлениеСвойствами.ОбновитьЭлементыДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьЗависимостиДополнительныхРеквизитов()
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПриИзмененииДополнительногоРеквизита(Элемент)
    УправлениеСвойствамиКлиент.ОбновитьЗависимостиДополнительныхРеквизитов(ЭтотОбъект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.Свойства 
