#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// СтандартныеПодсистемы.ВерсионированиеОбъектов

// Определяет настройки объекта для подсистемы ВерсионированиеОбъектов.
//
// Параметры:
//  Настройки - Структура - настройки подсистемы.
Процедура ПриОпределенииНастроекВерсионированияОбъектов(Настройки) Экспорт

КонецПроцедуры

// Конец СтандартныеПодсистемы.ВерсионированиеОбъектов

#КонецОбласти

#Область ОбработчикиОбновления

Процедура СоздатьОсновнуюНоменклатурнуюГруппу() Экспорт
	
	Если КоличествоНоменклатурныхГрупп() > 0 Тогда
		Возврат;
	КонецЕсли;
	
	НоменклатурнаяГруппа = Справочники.НоменклатурныеГруппы.СоздатьЭлемент();
	НоменклатурнаяГруппа.Наименование = НСтр("ru = 'Основная номенклатурная группа'", ОбщегоНазначения.КодОсновногоЯзыка());
	
	ОбновлениеИнформационнойБазы.ЗаписатьОбъект(НоменклатурнаяГруппа);
	
КонецПроцедуры

Процедура ПерезаполнитьКонстантуИспользоватьНесколькоНоменклатурныхГрупп() Экспорт
	
	Если КоличествоНоменклатурныхГрупп() > 1 Тогда
		МенеджерКонстанты = Константы.ИспользоватьНесколькоНоменклатурныхГрупп.СоздатьМенеджерЗначения();
		МенеджерКонстанты.Значение = Истина;
		ОбновлениеИнформационнойБазы.ЗаписатьДанные(МенеджерКонстанты);
	КонецЕсли;
	
КонецПроцедуры

#КонецОбласти

#Область ИспользоватьНесколькоНоменклатурныхГрупп

Процедура ПроверитьЗначениеОпцииИспользоватьНесколькоНоменклатурныхГрупп() Экспорт
	
	Если Не ПолучитьФункциональнуюОпцию("ИспользоватьНесколькоНоменклатурныхГрупп") И КоличествоНоменклатурныхГрупп() > 1 Тогда
		
		УстановитьПривилегированныйРежим(Истина);
		Константы.ИспользоватьНесколькоНоменклатурныхГрупп.Установить(Истина);
		ОбновитьПовторноИспользуемыеЗначения();
		
	КонецЕсли;
	
КонецПроцедуры

Функция КоличествоНоменклатурныхГрупп()
	
	Запрос = Новый Запрос;
	Запрос.Текст =
	"ВЫБРАТЬ
	|	КОЛИЧЕСТВО(*) КАК Количество
	|ИЗ
	|	Справочник.НоменклатурныеГруппы КАК НоменклатурныеГруппы
	|ГДЕ
	|	НЕ НоменклатурныеГруппы.ЭтоГруппа";
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Количество = Выборка.Количество;
	Иначе
		Количество = 0;
	КонецЕсли;
	
	Возврат Количество;
	
КонецФункции

#КонецОбласти

#КонецЕсли
