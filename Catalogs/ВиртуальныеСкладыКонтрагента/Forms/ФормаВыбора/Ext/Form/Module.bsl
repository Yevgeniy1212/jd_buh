
&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Свойство("Контрагент") Тогда
		ВСКлиентСерверПереопределяемый.УстановитьЭлементОтбораДинамическогоСписка(
		Список,
		"Контрагент",
		Параметры.Контрагент, 
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина,
		РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ
		);
	КонецЕсли;
	
	Если Параметры.Отбор.Количество() <> 0 Тогда
		Если Параметры.Отбор.Контрагент <> Неопределено Тогда
			
			Если Не ТипЗнч(Параметры.Отбор.Контрагент) = Тип("Массив") Тогда 
				ЗначениеОтбора 		= Параметры.Отбор.Контрагент;
				ВариантСравнения 	= ВидСравненияКомпоновкиДанных.Равно;
			Иначе 
				ЗначениеОтбора 		= Параметры.Отбор.Контрагент;
				ВариантСравнения	= ВидСравненияКомпоновкиДанных.ВСписке;
				
				ГруппировкаСписка = Список.КомпоновщикНастроек.Настройки.Структура.Добавить(Тип("ГруппировкаКомпоновкиДанных"));
				ДобавитьГруппировку(ГруппировкаСписка.ПоляГруппировки, "Контрагент");
				
			КонецЕсли;
			
			ЭСФКлиентСерверПереопределяемый.УстановитьЭлементОтбора(
			Список.КомпоновщикНастроек.ФиксированныеНастройки.Отбор,
			"Контрагент",
			ЗначениеОтбора, 
			ВариантСравнения,
			,
			Истина,
			РежимОтображенияЭлементаНастройкиКомпоновкиДанных.БыстрыйДоступ);
		
			СтандартнаяОбработка = Ложь;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция ДобавитьГруппировку(СписокГруппировок, ИмяПоля, Использование = Истина, ТипГруппировки = Неопределено)
	
	Поле = Новый ПолеКомпоновкиДанных(ИмяПоля);
	НовыйЭлемент = СписокГруппировок.Элементы.Добавить(Тип("ПолеГруппировкиКомпоновкиДанных"));
	НовыйЭлемент.Использование = Использование;
	НовыйЭлемент.Поле = Поле;
	НовыйЭлемент.ТипГруппировки = ?(ТипГруппировки = Неопределено, ТипГруппировкиКомпоновкиДанных.Элементы, ТипГруппировки);
	Возврат НовыйЭлемент;
	
КонецФункции

&НаКлиенте
Процедура ПомощникПолучитьВСКонтрагента(Команда)
	
	КонтрагентОтбора = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
	Для каждого ЭлементНастройки Из Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		//Использвание - для того чтоб не передавалось значение если галочка не установлена
		Если ТипЗнч(ЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") И ЭлементНастройки.Использование Тогда
			Если ТипЗнч(ЭлементНастройки.ПравоеЗначение) = Тип("СправочникСсылка.Контрагенты") и ЗначениеЗаполнено(ЭлементНастройки.ПравоеЗначение) Тогда
				КонтрагентОтбора = ЭлементНастройки.ПравоеЗначение;
				Прервать;
			КонецЕсли; 	
		КонецЕсли; 
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(КонтрагентОтбора) Тогда		
		ПараметрыФормы = Новый Структура("Контрагент", КонтрагентОтбора); 	
	КонецЕсли;
	//открытие формы помощника с/без отбора
	ОткрытьФорму("Обработка.ОбменСНТ.Форма.ПомощникПолученияВСКонтрагента", ПараметрыФормы);
	
	Закрыть();
	
КонецПроцедуры