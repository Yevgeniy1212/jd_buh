
&НаКлиенте
Процедура ПомощникПолучитьВСКонтрагента(Команда)
	
	КонтрагентОтбора = ПредопределенноеЗначение("Справочник.Контрагенты.ПустаяСсылка");
	Для каждого ЭлементНастройки Из Список.КомпоновщикНастроек.ПользовательскиеНастройки.Элементы Цикл
		//Использвание - для того чтоб не передавалось значение если галочка не установлена
		Если ТипЗнч(ЭлементНастройки) = Тип("ЭлементОтбораКомпоновкиДанных") И ЭлементНастройки.Использование Тогда
			Если ТипЗнч(ЭлементНастройки.ПравоеЗначение) = Тип("СправочникСсылка.Контрагенты") и ЗначениеЗаполнено(ЭлементНастройки.ПравоеЗначение) Тогда
				КонтрагентОтбора = ЭлементНастройки.ПравоеЗначение;
				Прервать;
			КонецЕсли; 	
		КонецЕсли; 
		
	КонецЦикла;
	
	ПараметрыФормы = Новый Структура;
	
	Если ЗначениеЗаполнено(КонтрагентОтбора) Тогда		
		ПараметрыФормы = Новый Структура("Контрагент", КонтрагентОтбора); 	
	КонецЕсли;
	//открытие формы помощника с/без отбора
	ОткрытьФорму("Обработка.ОбменСНТ.Форма.ПомощникПолученияВСКонтрагента", ПараметрыФормы);
	
	Закрыть();
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = СНТКлиентСервер.ИмяСобытияЗаписьВСКонтрагента() Тогда
		Элементы.Список.Обновить();
	ИначеЕсли ИмяСобытия = "ВС_ПроверятьОповещенияФоновогоЗадания"
		И ЭтаФорма.КлючУникальности = Источник Тогда
		
		СНТКлиентПереопределяемый.ОбработкаОповещенияСНТ_ПроверятьОповещенияФоновогоЗадания(ЭтаФорма, Параметр);
		Элементы.Список.Обновить();
		
	КонецЕсли;

	Если ИмяСобытия = "УстановкаОсновногоВС" Тогда
		ОсновнойВСКонтрагента = Параметр.ОсновнойВСКонтрагента;
		Элементы.Список.Обновить();
	Конецесли;
	
КонецПроцедуры

&НаКлиенте
Процедура ИспользоватьОсновным(Команда)
	
	Если Элементы.Список.ТекущиеДанные = Неопределено ИЛИ Не Элементы.Список.ТекущиеДанные.Свойство("Ссылка") Тогда
		Возврат;
	КонецЕсли;
	
	ВыделенныеСтроки = Элементы.Список.ВыделенныеСтроки;
	Если ВыделенныеСтроки.Количество() > 1 Тогда
		ЭСФКлиентСервер.СообщитьПользователю(НСтр("ru = 'Указать основным возможно только один склад контрагента. Пожалуйста перевыберите склад.'"));
		Возврат;
	КонецЕсли;
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные; 
	Если ДоступноИспользоватьОсновным И ТекущиеДанные <> Неопределено Тогда
		
		Если ТекущиеДанные.Статус = ПредопределенноеЗначение("Перечисление.СтатусыВиртуальныхСкладов.Активен") Тогда
			
			УстановленОсновнойВСКонтрагента = ПроверкаОсновныхВСКонтрагентов(ТекущиеДанные.Ссылка);
			
			Если УстановленОсновнойВСКонтрагента Тогда
				
				СтруктураПараметров = Новый Структура();
				СтруктураПараметров.Вставить("Контрагент", ТекущиеДанные.Контрагент);
				СтруктураПараметров.Вставить("ОсновнойВСКонтрагента", ТекущиеДанные.Ссылка);
				
				Оповестить("УстановкаОсновногоВС", СтруктураПараметров);
				
			КонецЕсли;
			
		Иначе 
			
			ЭСФКлиентСервер.СообщитьПользователю(НСтр("ru = 'Указать основным возможно склад со статусом ""Действует в ИС ЭСФ""'"));
			Возврат;
			
		КонецЕсли;
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)

	ДоступноИспользоватьОсновным = ПравоДоступа("Редактирование", Метаданные.Справочники.ВиртуальныеСкладыКонтрагента);
	Элементы.ИспользоватьОсновным.Видимость = ДоступноИспользоватьОсновным;

КонецПроцедуры

&НаСервере
Функция ПроверкаОсновныхВСКонтрагентов(Ссылка)

	УстановленОсновнойВСКонтрагента = Ложь;
	
	ВС = РегистрыСведений.ОсновныеВиртуальныеСкладыКонтрагентов.СоздатьМенеджерЗаписи();
	ВС.Контрагент = Ссылка.Контрагент;
	ВС.Прочитать();
	
	Если Не ВС.Выбран() Тогда
		
		ВС.Контрагент = Ссылка.Контрагент;
		ВС.ВиртуальныйСкладКонтрагента = Ссылка;
		УстановленОсновнойВСКонтрагента = Истина;
		
	Иначе
		
		Если ВС.ВиртуальныйСкладКонтрагента <> Ссылка Тогда
			УстановленОсновнойВСКонтрагента = Истина;
			ВС.ВиртуальныйСкладКонтрагента = Ссылка;
		КонецЕсли;
		
	КонецЕсли;
	
	Если УстановленОсновнойВСКонтрагента Тогда
		
		ВС.Записать();
		
		ТекстСообщения =  НСтр("ru = 'Склад ""%1"" контрагента ""%2""  установлен основным.'");
		ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, СокрЛП(Ссылка), СокрЛП(Ссылка.Контрагент));
		ЭСФКлиентСерверПереопределяемый.СообщитьПользователю(ТекстСообщения);
		
	КонецЕсли; 
	
	Возврат УстановленОсновнойВСКонтрагента;

КонецФункции
