#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиОбновления

Процедура ЗаполнитьПолныеКодыГСВСДляGTIN(Параметры) Экспорт
	
	Параметры.ОбработкаЗавершена = Ложь;
	
	Если Параметры.Свойство("МассивОбработанныхВСправочникеГСВС") Тогда
		МассивОбработанныхВСправочникеГСВС = Параметры.МассивОбработанныхВСправочникеГСВС;
	Иначе
		МассивОбработанныхВСправочникеГСВС = Новый Массив;
		Параметры.Вставить("МассивОбработанныхВСправочникеГСВС", МассивОбработанныхВСправочникеГСВС);
	КонецЕсли;	
	
	Если Параметры.Свойство("МассивОбработанныхВРегистреГСВС") Тогда
		МассивОбработанныхВРегистреГСВС = Параметры.МассивОбработанныхВРегистреГСВС;
	Иначе
		МассивОбработанныхВРегистреГСВС = Новый Массив;
		Параметры.Вставить("МассивОбработанныхВРегистреГСВС", МассивОбработанныхВРегистреГСВС);
	КонецЕсли;	
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ ПЕРВЫЕ 500
	|	НоменклатураГСВС.Ссылка КАК Ссылка,
	|	1 КАК СлужебныйКод
	|ПОМЕСТИТЬ ВТ_ДанныеДляОбработки
	|ИЗ
	|	Справочник.НоменклатураГСВС КАК НоменклатураГСВС
	|ГДЕ
	|	НоменклатураГСВС.ТипКодаГСВС = ЗНАЧЕНИЕ(Перечисление.ТипыКодовГСВС.GTIN)
	|	И ПОДСТРОКА(НоменклатураГСВС.Наименование, 3, 1) <> "".""
	|	И НЕ НоменклатураГСВС.Ссылка В (&МассивОбработанныхВСправочникеГСВС)
	|
	|ОБЪЕДИНИТЬ ВСЕ
	|
	|ВЫБРАТЬ РАЗЛИЧНЫЕ ПЕРВЫЕ 500
	|	СведенияОНоменклатуреГСВС.НоменклатураГСВС,
	|	2
	|ИЗ
	|	РегистрСведений.СведенияОНоменклатуреГСВС КАК СведенияОНоменклатуреГСВС
	|ГДЕ
	|	СведенияОНоменклатуреГСВС.НоменклатураГСВС.ТипКодаГСВС = ЗНАЧЕНИЕ(Перечисление.ТипыКодовГСВС.GTIN)
	|	И ПОДСТРОКА(СведенияОНоменклатуреГСВС.ПолныйКодГСВС, 3, 1) <> "".""
	|	И НЕ СведенияОНоменклатуреГСВС.НоменклатураГСВС В (&МассивОбработанныхВРегистреГСВС)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	|	ДанныеДляОбработки.Ссылка КАК Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДляОбработки.Ссылка.Родитель) КАК ПолныйКодГСВСРодителя,
	|	СУММА(ДанныеДляОбработки.СлужебныйКод) КАК СлужебныйКод
	|ИЗ
	|	ВТ_ДанныеДляОбработки КАК ДанныеДляОбработки
	|
	|СГРУППИРОВАТЬ ПО
	|	ДанныеДляОбработки.Ссылка,
	|	ПРЕДСТАВЛЕНИЕ(ДанныеДляОбработки.Ссылка.Родитель)");
	
	Запрос.УстановитьПараметр("МассивОбработанныхВСправочникеГСВС", МассивОбработанныхВСправочникеГСВС);
	Запрос.УстановитьПараметр("МассивОбработанныхВРегистреГСВС", МассивОбработанныхВРегистреГСВС);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Если РезультатЗапроса.Пустой() Тогда
		Параметры.ОбработкаЗавершена = Истина;
		Возврат;
	КонецЕсли;
	
	ОбъектовОбработано = 0;
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Пока Выборка.Следующий() Цикл
		
		Если Выборка.СлужебныйКод % 2 = 1 Тогда
		
			МассивОбработанныхВСправочникеГСВС.Добавить(Выборка.Ссылка);
			
			ГСВС = Выборка.Ссылка.ПолучитьОбъект();
			
			ГСВС.Наименование = Выборка.ПолныйКодГСВСРодителя + "/" + ГСВС.КодГСВС;
			
			ГСВС.ОбменДанными.Загрузка = Истина;
			
			Попытка
				ГСВС.Записать();
			Исключение
				ТекстСообщения = НСтр("ru='При записи элемента справочника %1 произошла ошибка: %2'");
				ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Ссылка, ОписаниеОшибки());
				ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
		КонецЕсли;
			
		Если Выборка.СлужебныйКод > 1 Тогда
			
			МассивОбработанныхВРегистреГСВС.Добавить(Выборка.Ссылка);
			
			НаборЗаписей = РегистрыСведений.СведенияОНоменклатуреГСВС.СоздатьНаборЗаписей();
			НаборЗаписей.Отбор.НоменклатураГСВС.Установить(Выборка.Ссылка);
			
			НаборЗаписей.Прочитать();
			
			Для Каждого ЗаписьНабора Из НаборЗаписей Цикл
				Если Сред(ЗаписьНабора.ПолныйКодГСВС, 3, 1) <> "." Тогда
					ЗаписьНабора.ПолныйКодГСВС = Выборка.ПолныйКодГСВСРодителя + "/" + ЗаписьНабора.ПолныйКодГСВС;
				КонецЕсли;
			КонецЦикла;
			
			НаборЗаписей.ОбменДанными.Загрузка = Истина;
			
			Попытка
				НаборЗаписей.Записать(Истина);
			Исключение
				ТекстСообщения = НСтр("ru='При записи в регистр сведений ""Сведения о номенклатуре ГСВС"" данных по номенклатуре ГСВС %1 произошла ошибка: %2'");
				ТекстСообщения = ЭСФКлиентСерверПереопределяемый.ПодставитьПараметрыВСтроку(ТекстСообщения, Выборка.Ссылка, ОписаниеОшибки());
				ЗаписьЖурналаРегистрации(ТекстСообщения, УровеньЖурналаРегистрации.Ошибка,,, ПодробноеПредставлениеОшибки(ИнформацияОбОшибке()));
			КонецПопытки;
			
		КонецЕсли;
		
		ОбъектовОбработано = ОбъектовОбработано + 1;
		
	КонецЦикла;
	
	Параметры.ПрогрессВыполнения.ОбработаноОбъектов = Параметры.ПрогрессВыполнения.ОбработаноОбъектов + ОбъектовОбработано;
	
КонецПроцедуры

#КонецОбласти

#КонецЕсли
