////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЗаполнитьСведенияОДатеАктуализации();
	
	Если Параметры.Свойство("ПризнакУчетаНаВиртуальномСкладе") Тогда
		ЭСФКлиентСерверПереопределяемый.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор,
		"ПризнакУчетаНаВиртуальномСкладе",
		Параметры.ПризнакУчетаНаВиртуальномСкладе, 
		ВидСравненияКомпоновкиДанных.Равно,
		,
		Истина);
	КонецЕсли;

	Если Параметры.Свойство("КодГСВС") Тогда
		ЭСФКлиентСерверПереопределяемый.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор,
		"КодГСВС",
		Параметры.КодГСВС, 
		ВидСравненияКомпоновкиДанных.Содержит,
		,
		Истина);
	КонецЕсли;
	
	Если Параметры.Свойство("КодГСВСРодителя") Тогда
		ЭСФКлиентСерверПереопределяемый.УстановитьЭлементОтбора(
		Список.КомпоновщикНастроек.Настройки.Отбор,
		"Родитель.КодГСВС",
		Параметры.КодГСВСРодителя, 
		ВидСравненияКомпоновкиДанных.Содержит,
		,
		Истина);

	КонецЕсли;
	
	Если Параметры.Свойство("ТекущийКодТНВЭД") Тогда
		ТекущийКодТНВЭД = Параметры.ТекущийКодТНВЭД;
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, "ЛЕВОЕ", "ВНУТРЕННЕЕ");
		Список.Параметры.УстановитьЗначениеПараметра("ТекущаяДата",  ТекущаяДатаСеанса());
	Иначе
		Список.ТекстЗапроса = СтрЗаменить(Список.ТекстЗапроса, ", ПризнакАктивности И (ДействиеЗаписиКонечнаяДата = ДАТАВРЕМЯ(1, 1, 1) ИЛИ ДействиеЗаписиКонечнаяДата >&ТекущаяДата)", "");
	КонецЕсли;

	Если ТекущийКодТНВЭД <> Неопределено И ТекущийКодТНВЭД <> "" Тогда
		Элементы.Список.ТекущаяСтрока = ПолучитьСсылкуПоКодТНВЭД(ТекущийКодТНВЭД);
	КонецЕсли;
	
	ЭтаФорма.Элементы.ФормаСправочникНоменклатураГСВСЗагрузитьСправочник.Видимость = ПолучитьФункциональнуюОпцию("ИспользоватьОбменВС");
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	КлючУникальности = Новый УникальныйИдентификатор;
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	Если ИмяСобытия = "ВС_ПроверятьОповещенияФоновогоЗадания"
		И ЭтаФорма.КлючУникальности = Источник Тогда
		
		ВСКлиентПереопределяемый.ОбработкаОповещенияВС_ПроверятьОповещенияФоновогоЗадания(ЭтаФорма, Параметр);
		
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ФОРМЫ

&НаКлиенте
Процедура ТехническаяИнформацияОбОбновленииНажатие(Элемент)
	
	ПараметрыФормы = Новый Структура;
	ПараметрыФормы.Вставить("СобытиеЖурналаРегистрации", "ОбменЭСФ.ЗагрузитьСправочникГСВС");	

	ВСКлиентПереопределяемый.ОткрытьФормуЖурналаРегистрации(ПараметрыФормы);
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ЗаполнитьСведенияОДатеАктуализации()
	ПараметрыМетодаСинхронизации = ЭСФСервер.ПолучитьПараметрыМетода(Неопределено, ВРег("gsvsUpdates"), Неопределено);	
	Если ПараметрыМетодаСинхронизации.Свойство("lastUpdateDate") и ЗначениеЗаполнено(ПараметрыМетодаСинхронизации.lastUpdateDate) Тогда
		ДатаАктуализации = ПараметрыМетодаСинхронизации.lastUpdateDate
	Иначе
		ДатаАктуализации = Неопределено;
	КонецЕсли;	
КонецПроцедуры

&НаСервере
Функция ПолучитьСсылкуПоКодТНВЭД(КодТНВЭД)
	
	СсылкаНоменклатураГСВС = Неопределено;
	
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	               |	НоменклатураГСВС.Ссылка КАК Ссылка
	               |ИЗ
	               |	Справочник.НоменклатураГСВС КАК НоменклатураГСВС
	               |ГДЕ
	               |	НоменклатураГСВС.КодГСВС = &КодГСВС
	               |	И НоменклатураГСВС.ТипКодаГСВС = &ТипКодаГСВС";
	
	Запрос.УстановитьПараметр("КодГСВС", КодТНВЭД);
	Запрос.УстановитьПараметр("ТипКодаГСВС", Перечисления.ТипыКодовГСВС.ТНВЭД);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ТаблицаКодов = РезультатЗапроса.Выгрузить();
	
	Если ТаблицаКодов.Количество() > 0 Тогда
		СсылкаНоменклатураГСВС = ТаблицаКодов[0].Ссылка;
	КонецЕсли;
	
	Возврат СсылкаНоменклатураГСВС;
	
КонецФункции
