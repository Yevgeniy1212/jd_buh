
&НаКлиенте
Процедура ПредоставлениеВычетаПриИзменении(Элемент)
	
	Если Объект.ПредоставлениеВычета <> ПредопределенноеЗначение("Перечисление.ВидыПредоставленияВычетов.Стандартный") Тогда
		Объект.ПределВычета 		 = 0; 
		Объект.ПериодПредоставления = ПредопределенноеЗначение("Перечисление.ПериодыРасчетаВычетовИКорректировокДоходов.ВПределахМесяца");
	КонецЕсли;	
	
	Если Объект.ПредоставлениеВычета <> ПредопределенноеЗначение("Перечисление.ВидыПредоставленияВычетов.ПоИсчисленнойСуммеВзносов") Тогда
		Объект.ВидВзносов = ПредопределенноеЗначение("Справочник.НалогиСборыОтчисления.ПустаяСсылка");	
	КонецЕсли;	
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	ВидимостьВидВзносов = Объект.ПредоставлениеВычета = ПредопределенноеЗначение("Перечисление.ВидыПредоставленияВычетов.ПоИсчисленнойСуммеВзносов");
	
	Элементы.ПериодПредоставления.Доступность 	= Объект.ПределВычета <> 0;
	Элементы.ВидВзносов.Видимость				= ВидимостьВидВзносов;		
	
	Если Не Форма.ТолькоПросмотр Тогда
		Форма.ТолькоПросмотр = ЗначениеЗаполнено(Объект.ИмяПредопределенныхДанных);
	КонецЕсли;	
	
КонецПроцедуры	

&НаКлиенте
Процедура ПределВычетаПриИзменении(Элемент)
	
	Если Объект.ПределВычета = 0 Тогда
		Объект.ПериодПредоставления = ПредопределенноеЗначение("Перечисление.ПериодыРасчетаВычетовИКорректировокДоходов.ВПределахМесяца");	
	КонецЕсли;
	
	УправлениеФормой(ЭтаФорма);
КонецПроцедуры
