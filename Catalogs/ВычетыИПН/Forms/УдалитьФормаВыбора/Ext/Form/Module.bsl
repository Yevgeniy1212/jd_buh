
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,	
		"НеИспользуется",
		Ложь,
		ВидСравненияКомпоновкиДанных.Равно,
		,Истина
		);			
	
	РежимВыбораСписка = Неопределено;
	МножественныйВыбор = Неопределено;
	
	Если Параметры.Свойство("РежимВыбора", РежимВыбораСписка) И ТипЗнч(РежимВыбораСписка) = Тип("Булево") Тогда
		Элементы.Список.РежимВыбора = РежимВыбораСписка;
	КонецЕсли;  
	
	Если Параметры.Свойство("МножественныйВыбор", МножественныйВыбор) И ТипЗнч(МножественныйВыбор) = Тип("Булево") Тогда
		Элементы.Список.МножественныйВыбор = МножественныйВыбор;
	КонецЕсли;  
	
КонецПроцедуры

&НаКлиенте
Процедура СписокВыборЗначения(Элемент, Значение, СтандартнаяОбработка)
	
	СтандартнаяОбработка 			= Ложь;
	
	Если ЕстьНеИспользуемыеВидыВычета(Значение)Тогда
		ТекстВопроса = НСтр("ru = 'Выбранные данные больше не используются. Продолжить выбор?'");
		Режим 		 = РежимДиалогаВопрос.ДаНет;
		Оповещение 	 = Новый ОписаниеОповещения("ПослеЗакрытияВопросаОпродолженииВыбора", ЭтотОбъект, Новый Структура("ВыбранныеЗначения", Значение));
		ПоказатьВопрос(Оповещение, ТекстВопроса, Режим, 0);
	Иначе	
		ОповеститьОВыборе(Значение);
	КонецЕсли;	
	
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗакрытияВопросаОпродолженииВыбора(Результат, Параметры) Экспорт 
	
	Если Результат = КодВозвратаДиалога.Да Тогда
		ОповеститьОВыборе(Параметры.ВыбранныеЗначения);	
	КонецЕсли;	
	
КонецПроцедуры

&НаКлиенте
Процедура ОтображатьНеиспользуемыеВидыВычетовПриИзменении(Элемент)
	
	Элементы.НеИспользуется.Видимость 			= ОтображатьНеиспользуемыеВидыВычетов;
	//Устнавливаем отбор
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(
		Список,	
		"НеИспользуется",
		Ложь,
		ВидСравненияКомпоновкиДанных.Равно,
		, Не ОтображатьНеиспользуемыеВидыВычетов
		);
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция  ЕстьНеИспользуемыеВидыВычета(ВыбранныеЗначения)
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	ВычетыИПН.Ссылка Как ВидВычетаИПН
	|ИЗ
	|	Справочник.ВычетыИПН КАК ВычетыИПН
	|ГДЕ
	|	ВычетыИПН.Ссылка В(&ВыбранныеЗначения)
	|	И ВычетыИПН.НеИспользуется";
	
	Запрос.УстановитьПараметр("ВыбранныеЗначения", ВыбранныеЗначения);
	
	Возврат Не Запрос.Выполнить().Пустой();
	
КонецФункции	