////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ И ФУНКЦИИ ОБЩЕГО НАЗНАЧЕНИЯ

&НаСервереБезКонтекста
Функция НайтиРодителя(КодРодителя)
	
	Возврат ПланыСчетов.Типовой.НайтиПоКоду(КодРодителя);
	
КонецФункции

&НаСервереБезКонтекста
Функция ПолучитьЗапрещенныеСчета()
	
	ЗапрещенныеСчета = Новый Массив;
	
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.СырьеИМатериалы);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ГотоваяПродукция);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.Товары);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.МонтажОборудования);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.МатериалыПринятыеВПереработку_);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОсновноеПроизводство_);
	
	//для проводок по курсовым разницам
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.РасходыПоКурсовойРазнице);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ДоходыОтКурсовойРазницы);
	
	//по расчету заработной платы
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.КраткосрочнаяЗадолженностьПоОплатеТруда); //3350
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.КраткосрочнаяЗадолженностьПоДепонированнойЗаработнойПлате); //3394
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОбязательстваПоПенсионнымОтчислениям); //3220
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОбязательстваПоСоциальномуСтрахованию); //3211
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОбязательстваПоВзносамОСМС); //3212
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОбязательстваПоОтчислениямОСМС); //3213
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОбязательстваПоОбязательнымПенсионнымВзносамРаботодателя); //3250
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.КраткосрочныеОценочныеОбязательстваПоВознаграждениямРаботникам); //3430
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ДолгосрочнаяЗадолженностьПоДепонированнойЗаработнойПлате); //4174
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ДолгосрочныеОценочныеОбязательстваПоВознаграждениямРаботникам); //4230
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.КраткосрочнаяЗадолженностьПоВыплаченнойЗаработнойПлате); //1252
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.КраткосрочнаяЗадолженностьПоПредоставленнымРаботникамЗаймам); //1253
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ПрочаяКраткосрочнаяЗадолженностьРаботников); //1254
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ДолгосрочнаяЗадолженностьПоВыплаченнойЗаработнойПлате); //2152
	
	//прибыль/убыток отчетного года
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.НераспределеннаяПрибыльНепокрытыйУбытокОтчетногоГода);
	
	//налоговые платежи
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОтложенныеНалоговыеАктивыПоКорпоративномуПодоходномуНалогу);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.КорпоративныйПодоходныйНалогПодлежащийУплате);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.РасходыПоКорпоративномуПодоходномуНалогу);
	ЗапрещенныеСчета.Добавить(ПланыСчетов.Типовой.ОтложенныеНалоговыеОбязательстваПоКорпоративномуПодоходномуНалогу);
	
	// счета доходов и затрат
	Запрос = Новый Запрос;
	Запрос.Текст = "ВЫБРАТЬ
	|	Типовой.Ссылка КАК Счет
	|ИЗ
	|	ПланСчетов.Типовой КАК Типовой
	|ГДЕ
	|	Типовой.Родитель В (&СчетаРодители)";
	
	// доходы			   
	СчетаДоходов = Новый СписокЗначений;
	СчетаДоходов.Добавить(ПланыСчетов.Типовой.ДоходОтРеализацииПродукцииИОказанияУслуг_); //6000
	СчетаДоходов.Добавить(ПланыСчетов.Типовой.ДоходыОтФинансирования); //6100
	СчетаДоходов.Добавить(ПланыСчетов.Типовой.ПрочиеДоходы_); //6200
	СчетаДоходов.Добавить(ПланыСчетов.Типовой.ДоходыСвязанныеСПрекращаемойДеятельностью_); //6300
	СчетаДоходов.Добавить(ПланыСчетов.Типовой.ДоляПрибылиОрганизацийУчитываемыхПоМетодуДолевогоУчастия); //6400
	
	Для Каждого СчетДохода Из СчетаДоходов Цикл
		ЗапрещенныеСчета.Добавить(СчетДохода.Значение);
	КонецЦикла;	
	
	Запрос.УстановитьПараметр("СчетаРодители", СчетаДоходов);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗапрещенныеСчета.Добавить(Выборка.Счет);
	КонецЦикла;	
	
	// затраты
	СчетаЗатрат = Новый СписокЗначений;
	СчетаЗатрат.Добавить(ПланыСчетов.Типовой.СебестоимостьРеализованнойПродукцииИОказанныхУслуг_); //7000
	СчетаЗатрат.Добавить(ПланыСчетов.Типовой.РасходыНаФинансирование); //7300
	СчетаЗатрат.Добавить(ПланыСчетов.Типовой.ПрочиеРасходы_); //7400
	СчетаЗатрат.Добавить(ПланыСчетов.Типовой.РасходыСвязанныеСПрекращаемойДеятельностью_); //7500
	СчетаЗатрат.Добавить(ПланыСчетов.Типовой.ДоляВУбыткеОрганизацийУчитываемыхМетодомДолевогоУчастия); //7600
	СчетаЗатрат.Добавить(ПланыСчетов.Типовой.РасходыПоКорпоративномуПодоходномуНалогу_); //7700
	
	Для Каждого СчетЗатрат Из СчетаЗатрат Цикл
		ЗапрещенныеСчета.Добавить(СчетЗатрат.Значение);
	КонецЦикла;	
	
	Запрос.УстановитьПараметр("СчетаРодители", СчетаЗатрат);
	Выборка = Запрос.Выполнить().Выбрать();
	
	Пока Выборка.Следующий() Цикл
		ЗапрещенныеСчета.Добавить(Выборка.Счет);
	КонецЦикла;	
	
	ТаблицаСчетовВзаиморасчетов = УправлениеВзаиморасчетамиСервер.ПолучитьСписокСчетовВзаиморасчетов();
	Для каждого Строка Из ТаблицаСчетовВзаиморасчетов Цикл
		ЗапрещенныеСчета.Добавить(Строка.Ссылка);
	КонецЦикла;
	
	Возврат Новый ФиксированныйМассив(ЗапрещенныеСчета);
	
КонецФункции

&НаКлиенте
Процедура ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ)
	
	ПоказатьПредупреждение(, НСтр("ru = 'Состав видов субконто на этом счете определяется настройкой параметров учета'"));
	Отказ = Истина;
	
КонецПроцедуры

&НаСервереБезКонтекста
Функция ЭтоЗапрещенныйСчет(Счет)

	Запрос = Новый Запрос();
	Запрос.УстановитьПараметр("Ссылка"          , Счет);
	Запрос.УстановитьПараметр("СписокСчетов"    , ПолучитьЗапрещенныеСчета());	
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	Типовой.Ссылка КАК Ссылка
	|ИЗ
	|	ПланСчетов.Типовой КАК Типовой
	|ГДЕ
	|	Типовой.Ссылка = &Ссылка
	|	И Типовой.Ссылка В ИЕРАРХИИ(&СписокСчетов)";
	
	ЗапрещенныйСчет = НЕ Запрос.Выполнить().Пустой();
	
	Возврат ЗапрещенныйСчет;

КонецФункции // ЭтоЗапрещенныйСчет()

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКоманды.ПриСозданииНаСервере(ЭтотОбъект);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды

	ЗапрещенныйСчет      = ЭтоЗапрещенныйСчет(Объект.Ссылка);

	Элементы.ВидыСубконтоДобавить.Доступность = НЕ ЗапрещенныйСчет;
	Элементы.ВидыСубконтоИзменить.Доступность = НЕ ЗапрещенныйСчет;
	Элементы.ВидыСубконтоУдалить.Доступность  = НЕ ЗапрещенныйСчет;
	Элементы.ВидыСубконтоПереместитьВверх.Доступность 	= НЕ ЗапрещенныйСчет;
	Элементы.ВидыСубконтоПереместитьВниз.Доступность 	= НЕ ЗапрещенныйСчет;
	Элементы.Количественный.Доступность                 = НЕ ЗапрещенныйСчет;
	Элементы.Забалансовый.Доступность					= НЕ ЗапрещенныйСчет И НЕ Объект.Предопределенный;
	Элементы.Валютный.Доступность                       = НЕ ЗапрещенныйСчет;
	
	Кнопка = Элементы.ВидыСубконто.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("ВидыСубконтоКонтекстноеМенюДобавить");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Доступность = НЕ ЗапрещенныйСчет;
	КонецЕсли;
	Кнопка = Элементы.ВидыСубконто.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("ВидыСубконтоКонтекстноеМенюИзменить");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Доступность = НЕ ЗапрещенныйСчет;
	КонецЕсли;
	Кнопка = Элементы.ВидыСубконто.КонтекстноеМеню.ПодчиненныеЭлементы.Найти("ВидыСубконтоКонтекстноеМенюУдалить");
	Если Кнопка <> Неопределено Тогда
		Кнопка.Доступность = НЕ ЗапрещенныйСчет;
	КонецЕсли;
	
	Элементы.Родитель.ТолькоПросмотр 	= Объект.Предопределенный;
	Элементы.Вид.ТолькоПросмотр 		= Объект.Предопределенный;
	
	Элементы.ВидыСубконтоВалютный.Видимость       = Объект.Валютный;
	Элементы.ВидыСубконтоКоличественный.Видимость = Объект.Количественный;
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)

	// СтандартныеПодсистемы.ПодключаемыеКоманды
	ПодключаемыеКомандыКлиент.ПослеЗаписи(ЭтотОбъект, Объект, ПараметрыЗаписи);
	// Конец СтандартныеПодсистемы.ПодключаемыеКоманды
	
	Оповестить("Запись_ПланСчетовТиповой", ПараметрыЗаписи, Объект.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ РЕКВИЗИТОВ ШАПКИ

&НаКлиенте
Процедура КодПриИзменении(Элемент)
	
	Если ЗначениеЗаполнено(Объект.Ссылка) Тогда
		Возврат;
	КонецЕсли;
	
	// Если задан субсчет, то в его коде должна быть точка
	Код      = Объект.Код;
	Родитель = Объект.Родитель;
	
	Если Найти(Код, ".") > 0 Тогда
		
		//Найдем код родителя, для этого найдем последнюю точку в коде счета
		ПозицияТочки = СтрДлина(Код);
		
		Пока Сред(Код, ПозицияТочки, 1) <> "." Цикл
			
			ПозицияТочки = ПозицияТочки - 1;
			
		КонецЦикла;
		
		КодРодителя    = Лев(Код, ПозицияТочки - 1);
		РодительПоКоду = НайтиРодителя(КодРодителя);
		
		Если НЕ ЗначениеЗаполнено(РодительПоКоду) Тогда
			
			ТекстСообщения = НСтр("ru = 'План счетов не содержит счета с кодом %1'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, КодРодителя);			
			
			ПоказатьПредупреждение(, ТекстСообщения);
			
		ИначеЕсли РодительПоКоду <> Объект.Ссылка Тогда
			
			СвойстваТекущегоРодителя = ПроцедурыБухгалтерскогоУчетаВызовСервераПовтИсп.ПолучитьСвойстваСчета(Объект.Родитель);
			
			Объект.Родитель       = РодительПоКоду;
			Объект.Вид            = СвойстваТекущегоРодителя.Вид;
			Объект.Забалансовый   = СвойстваТекущегоРодителя.Забалансовый;
			Объект.Количественный = СвойстваТекущегоРодителя.Количественный;
			Объект.Валютный       = СвойстваТекущегоРодителя.Валютный;
			
			Элементы.ВидыСубконтоВалютный.Видимость       = Объект.Валютный;
			Элементы.ВидыСубконтоКоличественный.Видимость = Объект.Количественный;
			
		КонецЕсли;
		
	КонецЕсли;
		
КонецПроцедуры

&НаКлиенте
Процедура ВалютныйПриИзменении(Элемент)
	
	//Элементы.ВидыСубконтоВалютный.Видимость = Объект.Валютный;
	
	ПредупреждениеОбИзмененииПризнакаУчета("Валютный", "ВидыСубконтоВалютный");
	
КонецПроцедуры

&НаКлиенте
Процедура КоличественныйПриИзменении(Элемент)
	
	//Элементы.ВидыСубконтоКоличественный.Видимость = Объект.Количественный;
	
	ПредупреждениеОбИзмененииПризнакаУчета("Количественный", "ВидыСубконтоКоличественный");
	
КонецПроцедуры

&НаКлиенте
Процедура ЗабалансовыйПриИзменении(Элемент)
	
	ПредупреждениеОбИзмененииПризнакаУчета("Забалансовый");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ПРОЦЕДУРЫ - ОБРАБОТЧИКИ СОБЫТИЙ ВИДЫ СУБКОНТО

&НаКлиенте
Процедура ВидыСубконтоПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа)
	
	Если ЗапрещенныйСчет Тогда
		ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыСубконтоПередНачаломИзменения(Элемент, Отказ)
	
	Если ЗапрещенныйСчет Тогда
		ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ);
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.Предопределенное Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыСубконтоПередУдалением(Элемент, Отказ)
	
	Если ЗапрещенныйСчет Тогда
		ПредупреждениеОНевозможностиИзмененияСоставаВидовСубконто(Отказ);
	КонецЕсли;
	
	Если Элемент.ТекущиеДанные.Предопределенное Тогда
		Отказ = Истина;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ВидыСубконтоПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока Тогда
		Элемент.ТекущиеДанные.Суммовой       = Истина;
		Элемент.ТекущиеДанные.Валютный       = Истина;
		Элемент.ТекущиеДанные.Количественный = Истина;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ПредупреждениеОбИзмененииПризнакаУчета(ПризнакУчета, ЭлементФормы = "")
	
	ПараметрыВопроса = Новый Структура("ПризнакУчета, ЭлементФормы", ПризнакУчета, ЭлементФормы);
	
	ТекстВопроса = НСтр(
		"ru = 'Изменение признаков учета может привести к потере данных.
		|Продолжить?'");
	
	ПризнакУчетаИзменениеЗавершение = Новый ОписаниеОповещения("ПризнакУчетаИзменениеЗавершение", ЭтаФорма, ПараметрыВопроса);
	ПоказатьВопрос(ПризнакУчетаИзменениеЗавершение, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена,, КодВозвратаДиалога.Отмена);
	
КонецПроцедуры

&НаКлиенте
Процедура ПризнакУчетаИзменениеЗавершение(РезультатВопроса, ПараметрыВопроса) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.Отмена Тогда
		Объект[ПараметрыВопроса.ПризнакУчета] = НЕ Объект[ПараметрыВопроса.ПризнакУчета];
	КонецЕсли;
	
	Если ПараметрыВопроса.ЭлементФормы <> "" Тогда
		Элементы[ПараметрыВопроса.ЭлементФормы].Видимость = Объект[ПараметрыВопроса.ПризнакУчета];
	КонецЕсли;
	
КонецПроцедуры

// СтандартныеПодсистемы.ПодключаемыеКоманды
&НаКлиенте
Процедура Подключаемый_ВыполнитьКоманду(Команда)
     ПодключаемыеКомандыКлиент.НачатьВыполнениеКоманды(ЭтотОбъект, Команда, Объект);
КонецПроцедуры

&НаКлиенте
Процедура Подключаемый_ПродолжитьВыполнениеКомандыНаСервере(ПараметрыВыполнения, ДополнительныеПараметры) Экспорт
     ВыполнитьКомандуНаСервере(ПараметрыВыполнения);
КонецПроцедуры
 
&НаСервере
Процедура ВыполнитьКомандуНаСервере(ПараметрыВыполнения)
     ПодключаемыеКоманды.ВыполнитьКоманду(ЭтотОбъект, ПараметрыВыполнения, Объект);
КонецПроцедуры
 
&НаКлиенте
Процедура Подключаемый_ОбновитьКоманды()
     ПодключаемыеКомандыКлиентСервер.ОбновитьКоманды(ЭтотОбъект, Объект);
КонецПроцедуры
// Конец СтандартныеПодсистемы.ПодключаемыеКоманды