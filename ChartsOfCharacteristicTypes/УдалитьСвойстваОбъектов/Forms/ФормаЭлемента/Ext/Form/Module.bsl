////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УправлениеФормой();
		
КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)

	УправлениеФормой();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НазначенияСвойствУстановитьОтборПоСвойству(ТекущийОбъект.Ссылка);
	ЗаписатьНазначенияСвойств(ТекущийОбъект.Ссылка);
	
	ЗначенияСвойствУстановитьОтборПоВладельцу(ТекущийОбъект.Ссылка);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура НазначениеСвойстваПриИзменении(Элемент)
	
	СтароеНазначениеСвойства = Неопределено;
	
	Если НЕ ВозможноИзменениеНазначенияСвойства(СтароеНазначениеСвойства) Тогда
		
		Объект.НазначениеСвойства = СтароеНазначениеСвойства;
		
		ТекстПредупреждения = НСтр(
		"ru = 'Существуют объекты, которым назначено свойство ""%1"".
		|Назначение свойства ""%1"" не может быть изменено.'");
		
		ТекстПредупреждения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстПредупреждения, Объект.Наименование);
		
		ПоказатьПредупреждение(, ТекстПредупреждения);
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияПриИзменении(Элемент)
	
	УстановитьДоступностьСтраницыЗначенияСвойств();
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ НазначенияСвойств

&НаКлиенте
Процедура НазначенияСвойствПриИзменении(Элемент)
	УстановитьТекстНазначениеСвойства();
КонецПроцедуры

&НаКлиенте
Процедура НазначенияСвойствПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	ОграничениеТипа = ТипЗначенияНазначенияСвойства(Объект.НазначениеСвойства);
	Элемент.ПодчиненныеЭлементы.НазначенияСвойствОбъект.ОграничениеТипа = ОграничениеТипа;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ЗначенияСвойств

&НаКлиенте
Процедура ЗначенияСвойствПередНачаломДобавления(Элемент, Отказ, Копирование, Родитель, Группа, Параметр)
	
	Если Объект.Ссылка.Пустая() Тогда
		
		Отказ = Истина;
		
		ТекстВопроса = НСтр(
		"ru = 'Данные ещё не записаны.
		|Выполнение действия ""Создать"" возможно только после записи данных.
		|Данные будут записаны.'");
		ОписаниеОповещения = Новый ОписаниеОповещения("ЗначенияСвойствПередНачаломДобавленияЗавершение", ЭтотОбъект);	
		ПоказатьВопрос(ОписаниеОповещения, ТекстВопроса, РежимДиалогаВопрос.ОКОтмена); 
		
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ЗначенияСвойствПередНачаломДобавленияЗавершение(РезультатВопроса, ДополнительныеДанные) Экспорт
	
	Если РезультатВопроса = КодВозвратаДиалога.ОК Тогда
		
		Если Записать() Тогда
			
			ЗначенияЗаполнения = Новый Структура("Владелец", Объект.Ссылка);
			ПараметрыФормы = Новый Структура("ЗначенияЗаполнения", ЗначенияЗаполнения);
			ОткрытьФорму("Справочник.УдалитьЗначенияСвойствОбъектов.ФормаОбъекта", ПараметрыФормы, Элементы.ЗначенияСвойств);
			
		КонецЕсли;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура УправлениеФормой()
	
	// Действия, связанные с таблицой "НазначенияСвойств".
	НазначенияСвойствУстановитьОтборПоСвойству(Объект.Ссылка);
	ПрочитатьНазначенияСвойств();
	УстановитьТекстНазначениеСвойства();
	
	// Действия, связанные с таблицой "ЗначенияСвойств".
	ЗначенияСвойствУстановитьОтборПоВладельцу(Объект.Ссылка);	
	УстановитьДоступностьСтраницыЗначенияСвойств();
	
КонецПроцедуры

&НаСервере
Процедура НазначенияСвойствУстановитьОтборПоСвойству(Знач Свойство)
	
	НазначенияСвойств.Отбор.Свойство.Значение = Свойство;
	НазначенияСвойств.Отбор.Свойство.Использование = Истина;
	
КонецПроцедуры

&НаСервере
Процедура ЗначенияСвойствУстановитьОтборПоВладельцу(Знач Владелец)
	
	ОтборыСписковКлиентСервер.ИзменитьЭлементОтбораСписка(ЗначенияСвойств, "Владелец", Владелец, Истина);
	
КонецПроцедуры

&НаСервере
Процедура ЗаписатьНазначенияСвойств(Свойство)
	
	Для Каждого НазначенияСвойствСтрока Из НазначенияСвойств Цикл
		НазначенияСвойствСтрока.Свойство = Свойство;	
	КонецЦикла;
	
	НазначенияСвойствУстановитьОтборПоСвойству(Свойство);
	
	НаборЗаписейНазначенияСвойств = РеквизитФормыВЗначение("НазначенияСвойств");
	НаборЗаписейНазначенияСвойств.Записать();
	ЗначениеВРеквизитФормы(НаборЗаписейНазначенияСвойств, "НазначенияСвойств");
	
КонецПроцедуры

&НаСервере
Процедура УстановитьДоступностьСтраницыЗначенияСвойств()
	
	// Установить доступность страницы СтраницаЗначенияСвойств.
	Если Объект.ТипЗначения.СодержитТип(Тип("СправочникСсылка.УдалитьЗначенияСвойствОбъектов")) Тогда
		Элементы.СтраницаЗначенияСвойств.Доступность = Истина;
	Иначе
		Элементы.СтраницаЗначенияСвойств.Доступность = Ложь;	
	КонецЕсли;
	
	// Сделать активной страницу СтраницаНазначенияСвойств, 
	// если СтраницаЗначенияСвойств была текущей и стала недоступной. 
	Если Элементы.ГруппаТаблицы.ТекущаяСтраница = Элементы.СтраницаЗначенияСвойств
		И НЕ Элементы.СтраницаЗначенияСвойств.Доступность Тогда 
		
		Элементы.ГруппаТаблицы.ТекущаяСтраница = Элементы.СтраницаНазначенияСвойств;
		
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Процедура УстановитьТекстНазначениеСвойства()
	
	Если СвойствоДоступноВсемЭкземплярамОбъектов() Тогда
		ТекстНазначениеСвойства = НСтр("ru = 'Свойство предназначено для всех объектов указанного типа.'");
	Иначе
		ТекстНазначениеСвойства = НСтр("ru = 'Свойство предназначено только для указанных объектов.'");
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СвойствоДоступноВсемЭкземплярамОбъектов()
	
	Если НазначенияСвойств.Количество() = 0 Тогда
		Возврат Истина;
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
КонецФункции

&НаСервереБезКонтекста
Функция ТипЗначенияНазначенияСвойства(Знач НазначениеСвойства)
	
	Возврат НазначениеСвойства.ТипЗначения;
	
КонецФункции

&НаСервере
Процедура ОбработкаПроверкиЗаполненияНаСервере(Отказ, ПроверяемыеРеквизиты)

	ДопустимыеТипыОбъектов = Объект.НазначениеСвойства.ТипЗначения;	
	
	Для Каждого СтрокаНазначенияСвойств Из НазначенияСвойств Цикл
		
		СвойствоЗаполнено = ЗначениеЗаполнено(СтрокаНазначенияСвойств.Объект);
		СвойствоДопустимогоТипа = ДопустимыеТипыОбъектов.СодержитТип(ТипЗнч(СтрокаНазначенияСвойств.Объект));
		
		Если НЕ СвойствоЗаполнено ИЛИ НЕ СвойствоДопустимогоТипа Тогда
			
			Отказ = Истина;
			
			ТекстСообщения = НСтр("ru = 'Назначение свойства ""%1"" не может быть применено для назначения: %2.'");
			ТекстСообщения = СтроковыеФункцииКлиентСервер.ПодставитьПараметрыВСтроку(ТекстСообщения, СтрокаНазначенияСвойств.Объект, Объект.НазначениеСвойства);
			
			ИндексСтроки = НазначенияСвойств.Индекс(СтрокаНазначенияСвойств);
			Поле = "НазначенияСвойств[" + Формат(ИндексСтроки, "ЧГ=") + "].Объект";
			
			ОбщегоНазначенияКлиентСервер.СообщитьПользователю(ТекстСообщения, Объект.Ссылка, Поле);
			
		КонецЕсли;
		
	КонецЦикла;
	
КонецПроцедуры

&НаСервере
Процедура ПрочитатьНазначенияСвойств()
	
	НаборЗаписейНазначенияСвойств = РеквизитФормыВЗначение("НазначенияСвойств");
	НаборЗаписейНазначенияСвойств.Прочитать();
	ЗначениеВРеквизитФормы(НаборЗаписейНазначенияСвойств, "НазначенияСвойств");	

КонецПроцедуры

&НаСервере
Функция ВозможноИзменениеНазначенияСвойства(СтароеНазначениеСвойства)
	
	ВозможноИзменение = Истина;
	
	СтароеНазначениеСвойства = Объект.Ссылка.НазначениеСвойства;
	
	Если НЕ Объект.Ссылка.Пустая() И Объект.НазначениеСвойства <> СтароеНазначениеСвойства Тогда 
		ОбъектСвойстваОбъектов = РеквизитФормыВЗначение("Объект");
		Если ОбъектСвойстваОбъектов.СуществуютСсылки() Тогда
			ВозможноИзменение = Ложь;
		КонецЕсли;
	КонецЕсли;
	
	Возврат ВозможноИзменение;
	
КонецФункции
