
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Возвращает дату первого проведенного документа
//
// Параметры:
// Организация - СправочникСсылка.Организация
//
// Возвращаемое значение:
// Дата или Неопределено, если нет ни одного проведенного документа
//
Функция ДатаПервогоПроведенногоДокумента(Организация) Экспорт
	
	ДоступныеОрганизации = ОбщегоНазначенияБКВызовСервераПовтИсп.ОрганизацииДанныеКоторыхДоступныПользователю();
	
	Запрос = Новый Запрос;
	
	ТекстЗапроса =
	"ВЫБРАТЬ ПЕРВЫЕ 1
	|	ЖурналОпераций.Дата
	|ИЗ
	|	ЖурналДокументов.ЖурналОпераций КАК ЖурналОпераций
	|ГДЕ
	|	&УсловиеПоОрганизации
	|	И ЖурналОпераций.Проведен
	|	И НЕ ЖурналОпераций.Ссылка ССЫЛКА Документ.ВводНачальныхОстатков
	|
	|УПОРЯДОЧИТЬ ПО
	|	ЖурналОпераций.Дата";
	
	Если ЗначениеЗаполнено(Организация) Тогда
		
		Если ДоступныеОрганизации.Найти(Организация) = Неопределено Тогда
			Возврат Неопределено;
		КонецЕсли;
		
		Запрос.УстановитьПараметр("Организация", Организация);
		УсловиеПоОрганизации = "ЖурналОпераций.Организация = &Организация";
		
	Иначе
		
		Запрос.УстановитьПараметр("ДоступныеОрганизации", ДоступныеОрганизации);
		УсловиеПоОрганизации = "ЖурналОпераций.Организация В(&ДоступныеОрганизации)";
		
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(ТекстЗапроса, "&УсловиеПоОрганизации", УсловиеПоОрганизации);
	
	УстановитьПривилегированныйРежим(Истина);
	
	Выборка = Запрос.Выполнить().Выбрать();
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Дата;
	КонецЕсли;
	
КонецФункции

#Область ДляВызоваИзДругихПодсистем

// СтандартныеПодсистемы.УправлениеДоступом

// См. УправлениеДоступомПереопределяемый.ПриЗаполненииСписковСОграничениемДоступа.
Процедура ПриЗаполненииОграниченияДоступа(Ограничение) Экспорт

	Ограничение.Текст =
	"РазрешитьЧтениеИзменение
	|ГДЕ
	|	ЧтениеОбъектаРазрешено(Ссылка)";

КонецПроцедуры

// Конец СтандартныеПодсистемы.УправлениеДоступом

#КонецОбласти

#КонецОбласти

#КонецЕсли