
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(РегистрСведенийКодыСтрокСписок, "ВидРасчета", Неопределено,,,Истина);
	
КонецПроцедуры

&НаСервере
Процедура ПриЗагрузкеДанныхИзНастроекНаСервере(Настройки)
	
	ОбработкаИзмененияПоказыватьКодыСтрокДекларацииПоИПНиСН(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаОповещения(ИмяСобытия, Параметр, Источник)
	
	Если ИмяСобытия = "Запись_КодыСтрокДекларацииПоИПНиСН" Тогда
		ПодключитьОбработчикОжидания("ОбновитьРегистрСведенийКодыСтрокСписок", 0.1, Истина);
	КонецЕсли;

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	
	ПодключитьОбработчикОжидания("ОбновитьРегистрСведенийКодыСтрокСписок", 0.1, Истина);
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура ПоказыватьКодыСтрокДекларацииПоИПНиСН(Команда)
	
	ПоказыватьКодыСтрокДекларацииПоИПНиСН = НЕ ПоказыватьКодыСтрокДекларацииПоИПНиСН;
	ОбработкаИзмененияПоказыватьКодыСтрокДекларацииПоИПНиСН(ЭтаФорма);

КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаКлиенте
Процедура ОбновитьРегистрСведенийКодыСтрокСписок() Экспорт
	
	ТекущиеДанные = Элементы.Список.ТекущиеДанные;
	Если ТекущиеДанные = Неопределено Тогда
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(РегистрСведенийКодыСтрокСписок, "ВидРасчета", Неопределено,,, Истина);
	Иначе
		ОбщегоНазначенияКлиентСервер.УстановитьЭлементОтбораДинамическогоСписка(РегистрСведенийКодыСтрокСписок, "ВидРасчета", ТекущиеДанные.Ссылка,,,Истина);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура ОбработкаИзмененияПоказыватьКодыСтрокДекларацииПоИПНиСН(Форма)
	
	Список   = Форма.Список;
	Элементы = Форма.Элементы;
	
	Элементы.ФормаПоказыватьКодыСтрокДекларацииПоИПНиСН.Пометка = Форма.ПоказыватьКодыСтрокДекларацииПоИПНиСН;	
	
	Элементы.РегистрСведенийКодыСтрокСписок.Видимость = Форма.ПоказыватьКодыСтрокДекларацииПоИПНиСН;
	
КонецПроцедуры

