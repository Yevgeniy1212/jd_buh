
////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ФОРМЫ

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	Если Параметры.Ключ.Пустая() Тогда
		
		ПодготовитьФормуНаСервере();
		
	КонецЕсли;

КонецПроцедуры

&НаСервере
Процедура ПриЧтенииНаСервере(ТекущийОбъект)
	
	ПодготовитьФормуНаСервере();
	
КонецПроцедуры

&НаСервере
Процедура ПриЗаписиНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	НаборЗаписейШкала = РегистрыСведений.ШкалаСтавокУдержаний.СоздатьНаборЗаписей();
	
	НаборЗаписейШкала.Отбор.ВидРасчета.Использование = Истина;
	НаборЗаписейШкала.Отбор.ВидРасчета.Значение = Объект.Ссылка;
	НаборЗаписейШкала.Отбор.ВидРасчета.ВидСравнения = ВидСравнения.Равно;

	НаборЗаписейШкала.Загрузить(ШкалаСтавок.Выгрузить());
	НаборЗаписейШкала.Записать(Истина);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)
	
	Если ИсточникВыбора.ИмяФормы = "ПланВидовРасчета.ОсновныеНачисленияОрганизаций.Форма.ФормаВыбора" Тогда
		
		Для Каждого СтрокаМассива Из ВыбранноеЗначение Цикл
			СтрокаВидыРасчета = Объект.БазовыеВидыРасчета.Добавить();	
			СтрокаВидыРасчета.ВидРасчета = СтрокаМассива;
		КонецЦикла;
		
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ЭЛЕМЕНТОВ ШАПКИ ФОРМЫ

&НаКлиенте
Процедура СпособРасчетаПриИзменении(Элемент)
	
	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиенте
Процедура СпособОтраженияВБухучетеПриИзменении(Элемент)
	
	ОбновитьПредставлениеЭлемента("СпособОтраженияВБухучете");
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ СОБЫТИЙ ТАБЛИЦЫ ФОРМЫ ШкалаСтавок

&НаКлиенте
Процедура ШкалаСтавокПриНачалеРедактирования(Элемент, НоваяСтрока, Копирование)
	
	Если НоваяСтрока ИЛИ Копирование Тогда
		Если Элемент.ТекущиеДанные <> Неопределено Тогда
			
			Элементы.ШкалаСтавок.ТекущиеДанные.ВидРасчета = Объект.Ссылка;
			 			
			// назначим очередной номер строки
			МаксНомерСтроки = 0;
			Для Каждого ЗаписьНабора Из ШкалаСтавок Цикл
				МаксНомерСтроки = Макс(МаксНомерСтроки, ЗаписьНабора.НомерСтрокиШкалы);
			КонецЦикла;
			
			Элемент.ТекущиеДанные.НомерСтрокиШкалы = МаксНомерСтроки + 1;
			
			Если НЕ Копирование Тогда
				НовыйПериод = НачалоМесяца(ОбщегоНазначенияКлиент.ДатаСеанса());
				Элемент.ТекущиеДанные.Период = НовыйПериод;
			КонецЕсли;
			
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШкалаСтавокПриОкончанииРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования)
	
	Если Элемент.ТекущиеДанные <> Неопределено Тогда 
		Элемент.ТекущиеДанные.Период = НачалоМесяца(Элемент.ТекущиеДанные.Период);
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ШкалаСтавокПередОкончаниемРедактирования(Элемент, НоваяСтрока, ОтменаРедактирования, Отказ)
	
	Если НЕ ОтменаРедактирования Тогда
		Если Элемент.ТекущиеДанные <> Неопределено Тогда
			Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Период) Тогда
				СообщениеОбОшибке = НСтр("ru = 'Необходимо указать месяц действия шкалы ставок'");
				ОбщегоНазначенияКлиент.СообщитьПользователю(СообщениеОбОшибке,,"НаборЗаписей.Период", , Отказ);
			КонецЕсли; 
		КонецЕсли;
	Иначе
		Если НЕ ЗначениеЗаполнено(Элемент.ТекущиеДанные.Период) Тогда
			Отказ = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры

////////////////////////////////////////////////////////////////////////////////
// ОБРАБОТЧИКИ КОМАНД ФОРМЫ

&НаКлиенте
Процедура Подбор(Команда)
	
	ПараметрыФормы	= Новый Структура;
	ПараметрыФормы.Вставить("ЗакрыватьПриЗакрытииВладельца",	Истина);
	ПараметрыФормы.Вставить("ЗакрыватьПриВыборе",				Ложь);
	ПараметрыФормы.Вставить("РежимВыбора",						Истина);
	ПараметрыФормы.Вставить("МножественныйВыбор",				Истина);
	ПараметрыФормы.Вставить("ПараметрВыборГруппИЭлементов",		ИспользованиеГруппИЭлементов.Элементы);
	
	ОткрытьФорму("ПланВидовРасчета.ОсновныеНачисленияОрганизаций.ФормаВыбора", ПараметрыФормы, ЭтаФорма);
	
КонецПроцедуры     

////////////////////////////////////////////////////////////////////////////////
// СЛУЖЕБНЫЕ ПРОЦЕДУРЫ И ФУНКЦИИ

&НаСервере
Процедура ПодготовитьФормуНаСервере()

	Если Параметры.Ключ.Пустая() Тогда
		
		// Инициализация реквизитов для нового объекта
		Если НЕ ЗначениеЗаполнено(Объект.СпособРасчета) Тогда
			Объект.СпособРасчета		= Перечисления.СпособыРасчетаОплатыТруда.ФиксированнойСуммой;
		КонецЕсли;
		
	КонецЕсли;
	
	// Доступные способы расчета
	НовыеПараметры = Новый Массив;
	НовыеПараметры.Добавить(Новый ПараметрВыбора("Отбор.Ссылка", ПроведениеРасчетовСервер.ПолучитьСписокВариантовУдержанийОрганизации()));
	Элементы.СпособРасчета.ПараметрыВыбора = Новый ФиксированныйМассив(НовыеПараметры);
	
	// Для предопределённых элементов запрещено редактирование способа расчета 
	Элементы.СпособРасчета.Доступность = НЕ Объект.Предопределенный;
	
	// Прочитаем шкалу ставок
	ПрочитатьШкалуСтавок();

	ОбновитьПредставлениеЭлемента("СпособОтраженияВБухучете");

	УправлениеФормой(ЭтаФорма);
	
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Процедура УправлениеФормой(Форма)
	
	Объект   = Форма.Объект;
	Элементы = Форма.Элементы;
	
	Элементы.ГруппаРасчеты.Видимость = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаОплатыТруда.Процентом")
			ИЛИ Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаОплатыТруда.ИсполнительныйЛистПроцентом");
	
	Элементы.ГруппаШкалаСтавок.Видимость = Объект.СпособРасчета = ПредопределенноеЗначение("Перечисление.СпособыРасчетаОплатыТруда.ПочтовыйСбор");

КонецПроцедуры

&НаСервере
Процедура ПрочитатьШкалуСтавок()
	
	НаборЗаписейШкала = РегистрыСведений.ШкалаСтавокУдержаний.СоздатьНаборЗаписей();
	
	НаборЗаписейШкала.Отбор.ВидРасчета.Использование = Истина;
	НаборЗаписейШкала.Отбор.ВидРасчета.Значение = Объект.Ссылка;
	НаборЗаписейШкала.Отбор.ВидРасчета.ВидСравнения = ВидСравнения.Равно;
	НаборЗаписейШкала.Прочитать();
	
	ШкалаСтавок.Загрузить(НаборЗаписейШкала.Выгрузить());
	
КонецПроцедуры 

&НаСервере
Процедура ОбновитьПредставлениеЭлемента(ИмяОбновляемогоЭлемента)
	
	Если ИмяОбновляемогоЭлемента = "СпособОтраженияВБухучете" Тогда
		
		РасшифровкаОтражениеВБухучете = Справочники.СпособыОтраженияЗарплатыВРеглУчете.ПолучитьПредставлениеСпособаОтраженияНачисленияВУчетах(Объект.СпособОтраженияВБухучете);
		
	КонецЕсли;
	
КонецПроцедуры
